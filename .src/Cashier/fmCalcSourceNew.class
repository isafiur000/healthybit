' Gambas class file

Private $encid As String
Private $sBillNo As String[]

Private $cashInHand As String = "Cash in Hand"

Public Sub _new(encid As String, sBillNo As String[])

  $encid = encid
  $sBillNo = sBillNo

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  txtencid.Text = $encid
  txtname.Text = modPatient.GetPatientNameByEnc($encid)

  cmbformatone.List = modBasic.$CashPayVendors
  cmbformattwo.List = modBasic.$CashPayVendors
  cmbformathand.Text = $cashInHand

  SetBillGridView()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key["S"] And If Key.Control Then
    btnclose_Click()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Private Sub BlankAll()

  cmbformatone.Text = ""
  txtformatone.Text = ""
  txtcashone.Value = 0

  cmbformattwo.Text = ""
  txtformattwo.Text = ""
  txtcashtwo.Value = 0

  txtformathand.Text = ""
  txtcashhand.Value = 0

End

Private Sub SetBillGridView()

  Dim xList As String[]
  Dim Row As Integer
  Dim sType As String
  Dim xcash As Float
  Dim xbool As Boolean

  xList = $sBillNo.Copy()
  GridView1.Clear
  GridView1.Rows.Count = xList.Count
  GridView1.Columns.Count = 3

  For Row = 0 To xList.Count - 1
    sType = modBillLock.GetInvoiceFormatNo(xList[Row])
    If sType = "Invoice" Then
      xcash = modNonMedical.GetCashAMTFromInvoice(xList[Row])
    Else If sType = "Receipt" Then
      xcash = modNonMedical.GetCashAMTFromReceipt(xList[Row])
    Endif
    xbool = modNonMedical.GetQRPayBooleanBill(xList[Row])

    If xcash > 0 Then
      If xbool = True Then
        GridView1[Row, 0].Picture = Picture["icon:/small/cancel"]
      Else If xbool = False Then
        GridView1[Row, 0].Picture = Picture["icon:/small/added"]
      Endif
    Else
      GridView1[Row, 0].Picture = Picture["icon:/small/cancel"]
    Endif
    GridView1[Row, 0].Text = ""
    GridView1[Row, 1].Text = xList[Row]
    GridView1[Row, 2].Text = xcash
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 30 * modBasic.$AppWidthRatio
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Invoice"
    .Columns[2].Text = "Cash"
  End With

End

Public Sub GridView1_Select()

  BlankAll()
  txtinvoices.Text = GridView1[GridView1.Row, 1].Text
  txtrecv.Value = GridView1[GridView1.Row, 2].Text
  txtcashhand.Value = GridView1[GridView1.Row, 2].Text
  If GridView1[GridView1.Row, 0].Picture = Picture["icon:/small/added"] Then
    Frame1.Enabled = True
    Panel2.Enabled = True
    cmbformatone.SetFocus
  Else
    Frame1.Enabled = False
    Panel2.Enabled = False
  Endif

End

Public Sub txtreturn_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    Me.Close
  Endif

End

Public Sub txtcashone_Change()

  txtcashhand.Value = txtrecv.Value - (txtcashone.Value + txtcashtwo.Value)

End

Public Sub txtcashtwo_Change()

  txtcashhand.Value = txtrecv.Value - (txtcashone.Value + txtcashtwo.Value)

End

Public Sub btnclose_Click()

  If txtcashhand.Value >= 0 Then
    txttotal.Value = txttotal.Value + txtrecv.Value
    If txtcashone.Value Then
      modBillings.AddPaymentCashSource(txtencid.Text, txtinvoices.Text, cmbformatone.Text, txtformatone.Text, txtcashone.Value)
    Endif
    If txtcashtwo.Value Then
      modBillings.AddPaymentCashSource(txtencid.Text, txtinvoices.Text, cmbformattwo.Text, txtformattwo.Text, txtcashtwo.Value)
    Endif
    If txtcashhand.Value Then
      modBillings.AddPaymentCashSource(txtencid.Text, txtinvoices.Text, cmbformathand.Text, txtformathand.Text, txtcashhand.Value)
    Endif
    SetBillGridView()
  Endif

End

Public Sub cmbformatone_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbformatone)
  modFillContainer.RestrictComboToContent(cmbformatone)

End

Public Sub cmbformattwo_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbformattwo)
  modFillContainer.RestrictComboToContent(cmbformattwo)

End

Public Sub txtcashone_GotFocus()

  txtcashone.SelectAll

End

Public Sub txtcashtwo_GotFocus()

  txtcashtwo.SelectAll

End
