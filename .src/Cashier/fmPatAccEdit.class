' Gambas class file

Public Sub _new(encid As String)

  If encid Then
    txtencid.Text = encid
    txtencid.ReadOnly = True
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbbillmode.List = modNonMedical.FillCashModeCombo()
  If modHelpVariable.$LogInCategory = "Account" Then
    cmbdiscmode.List = modNonMedical.FillDiscountCombo()
  Else
    cmbdiscmode.List = modBasic.$BillDiscountAll
  Endif
  If modGlobalSetting.ShowSettingFromDB("GeneralSettings/Registration_PatNoEdit") = "Yes" Then
    txtpatno.Enabled = True
  Endif
  If txtencid.Text Then
    btnrefresh_Click()
  Else
    txtencid.SetFocus
  Endif

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["F"] And If Key.Control Then
    txtencid.Text = GetEncid()
  Else If Key.Code = Key.Esc Then
    Me.Close()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnneprefer_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtrefer.Value))
  If xx Then
    dtrefer.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub cmbbillmode_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbbillmode)

End

Public Sub cmbdiscmode_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbdiscmode)

End

Public Sub btnrefrespatno_Click()

  txtpatientname.Text = modPatient.PatientFullNameByPatID(Trim(txtpatno.Text))

End

Public Sub cmbdiscmode_Click()

  If cmbdiscmode.Text Then
    txtcredit.Value = modNonMedical.ShowCreditVAlues(cmbdiscmode.Text)
    cmbbillmode.Text = modNonMedical.GetDiscBindBillMode(cmbdiscmode.Text)
  Endif

End

Public Sub txtencid_KeyPress()

  modGeneralMain.InputUpCaseOnly()

End

Public Sub btnnepvalid_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtvalid.Value))
  If xx Then
    dtvalid.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnpatcode_Click()

  Dim res As Result

  If txtpatno.Text Then
    res = modDatabase.$myConn.Edit("tblpatientinfo", "fldpatientval=&1", txtpatno.Text)
    res["fldptcode"] = Trim(txtpatcode.Text)
    res.Update
    Balloon.Info(("Information updated"), btnpatcode)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnrefresh_Click()

  Dim res As Result
  Dim xpolice As String
  Dim xgenvio As String
  Dim xrefdate As String

  If txtencid.Text Then
    txtpatientname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    res = modDatabase.$myConn.Exec("select fldpatientval,flddisctype,fldcashcredit,fldbillingmode,fldcharity,fldvalidity from tblencounter where fldencounterval=&1", Trim(txtencid.Text))                                      ''
    If res.Available Then
      txtpatno.Text = res["fldpatientval"]
      cmbdiscmode.Text = res["flddisctype"]
      cmbbillmode.Text = res["fldbillingmode"]
      txtcredit.Value = res["fldcashcredit"]
      txtcharity.Value = res["fldcharity"]
      txtpatcode.Text = modPatient.GetPatientExtCOdebyEnc(Trim(txtencid.Text))
      If txtpatcode.Text Then
        btnpatcode.Enabled = False
      Endif
      If res["fldvalidity"] Then
        dtvalid.Value = res["fldvalidity"]
      Endif

      xpolice = modPatient.GetDemographAccountValue(Trim(txtencid.Text), "Police Case")
      If xpolice = "Yes" Then
        chkpolice.Value = True
      Else
        chkpolice.Value = False
      Endif
      xgenvio = modPatient.GetDemographAccountValue(Trim(txtencid.Text), "Gender Based Violence")
      If xgenvio = "Yes" Then
        chkgenviol.Value = True
      Else
        chkgenviol.Value = False
      Endif
      txtreferhosp.Text = modPatient.GetDemographAccountValue(Trim(txtencid.Text), "Referral Hospital")
      xrefdate = modPatient.GetDemographAccountValue(Trim(txtencid.Text), "Referral Date")
      If xrefdate Then
        dtrefer.Value = CDate(xrefdate)
      Endif

      If modHelpVariable.$LogInCategory = "Cashier" Then
        LockFilledControls()
      Else If modHelpVariable.$LogInCategory = "Dispensar" Then
        LockFilledControls()
      Endif
      txtreferhosp.SetFocus
    Endif
  Endif

End

Private Sub LockFilledControls()

  If txtcredit.Value Then
    txtcredit.Enabled = False
  Endif
  If txtcharity.Value Then
    txtcharity.Enabled = False
  Endif
  If dtvalid.Value Then
    dtvalid.Enabled = False
    btnnepvalid.Enabled = False
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If txtencid.Text Then
    If cmbbillmode.Text And If cmbdiscmode.Text Then
      If txtpatno.Text Then
        res = modDatabase.$myConn.Edit("tblencounter", "fldencounterval=&1", Trim(txtencid.Text))
        res["fldpatientval"] = Trim(txtpatno.Text)
        res["fldbillingmode"] = cmbbillmode.Text
        res["flddisctype"] = cmbdiscmode.Text
        res["fldcashcredit"] = txtcredit.Value
        res["fldcharity"] = txtcharity.Value
        If dtvalid.Value <> Date() Then
          res["fldvalidity"] = dtvalid.Value
        Endif
        res["xyz"] = False
        res.Update
        Balloon.Info(("Information updated"), btnedit)
        Balloon.Delay = modBasic.$BalloonDelay
      Endif
    Endif
  Endif

End

Public Sub btneditgovt_Click()

  If txtencid.Text Then
    If chkpolice.Value = True Then
      modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", "Police Case", "Yes", "", Now(), True)
    Endif
    If chkgenviol.Value = True Then
      modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", "Gender Based Violence", "Yes", "", Now(), True)
    Endif
    If txtreferhosp.Text Then
      modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", "Referral Hospital", Trim(txtreferhosp.Text), "", Now(), True)
    Endif
    If dtrefer.Value Then
      modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", "Referral Date", CStr(dtrefer.Value), "", Now(), True)
    Endif

    Balloon.Info(("Information updated"), btneditgovt)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtcredit_GotFocus()

  txtcredit.SelectAll

End

Public Sub txtcharity_GotFocus()

  txtcharity.SelectAll

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btnapi_Click()

  Dim xrefer As String
  Dim xmsg As Variant[]

  If cmbdiscmode.Text Then

    xrefer = modNonMedical.GetFixedReferenceScheme(cmbdiscmode.Text)
    If xrefer = "Claim Code" Then
      xmsg = modClaim.CheckIMISValues(Trim(txtencid.Text))
    Else If xrefer = "Accident SSF" Then
      xmsg = modSSF.CheckSSFValues(Trim(txtencid.Text), "Accident SSF")
    Else If xrefer = "Critical SSF" Then
    Else If xrefer = "Medical SSF" Then
      xmsg = modSSF.CheckSSFValues(Trim(txtencid.Text), "Medical SSF")
    Endif

    If xmsg Then
      If xmsg[0] Then
        txtcredit.Value = xmsg[0]
        txtcredit.Enabled = False
      Endif
      If xmsg[1] Then
        dtvalid.Value = xmsg[1]
        dtvalid.Enabled = False
      Endif
    Endif

  Endif

End
