' Gambas class file

Private $sid As Long
Private $encid As String
Private $sLevel As String

Public Sub _new(sid As Long)

  $sid = sid

End

Public Sub Form_Open()

  Dim res As Result

  modGeneralMain.ArrangeFormCentre(Me)
  If modBasic.$PayableManualVerify = "Disable" Then
    $sLevel = "Active"
  Else
    $sLevel = "Waiting"
  Endif
  cmbtitle.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldusertype) as col from tblprocedureshare where fldactive=&1", "Active"))

  res = modDatabase.$myConn.Exec("select fldencounterval,flditemname,fldditemamt from tblpatbilling where fldid=&1", $sid)
  If res.Available Then
    $encid = res["fldencounterval"]
    lblitemname.Text = res["flditemname"]
    txttotamt.Value = res["fldditemamt"]
    ShowGridView()
    If GridView1.Rows.Count Then
      txtperpersonal.Value = GridView1[GridView1.Row, 3].Text
      txttotpersonal.Value = Round((txtperpersonal.Value * txttotamt.Value) / 100, -2)
      txtconsper.Value = 100 - txtperpersonal.Value
      txtconsamt.Value = txttotamt.Value - txttotpersonal.Value
      txtconsper.Enabled = False
      txtconsamt.Enabled = False
    Endif
  Endif

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnconsult_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Payable User"), modBasic.$PayUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif

End

Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

Public Sub txtconsper_LostFocus()

  txtconsamt.Value = Round((txtconsper.Value * txttotamt.Value) / 100, -2)
  txtperpersonal.Value = 100 - txtconsper.Value
  txttotpersonal.Value = txttotamt.Value - txtconsamt.Value

End

Public Sub txtconsamt_LostFocus()

  If Not txtconsper.Value Then
    txtconsper.Value = (txtconsamt.Value * 100) / txttotamt.Value
    txtperpersonal.Value = 100 - txtconsper.Value
    txttotpersonal.Value = txttotamt.Value - txtconsamt.Value
  Endif

End

Public Sub txtuserper_LostFocus()

  If txtuserper.Value > (100 - modGridView.SumGridAmountt(GridView1, 4)) Then
    Message.Warning("Not Allowed", ("OK"))
    txtuserper.Value = 100 - modGridView.SumGridAmountt(GridView1, 4)
  Else
    txtuseramt.Value = Round((txtuserper.Value * txttotpersonal.Value) / 100, -2)
  Endif

End

Public Sub txtuseramt_LostFocus()

  If Not txtuserper.Value Then
    txtuserper.Value = (txtuseramt.Value * 100) / txttotpersonal.Value
  Endif

End

Public Sub txtuserper_GotFocus()

  txtuserper.Value = 100 - modGridView.SumGridAmountt(GridView1, 4)

End

Public Sub btnconsult_GotFocus()

  txtconsper.Enabled = False
  txtconsamt.Enabled = False

End

Public Sub btnadd_Click()

  Dim res As Result

  res = modDatabase.$myConn.Create("tblpatusershares")
  res["fldbillid"] = $sid
  res["fldsharetype"] = "Payable"
  res["fldencounterval"] = $encid
  res["flditemname"] = lblitemname.Text
  res["fldshareuser"] = btnconsult.Tag
  res["fldsharename"] = cmbtitle.Text
  res["fldtotalper"] = txtperpersonal.Value
  res["flduserper"] = txtuserper.Value
  res["flduseramt"] = txtuseramt.Value
  res["fldusertax"] = txttaxval.Value
  res["fldactive"] = $sLevel

  res["flduserid"] = modBasic.$lbluser
  res["fldtime"] = Now()
  res["fldcomp"] = modBasic.$compID
  res.Update
  ShowGridView()
  btnconsult.Tag = ""
  btnconsult.Text = ""
  txtuserper.Value = 0
  txtuseramt.Value = 0
  btnconsult.SetFocus

End

Private Sub ShowGridView()

  Dim res As Result
  Dim Column As Integer
  Dim fld As ResultField

  res = modDatabase.$myConn.Exec("select fldid,flditemname,fldshareuser,fldtotalper,flduserper,flduseramt,fldusertax from tblpatusershares where fldbillid=&1 and fldencounterval=&2 and fldactive=&3", $sid, $encid, $sLevel)                            ''
  GridView1.Clear
  GridView1.Columns.Count = res.Fields.Count
  GridView1.Rows.Count = res.Count

  For Each res
    Column = 0
    For Each fld In res.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, res.Index, Column)
      If Column = 2 Then
        GridView1[res.Index, Column].Text = modGeneral.GetUserFullName(res[fld.Name])
      Else
        GridView1[res.Index, Column].Text = res[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 200 * modBasic.$AppWidthRatio
    .Columns[2].Width = 200 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1
    .Columns[4].Width = 50 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Prticulrs"
    .Columns[2].Text = "Name"
    .Columns[4].Text = "User%"
    .Columns[5].Text = "UserAmt"
    .Columns[6].Text = "TDS%"
  End With

End

Public Sub GridView1_Menu()

  mnufile.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete("tblpatusershares", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    ShowGridView()
  Endif

End
