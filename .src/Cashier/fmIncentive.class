' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  cmbpayer.Text = modPatient.GetPatientNameByEnc($encid, modDatabase.$myConn, "FirstName+SurName")
  LoadItemList()
  ShowPaymentGrid()

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Private Sub LoadItemList()

  Dim aList As String[]
  Dim xList As String[]

  aList = New String[]
  aList = ["Transport Incentive", "ANC Incentive"]
  xList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tblextrapayitems"))
  If xList.Count Then
    aList.Insert(xList)
  Endif
  cmbitem.List = aList

End

Public Sub btnaddpayitem_Click()

  Dim hForm As FmAddVariableGrid

  hform = New FmAddVariableGrid("flditemname", "tblextrapayitems")
  hform.ShowModal
  LoadItemList()
  cmbitem.SetFocus

End

Public Sub btnadd_Click()

  Dim res As Result

  If txtamount.Value And If cmbitem.Text And If cmbpayer.Text And If txtrecby.Text Then                 ''
    res = modDatabase.$myConn.Create("tblextrareceipt")
    res["fldpayername"] = cmbpayer.Text
    res["fldpayeradd"] = $encid
    res["fldreference"] = ""
    res["flditemname"] = cmbitem.Text
    res["fldpayamount"] = 0 - txtamount.Value
    res["fldpaytype"] = "Cash"
    res["fldchequeno"] = ""
    res["fldbankname"] = ""
    res["fldusername"] = txtrecby.Text
    res["flduserpost"] = txtrecpost.Text
    res["flduserid"] = modBasic.$lbluser
    res["fldtime"] = Now()
    res["fldcomp"] = modBasic.$compID
    res["fldsave"] = False
    res["xyz"] = False
    res.Update
    ShowPaymentGrid()
    cmbitem.Text = ""
    txtamount.Value = 0
  Endif

End

Public Sub btnsave_Click()

  Dim res As Result
  Dim billno As String
  Dim idLock As Boolean

  If cmbpayer.Text Then
    Inc Application.Busy
    txtreference.Text = ""
    idLock = modBillLock.LockTableForID("Voucher")
    If idLock = True Then

      modDatabase.$myConn.Begin
      res = modDatabase.$myConn.Edit("tblextrareceipt", "fldsave=&1 and fldpayeradd=&2", False, $encid)
      For Each res
        billno = modBillLock.VoucherNumber()
        If billno Then
          res["fldreference"] = billno
          res["flduserid"] = modBasic.$lbluser
          res["fldtime"] = Now()
          res["fldcomp"] = modBasic.$compID
          res["fldsave"] = True
          res["xyz"] = False
          res.Update
        Endif
      Next
      modDatabase.$myConn.Commit

      modBillLock.ReleaseLockTable("Voucher")
    Endif
    Dec Application.Busy

    txtreference.Text = billno
    If txtreference.Text Then
      modVoucher.VoucherReceipt(txtreference.Text, "Voucher", $encid & " : " & cmbpayer.Text, Now(), modBasic.$lbluser)
    Endif
    ShowPaymentGrid()
  Endif

Catch
  modDatabase.$myConn.Rollback
  If idLock = True Then
    modBillLock.ReleaseLockTable("Voucher")
  Endif
  Dec Application.Busy
  modHelpVariable.CreateErrorReport()

End

Private Sub ShowPaymentGrid()

  Dim sql As String

  If cmbpayer.Text Then
    sql = "select fldid,fldtime,fldpayername,fldreference,flditemname,fldpayamount,fldpaytype,flduserid,fldcomp,fldtime from tblextrareceipt where fldsave=&1 and fldpayeradd=&2"
    $rData = modDatabase.$myConn.Exec(sql, False, $encid)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ResizeGrid()
  Endif

End

Private Sub ResizeGrid()

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 175 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 1
    .Columns[9].Width = 1

    .Columns[1].Text = "Date"
    .Columns[3].Text = "Reference"
    .Columns[4].Text = "Particulars"
    .Columns[5].Text = "Payment"
    .Columns[6].Text = "PayMode"
    .Columns[7].Text = "RecvBy"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 1 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldtime"], gb.MediumDate)
  Else If Column = 5 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Click()

  txtreference.Text = GridView1[GridView1.Row, 3].Text

End

Public Sub btnshow_Click()

  Dim sql As String

  txtreference.Text = ""
  If cmbpayer.Text Then
    If btnshow.Text = "Show" Then
      sql = "select fldid,fldtime,fldpayername,fldreference,flditemname,fldpayamount,fldpaytype,flduserid,fldcomp,fldtime from tblextrareceipt where fldsave=&1 and fldpayeradd=&2"                                           ''
      $rData = modDatabase.$myConn.Exec(sql, True, $encid)
      $aMyFields = New String[]
      modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
      ResizeGrid()
      btnshow.Text = "Close"
    Else If btnshow.Text = "Close" Then
      ShowPaymentGrid()
      btnshow.Text = "Show"
    Endif
  Endif

End

Public Sub GridView1_Menu()

  mnuhidden.Popup

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblextrareceipt", "fldid=&1 and fldsave=&2", GridView1[GridView1.Row, 0].Text, False)
    ShowPaymentGrid()
  Endif

End

Public Sub mnureprint_Click()

  Dim xx As String

  If GridView1.Rows.Selection.Count > 0 Then
    xx = GridView1[GridView1.Row, 3].Text
    If xx Then
      modVoucher.VoucherReceipt(xx, "Voucher Copy", GridView1[GridView1.Row, 2].Text, GridView1[GridView1.Row, 9].Text, GridView1[GridView1.Row, 7].Text)
    Endif
  Endif

End

Public Sub btnadd_GotFocus()

  btnadd.Font.Bold = True

End

Public Sub btnadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnadd_Click()
  Endif

End

Public Sub btnadd_LostFocus()

  btnadd.Font.Bold = False

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub txtamount_GotFocus()

  txtamount.SelectAll

End
