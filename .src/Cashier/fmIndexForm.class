' Gambas class file

Private $selDate As Boolean
Private $Edit As Boolean
Private $AdmitLock As Boolean

Public Sub _new(encid As String, Editable As Boolean, AdmitLock As Boolean)

  txtencounter.Text = encid
  $Edit = Editable
  $AdmitLock = AdmitLock

  If txtencounter.Text Then
    Me.Tag = "Essential Demographics"
    btnencid_Click()
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

  cmbsex.List = ["Male", "Female", "Other"]
  cmbdistrict.List = modBasic.$DistrictList
  cmbrelation.List = modBasic.$RelationList

  btnyear.Text = "Year"
  btnmon.Text = "Mon"

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnadddist_Click()

  ' Dim hForm As FmAddVarThree
  '
  ' hForm = New FmAddVarThree("fldpality", "fldprovince", "tblmunicipals", "flddistrict", "")
  ' hForm.ShowModal
  ' cmbdistrict.List = modMedicine.GetDistrictListFromMap()

End

Private Sub SetEditable(sDate As Date, sFileNo As String)

  Dim xLock As Boolean

  If modBasic.$RegistLockProfile And If DateDiff(sDate, Now(), gb.Hour) > modBasic.$RegistLockProfile Then
    xLock = True
  Else If $AdmitLock And If sFileNo Then
    xLock = True
  Else
    xLock = False
  Endif

  If $Edit = False Or If xLock = True Then
    txtnamefir.Enabled = False
    txtnamelast.Enabled = False
    cmbsex.Enabled = False
    chkencrypt.Enabled = False

    If Trim(txtpatno.Text) Then
      txtpatno.Enabled = False
    Endif

    If Trim(txtfileno.Text) Then
      txtfileno.Enabled = False
    Endif

    If Trim(txtaddfir.Text) Then
      txtaddfir.Enabled = False
    Endif

    If Trim(cmbdistrict.Text) Then
      cmbdistrict.Enabled = False
    Endif

    If txtyears.Value Or If txtmonths.Value Then
      txtyears.Enabled = False
      txtmonths.Enabled = False
      btnyear.Enabled = False
      btnmon.Enabled = False
      dtdobmain.Enabled = False
      chknull.Enabled = False
    Endif

    If Trim(txtcontact.Text) Then
      txtcontact.Enabled = False
    Endif

    If Trim(txtemail.Text) Then
      txtemail.Enabled = False
    Endif

    If Trim(txtguardian.Text) Then
      txtguardian.Enabled = False
    Endif

    If Trim(cmbrelation.Text) Then
      cmbrelation.Enabled = False
    Endif

    If Trim(txtextcode.Text) Then
      txtextcode.Enabled = False
    Endif

  Endif

End

Public Sub btnChange_Click()

  Dim res As Result
  Dim rex As Result
  Dim xbool As Boolean
  Dim $DOBirth As Date
  Dim xgo As Boolean
  Dim xPatLst As String[]

  If $selDate = True Then
    $DOBirth = dtdobmain.Value
  Else
    If txtmonths.Value = 0 Then
      If btnyear.Text = "Year" Then
        $DOBirth = DateAdd(Now(), gb.Year, 0 - CInt(txtyears.Value))
      Else If btnyear.Text = "Mon" Then
        $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(txtyears.Value))
      Else If btnyear.Text = "Day" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(txtyears.Value))
      Endif

    Else
      If btnyear.Text = "Year" And If btnmon.Text = "Mon" Then
        $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(12 * txtyears.Value + txtmonths.Value))
      Else If btnyear.Text = "Mon" And If btnmon.Text = "Day" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(30 * txtyears.Value + txtmonths.Value))
      Else If btnyear.Text = "Day" And If btnmon.Text = "Hour" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(24 * txtyears.Value + txtmonths.Value))
      Endif

    Endif
  Endif

  If DateDiff($DOBirth, Now(), gb.Day) < 73000 Then

    If modHelpVariable.$LogInCategory = "Cashier" Then
      xbool = True
    Else
      rex = modDatabase.$myConn.Exec("select fldpatientval from tblpatientinfo where fldpatientval=&1 and fldadmitfile IS &2", Trim(txtpatno.Text), Null)
      If rex.Available Then
        xbool = True
      Else
        xbool = False
      Endif
    Endif

    Inc Application.Busy
    If txtpatno.Text Then
      If xbool = True Then

        If txtfileno.Text Then
          xPatLst = modPatient.GetPatientListFileNo(Trim(txtfileno.Text))
          If xPatLst And If xPatLst.Count Then
            If modBasic.$DiaryUnique = "Enable" Then
              xgo = False
              Message.Warning("Diary Number must be Unique", ("OK"))
            Else
              If Message.Warning(("This Diary No already registered to " & xPatLst.Join(", ") & ". Are you sure ?"), ("No"), ("Yes")) = 2 Then
                xgo = True
              Else
                xgo = False
              Endif
            Endif
          Else
            xgo = True
          Endif
        Else
          xgo = True
        Endif

        If xgo = True Then
          res = modDatabase.$myConn.Edit("tblpatientinfo", "fldpatientval=&1", Trim(txtpatno.Text))
          If chkencrypt.Value = True Then
            res["fldptnamefir"] = modPassword.EncryptPatSimple(String.UCaseFirst(Trim(txtnamefir.text)))
            res["fldptnamelast"] = modPassword.EncryptPatSimple(String.UCaseFirst(Trim(txtnamelast.text)))
            res["fldptcontact"] = modPassword.EncryptPatSimple(Trim(txtcontact.Text))
            res["fldemail"] = modPassword.EncryptPatSimple(Trim(txtemail.Text))
          Else
            res["fldptnamefir"] = String.UCaseFirst(Trim(txtnamefir.text))
            res["fldptnamelast"] = String.UCaseFirst(Trim(txtnamelast.text))
            res["fldptcontact"] = Trim(txtcontact.Text)
            res["fldemail"] = Trim(txtemail.Text)
          Endif
          res["fldethniccode"] = txtethcode.Text
          If txtaddfir.Text Then
            res["fldptaddvill"] = String.UCaseFirst(Trim(txtaddfir.Text))
          Endif
          res["fldptaddward"] = Trim(txtward.Text)
          If cmbdistrict.Text Then
            res["fldptadddist"] = String.UCaseFirst(Trim(cmbdistrict.Text))
          Endif
          res["fldptsex"] = cmbsex.text

          res["fldptguardian"] = String.UCaseFirst(Trim(txtguardian.Text))
          res["fldrelation"] = String.UCaseFirst(Trim(cmbrelation.Text))
          If chknull.Value = False Then
            res["fldptbirday"] = $DOBirth
          Endif
          res["fldptcode"] = Trim(txtcomment.Text)
          res["fldadmitfile"] = Trim(txtfileno.Text)
          res["fldcomment"] = Trim(txtcomment.Text)
          If chkextcode.Value = True Then
            res["fldptcode"] = ""
          Else
            If txtextcode.Text Then
              res["fldptcode"] = Trim(txtextcode.Text)
            Endif
          Endif
          res["fldpassword"] = modPassword.EncryptSimple(Trim(txtpassword.Text))
          res["fldupuser"] = modBasic.$lbluser
          res["flduptime"] = Now()
          res["fldencrypt"] = chkencrypt.Value
          res["xyz"] = False
          res.Update()
          modPatientSub.RemovePatientCache(Trim(txtpatno.Text))
          Balloon.Info(("Information updated"), btnChange)
          Balloon.Delay = modBasic.$BalloonDelay
        Endif

      Endif
    Endif
    Dec Application.Busy

  Endif

End

Public Sub dtdobmain_Click()

  If dtdobmain.Value Then
    If dtdobmain.Value > modDate.EndSqlDate(Now()) Then
      dtdobmain.Value = ""
      Message.Warning("Incorrect Date and Time", ("OK"))
    Else
      $selDate = True
      modDateSub.DateToYearandMonth(dtdobmain.Value, txtyears, txtmonths, btnyear, btnmon)
    Endif
  Endif

End

Public Sub btnnepdate_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtdobmain.Value))
  If xx Then
    dtdobmain.Value = modDate.ConvertToEnglishdate(xx)
  Endif
  If dtdobmain.Value Then
    If dtdobmain.Value > modDate.EndSqlDate(Now()) Then
      dtdobmain.Value = ""
      Message.Warning("Incorrect Date and Time", ("OK"))
    Else
      $selDate = True
      modDateSub.DateToYearandMonth(dtdobmain.Value, txtyears, txtmonths, btnyear, btnmon)
    Endif
  Endif

End

Public Sub btnyear_Click()

  If btnyear.Text = "Year" Then
    btnyear.Text = "Mon"
    btnmon.Text = "Day"
  Else If btnyear.Text = "Mon" Then
    btnyear.Text = "Day"
    btnmon.Text = "Hour"
  Else If btnyear.Text = "Day" Then
    btnyear.Text = "Year"
    btnmon.Text = "Mon"
  Endif

End

Private Sub CheckYearMonth()

  If txtyears.Value = 0 And If txtmonths.Value = 0 Then
    chknull.Value = True
  Else If txtmonths.Value > 12 And If btnmon.Text = "Mon" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else If txtmonths.Value > 30 And If btnmon.Text = "Day" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else If txtmonths.Value > 24 And If btnmon.Text = "Hour" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else
    $selDate = False
  Endif

End

Public Sub txtyears_LostFocus()

  CheckYearMonth()

End

Public Sub txtmonths_LostFocus()

  CheckYearMonth()

End

Public Sub cmbsex_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbsex)
  modFillContainer.RestrictComboToContent(cmbsex)

End

Public Sub cmbsex_LostFocus()

  modFillContainer.RestrictCombo(cmbsex)

End

Public Sub txtnamelast_KeyRelease()

  If Not txtnamelast.Text Then
    If modBasic.$SurNameList Then

      If Key.Code = Key.Down Then
        txtnamelast.Text = GridViewNew("Select SurName", modBasic.$SurNameList, False, txtnamelast)
      Endif

    Endif
  Endif

End

Public Sub txtethcode_KeyRelease()

  Dim xval As String
  Dim sList As String[] = ["Dalit", "Janajati", "Madhesi", "Muslim", "Brahmin/Chettri", "Other"]

  If Key.Code = Key.Down Then
    xval = InputListView("Select Ethnic Group", "Ethnicity", sList)
    If xval Then
      txtethcode.Text = modMisc.GetEthnicStringCode(xval)
    Endif
  Endif

End

Public Sub cmbdistrict_Click()

  txtaddfir.Clear()
  If cmbdistrict.Text Then
    txtaddfir.List = modDataRepo.GetMunicipalsByDistrict(cmbdistrict.Text)
  Endif

End

Public Sub txtaddfir_KeyRelease()

  modFillContainer.AutoFillComboBox(txtaddfir)
  If modBasic.$UnLockAddress = "Yes" Then
  Else
    modFillContainer.RestrictComboToContent(txtaddfir)
  Endif

End

Public Sub cmbdistrict_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdistrict)
  If modBasic.$UnLockDistrict = "Yes" Then
  Else
    modFillContainer.RestrictComboToContent(cmbdistrict)
  Endif

End

Public Sub cmbrelation_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbrelation)
  If modBasic.$UnLockRelation = "Yes" Then
  Else
    modFillContainer.RestrictComboToContent(cmbrelation)
  Endif

End

Public Sub txtfileno_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnfileno_Click()
  Endif

End

Public Sub btnfileno_Click()

  Dim xList As String[]

  If txtfileno.Text Then
    xList = modPatient.GetPatientListFileNo(Trim(txtfileno.Text))
    If xList Then
      If xList.Count = 1 Then
        txtpatno.Text = xList[0]
        btnpatno_Click()
      Else
        txtpatno.Text = InputListView("Select Patient No", "Index Form", xList)
        btnpatno_Click()
      Endif
    Else
      Message.Warning(("File No not set"), ("OK"))
    Endif
  Endif

End

Public Sub txtpatno_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnpatno_Click()
  Endif

End

Public Sub btnpatno_Click()

  Dim res As Result
  Dim sql As String
  Dim xaddr As String[]

  $selDate = False
  dtdobmain.Value = ""
  If txtpatno.Text Then
    sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldptadmindate,fldemail,fldrelation,fldcomment,fldadmitfile,fldptcode,fldencrypt,fldpassword from tblpatientinfo where fldpatientval = &1 "                                    ''"
    res = modDatabase.$myConn.Exec(sql, Trim(txtpatno.Text))
    If res.Available = False Then
      modLinkDB.CheckPatientDataRemote(Trim(txtpatno.Text))
      txtpatno.SetFocus
    Else
      If res["fldencrypt"] Then
        chkencrypt.Value = res["fldencrypt"]
      Else
        chkencrypt.Value = False
      Endif
      cmbdistrict.Text = res["fldptadddist"]  ''district always before address
      cmbsex.Text = res["fldptsex"]
      If res["fldptaddward"] Then
        txtward.Text = res["fldptaddward"]
        txtaddfir.Text = res["fldptaddvill"]
      Else
        If res["fldptaddvill"] Then
          xaddr = Split(res["fldptaddvill"], "-")
          If xaddr.Count > 1 Then
            If IsInteger(xaddr[xaddr.Count - 1]) Then
              txtward.Text = xaddr[xaddr.Count - 1]
              txtaddfir.Text = xaddr.Copy(0, xaddr.Count - 1).Join("-")
            Else
              txtward.Text = ""
              txtaddfir.Text = xaddr.Join("-")
            Endif
          Else
            txtward.Text = ""
            txtaddfir.Text = xaddr.Join("-")
          Endif
        Endif
      Endif
      txtguardian.Text = res["fldptguardian"]
      cmbrelation.Text = res["fldrelation"]
      If modBasic.$SuperUser = True Then
        txtnamefir.Text = modPassword.DecryptPatData(res["fldptnamefir"], res["fldencrypt"])
        txtnamelast.Text = modPassword.DecryptPatData(res["fldptnamelast"], res["fldencrypt"])
        txtcontact.Text = modPassword.DecryptPatData(res["fldptcontact"], res["fldencrypt"])
        txtemail.Text = modPassword.DecryptPatData(res["fldemail"], res["fldencrypt"])
      Else
        txtnamefir.Text = res["fldptnamefir"]
        txtnamelast.Text = res["fldptnamelast"]
        txtcontact.Text = res["fldptcontact"]
        txtemail.Text = res["fldemail"]
      Endif
      txtethcode.Text = res["fldethniccode"]
      txtfileno.Text = res["fldadmitfile"]
      txtcomment.Text = res["fldcomment"]
      txtextcode.Text = res["fldptcode"]
      txtpassword.Text = modPassword.DecryptSimple(res["fldpassword"])
      If res["fldptbirday"] Then
        chknull.Value = False
        dtdobmain.Value = res["fldptbirday"]
        $selDate = True
        modDateSub.DateToYearandMonth(res["fldptbirday"], txtyears, txtmonths, btnyear, btnmon)
      Else
        chknull.Value = True
      Endif
      txtencounter.Text = ""
      SetEditable(res["fldptadmindate"], res["fldadmitfile"])
    Endif
  Endif

End

Public Sub txtencounter_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnencid_Click()
  Endif

End

Public Sub btnencid_Click()

  Dim res As Result
  Dim sql As String
  Dim xaddr As String[]

  $selDate = False
  dtdobmain.Value = ""
  If txtencounter.Text Then
    sql = "select fldpatientval,fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldptadmindate,fldemail,fldrelation,fldcomment,fldadmitfile,fldptcode,fldencrypt,fldpassword from tblpatientinfo where fldpatientval=&1"                                    ''
    res = modDatabase.$myConn.Exec(sql, modPatient.GetPatientNoByEnc(Trim(txtencounter.Text)))
    If res.Available Then
      If res["fldencrypt"] Then
        chkencrypt.Value = res["fldencrypt"]
      Else
        chkencrypt.Value = False
      Endif
      txtpatno.Text = res["fldpatientval"]
      cmbdistrict.Text = res["fldptadddist"]  ''district always before address
      cmbsex.Text = res["fldptsex"]
      If res["fldptaddward"] Then
        txtward.Text = res["fldptaddward"]
        txtaddfir.Text = res["fldptaddvill"]
      Else
        If res["fldptaddvill"] Then
          xaddr = Split(res["fldptaddvill"], "-")
          If xaddr.Count > 1 Then
            If IsInteger(xaddr[xaddr.Count - 1]) Then
              txtward.Text = xaddr[xaddr.Count - 1]
              txtaddfir.Text = xaddr.Copy(0, xaddr.Count - 1).Join("-")
            Else
              txtward.Text = ""
              txtaddfir.Text = xaddr.Join("-")
            Endif
          Else
            txtward.Text = ""
            txtaddfir.Text = xaddr.Join("-")
          Endif
        Endif
      Endif
      txtguardian.Text = res["fldptguardian"]
      cmbrelation.Text = res["fldrelation"]
      If modBasic.$SuperUser = True Then
        txtnamefir.Text = modPassword.DecryptPatData(res["fldptnamefir"], res["fldencrypt"])
        txtnamelast.Text = modPassword.DecryptPatData(res["fldptnamelast"], res["fldencrypt"])
        txtcontact.Text = modPassword.DecryptPatData(res["fldptcontact"], res["fldencrypt"])
        txtemail.Text = modPassword.DecryptPatData(res["fldemail"], res["fldencrypt"])
      Else
        txtnamefir.Text = res["fldptnamefir"]
        txtnamelast.Text = res["fldptnamelast"]
        txtcontact.Text = res["fldptcontact"]
        txtemail.Text = res["fldemail"]
      Endif
      txtethcode.Text = res["fldethniccode"]
      txtfileno.Text = res["fldadmitfile"]
      txtcomment.Text = res["fldcomment"]
      txtextcode.Text = res["fldptcode"]
      txtpassword.Text = modPassword.DecryptSimple(res["fldpassword"])
      If res["fldptbirday"] Then
        chknull.Value = False
        dtdobmain.Value = res["fldptbirday"]
        $selDate = True
        modDateSub.DateToYearandMonth(res["fldptbirday"], txtyears, txtmonths, btnyear, btnmon)
      Else
        chknull.Value = True
      Endif
      SetEditable(res["fldptadmindate"], res["fldadmitfile"])
    Endif
  Endif

End

Public Sub btncancel_Click()

  txtencounter.Text = ""
  txtpatno.Text = ""
  txtfileno.Text = ""
  txtnamefir.Text = ""
  txtnamelast.Text = ""
  txtaddfir.Text = ""
  cmbdistrict.Text = ""
  cmbsex.Text = ""
  txtyears.Value = 0
  txtmonths.Value = 0
  txtcontact.Text = ""
  txtemail.Text = ""
  txtguardian.Text = ""
  cmbrelation.Text = ""
  txtcomment.Text = ""
  txtextcode.Text = ""

End

Public Sub btnsearch_Click()

  txtpatno.Text = PatSearch("PatientNo")

End

Public Sub txtencounter_KeyPress()

  modGeneralMain.InputUpCaseOnly()

End

Public Sub txtpatno_KeyPress()

  modGeneralMain.InputUpCaseOnly()

End

Public Sub txtfileno_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub chkpatpass_Click()

  If chkpatpass.Value = True Then
    txtpassword.Password = False
  Else If chkpatpass.Value = False Then
    txtpassword.Password = True
  Endif

End

Public Sub btnspecial_Click()

  Dim hForm As FmPatSpecial

  If MMain.$GovtApp = "Yes" Then
  Else
    If Message.Question(("Are you sure that patient table consists of Rank and Category Columns ?"), ("No"), ("Yes")) = 2 Then
      hForm = New FmPatSpecial(Trim(txtpatno.Text))
      hForm.ShowModal
    Endif
  Endif

End

Public Sub btnpatsms_Click()

  Dim xpass As String

  If txtpatno.Text And If txtcontact.Text Then
    xpass = modPassword.GetPatientWebPassword(Trim(txtpatno.Text), True)
    If xpass Then
      If modBasic.$SMSQueDisable = "No" Then
        modAppSupport.SaveSMSEntry(Trim(txtcontact.Text), xpass, "Waiting", "")
        Message.Info("SMS Qued", ("OK"))
      Else
        modAppSupport.SendSMSSingle(Trim(txtcontact.Text), xpass)
      Endif
    Endif
  Endif

End

Public Sub txtyears_GotFocus()

  txtyears.SelectAll

End

Public Sub txtmonths_GotFocus()

  txtmonths.SelectAll

End

Public Sub btnfindpatno_Click()

  Dim xval As String

  xval = InputBox(("Enter Patient's Code/PAN"), "Code/PAN", "")
  If xval Then
    txtpatno.Text = modPatient.GetPatientNoByHospCode(xval)
    txtpatno.SetFocus
  Endif

End
