' Gambas class file

Private $sType As String
Private $rData As Result
Private $aMyFields As String[]

Private $patMessage As String
Private $clinMessage As String

Public Sub _new(sType As String)

  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Vertical")
  Me.Icon = Picture[modGeneralMain.$strLogo]
  Me.Title = $sType
  Me.Tag = $sType
  lbldate.Value = Date()

  If $sType = "Major Procedures" Then
    ppnlfollow.Visible = False
    cmbselect.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tblservicecost where flditemtype=&1 and fldtarget like &2", "Procedures", "Major"))
    mnuprocalter.Visible = True
  Else If $sType = "Extra Procedures" Then
    ppnlfollow.Visible = False
    cmbselect.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldprocname) as col from tblgroupproc"))
    mnuprocalter.Visible = True
  Else If $sType = "FollowUp List" Then
    ppnlfollow.Visible = True
    mnuprocalter.Visible = False
    rbopd.Value = True
    rbopd_Click()
  Else If $sType = "Radiology List" Then
    ppnlfollow.Visible = False
    cmbselect.List = modMedicine.FillRadioTestCombo("%")
    mnuprocalter.Visible = False
  Endif
  cmbselect.Text = "%"

  $patMessage = modSettings.ShowSettingFromFIle("Registration/PatientMessage")
  $clinMessage = modSettings.ShowSettingFromFIle("Registration/PhysicianMessage")

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub rbopd_Click()

  cmbselect.List = modGeneral.GetDepartForConsultOnly()

End

Public Sub rbipd_Click()

  cmbselect.List = modGeneral.GetDepartmentsForOPDBoth()

End

Public Sub rbother_Click()

  cmbselect.List = modGeneral.GetDepartForOPVisitOnly()

End

''======================================== ADDED GRID =======================
Private Sub FillPatDateGrid()

  Dim sql As String

  If $sType = "Major Procedures" Then
    sql = "select fldencounterval,fldnewdate,fldnewdate,flditem,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldid,fldnewdate,fldid,flduserid,fldencounterval from tblpatgeneral where fldnewdate>=&1 and fldnewdate<=&2 and fldinput=&3 and flditem like &4 and fldreportquali<>&5"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, modDate.StartSqlDate(lbldate.Value), modDate.EndSqlDate(lbldate.Value), "Procedures", cmbselect.Text, "Done")

  Else If $sType = "Extra Procedures" Then
    sql = "select fldencounterval,fldnewdate,fldnewdate,flditem,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldid,fldnewdate,fldid,flduserid,fldencounterval from tblpatgeneral where fldnewdate>=&1 and fldnewdate<=&2 and fldinput=&3 and flditem like &4 and fldreportquali<>&5"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, modDate.StartSqlDate(lbldate.Value), modDate.EndSqlDate(lbldate.Value), "Extra Procedures", cmbselect.Text, "Done")

  Else If $sType = "Radiology List" Then
    sql = "select fldencounterval,fldnewdate,fldnewdate,fldtestid,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldid,fldnewdate,fldid,flduserid_report,fldencounterval from tblpatradiotest where fldnewdate>=&1 and fldnewdate<=&2 and fldtestid like &3 and fldstatus=&4 and fldsave_report=&5"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, modDate.StartSqlDate(lbldate.Value), modDate.EndSqlDate(lbldate.Value), cmbselect.Text, "Sampled", False)

  Else If $sType = "FollowUp List" Then
    If rbopd.Value = True Then
      sql = "select fldencounterval,fldfollowdate,fldfollowdate,fldconsultname,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldfollowdate,fldencounterval,flduserid,fldencounterval from tblconsult where fldfollowdate>=&1 and fldfollowdate<=&2 and fldconsultname like &3"
    Else If rbother.Value = True
      sql = "select fldencounterval,fldfollowdate,fldfollowdate,fldconsultname,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldfollowdate,fldencounterval,fldflaguser,fldencounterval from tblopvisit where fldfollowdate>=&1 and fldfollowdate<=&2 and fldconsultname like &3"
    Else If rbipd.Value = True Then
      sql = "select fldencounterval,fldfollowdate,fldfollowdate,fldcurrlocat,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldfollowdate,fldpatientval,flduserid,fldencounterval from tblencounter where fldfollowdate>=&1 and fldfollowdate<=&2 and fldcurrlocat like &3"                                                                              ''
    Endif
    $rData = modDatabase.$myConn.Exec(sql, modDate.StartSqlDate(lbldate.Value), modDate.EndSqlDate(lbldate.Value), cmbselect.Text)
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Private Sub ResizeGrid()

  With GridView1
    .Columns[0].Width = 25 * modBasic.$AppWidthRatio
    .Columns[1].Width = 1
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 150 * modBasic.$AppWidthRatio
    .Columns[4].Width = 125 * modBasic.$AppWidthRatio
    .Columns[5].Width = 225 * modBasic.$AppWidthRatio
    .Columns[6].Width = 125 * modBasic.$AppWidthRatio
    .Columns[7].Width = 125 * modBasic.$AppWidthRatio
    .Columns[8].Width = 1
    .Columns[9].Width = 1
    .Columns[10].Width = 1
    .Columns[11].Width = 150 * modBasic.$AppWidthRatio
    .Columns[12].Width = 100 * modBasic.$AppWidthRatio

    .Columns[2].Text = "Time"
    .Columns[3].Text = "Particulars"
    .Columns[4].Text = "EncID"
    .Columns[5].Text = "Name"
    .Columns[6].Text = "Age/Sex"
    .Columns[7].Text = "Contact"
    .Columns[11].Text = "Consultant"
    .Columns[12].Text = "FileNo"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Picture = Picture["icons/unchecked.png"]
  Else If Column = 2 Then
    GridView1.Data.Text = Time($rData[$aMyFields[Column]])
  Else If Column = 5 Then
    GridView1.Data.Text = modPatient.GetPatientNameByEnc($rData[$aMyFields[Column]])
  Else If Column = 6 Then
    GridView1.Data.Text = modPatient.GetPatientAgeString($rData[$aMyFields[Column]], Now()) & "/" & modPatient.GetPatientSex($rData[$aMyFields[Column]])
  Else If Column = 7 Then
    GridView1.Data.Text = modPatient.GetPatientContactByEnc($rData[$aMyFields[Column]])
  Else If Column = 11 Then
    If $sType = "FollowUp List" Then
      GridView1.Data.Text = modGeneral.GetUserFullName($rData[$aMyFields[Column]])
    Else
      GridView1.Data.Text = ""
    Endif
  Else If Column = 12 Then
    GridView1.Data.Text = modPatient.GetPatientFileByEnc($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub GridView1_Click()

  Dim row As Integer
  Dim col As Integer
  Dim xdate As Date
  Dim res As Result

  row = GridView1.Row
  col = GridView1.Column
  If GridView1.Column = 0 Then
    modGridView.CheckUncheckGridNoDB(GridView1, 0)

  Else If GridView1.Column = 1 Or If GridView1.Column = 2 Then
    xdate = GetDateValue(("Select New Date Time"), GridView1[GridView1.Row, 5].Text, GridView1[GridView1.Row, 9].Text)
    If xdate Then
      If $sType = "Major Procedures" Then
        res = modDatabase.$myConn.Edit("tblpatgeneral", "fldid=&1", GridView1[GridView1.Row, 8].Text)
        res["fldnewdate"] = xdate
        res["xyz"] = False
        res.Update
      Else If $sType = "Extra Procedures" Then
        res = modDatabase.$myConn.Edit("tblpatgeneral", "fldid=&1", GridView1[GridView1.Row, 8].Text)
        res["fldnewdate"] = xdate
        res["xyz"] = False
        res.Update
      Else If $sType = "Radiology List" Then
        res = modDatabase.$myConn.Edit("tblpatradiotest", "fldid=&1", GridView1[GridView1.Row, 8].Text)
        res["fldnewdate"] = xdate
        res["xyz"] = False
        res.Update
      Else If $sType = "FollowUp List" Then
        res = modDatabase.$myConn.Edit("tblencounter", "fldencounterval=&1", GridView1[GridView1.Row, 8].Text)
        res["fldfollowdate"] = xdate
        res["xyz"] = False
        res.Update
      Endif
      FillPatDateGrid()
    Endif

  Endif
  GridView1.Row = row
  GridView1.Column = col

End

''-----------------------------  button and menu ------------------------------------------------
Public Sub btnrefresh_Click()

  FillPatDateGrid()
  GridView1.SetFocus

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $sType & " Report", modReportVar.GetDateTimeReport(lbldate.Value, gb.MediumDate))

End

Public Sub mnuselall_Click()

  Dim i As Integer

  For i = 0 To GridView1.Rows.Count - 1
    GridView1[i, 0].Picture = Picture["icons/checked.png"]
  Next

End

Public Sub mnudesel_Click()

  Dim i As Integer

  For i = 0 To GridView1.Rows.Count - 1
    GridView1[i, 0].Picture = Picture["icons/unchecked.png"]
  Next

End

''------------------------------------ change procedure status
Public Sub mnuprocalter_Click()

  Dim xx As String
  Dim status As String[] = ["Planned", "Referred", "On Hold", "Cancelled"]
  Dim res As Result

  If GridView1.Rows.Selection.Count > 0 Then
    xx = InputCombo(("Select Action for selected Procedure"), ("Procedures"), status, "Planned", True)
    If xx Then
      res = modDatabase.$myConn.Edit("tblpatgeneral", "fldid=&1 and fldstatus=&2", GridView1[GridView1.Row, 8].Text, "Waiting")
      res["fldreportquali"] = xx
      res["flduptime"] = Now()
      res["xyz"] = False
      res.Update
    Endif
    FillPatDateGrid()
  Endif

End

''----------------------------------- extra activities -------------------------------------------------
Public Sub btnsearch_Click()

  Dim xx As String

  If modBasic.$AutoEncPrefix Then
    xx = InputBox(("Enter Encounter ID"), $sType, modBasic.$EncIdPrefix)
  Else
    xx = InputBox(("Enter Encounter ID"), $sType, "")
  Endif
  If xx Then
    FillPatSearchGrid(xx)
    GridView1.SetFocus
  Endif

End

Private Sub FillPatSearchGrid(encid As String)

  Dim sql As String

  If $sType = "Major Procedures" Then
    sql = "select fldencounterval,fldnewdate,fldnewdate,flditem,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldid,fldnewdate,fldid,flduserid,fldencounterval from tblpatgeneral where fldencounterval=&1 and fldinput=&2"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, encid, "Procedures")
  Else If $sType = "Extra Procedures" Then
    sql = "select fldencounterval,fldnewdate,fldnewdate,flditem,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldid,fldnewdate,fldid,flduserid,fldencounterval from tblpatgeneral where fldencounterval=&1 and fldinput=&2"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, encid, "Extra Procedures")
  Else If $sType = "Radiology List" Then
    sql = "select fldencounterval,fldnewdate,fldnewdate,fldtestid,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldid,fldnewdate,fldid,flduserid_report,fldencounterval from tblpatradiotest where fldencounterval=&1"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, encid)
  Else If $sType = "FollowUp List" Then
    sql = "select fldencounterval,fldfollowdate,fldfollowdate,fldcurrlocat,fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldpatientval,fldfollowdate,fldpatientval,flduserid,fldencounterval from tblencounter where fldencounterval=&1"                                                                              ''
    $rData = modDatabase.$myConn.Exec(sql, encid)
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["R"] And If Key.Control Then
    btnrefresh_Click()
  Endif

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(lbldate.Value))
  If xx Then
    lbldate.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub mnuemail_Click()

  Dim i As Integer
  Dim xx As String[]
  Dim hForm As FmRemoteMail

  If Desktop.NetworkAvailable = True Then
    xx = New String[]
    For i = 0 To GridView1.Rows.Count - 1
      If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
        xx.Add(modPatient.GetPatientEmail(GridView1[i, 4].Text))
      Endif
    Next
    hForm = New FmRemoteMail(xx, "", $sType, "")
    hForm.ShowModal
  Endif

End

Public Sub mnusmspat_Click()

  Dim i As Integer
  Dim sText As String
  Dim sphno As String

  For i = 0 To GridView1.Rows.Count - 1
    If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
      sphno = modPatient.GetPatientContactByEnc(GridView1[i, 4].Text)
      If sphno Then
        sText = $patMessage
        If sText Then
          sText = modReportVar.GetPatientSMSVAr(GridView1[i, 4].Text, sText)
          sText = modReportVar.GetReportLastConsult(GridView1[i, 4].Text, sText)
          If modBasic.$SMSQueDisable = "No" Then
            modAppSupport.SaveSMSEntry(sphno, sText, "Waiting", "")
          Else
            modAppSupport.SendSMSSingle(sphno, sText)
          Endif
        Endif
      Endif

      GridView1[i, 0].Picture = Picture["icons/unchecked.png"]
    Endif
  Next

End
