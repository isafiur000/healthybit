' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private xPayerList As String[]
Private xItemList As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbmode.List = ["Cash", "Cheque"]
  xPayerList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldpayername as col from tblextrapayers"))
  xItemList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tblextrapayitems"))
  cmbpayer.List = xPayerList
  cmbitem.List = xItemList
  txtrecby.Text = modGeneral.GetUserFullName(modBasic.$lbluser)
  If Not txtrecby.Text Then
    txtrecby.Text = modBasic.$lbluser
  Endif
  ShowPaymentGrid()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub btnaddpayer_Click()

  Dim hForm As FmAddVarTwo
  Dim xx As String[]
  Dim yy As String[]

  xx = New String[]
  yy = New String[]
  hform = New FmAddVarTwo("fldpayername", "fldpayeradd", "tblextrapayers", xx, yy)
  hform.ShowModal
  xPayerList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldpayername as col from tblextrapayers"))
  cmbpayer.List = xPayerList
  cmbpayer.SetFocus

End

Public Sub btnaddpayitem_Click()

  Dim hForm As FmAddVariableGrid

  hform = New FmAddVariableGrid("flditemname", "tblextrapayitems")
  hform.ShowModal
  xItemList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tblextrapayitems"))
  cmbitem.List = xItemList
  cmbitem.SetFocus

End

Public Sub cmbpayer_Click()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldpayeradd from tblextrapayers where fldpayername=&1", cmbpayer.Text)
  If res.Available Then
    res.MoveFirst
    txtpayeradd.Text = res!fldpayeradd
  Endif

End

Public Sub cmbmode_Click()

  If cmbmode.Text = "Cheque" Then
    Panel2.Visible = True
  Else If cmbmode.Text = "Cash" Then
    Panel2.Visible = False
    txtchequeno.Text = ""
    txtbank.Text = ""
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If txtamount.Value And If cmbitem.Text And If cmbpayer.Text And If txtrecby.Text Then                 ''
    res = modDatabase.$myConn.Create("tblextrareceipt")
    res["fldpayername"] = cmbpayer.Text
    res["fldpayeradd"] = txtpayeradd.Text
    res["fldreference"] = ""
    res["flditemname"] = cmbitem.Text
    res["fldpayamount"] = txtamount.Value
    res["fldpaytype"] = cmbmode.Text
    res["fldchequeno"] = txtchequeno.Text
    res["fldbankname"] = txtbank.Text
    res["fldusername"] = txtrecby.Text
    res["flduserpost"] = txtrecpost.Text
    res["flduserid"] = modBasic.$lbluser
    res["fldtime"] = Now()
    res["fldcomp"] = modBasic.$compID
    res["fldsave"] = False
    res["xyz"] = False
    res.Update
    ShowPaymentGrid()
  Endif

End

Private Sub ShowPaymentGrid()

  Dim sql As String

  If cmbpayer.Text Then
    sql = "select fldid,fldtime,fldpayername,fldreference,flditemname,fldpayamount,fldpaytype,flduserid,fldcomp,fldtime from tblextrareceipt where fldsave=&1 and fldpayername=&2"
    $rData = modDatabase.$myConn.Exec(sql, False, cmbpayer.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ResizeGrid()
  Endif

End

Private Sub ResizeGrid()

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 120 * modBasic.$AppWidthRatio
    .Columns[2].Width = 150 * modBasic.$AppWidthRatio
    .Columns[3].Width = 120 * modBasic.$AppWidthRatio
    .Columns[4].Width = 200 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 1
    .Columns[9].Width = 1

    .Columns[1].Text = "Date"
    .Columns[2].Text = "Payer"
    .Columns[3].Text = "Reference"
    .Columns[4].Text = "ItemName"
    .Columns[5].Text = "Payment"
    .Columns[6].Text = "PayMode"
    .Columns[7].Text = "RecvBy"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 1 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldtime"], gb.MediumDate)
  Else If Column = 5 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Menu()

  mnuhidden.Popup

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblextrareceipt", "fldid=&1 and fldsave=&2", GridView1[GridView1.Row, 0].Text, False)
    ShowPaymentGrid()
  Endif

End

Public Sub cmbpayer_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbpayer)
  modFillContainer.RestrictComboToContent(cmbpayer)

End

Public Sub cmbpayer_LostFocus()

  modFillContainer.RestrictCombo(cmbpayer)

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub cmbitem_LostFocus()

  modFillContainer.RestrictCombo(cmbitem)

End

Public Sub cmbmode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmode)
  modFillContainer.RestrictComboToContent(cmbmode)

End

Public Sub cmbmode_LostFocus()

  modFillContainer.RestrictCombo(cmbmode)

End

Public Sub btnsave_Click()

  Dim res As Result
  Dim billno As String
  Dim idLock As Boolean

  If cmbpayer.Text Then

    Inc Application.Busy
    txtreference.Text = ""
    idLock = modBillLock.LockTableForID("Voucher")
    If idLock = True Then

      modDatabase.$myConn.Begin
      res = modDatabase.$myConn.Edit("tblextrareceipt", "fldsave=&1 and fldpayername=&2", False, cmbpayer.Text)
      For Each res
        billno = modBillLock.VoucherNumber()
        If billno Then
          res["fldreference"] = billno
          res["flduserid"] = modBasic.$lbluser
          res["fldtime"] = Now()
          res["fldcomp"] = modBasic.$compID
          res["fldsave"] = True
          res["xyz"] = False
          res.Update
        Endif
      Next
      modDatabase.$myConn.Commit

      modBillLock.ReleaseLockTable("Voucher")
    Endif
    Dec Application.Busy

    txtreference.Text = billno
    If txtreference.Text Then
      modVoucher.VoucherReceipt(txtreference.Text, "Voucher", cmbpayer.Text, Now(), modBasic.$lbluser)
    Endif
    ShowPaymentGrid()

  Endif

Catch
  modDatabase.$myConn.Rollback
  Dec Application.Busy
  If idLock = True Then
    modBillLock.ReleaseLockTable("Voucher")
  Endif
  modHelpVariable.CreateErrorReport()

End

Public Sub btnshow_Click()

  Dim sql As String

  If cmbpayer.Text Then
    If btnshow.Text = "Show" Then
      sql = "select fldid,fldtime,fldpayername,fldreference,flditemname,fldpayamount,fldpaytype,flduserid,fldcomp,fldtime from tblextrareceipt where fldsave=&1 and fldpayername=&2"                                           ''
      $rData = modDatabase.$myConn.Exec(sql, True, cmbpayer.Text)
      $aMyFields = New String[]
      modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
      ResizeGrid()
      btnshow.Text = "Close"
    Else If btnshow.Text = "Close" Then
      ShowPaymentGrid()
      btnshow.Text = "Show"
    Endif
  Endif

End

Public Sub btnexpo_Click()

  If cmbpayer.Text Then
    modCHTMLReport.ExportGridToHTML(GridView1, "PARTY NAME:" & cmbpayer.Text, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))
  Endif

End

Public Sub mnureprint_Click()

  Dim xx As String

  If GridView1.Rows.Selection.Count > 0 Then
    xx = GridView1[GridView1.Row, 3].Text
    If xx Then
      modVoucher.VoucherReceipt(xx, "Voucher Copy", GridView1[GridView1.Row, 2].Text, GridView1[GridView1.Row, 9].Text, GridView1[GridView1.Row, 7].Text)
    Endif
  Endif

End

Public Sub btnadd_GotFocus()

  btnadd.Font.Bold = True

End

Public Sub btnadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnadd_Click()
  Endif

End

Public Sub btnadd_LostFocus()

  btnadd.Font.Bold = False

End

Public Sub txtamount_GotFocus()

  txtamount.SelectAll

End

Public Sub btnselectpayer_Click()

  cmbpayer.Text = GridViewNew("Select Payer Name", xPayerList, False, cmbpayer, GridView1.Height)

End

Public Sub btnselectitem_Click()

  cmbitem.Text = GridViewNew("Select Item Name", xItemList, False, cmbitem, GridView1.Height)

End
