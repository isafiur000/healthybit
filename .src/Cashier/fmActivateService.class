' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $strType As String

Public Sub _new(sType As String)

  $strType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  Me.Tag = $strType & " Activation"
  ShowStockGrid()
  txtname.SetFocus

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Endif

End

Public Sub txtname_Change()

  ShowStockGrid()

End

Private Sub ShowStockGrid()

  Dim sql As String

  sql = "select flditemname,flditemunit,flditemcost,fldtarget,fldgroup,fldcode,fldstatus,fldbillitem,fldreport from tblservicecost where flditemtype=&1 and lower(flditemname) like &2 ORDER BY flditemname ASC"
  $rData = modDatabase.$myConn.Exec(sql, $strType, LCase(txtname.Text) & "%")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 275 * modBasic.$AppWidthRatio
    .Columns[1].Width = 50 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1
    .Columns[4].Width = 125 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[6].Width = 75 * modBasic.$AppWidthRatio
    .Columns[7].Width = 1
    .Columns[8].Width = 100 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Particulars"
    .Columns[1].Text = "Unit"
    .Columns[2].Text = "Rate"
    .Columns[4].Text = "RatePlan"
    .Columns[6].Text = "Status"
    .Columns[8].Text = "Section"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 2 Then
    GridView1.Data.Text = Format($rData[$aMyFields[Column]], gb.Fixed)
  Else If Column = 1 Then
    GridView1.Data.Text = GetCurrencyFormat($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Private Function GetCurrencyFormat(sCurr As String) As String

  Dim xval As String

  If sCurr Then
    xval = sCurr
  Else
    xval = modBasic.$currency
  Endif
  Return xval

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $strType, "Active Status")

End

Public Sub btnactivate_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit("tblservicecost", "flditemname=&1", GridView1[GridView1.Row, 0].Text)
    res["fldstatus"] = "Active"
    res.Update()
    ShowStockGrid()
    GridView1.Row = Row
  Endif

End

Public Sub btndeactive_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit("tblservicecost", "flditemname=&1", GridView1[GridView1.Row, 0].Text)
    res["fldstatus"] = "Inactive"
    res.Update()
    ShowStockGrid()
    GridView1.Row = Row
  Endif

End
