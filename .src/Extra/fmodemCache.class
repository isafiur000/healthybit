' Gambas class file

Private $SMSPathList As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbcategory.List = ["sent", "received"]
  ModemPlaySetting()

End

Private Sub ModemPlaySetting()

  If modGSM.$ModemEnabled = True Then
    btnmodemplay.Enabled = False
    btnmodemstop.Enabled = True
  Else
    btnmodemplay.Enabled = True
    btnmodemstop.Enabled = False
  Endif

End

Public Sub btnmodemplay_Click()

  modDevice.StartDefaultModem()
  ModemPlaySetting()

End

Public Sub btnmodemstop_Click()

  modGSM.StopDefaultModem()
  ModemPlaySetting()

End

Public Sub btnrefresh_Click()

  If modGSM.$ModemEnabled = True Then
    $SMSPathList = GetModemSMSPath(modGSM.$ModemIndex)
    If $SMSPathList Then
      LoadGridData($SMSPathList)
    Endif
  Endif

End

Private Sub LoadGridData(sList As String[])

  Dim Row As Integer
  Dim sLine As String
  Dim xval As String
  Dim asx As String[]
  Dim cSMS As CSMSText

  GridView1.Clear()
  GridView1.Columns.Count = 7
  GridView1.Rows.Count = sList.Count

  Row = 0
  For Each sLine In sList
    asx = Split(sLine, Space(1))
    GridView1[Row, 0].Text = Mid(asx[1], 2, Len(asx[1]) - 2)
    xval = GetSMSDetail(asx[0])
    cSMS = New CSMSText(xval)
    GridView1[Row, 1].Text = cSMS.ModemPhone()
    GridView1[Row, 2].Text = cSMS.ModemMessage()
    GridView1[Row, 3].Text = cSMS.ModemPUD()
    GridView1[Row, 4].Text = cSMS.Modemreference()
    GridView1[Row, 5].Text = cSMS.ModemDelivery()
    GridView1[Row, 6].Text = asx[0]
    Row = Row + 1
  Next

  With GridView1
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 300 * modBasic.$AppWidthRatio
    .Columns[3].Width = 120 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 150 * modBasic.$AppWidthRatio
    .Columns[6].Width = 1

    .Columns[0].Text = "Category"
    .Columns[1].Text = "Phone"
    .Columns[2].Text = "Message"
    .Columns[3].Text = "PUD Type"
    .Columns[4].Text = "Reference"
    .Columns[5].Text = "Delivery"
  End With

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If modGSM.$ModemEnabled = True Then
      DeleteSelectSMS(modGSM.$ModemIndex, GridView1[GridView1.Row, 6].Text)
      btnrefresh_Click()
    Endif
  Endif

End

''============================= Procedures --------------------------------
Private Function GetModemSMSPath(sIndex As String) As String[]

  Dim xx As String
  Dim yy As String
  Dim sLine As String[]
  Dim modPath As String[]
  Dim arcmd As String[]

  modPath = New String[]
  If System.Exist("mmcli") = True Then
    arcmd = ["mmcli", "-m", sIndex, "--messaging-list-sms"]
    modBasic.DebugString("Execute " & arcmd.Join(Space(1)))
    Exec arcmd To xx
    sLine = Split(xx, gb.NewLine)
    For Each yy In sLine
      If Left(Trim(yy), 1) = "/" Then
        modPath.Add(Trim(yy))
      Endif
    Next
  Else
    modApplSub.InstallSelectedApplication(["modemmanager"])
  Endif
  Return modPath

End

Private Function GetSMSDetail(sPath As String) As String

  Dim arcmd As String[]
  Dim xx As String

  If System.Exist("mmcli") = True Then
    arcmd = ["mmcli", "-s", sPath]
    modBasic.DebugString("Execute " & arcmd.Join(Space(1)))
    Exec arcmd To xx
  Else
    modApplSub.InstallSelectedApplication(["modemmanager"])
  Endif
  Return xx

End

Private Sub DeleteSelectSMS(sIndex As String, sPath As String)

  Dim arcmd As String[]

  If System.Exist("mmcli") = True Then
    arcmd = ["mmcli", "-m", sIndex, "--messaging-delete-sms='" & sPath & "'"]
    modBasic.DebugString("Execute " & arcmd.Join(Space(1)))
    Exec arcmd Wait
  Else
    modApplSub.InstallSelectedApplication(["modemmanager"])
  Endif

End
