' Gambas class file

Private $MasterColl As Collection

Public Sub Form_Open()

  Me.Center

End

Private Sub GetMasterCollection(sPath As String, xdelim As String)

  Dim hFile As File
  Dim sLine As String
  Dim sWord As String[]

  hFile = Open sPath
  For Each sLine In hFile.Lines
    If Len(Trim(sLine)) Then
      sWord = Split(sLine, xdelim)
      If sWord.Count = 2 Then
        $MasterColl.Add(sWord[1], sWord[0])
      Endif
    Endif
  Next

End

Public Sub txtmaster_Click()

  If Dialog.OpenFile() Then Return
  txtmaster.Text = Dialog.Path

End

Public Sub btnmaster_Click()

  $MasterColl = New Collection

  If Exist(txtmaster.Text) Then
    GetMasterCollection(txtmaster.Text, txtdelimmastr.Text)
    Balloon.Info(("Information saved"), btnmaster)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtsource_Click()

  If Dialog.OpenFile() Then Return
  txtsource.Text = Dialog.Path

End

Public Sub btnsource_Click()

  Dim hCSV As CsvFile

  If Exist(txtsource.Text) Then
    hCSV = New CsvFile(txtsource.Text, txtdelimsource.Text)
    cmbcolumn.List = hCSV.Columns
    Balloon.Info(("Information saved"), btnsource)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnimport_Click()

  Dim hCSV As CsvFile
  Dim xcoll As Collection
  Dim xxx As String[]

  Dim csvPath As String
  Dim Colm As String
  Dim astr As String[]

  If Exist(txtmaster.Text) Then
    If Exist(txtsource.Text) And If cmbcolumn.Text Then
      If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
        csvPath = User.Home &/ File.BaseName(txtsource.Text) & "_1.csv"

        xxx = New String[]
        xxx.Add(cmbcolumn.List.Join(Trim(txtdelimsource.Text)))

        hCSV = New CsvFile(txtsource.Text, Trim(txtdelimsource.Text))
        While hCSV.Eof = False
          xcoll = hCSV.Read()

          astr = New String[]
          For Each Colm In cmbcolumn.List
            If Colm = cmbcolumn.Text Then
              astr.Add($MasterColl[xcoll[Colm]])
            Else
              astr.Add(xcoll[Colm])
            Endif
          Next
          xxx.Add(astr.Join(Trim(txtdelimsource.Text)))

        Wend
        hCSV.Close()
        File.Save(csvPath, xxx.Join(gb.NewLine))
        Message.Info("File is " & csvPath, "OK")

      Endif
    Endif
  Endif

End
