' Gambas class file

Private $virtualTerminal As String

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbkey.List = ["Local", "Remote"]

End

Public Sub txtfile_Click()

  If Dialog.OpenFile() Then Return
  txtfile.Text = Dialog.Path
  TextArea1.Text = ""

End

Public Sub btncheck_Click()

  Dim res As Result
  Dim extmd5 As String
  Dim xx As String

  Dim sval As String
  Dim aFile As String
  Dim tempFile As String
  Dim hFile As Blob
  Dim xpdf As String
  Dim arcmd As String[]

  TextArea1.Text = ""
  If txtfile.Text Then
    extmd5 = modApplication.GetMD5SumFile(txtfile.Text)
    res = modDatabase.$myConn.Exec("select fldfile,fldlink,flduserid,fldtype,fldwindow,fldtime,fldhostmac,fldhostuser,fldhostip,fldhostname,fldcomp from tblreportlog where fldcomment=&1", extmd5)                                                                   ''
    If res.Available = False Then
      Message.Warning(("No log record found"), ("OK"))
    Else
      For Each res
        tempFile = Temp() & ".pdf"
        If res!fldlink Then
          Inc Application.Busy
          aFile = modFTPSub.GetFileFromLocalFTP(res!fldlink)
          If Exist(aFile) Then
            Copy aFile To tempFile
          Endif
          Dec Application.Busy
        Else
          hFile = res["fldfile"]
          If hFile.Length Then
            File.Save(tempFile, hFile.Data)
          Endif
        Endif

        If modGlobalSetting.ShowSettingFromDBAny("GeneralSettings/PDFEncrypt", res!fldcomp) = "Yes" Then
          xpdf = Temp() & ".pdf"
          modDevAll.GetDecryptPDF(tempFile, xpdf, modHelpVariable.$CryptPDF)
        Else
          xpdf = tempFile
        Endif

        If System.Exist("comparepdf") = True Then

          arcmd = ["comparepdf", xpdf, txtfile.Text]
          modBasic.DebugString("Execute " & arcmd.Join(Space(1)))
          Exec arcmd To sval
          If sval Then
          Else
            xx = ""
            xx = xx & "Generated by: " & res!flduserid & gb.NewLine
            xx = xx & "Generated as: " & res!fldtype & gb.NewLine
            xx = xx & "Generated from: " & res!fldwindow & gb.NewLine
            xx = xx & "Generation time: " & res!fldtime & gb.NewLine
            xx = xx & "MAC Address: " & res!fldhostmac & gb.NewLine
            xx = xx & "IP Address: " & res!fldhostip & gb.NewLine
            xx = xx & "Host User: " & res!fldhostuser & gb.NewLine
            xx = xx & "Host Name: " & res!fldhostname & gb.NewLine
            xx = xx & "Comp ID: " & res!fldcomp & gb.NewLine
            TextArea1.Text = xx
          Endif

        Else
          modApplSub.InstallSelectedApplication(["comparepdf"])
        Endif
      Next
    Endif
  Endif

End

Public Sub btnchecksig_Click()

  TextArea1.Text = ""
  VerifyGnuPgKey(txtfile.Text, cmbkey.Text)
  TextArea1.Text = $virtualTerminal

End

Public Sub cmbkey_Click()

  If cmbkey.Text = "Local" Then
    lblkey.Text = modSettings.ShowSettingFromFIle("GeneralSettings/GnuPG_KeyID")
  Else If cmbkey.Text = "Remote" Then
    lblkey.Text = modSettings.ShowSettingFromFIle("Report" & "/PDF_GnuPGLink")
  Endif

End

''--------------------- GNUPG-----------------------------------
Private Sub VerifyGnuPgKey(sInFile As String, sType As String)

  Dim res As Result
  Dim prikey As String
  Dim keyname As String
  Dim keyid As String
  Dim delkey As String
  Dim hFile As Blob
  Dim aFile As String

  Dim arcmd As String[]
  Dim brcmd As String[]
  Dim ycmd As String

  $virtualTerminal = ""
  If Exist("/tmp/.gnupg") Then
    modExternal.DeleteFolderRecursive("/tmp/.gnupg")
  Endif

  If sType = "Remote" Then

    keyid = modSettings.ShowSettingFromFIle("Report" & "/PDF_GnuPGLink")
    If keyid Then
      keyname = modSettings.ShowSettingFromFIle("Report" & "/PDF_GnuPGName")
      Mkdir "/tmp/.gnupg"
      modDevAll.GetWgetFileToSelFolder(keyid, "/tmp/.gnupg")
      prikey = "/tmp/.gnupg" &/ File.Name(keyid)
    Endif

  Else

    keyid = modSettings.ShowSettingFromFIle("GeneralSettings/GnuPG_KeyID")
    If keyid Then
      res = modDatabase.$myConn.Exec("select fldkeyname,fldpublic,fldpublink from tblgnupg where fldkeyid=&1", keyid)
      If res.Available Then
        keyname = res!fldkeyname
        Mkdir "/tmp/.gnupg"
        res.MoveFirst
        prikey = Temp() & ".gpg"
        If res!fldpublink Then
          Inc Application.Busy
          aFile = modFTPSub.GetFileFromLocalFTP(res!fldpublink)
          If Exist(aFile) Then
            Copy aFile To prikey
          Endif
          Dec Application.Busy
        Else
          hFile = res["fldpublic"]
          If hFile.Length Then
            File.Save(prikey, hFile.Data)
          Endif
        Endif
      Endif
    Endif

  Endif

  If Exist(prikey) Then
    arcmd = ["gpg", "--homedir", "/tmp/.gnupg", "--import", prikey]
    modBasic.DebugString("Execute " & arcmd.Join(Space(1)))
    Exec arcmd Wait

    brcmd = ["gpg", "--homedir", "/tmp/.gnupg", "--verify", sInFile]
    modBasic.DebugString("Execute " & brcmd.Join(Space(1)))
    Exec brcmd Wait For Input As "VirtOutput"

    delkey = "\"" & keyname & "\""
    ycmd = "gpg --homedir /tmp/.gnupg --batch --yes --quiet --delete-keys " & delkey
    modBasic.DebugString("Execute " & ycmd)
    Shell ycmd Wait

    modExternal.DeleteFolderRecursive("/tmp/.gnupg")
  Endif

End

Public Sub VirtOutput_Read()

  Dim sLine As String

  Read #Last, sLine, -256
  $virtualTerminal = $virtualTerminal & sLine

End
