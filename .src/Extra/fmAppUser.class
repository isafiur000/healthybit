' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $rData1 As Result
Private $aMyFields1 As String[]

Private $rData2 As Result
Private $aMyFields2 As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

  txtcomp.List = modBasic.$AllCompList
  txtdate.Value = Date()
  txtuser.Text = modBasic.$lbluser
  txtcomp.Text = modBasic.$compID
  If modHelpVariable.$LogInCategory = "Record" Then
    txtuser.ReadOnly = False
  Endif
  ActivityByDay()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
    ActivityByDay()
  Else If TabPanel1.Index = 1 Then
    ReportLog()
  Else If TabPanel1.Index = 2 Then
    SMSLogReport
  Endif

End

Private Sub ActivityByDay()

  Dim sql As String

  sql = "select fldcomp,fldfrmlabel,fldentrytime,fldexittime,fldpresent from tbljobrecord where flduser=&1 and fldentrytime>=&2 and fldentrytime<=&3 and fldcomp=&4"                                                  ''
  $rData = modDatabase.$syConn.Exec(sql, txtuser.Text, modDate.StartSqlDate(txtdate.Value), modDate.EndSqlDate(txtdate.Value), txtcomp.text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 250 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 25
    .Columns[1].Text = "Window"
    .Columns[2].Text = "Entry"
    .Columns[3].Text = "Exit"
  End With

  txteltime.Value = Round(Timer() / 60, -2)
  txtcpu.Text = modApplication.GetPercentCPUUsage(Application.Handle)

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 2 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldentrytime"], gb.LongTime)
  Else If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldexittime"], gb.LongTime)
  Else If Column = 4 Then
    GridView1.Data.Picture = Picture[modMisc.GetGridIcon($rData["fldpresent"])]
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnrefresh_Click()

  ActivityByDay()

End

Public Sub Button1_Click()

  If TabPanel1.Index = 0 Then
    modCHTMLReport.ExportGridToHTML(GridView1, "Activity Log", "User: " & txtuser.Text & "  Comp: " & txtcomp.Text & "  Date: " & modReportVar.GetDateTimeReport(txtdate.Value, gb.MediumDate))
  Else If TabPanel1.Index = 1 Then
    modCHTMLReport.ExportGridToHTML(GridView2, "Report Log", "User: " & txtuser.Text & "  Comp: " & txtcomp.Text & "  Date: " & modReportVar.GetDateTimeReport(txtdate.Value, gb.MediumDate))
  Endif

End

Private Sub ReportLog()

  Dim sql As String

  sql = "select fldcomp,fldtype,fldwindow,fldcomment,fldid from tblreportlog where flduserid=&1 and fldtime>=&2 and fldtime<=&3 and fldcomp=&4"                                                  ''
  $rData1 = modDatabase.$syConn.Exec(sql, txtuser.Text, modDate.StartSqlDate(txtdate.Value), modDate.EndSqlDate(txtdate.Value), txtcomp.text)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)
  With GridView2
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 140 * modBasic.$AppWidthRatio
    .Columns[3].Width = 250 * modBasic.$AppWidthRatio
    .Columns[4].Width = 1
    .Columns[1].Text = "Type"
    .Columns[2].Text = "Window"
    .Columns[3].Text = "Comment"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData1.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  GridView2.Data.Text = $rData1[$aMyFields1[Column]]

End

Private Sub SMSLogReport()

  Dim sql As String

  sql = "select fldcomp,fldtarget,fldwindow,fldmesstext,fldid from tblmessagelog where flduserid=&1 and fldtime>=&2 and fldtime<=&3 and fldcomp=&4"                                                  ''
  $rData2 = modDatabase.$syConn.Exec(sql, txtuser.Text, modDate.StartSqlDate(txtdate.Value), modDate.EndSqlDate(txtdate.Value), txtcomp.text)
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView3, $rData2, $aMyFields2)
  With GridView3
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 140 * modBasic.$AppWidthRatio
    .Columns[3].Width = 240 * modBasic.$AppWidthRatio
    .Columns[4].Width = 1
    .Columns[1].Text = "Target"
    .Columns[2].Text = "Category"
    .Columns[3].Text = "Message"
  End With

End

Public Sub GridView3_Data(Row As Integer, Column As Integer)

  $rData2.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView3, Row)
  GridView3.Data.Text = $rData2[$aMyFields2[Column]]

End

Public Sub GridView2_Menu()

  If modHelpVariable.$LogInCategory = "Record" Then
    mnuhidereport.Popup
  Endif

End

Public Sub mnuopenreport_Click()

  Dim res As Result
  Dim tempFile As String
  Dim hFile As Blob
  Dim xpdf As String

  If GridView1.Rows.Selection.Count > 0 Then
    res = modDatabase.$syConn.Exec("select fldfile,fldlink,fldcomp from tblreportlog where fldid=&1", GridView2[GridView2.Row, 4].Text)
    If res!fldlink Then
      Inc Application.Busy
      tempFile = modFTPSub.GetFileFromLocalFTP(res!fldlink)
      Dec Application.Busy
    Else
      hFile = res["fldfile"]
      If hFile.Length Then
        tempFile = Temp()
        File.Save(tempFile, hFile.Data)
      Endif
    Endif

    If modGlobalSetting.ShowSettingFromDBAny("GeneralSettings/PDFEncrypt", res!fldcomp) = "Yes" Then
      xpdf = Temp() & ".pdf"
      modDevAll.GetDecryptPDF(tempFile, xpdf, modHelpVariable.$CryptPDF)
    Else
      xpdf = tempFile
    Endif

    modDevice.DesktopOpenFile(xpdf)
  Endif

End
