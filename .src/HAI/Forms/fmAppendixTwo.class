' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $Manual As Boolean

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False", False)
  cmbward.List = modBasic.$IPDDepartmentsAll
  cmbweightrange.List = ["< 750 Grams", "751-1000 Grams", "1001-1500 Grams", "1501-2500 Grams", "> 2500 Grams"]

  rbnew.Value = True
  chksearch.Value = True
  dtsearch.Value = Date()
  dtcurrent.Value = Date()
  tmcurrent.Value = Time()

  If modBasic.$SuperUser = True Then
    btndelete.Visible = True
  Endif

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtcurrent.Value))
  If xx Then
    dtcurrent.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub chksearch_Click()

  btnsearch.Enabled = chksearch.Value

End

Public Sub cmbward_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbward)
  modFillContainer.RestrictComboToContent(cmbward)

End

Public Sub cmbweightrange_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbweightrange)
  modFillContainer.RestrictComboToContent(cmbweightrange)

End

Private Sub GetBlankData()

  cmbweightrange.Text = ""
  txtcentral.Value = 0
  txtperiphers.Value = 0
  txturinary.Value = 0
  txtall.Value = 0

End

Public Sub btnsearch_Click()

  If cmbward.Text Then
    ShowDataInputDate(dtsearch.Value)
    btnupload.Enabled = True
  Endif

End

Public Sub btnrefresh_Click()

  If cmbward.Text Then
    ShowDataInputDate(dtcurrent.Value)
    btnupload.Enabled = False
  Endif

End

Private Sub ShowDataInputDate(dtdate As Date)

  Dim sql As String
  Dim xstr As String

  If rbnew.Value = True Then
    xstr = db.Subst(" and dhis=&1", False)
  Else If rbsaved.Value = True Then
    xstr = db.Subst(" and dhis=&1", True)
  Else
    xstr = ""
  Endif

  If chksearch.Value = True Then
    sql = "select fldid,fldward,fldweight,flddate,flddate,fldcentral,fldperipheral,fldurinary,fldtotal,fldsave from tblhaidenominators where fldward=&1 and fldweight<>&2 and flddate>=&3 and flddate<=&4" & xstr
  Else
    sql = "select fldid,fldward,fldweight,flddate,flddate,fldcentral,fldperipheral,fldurinary,fldtotal,fldsave from tblhaidenominators where fldward=&1 and fldweight<>&2" & xstr
  Endif
  $rData = modDatabase.$myConn.Exec(sql, cmbward.Text, "Adult", modDate.StartSqlDate(dtdate), modDate.EndSqlDate(dtdate))
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 125 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 75 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 75 * modBasic.$AppWidthRatio
    .Columns[9].Width = 50 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Ward"
    .Columns[2].Text = "Weight"
    .Columns[3].Text = "Date"
    .Columns[4].Text = "Time"
    .Columns[5].Text = "Central"
    .Columns[6].Text = "Peripheral"
    .Columns[7].Text = "Urinary"
    .Columns[8].Text = "Total"
    .Columns[9].Text = "Manual"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else If Column = 4 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumTime)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If cmbward.Text Then
    If cmbweightrange.Text And If txtall.Value Then
      res = modDatabase.$myConn.Create("tblhaidenominators")
      res["flddate"] = Date(Year(dtcurrent.Value), Month(dtcurrent.Value), Day(dtcurrent.Value), Hour(tmcurrent.Value), Minute(tmcurrent.Value), Second(tmcurrent.Value))
      res["fldward"] = cmbward.Text
      res["fldweight"] = cmbweightrange.Text
      res["fldcentral"] = txtcentral.Value
      res["fldperipheral"] = txtperiphers.Value
      res["fldurinary"] = txturinary.Value
      res["fldtotal"] = txtall.Value
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldsave"] = $Manual
      res["dhis"] = False
      res.Update
      Balloon.Info(("Information saved"), btnadd)
      Balloon.Delay = modBasic.$BalloonDelay
      GetBlankData()
      ShowDataInputDate(dtcurrent.Value)
    Endif
  Endif

End

Public Sub btndelete_Click()

  If GridView1.Rows.Selection.Count Then
    If Message.Question("Are you sure ?", ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblhaidenominators", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      ShowDataInputDate(dtcurrent.Value)
    Endif
  Endif

End

''==================== Calculations =======================
Private Function GetWeightRange(sType As String) As Float[]

  Dim aLst As Float[]

  If sType = "< 750 Grams" Then
    aLst = [0, 750]
  Else If sType = "751-1000 Grams" Then
    aLst = [751, 1000]
  Else If sType = "1001-1500 Grams" Then
    aLst = [1001, 1500]
  Else If sType = "1501-2500 Grams" Then
    aLst = [1501, 2500]
  Else If sType = "> 2500 Grams" Then
    aLst = [2500, 6000]
  Endif

  Return aLst

End

Public Sub btncalc_Click()

  If cmbward.Text Then
    txtcentral.Value = GetDeviceUserNumber("Central Line Catheter")
    txtperiphers.Value = GetDeviceUserNumber("Peripheral Line Catheter")
    txturinary.Value = GetDeviceUserNumber("Urinary Catheter")
    txtall.Value = GetWardPatientNumber()
    $Manual = False
  Endif

End

Private Function GetDeviceUserNumber(sCategory As String) As Integer

  Dim sql As String
  Dim res As Result
  Dim xval As Integer
  Dim xdate As Date
  Dim xfir As Date
  Dim xlast As Date
  Dim aWtLst As Float[]

  aWtLst = GetWeightRange(cmbweightrange.Text)
  xdate = Date(Year(dtcurrent.Value), Month(dtcurrent.Value), Day(dtcurrent.Value), Hour(tmcurrent.Value), Minute(tmcurrent.Value), Second(tmcurrent.Value))
  xfir = DateAdd(xdate, gb.Minute, -5)
  xlast = DateAdd(xdate, gb.Minute, 5)
  sql = "select count(fldencounterval) as xcnt from tbldepartmentbed where flddept like &1 and fldencounterval in(select fldencounterval from tblpatevents where fldtype=&2 and fldfirsttime<&3 and (fldsecondtime>&4 or fldsecondtime IS NULL) and flditem in(select flditem from tblevents where fldcategory like &5)) and fldencounterval in(select fldencounterval from tblencounter where fldpatientval in(select fldbabypatno from tblconfinement where flddelwt>=&6 and flddelwt<=&7))"                     ''
  res = modDatabase.$myConn.Exec(sql, cmbward.Text, "Devices", xfir, xlast, sCategory, aWtLst[0], aWtLst[1])
  If res.Available Then
    If res["xcnt"] Then
      xval = res["xcnt"]
    Else
      xval = 0
    Endif
  Else
    xval = 0
  Endif

  Return xval

End

Private Function GetWardPatientNumber() As Integer

  Dim sql As String
  Dim res As Result
  Dim xval As Integer
  Dim aWtLst As Float[]

  aWtLst = GetWeightRange(cmbweightrange.Text)
  sql = "select count(fldencounterval) as xcnt from tbldepartmentbed where flddept like &1 and fldencounterval in(select fldencounterval from tblencounter where fldpatientval in(select fldbabypatno from tblconfinement where flddelwt>=&2 and flddelwt<=&3))"
  res = modDatabase.$myConn.Exec(sql, cmbward.Text, aWtLst[0], aWtLst[1])
  If res.Available Then
    If res["xcnt"] Then
      xval = res["xcnt"]
    Else
      xval = 0
    Endif
  Else
    xval = 0
  Endif

  Return xval

End

Public Sub txtcentral_Change()

  $Manual = True

End

Public Sub txtperiphers_Change()

  $Manual = True

End

Public Sub txturinary_Change()

  $Manual = True

End

Public Sub txtall_Change()

  $Manual = True

End

Public Sub btnupload_Click()

  Dim xDepart As String

  If cmbward.Text Then
    If rbnew.Value = True Then
      xDepart = modGeneral.GetDepartCaption(GridView1[0, 1].Text)
      If xDepart Then
        UploadSelectedHAI("Denominators:Newborn", GridView1[0, 1].Text, xDepart)
      Endif
      ShowDataInputDate(dtsearch.Value)
    Endif
  Endif

End

Private Function UploadSelectedHAI(sChapter As String, xDepart As String, sDepart As String) As Variant[]

  Dim res As Result
  Dim refld As ResultField
  Dim xWtCode As String
  Dim aVar As Variant[]
  Dim xArr As String[]
  Dim aColl As Collection

  Dim xVar As Variant[]
  Dim hCApi As CFindAPI
  Dim xdate As Date
  Dim xmsg As String
  Dim res1 As Result
  Dim aIndex As Long[]
  Dim xid As Long

  aIndex = New Long[]
  xVar = New Variant[]
  aVar = modHAITags.GetHAITags(sChapter)
  res = modDatabase.$myConn.Exec("select fldid,fldward,fldweight,flddate,fldcentral,fldperipheral,fldurinary,fldtotal,fldsave from tblhaidenominators where fldward=&1 and fldweight<>&2 and flddate>=&3 and flddate<=&4 and dhis=&5", xDepart, "Adult", modDate.StartSqlDate(dtsearch.value), modDate.EndSqlDate(dtsearch.value), False)
  If res.Available Then
    res.MoveFirst

    ''title values
    xdate = res["flddate"]
    For Each refld In res.Fields
      For Each xArr In aVar
        If refld.Name = xArr[0] Then

          If refld.Name = "fldward" Or If refld.Name = "flddate" Or If refld.Name = "fldsave" Then
            aColl = New Collection
            aColl.Add(xArr[0], "_comment")
            aColl.Add(xArr[2], "dataElement")
            If xArr[1] = "Time" Then
              aColl.Add(Format(res[refld.Name], "hh:nn"), "value")
            Else If xArr[1] = "Department" Then
              aColl.Add(sDepart, "value")
            Else
              aColl.Add(res[refld.Name], "value")
            Endif
            xVar.Add(aColl)
          Endif

        Endif
      Next
    Next

    ''integer values for each rows
    For Each res
      aIndex.Add(res["fldid"])
      xWtCode = modHAITags.GetHAIBirthWeightCode(res["fldweight"])
      For Each refld In res.Fields
        For Each xArr In aVar
          If refld.Name = xArr[0] Then

            If refld.Name = "fldcentral" Or If refld.Name = "fldperipheral" Or If refld.Name = "fldurinary" Or If refld.Name = "fldtotal" Then
              aColl = New Collection
              aColl.Add(xArr[0], "_comment")
              aColl.Add(xArr[2], "dataElement")
              aColl.Add(xWtCode, "categoryOptionCombo")
              aColl.Add(res[refld.Name], "value")
              xVar.Add(aColl)
            Endif

          Endif
        Next
      Next
    Next

    Inc Application.Busy
    hCApi = New CFindAPI(sChapter, xVar, Format(xdate, "yyyymmdd"))
    xmsg = hCApi.GetAPIOutput()
    Dec Application.Busy

    If xmsg = "SUCCESS" Then
      For Each xid In aIndex
        res1 = modDatabase.$myConn.Edit("tblhaidenominators", "fldid=&1", xid)
        res1["dhis"] = True
        res1.Update
      Next
      Balloon.Info("Upload completed", btnupload)
      Balloon.Delay = modBasic.$BalloonDelay
    Else
      Message.Warning(xmsg)
    Endif
  Endif

End
