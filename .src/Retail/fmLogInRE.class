' Gambas class file

Public Sub Form_Open()

  Me.Center
  ToolButton1.Text = Application.Title & " Homepage"
  If MMain.$GovtApp = "Yes" Then
    PictureBox1.Picture = Image.Load("Nepal.png").Stretch(PictureBox1.Width, PictureBox1.Height).Picture
  Else
    PictureBox1.Picture = Image.Load("logo.png").Stretch(PictureBox1.Width, PictureBox1.Height).Picture
  Endif
  If Not Exist(modHelpVariable.$SysConfigPath) Then
    modHelpVariable.$FIrstTimeSetting = True
    fmSetSqlite.Show
    fmLogIn.Close
  Else
    If Desktop.NetworkAvailable = True Then
      ToolButton1.Enabled = True
      ToolButton1.Tag = modControlSub.ShowSettingValue(modHelpVariable.$appWebconfFile, "Remote/HomaPage")
    Endif

    LoadCategor()
    cmbusname.Text = ""
    GetConnType()
    If modSettings.ShowSettingFromFIle("GeneralSettings/EnableTabletMode") = "Yes" Then
      modExternal.RunKeyBoardScript()
    Endif

    cmbusname.SetFocus
  Endif

End

Private Sub DefaultLog()

  Dim def As String

  def = modSettings.ShowLogValues("LogIn/Default")
  If def = optadmin.Tag Then
    optadmin.Value = True
  Else If def = optphm.Tag Then
    optphm.Value = True
  Endif

End

Private Sub LoadCategor()

  cmbusname.Text = ""
  cmbusname.Clear

  If optadmin.Value = True Then
    modHelpVariable.$LogInGroup = optadmin.Tag
    cmbusname.List = ["Medical", "Record"]
    'cmbusname.add ("Admin")

  Else If optphm.Value = True Then
    modHelpVariable.$LogInGroup = optphm.Tag
    cmbusname.List = ["Pharmacist", "Dispensar"]
  Endif

  DefaultLog()
  cmbusname.SetFocus

End

Public Sub optadmin_Click()

  modSettings.SaveValuesToLog("LogIn/Default", optadmin.Tag)
  LoadCategor()

End

Public Sub optphm_Click()

  modSettings.SaveValuesToLog("LogIn/Default", optphm.Tag)
  LoadCategor()

End

Public Sub cmbusname_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbusname)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub ToolButton1_Click()

  Desktop.Open(ToolButton1.Tag)

End

Public Sub tlbtnadd_Click()

  fmSplash.Close
  fmSplash.ShowModal

End

Public Sub btnOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnOK_Click()
  Endif

End

Public Sub btnOK_GotFocus()

  btnOK.Font.Bold = True

End

Public Sub btnOK_LostFocus()

  btnOK.Font.Bold = False

End

Public Sub btnOK_Click()

  Dim res As Result
  Dim rs1 As Result
  Dim res2 As Result
  Dim xgo As Boolean

  modExternal.KillKeyBoardScript()
  Wait
  modBasic.$lbluser = Trim(txtuser.Text)
  modHelpVariable.$LogInCategory = cmbusname.Text
  modHelpVariable.CreateUserCacheDir(modBasic.$lbluser)

  modHelpVariable.ConnectionMain()
  modGeneralMain.EnableCollectionCache()
  modTableCache.InitializeTableCache()
  If modHelpVariable.$DownloadAdvertise = "No" Then
  Else
    modScript.GetAdvertiseData()
  Endif
  If modDatabase.$myConn.Opened = True Then

    xgo = modDBApp.DateTimeDiffChecking()
    If xgo = True Then

      modHelpVariable.$MACAddress = modApplication.GetMACAddress()
      modHelpVariable.$localIP = modApplication.GetLocalIP()
      If modPassword.AloowMacAddressCheck(modHelpVariable.$MACAddress) = True Then
        res2 = modDatabase.$myConn.Exec("select fldcategory,flduserid,fldpass,fldusercode,fldfromdate,fldtodate from tbluser where flduserid=&1 and fldpass=&2 and fldstatus=&3", Trim(txtuser.text), modPassword.EncryptSimple(txtpass.Text), "Active")
        If res2.Available = False Then
          modDevice.CamOnError()
          Message.Warning(("Incorrect User or Password"), ("OK"))
          txtpass.SetFocus()
        Else
          modBasic.$lbluser = res2["flduserid"]
          modHelpVariable.$LogInTime = Now()

          If modPassword.LogInActiveDuration(res2["fldfromdate"], res2["fldtodate"]) Then
            MMain.InitialAppMode()

            rs1 = modDatabase.$myConn.Exec("select fldid,fldcomp from tblcompaccess where flduserid=&1 and fldcomp=&2", Trim(txtuser.text), modBasic.$compID)
            If rs1.Available = False Then
              Message.Warning(("Access to this Computer not granted"), ("OK"))
            Else
              rs1.MoveFirst
              modBasic.$compID = rs1["fldcomp"]

              res = modDatabase.$myConn.Exec("select fldaccess from tbluseraccess where flduserid=&1 and fldaccess=&2", Trim(txtuser.text), Base64(cmbusname.Text))
              If res.Available = False Then
                Message.Warning(("Access not granted for one/all department"), ("OK"))
              Else If res.Available = True Then
                res.MoveFirst
                modBasic.DebugString("Log In User is " & Trim(txtuser.Text) & "@" & cmbusname.Text)

                If optadmin.Value = True And If cmbusname.Text = "Admin" Then
                  modHelpVariable.$ApplKey = 0
                Else
                  modHelpVariable.$ApplKey = modPassword.ValidateKey()
                Endif
                If modHelpVariable.$ApplKey = 3 Then
                  Return
                Else

                  If optadmin.Value = True Then
                    If cmbusname.Text = "Record" Then
                      fmRecord.Show
                    Else If cmbusname.Text = "Admin" Then
                      fmAdmin.Show
                    Endif

                  Else If optphm.Value = True Then
                    If cmbusname.Text = "Pharmacist" Then
                      fmAccountRE.Show
                    Else If cmbusname.Text = "Dispensar" Then
                      fmDispensarRE.Show
                    Endif
                  Endif
                  Try Me.Close

                Endif
              Endif

            Endif

          Else
            Message.Warning(("Account expired"), ("OK"))
          Endif

        Endif
      Endif

    Else
      If Message.Question(("Client Date Time different from Server. Do you want to reconfigure?"), ("No"), ("Yes")) = 2 Then
        modApplSub.ConfigureDateTime()
      Endif
    Endif

  Else
    Message.Warning(("Unable to connect to specified host"), ("OK"))
  Endif

End

Public Sub btnChange_Click()

  Dim hForm As FmChangeText
  Dim sql As String
  Dim res As Result

  modHelpVariable.ConnectionMain()

  sql = "select fldaccess from tbluseraccess where flduserid in(select flduserid from tbluser where flduserid=&1 and fldpass=&2)"
  res = modDatabase.$myConn.Exec(sql, Trim(txtuser.text), modPassword.EncryptSimple(txtpass.Text))
  If res.Available = False Then
    Message.Warning(("UserID or Password incorrect"), ("OK"))
  Else If res.Available = True Then
    hForm = New FmChangeText("Change Password", txtuser.Text, txtpass.text, "tbluser", "flduserid", "fldpass")
    hForm.ShowModal
  Endif

End

Public Sub btnCancel_Click()

  modDatabase.CloseDatabase()
  Me.Delete

End

Public Sub tlbtnadd2_Click()

  If rbremote.Value = True Then
    fmRemoteDB.Close
    fmRemoteDB.ShowModal
  Endif

End

Public Sub rbremote_Click()

  modSettings.SaveSettingsToFile("SISH_Connection/ConnectionType", "Remote")

End

Public Sub rblocal_Click()

  modSettings.SaveSettingsToFile("SISH_Connection/ConnectionType", "Local")

End

Private Sub GetConnType()

  Dim xtype As String

  xtype = modSettings.ShowSettingFromFIle("SISH_Connection/ConnectionType")
  If xtype = "Remote" Then
    rbremote.Value = True
  Else
    rblocal.Value = True
  Endif

End

Public Sub Form_Close()

  Me.Delete

End

Public Sub btnexec_Click()

  If btnexec.Value = True Then
    modExternal.RunKeyBoardScript()
  Else
    modExternal.KillKeyBoardScript()
  Endif

End

Public Sub Timer1_Timer()

  Dim xval As Boolean

  xval = modApplication.GetIsCapsLock()
  If xval = True Then
    txtpass.Background = Color.LightGray
  Else
    txtpass.Background = Color.White
  Endif

End

Public Sub btnremote_Click()

  modScript.GetDataRepositoryUploader()
  modScript.GetImgRepositoryUploader()
  ' If System.Exist("tmate") = False Then
  '   If UCase(modApplication.DistributionOSName()) = "UBUNTU" Then
  '     modApplSub.InstallSelectedApplication(["tmate"])
  '   Endif
  ' Else
  '   fmTmate.ShowModal
  ' Endif

End
