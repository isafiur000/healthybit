' Gambas module file

Public Sub CREATEtblradioque($con As Connection)

  Dim hTable As Table

  If Not $con.Tables.Exist("tblradioque") Then
    hTable = $con.Tables.Add("tblradioque", modDBTables.$DbEngine)
    hTable.Fields.Add("fldid", db.Serial)
    hTable.Fields.Add("flddate", db.Date)
    hTable.Fields.Add("flddepart", db.String, 100)
    hTable.Fields.Add("fldque", db.Integer)
    hTable.PrimaryKey = ["fldid"]
    hTable.Update
    modDBApp.AlterMySQLSpecCollationTable($con, hTable.Name)
  Endif

End

Public Sub AddRadiologyQues(sEnd As Date)

  Dim sCateg As String[]
  Dim xcateg As String
  Dim dtLst As Date[]
  Dim xdate As Date

  Dim res As Result
  Dim res1 As Result

  dtLst = modDate.GetSelectDateArrayBetween(gb.Year, Now(), sEnd)
  sCateg = modMedicine.GetPathoCategoryList("Radio")
  For Each xcateg In sCateg
    For Each xdate In dtLst
      res1 = modDatabase.$myConn.Exec("select fldque from tblradioque where Date(flddate)=&1 and flddepart=&2", modDate.StartSqlDate(xdate), xcateg)
      If res1.Available Then
      Else
        res = modDatabase.$myConn.Create("tblradioque")
        res["flddate"] = modDate.StartSqlDate(xdate)
        res["flddepart"] = xcateg
        res["fldque"] = 1
        res.Update
      Endif
    Next
  Next

  Message.Info("Completed")

End

Public Function GetRadiologyQueNo(sDate As Date, sDepart As String) As Integer

  Dim res As Result
  Dim xx As Long

  res = modDatabase.$myConn.Edit("tblradioque", "flddate>=&1 and flddate<=&2 and flddepart=&3", modDate.StartSqlYear(sDate), modDate.EndSqlYear(sDate), sDepart)
  xx = res["fldque"]
  res["fldque"] = xx + 1
  res.Update()
  Return xx
  'donot catch error. Error will rollback

End

Public Function GetRadiologyInvoiceQueNo(encid As String, sDepart As String, sInvoice As String) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldsampleid from tblpatradiotest where fldencounterval=&1 and fldtestid in(select fldexamid from tblradio where fldcategory=&2) and fldbillno=&3", encid, sDepart, sInvoice)
  If res.Available Then
    res.MoveLast
    If res["fldsampleid"] Then
      xx = res["fldsampleid"]
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif

  Return xx

End
