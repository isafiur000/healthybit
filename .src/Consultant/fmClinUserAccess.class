' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbtarget.List = modBasic.$AllCompPerList

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub btnconsult_Click()

  Dim xxx As String[]
  Dim xVar As Variant[]
  Dim xval As String
  Dim xMedUser As String[]

  xVar = New Variant[]
  xxx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(flduserid) as col from tbluserformaccess where fldcategory=&1", "Clinician"))                                            ''
  If xxx Then
    xxx.Sort()
    For Each xval In xxx
      xVar.Add(xval, modGeneral.GetUserFullName(xval))
    Next
  Endif
  xMedUser = MedicalSelectedValue(("Select Application User"), xVar)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Endif

End

Public Sub btnrefresh_Click()

  If btnconsult.Tag Then
    SHowAccGrid(btnconsult.Tag)
  Endif

End

Public Sub btnselcomponent_Click()

  Dim xx As String[]

  xx = SelectListView(("Select Components to Disable"), modMedicine.CompleteAccessList(), False)
  If xx Then
    ListBox1.List = xx
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result
  Dim xx As String

  If btnconsult.Tag And If cmbtarget.Text Then
    If ListBox1.Count > 0 Then
      For Each xx In ListBox1.List
        res = modDatabase.$myConn.Create("tblclinicentry")
        res["flduserid"] = btnconsult.Tag
        res["fldaccess"] = xx
        res["fldcomp"] = cmbtarget.Text
        res["fldstatus"] = "Inactive"
        res.Update
      Next
      SHowAccGrid(btnconsult.Tag)
      Balloon.Info(("Information saved"), btnadd)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Private Sub SHowAccGrid(sUser As String)

  $rData = modDatabase.$myConn.Exec("select fldid,fldaccess,fldcomp from tblclinicentry where flduserid=&1 and fldstatus=&2", sUser, "Inactive")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 350 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Components"
    .Columns[2].Text = "Target"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Menu()

  mnufile.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If btnconsult.Tag Then
      modDatabase.$myConn.Delete("tblclinicentry", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      SHowAccGrid(btnconsult.Tag)
    Endif
  Endif

End
