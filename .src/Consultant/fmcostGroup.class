' Gambas class file

Private $rData As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

  cmbtarget.List = modBasic.$AllCompPerList
  cmbtarget.Text = modBasic.$compID
  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbtarget.Enabled = False
  Endif
  cmbtype.List = ["Test", "Radio"]
  cmbgroup.List = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblcostgroup"))

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub cmbtarget_Click()

  lblcomp.Text = modGeneral.GetCompNameFromCompID(cmbtarget.Text)

End

Public Sub cmbtype_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtype)
  modFillContainer.RestrictComboToContent(cmbtype)

End

Public Sub btnrefresh_Click()

  If cmbgroup.Text And If cmbtarget.Text Then
    FillGroupGrid()
    cmbtest.SetFocus
  Endif

End

Public Sub btnlist_Click()

  If cmbtype.Text Then
    cmbtest.List = modNonMedical.NonStockBillActiveItemArray(modNonMedical.GetBillItemCategoryFromCombo(cmbtype.Text), "%")
  Endif

End

Private Sub FillGroupGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  If cmbgroup.Text Then
    sql = "select fldid,flditemtype,flditemname from tblcostgroup where fldgroup=&1 and fldcomp=&2"
    $rData = modDatabase.$myConn.Exec(sql, cmbgroup.Text, cmbtarget.Text)

    GridView1.Clear
    GridView1.Columns.Count = $rData.Fields.Count
    GridView1.Rows.Count = $rData.Count

    For Each $rData
      Column = 0
      For Each fld In $rData.Fields
        modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
        Column = Column + 1
      Next
    Next
    GridView1.Row = 0

    With GridView1
      .Rows.Height = modBasic.$AppGridRowHeight
      .Columns[0].Width = 1
      .Columns[1].Width = 120 * modBasic.$AppWidthRatio
      .Columns[2].Width = 375 * modBasic.$AppWidthRatio

      .Columns[1].Text = "Item Type"
      .Columns[2].Text = "Item Name"
    End With
  Endif

End

Public Sub btnBrOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnBrOK_Click()
  Endif

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If cmbgroup.Text And If cmbtest.Text And If cmbtype.Text And If cmbtarget.Text Then
    res = modDatabase.$myConn.Create("tblcostgroup")
    res["fldgroup"] = cmbgroup.Text
    res["fldcomp"] = cmbtarget.Text
    res["flditemtype"] = cmbtype.Text
    res["flditemname"] = cmbtest.Text
    res.Update
    FillGroupGrid()
    cmbtest.Text = ""
    Balloon.Info(("Information saved"), btnBrOK)
    Balloon.Delay = modBasic.$BalloonDelay
    cmbgroup.List = modNonMedical.NonSTockCostingGroupList(cmbtype.Text)
    cmbtest.SetFocus
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblcostgroup", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    FillGroupGrid()
    Balloon.Delete(("Information deleted"), GridView1)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, cmbtype.Text & " : " & cmbgroup.Text)

End

Public Sub cmbgroup_LostFocus()

  cmbgroup.Text = String.UCaseFirst(cmbgroup.Text)

End

Public Sub cmbtest_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtest)
  modFillContainer.RestrictComboToContent(cmbtest)

End
