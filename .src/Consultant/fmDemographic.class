' Gambas class file

Private $rData As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmboption.List = ["No Selection", "Single Selection", "Dichotomous", "Multiple Selection", "Quantitative", "Date Time", "BS Date"]
  cmbtarget.List = modBasic.$AllCompPerList

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbtarget_Click()

  lblcomp.Text = modGeneral.GetCompNameFromCompID(cmbtarget.Text)

End

Public Sub cmbtarget_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbtarget)

End

Public Sub btnrefresh_Click()

  If cmbtarget.Text Then
    FillGridSubTest()
  Endif

End

Public Sub txttestname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnoption_Click()

  Dim hForm As FmSubQuali

  If cmboption.Text Then
    If cmboption.Text = "No Selection" Then
    Else If cmboption.Text = "Quantitative" Then
    Else If cmboption.Text = "Date Time" Then
    Else If cmboption.Text = "BS Date" Then
    Else
      hForm = New FmSubQuali("Clinical Demographics", txttestname.Text, cmbtarget.Text, cmboption.Text)
      hForm.ShowModal
    Endif
  Endif

End

Private Sub FillGridSubTest()

  Dim Column As Integer
  Dim fld As ResultField

  $rData = modDatabase.$myConn.Exec("select flddemoid,fldoption,fldindex,flddefault from tbldemographic where fldcomp=&1", cmbtarget.Text)                                ''
  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0
  txtindex.Value = $rData.Count

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 300 * modBasic.$AppWidthRatio
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 40 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1
    .Columns[0].Text = "Demographics"
    .Columns[1].Text = "Option"
  End With

End

Public Sub GridView1_Click()

  txttestname.Text = GridView1[GridView1.Row, 0].Text
  cmboption.Text = GridView1[GridView1.Row, 1].Text
  txtindex.Value = GridView1[GridView1.Row, 2].Text
  txtdefault.Text = GridView1[GridView1.Row, 3].Text

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If txttestname.Text And If cmbtarget.Text Then
    res = modDatabase.$myConn.Create("tbldemographic")
    res["flddemoid"] = Trim(txttestname.Text)
    res["fldoption"] = cmboption.Text
    res["flddefault"] = Trim(txtdefault.Text)
    res["fldcomp"] = cmbtarget.Text
    res["fldindex"] = txtindex.Value
    res.Update
    FillGridSubTest()
  Endif
  txttestname.Text = ""
  cmboption.Text = ""
  Balloon.Info(("Information saved"), btnBrOK)
  Balloon.Delay = modBasic.$BalloonDelay
  txttestname.SetFocus

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim Row As Integer

  If txttestname.Text And If cmbtarget.Text Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit("tbldemographic", "flddemoid=&1", txttestname.Text)
    res["fldoption"] = cmboption.Text
    res["flddefault"] = Trim(txtdefault.Text)
    res["fldcomp"] = cmbtarget.Text
    res["fldindex"] = txtindex.Value
    res.Update
    FillGridSubTest()
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
    GridView1.Row = Row
  Endif

End

Public Sub GridView1_Menu()

  mnufile.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tbldemographic", "flddemoid=&1 and fldcomp=&2", txttestname.Text, cmbtarget.Text)
    FillGridSubTest()
    txttestname.Text = ""
    cmboption.Text = ""
  Endif

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If Trim(txtdelim.Text) Then
    xdelim = Trim(txtdelim.Text)
  Else
    xdelim = ";"
  Endif

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    If cmbtarget.Text Then
      modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tbldemographic", txtcsv.Text, xdelim, "fldcomp", cmbtarget.Text)
    Else
      modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tbldemographic", txtcsv.Text, xdelim)
    Endif
    Balloon.Info(("Data Import completed"), btnupload)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End
