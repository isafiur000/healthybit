' Gambas class file

Private $AllowReport As Boolean

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  FillListView()
  rbmetric.Value = True
  txtencid.SetFocus

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub btnshow_Click()

  Dim xstatus As String

  If txtencid.Text Then
    txtpatientname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    If txtpatientname.Text Then
      txtgender.Text = modPatient.GetPatientAgeString(Trim(txtencid.Text), Now()) & "/" & Left(modPatient.GetPatientSex(Trim(txtencid.Text)), 1)
      xstatus = modPatient.CurrentAdmissionStatus(Trim(txtencid.Text))
      txtpatlocation.Text = modPatient.GetLocationSetting(Trim(txtencid.Text), xstatus)
      $AllowReport = modPatientSub.AllowEncIDHistory(Trim(txtencid.Text), modDatabase.$myConn)
    Endif
  Endif

End

Private Sub FillListView()

  Dim i As Integer
  Dim xx As String
  Dim ReportList As String[] = ["Discharge Letter", "Death Certificate", "Referral Letter", "LAMA Letter", "Admission Report", "Birth Report", "Baby Report", "OPD Sheet", "Diagnostic Help"]                                                                                                       ''

  For Each xx In ReportList
    If modSettings.ShowSettingFromFIle(xx & "/Name") Then
      ListView1.Add(xx, modSettings.ShowSettingFromFIle(xx & "/Name"))
    Endif
  Next

  For i = 1 To 10
    If modSettings.ShowSettingFromFIle("CustomReport" & CStr(i) & "/Name") Then
      ListView1.Add("CustomReport" & CStr(i), modSettings.ShowSettingFromFIle("CustomReport" & CStr(i) & "/Name"))
    Endif
  Next

End

Public Sub btnrepo_Click()

  Dim hCls As CReportCustom

  If $AllowReport = True Then
    If ListView1.Selection.Count > 0 Then
      hCls = New CReportCustom(Trim(txtencid.Text), ListView1.Current.Key, "ReportSize", modLabSub.GetTestUnitFromButton(rbsi, rbmetric))
      hCls.Preview()
    Endif
  Endif

End

Public Sub btnsave_Click()

  Dim hCls As CReportCustom

  If $AllowReport = True Then
    If ListView1.Selection.Count > 0 Then
      hCls = New CReportCustom(Trim(txtencid.Text), ListView1.Current.Key, "ReportSize", modLabSub.GetTestUnitFromButton(rbsi, rbmetric))
      SaveReportAsHTML(hCls.NewHTMLPath())
    Endif
  Endif

End

Private Sub SaveReportAsHTML(sHTML As String)

  Dim xx As String
  Dim sType As String
  Dim sPath As String
  Dim sInt As String
  Dim sLongID As Long

  sInt = modString.GetNowString()
  sPath = modPrint.ConvertHTMLToPDFString(sHTML, "ReportSize", sInt)
  sType = "General Reports"
  xx = InputBox(("Title of Report"), sType, "")
  sLongID = modImage.SavePatientFileGeneral(Trim(txtencid.Text), sType, xx, sPath, modReportVar.$ReportHash)

End

Public Sub txtencid_KeyPress()

  If Key.Code = Key.Down Then
    If Not txtencid.Text Then
      txtencid.Text = PatSearch("Encounter")
      txtencid.SetFocus
    Else
      If modBasic.$AutoEncSuffix = "Yes" Then
        txtencid.Text = txtencid.Text & modBasic.$HospCode
      Endif
    Endif
  Else
    modGeneralMain.InputUpCaseOnly()
  Endif

End
