' Gambas class file

Private $FixedFormats As String[] = ["Common", "Claim Code", "Accident SSF", "Medical SSF", "MedExtra SSF", "Critical SSF", "Staff List"]
Private $OptionList As Variant[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  rbhistory.Value = True
  LoadFormats()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Private Function GetDefTable() As String

  Dim sTable As String

  If rbhistory.Text Then
    sTable = "tblhistory"
  Else If rbdischarge.Value Then
    sTable = "tbldischarge"
  Endif

  Return sTable

End

Private Sub LoadFormats()

  Dim sTable As String

  sTable = GetDefTable()
  If sTable Then
    cmbformat.List = GetFormatList(sTable)
  Endif

End

Private Function GetFormatList(sTable As String) As String[]

  Dim res As Result
  Dim sList As String[]

  sList = $FixedFormats.Copy()
  res = modDatabase.$syConn.Exec(Subst("select distinct(fldformat) as col from &1", sTable))
  If res.Available Then
    For Each res
      If sList.Exist(res["col"]) = False Then
        sList.Add(res["col"])
      Endif
    Next
  Endif

  Return sList

End

Public Sub rbhistory_Click()

  LoadFormats()

End

Public Sub rbdischarge_Click()

  LoadFormats()

End

Private Sub ShowParameters()

  Dim xxx As String[]
  Dim xList As String[]

  xxx = New String[]
  If cmbformat.Text Then
    If rbhistory.Value Then
      xList = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select flditem as col from tblhistory where fldformat like &1", cmbformat.Text))
      If xList.Exist("History of Illness") = False Then
        xxx.Add("History of Illness")
      Endif
    Else If rbdischarge.Value Then
      xList = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select flditem as col from tbldischarge where fldformat like &1", cmbformat.Text))
    Endif

    xxx.Insert(xList)
    ListBox1.List = xxx
  Endif

End

Public Sub btnshow_Click()

  ShowParameters()

End

Public Sub btnaddtest_Click()

  Dim sTable As String
  Dim res1 As Result

  If txtsubtest.Text And If cmbformat.Text Then
    sTable = GetDefTable()
    If sTable Then
      res1 = modDatabase.$medConn.Create(sTable)
      res1["flditem"] = Trim(txtsubtest.Text)
      res1["fldformat"] = cmbformat.Text
      res1["flddetail"] = ""
      res1["fldoptions"] = ""
      res1.Update()
      ShowParameters()
      txtsubtest.Text = ""
    Endif
  Endif

End

Public Sub ListBox1_Click()

  $OptionList = New Variant[]
  ShowItemOptions()

End

''------------------ Show Option Grid ----------------------
Public Sub btnhtml_Click()

  Dim xx As String

  xx = GetTextArea("HTML View", txtdetail.RichText)

End

Private Sub ShowItemOptions()

  Dim res As Result
  Dim xVar As Variant[]

  res = modDatabase.$myConn.Exec(Subst("select fldid,flddetail,fldoptions from &1", GetDefTable()) & " where flditem=&1 and fldformat like &2", ListBox1.Text, cmbformat.Text)             ''
  If res.Available Then
    res.MoveFirst
    txtdetail.RichText = res["flddetail"]
    If res["fldoptions"] Then
      Try xVar = JSON.Decode(res["fldoptions"])
      If xVar Then
        $OptionList = xVar
      Endif
    Endif
  Else
    txtdetail.RichText = ""
  Endif
  SetOptionGrid()
  Frame1.Enabled = True

End

Private Sub SetOptionGrid()

  GridView1.Clear()
  GridView1.Columns.Count = 4
  If $OptionList.Count Then
    GridView1.Rows.Count = $OptionList.Count
    LoadVariableToGrid($OptionList, GridView1)
  Else
    GridView1.Rows.Count = 0
  Endif

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 100 * modBasic.$AppWidthRatio
    .Columns[1].Width = 200 * modBasic.$AppWidthRatio
    .Columns[2].Width = 250 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1

    .Columns[0].Text = "Variable"
    .Columns[1].Text = "Value"
    .Columns[2].Text = "Options"
  End With

End

Private Sub LoadVariableToGrid(xFinal As Variant[], xGridView As GridView)

  Dim xRowVal As Collection
  Dim Row As Integer
  Dim Column As Integer

  For Row = 0 To xFinal.Count - 1
    xRowVal = xFinal[Row]
    xGridView[Row, 0].Text = xRowVal["Variable"]
    xGridView[Row, 1].Text = xRowVal["Value"]
    xGridView[Row, 2].Text = xRowVal["Option"]
    xGridView[Row, 3].Text = xRowVal["Select"]
  Next

End

Public Sub GridView1_Click()

  txtvariable.Text = GridView1[GridView1.Row, 0].Text
  txtvalues.Text = GridView1[GridView1.Row, 1].Text
  txtoptions.Text = GridView1[GridView1.Row, 2].Text
  txtselect.Text = GridView1[GridView1.Row, 3].Text

End

Public Sub btnaddoption_Click()

  Dim sColl As Collection

  sColl = New Collection
  If txtvalues.Text Then
    sColl.Add(Trim(txtvariable.Text), "Variable")
    sColl.Add(Trim(txtvalues.Text), "Value")
    sColl.Add(Trim(txtoptions.Text), "Option")
    sColl.Add(Trim(txtselect.Text), "Select")
    $OptionList.Add(sColl)
    Balloon.Info(("Information saved"), btnaddoption)
    Balloon.Delay = modBasic.$BalloonDelay
    SetOptionGrid()
    txtvalues.Text = ""
    txtoptions.Text = ""
  Endif

End

Public Sub btndeloption_Click()

  If GridView1.Rows.Selection.Count Then
    $OptionList.Remove(GridView1.Row)
    Balloon.Delete(("Information deleted"), btndeloption)
    Balloon.Delay = modBasic.$BalloonDelay
    SetOptionGrid()
  Endif

End

'-------------------------- Main procedures ---------------------
Public Sub btnBrOK_Click()

  Dim res As Result
  Dim res1 As Result

  If ListBox1.Text And If cmbformat.Text Then
    res = modDatabase.$medConn.Edit(GetDefTable(), "flditem=&1 and fldformat like &2", ListBox1.Text, cmbformat.Text)
    If res.Available Then
      res["flddetail"] = txtdetail.RichText
      res["fldoptions"] = JSON.Encode($OptionList)
      res.Update()
      Balloon.Info(("Information updated"), btnBrOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Else
      res1 = modDatabase.$medConn.Create(GetDefTable())
      res1["flditem"] = ListBox1.Text
      res1["fldformat"] = cmbformat.Text
      res1["flddetail"] = txtdetail.RichText
      res1["fldoptions"] = JSON.Encode($OptionList)
      res1.Update()
      ShowParameters()
      Balloon.Info(("Information saved"), btnBrOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif
  txtsubtest.SetFocus

End

Public Sub btnBrCancel_Click()

  If ListBox1.Text And If cmbformat.Text Then
    modDatabase.$medConn.Delete(GetDefTable(), "flditem=&1 and fldformat=&2", ListBox1.Text, cmbformat.Text)
    txtdetail.RichText = ""
    ShowParameters()
    GridView1.Clear()
    $OptionList = New Variant[]
    Balloon.Delete(("Information deleted"), btnBrCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtsubtest_LostFocus()

  txtsubtest.Text = String.UCaseFirst(txtsubtest.Text)

End

Public Sub btndefaultformats_Click()

  Dim xList As String[] = ["Past History", "Treatment History", "Medication History", "Personal History", "Surgical History", "Occupational History", "Social History", "Family History"]
  Dim xItem As String
  Dim yList As String[] = ["Condition of Discharge", "Advice on Discharge"]
  Dim yItem As String
  Dim res As Result
  Dim res1 As Result

  For Each xItem In xList
    res = modDatabase.$medConn.Edit("tblhistory", "flditem=&1 and fldformat=&2", xItem, "Common")
    If Not res.Available Then
      res1 = modDatabase.$medConn.Create("tblhistory")
      res1["flditem"] = xItem
      res1["fldformat"] = "Common"
      res1["flddetail"] = ""
      res1["fldoptions"] = ""
      res1.Update()
    Endif
  Next

  For Each yItem In yList
    res = modDatabase.$medConn.Edit("tbldischarge", "flditem=&1 and fldformat=&2", yItem, "Common")
    If Not res.Available Then
      res1 = modDatabase.$medConn.Create("tbldischarge")
      res1["flditem"] = yItem
      res1["fldformat"] = "Common"
      res1["flddetail"] = ""
      res1["fldoptions"] = ""
      res1.Update()
    Endif
  Next

  Balloon.Info(("Information saved"), btndefaultformats)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnclaim_Click()

  Dim xList As String[] = ["Co-Morbidity Disease"]
  Dim xItem As String
  Dim yList As String[] = ["ICU Note", "Ventilator Note", "BIPAP/CPAP Note", "Operation Description", "Condition of Discharge", "Follow Up Advice"]
  Dim yItem As String
  Dim res As Result
  Dim res1 As Result

  For Each xItem In xList
    res = modDatabase.$medConn.Edit("tblhistory", "flditem=&1 and fldformat=&2", xItem, "Claim Code")
    If Not res.Available Then
      res1 = modDatabase.$medConn.Create("tblhistory")
      res1["flditem"] = xItem
      res1["fldformat"] = "Claim Code"
      res1["flddetail"] = ""
      res1["fldoptions"] = ""
      res1.Update()
    Endif
  Next

  For Each yItem In yList
    res = modDatabase.$medConn.Edit("tbldischarge", "flditem=&1 and fldformat=&2", yItem, "Claim Code")
    If Not res.Available Then
      res1 = modDatabase.$medConn.Create("tbldischarge")
      res1["flditem"] = yItem
      res1["fldformat"] = "Claim Code"
      res1["flddetail"] = ""
      res1["fldoptions"] = ""
      res1.Update()
    Endif
  Next

  Balloon.Info(("Information saved"), btndefaultformats)
  Balloon.Delay = modBasic.$BalloonDelay

End
