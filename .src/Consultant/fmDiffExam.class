' Gambas class file

Private $rData As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbtarget.List = GetCategList()
  cmbtype.List = ["Qualitative", "Quantitative"]
  cmbconstant.List = modSysCons.GetAllSysConstantsList()
  cmboption.List = modMedicine.FillClinSepOptionCombo()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Private Function GetCategList() As String[]

  Dim xx As String[]
  Dim yy As String[]

  xx = New String[]
  xx.Insert(["Pre Delivery", "On Delivery", "Post Delivery", "Baby Examination", "Hospital Acquired Infection", "Pre-Operative", "Operative", "Post-Operative", "Anaesthesia"])

  yy = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldprocname as col from tblprocname"))
  If yy Then
    yy.Sort()
    xx.Insert(yy)
  Endif

  Return xx

End

Public Sub btnselect_Click()

  Dim xList As String[]

  If cmbtarget.Text = "Hospital Acquired Infection" Then
    xList = modHAI.GetHAIExaminationList()
  Endif
  If xList Then
    txtexamname.Text = GridViewNew("Select Examination", xList, False)
  Endif

End

Public Sub cmbtype_LostFocus()

  If cmbtype.Text Then
    cmbconstant.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldsysconst) as col from tblexam where fldtype=&1 and fldstatus=&2", cmbtype.Text, "Active"))                                   '')
  Endif

End

Public Sub btnshowsetting_Click()

  FillGroupGrid()

End

Private Sub FillGroupGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  If cmbtarget.Text Then
    sql = "select fldid,fldtype,fldexamid,fldsysconst,fldtanswertype from tbldeptexam where flddept=&1"
    $rData = modDatabase.$myConn.Exec(sql, cmbtarget.Text)

    GridView1.Clear
    GridView1.Columns.Count = $rData.Fields.Count
    GridView1.Rows.Count = $rData.Count

    For Each $rData
      Column = 0
      For Each fld In $rData.Fields
        modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
        Column = Column + 1
      Next
    Next
    GridView1.Row = 0

    With GridView1
      .Rows.Height = modBasic.$AppGridRowHeight
      .Columns[0].Width = 1
      .Columns[1].Width = 100 * modBasic.$AppWidthRatio
      .Columns[2].Width = 200 * modBasic.$AppWidthRatio
      .Columns[3].Width = 150 * modBasic.$AppWidthRatio
      .Columns[4].Width = 100 * modBasic.$AppWidthRatio
      .Columns[1].Text = "Variable"
      .Columns[2].Text = "Examination"
      .Columns[3].Text = "SysConstant"
      .Columns[4].Text = "Option"
    End With
  Endif

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If cmbtarget.Text And If txtexamname.Text And If cmbtype.Text And If cmboption.Text Then
    res = modDatabase.$myConn.Create("tbldeptexam")
    res["fldexamid"] = txtexamname.Text
    res["fldsysconst"] = cmbconstant.Text
    res["fldtype"] = cmbtype.Text
    res["fldtanswertype"] = cmboption.Text
    res["flddept"] = cmbtarget.Text
    res.Update
    FillGroupGrid()
    cmbtype.Text = ""
    txtexamname.Text = ""
    cmbconstant.Text = ""
    cmboption.Text = ""
    cmbconstant.SetFocus
  Endif

End

Public Sub cmbtype_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtype)
  modFillContainer.RestrictComboToContent(cmbtype)

End

Public Sub cmboption_KeyRelease()

  modFillContainer.AutoFillComboBox(cmboption)
  modFillContainer.RestrictComboToContent(cmboption)

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tbldeptexam", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    FillGroupGrid()
    Balloon.Delete(("Information deleted"), GridView1)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "DEPARTMENTAL EXAMINATIONS", cmbtarget.Text)

End

Public Sub btnBrOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnBrOK_Click()
  Endif

End

Public Sub GridView1_Click()

  txtid.Value = GridView1[GridView1.Row, 0].Text
  txtexamname.Text = GridView1[GridView1.Row, 2].Text
  cmbtype.Text = GridView1[GridView1.Row, 1].Text
  cmbconstant.Text = GridView1[GridView1.Row, 3].Text
  cmboption.Text = GridView1[GridView1.Row, 4].Text

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim res1 As Result

  If cmbtarget.Text And If txtexamname.Text And If cmbtype.Text And If cmboption.Text Then
    res = modDatabase.$myConn.Edit("tbldeptexam", "fldid=&1", txtid.Value)

    res1 = modDatabase.$myConn.Edit("tbldeptexamoption", "fldexamid=&1 and flddept=&2", res["fldexamid"], cmbtarget.Text)
    If res1.Available Then
      For Each res1
        res1["fldexamid"] = txtexamname.Text
        res1.Update
      Next
    Endif

    res["fldtype"] = cmbtype.Text
    res["fldexamid"] = txtexamname.Text
    res["fldtanswertype"] = cmboption.Text
    res["fldsysconst"] = cmbconstant.Text
    res.Update
    FillGroupGrid()
    Balloon.Info(("Information saved"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnoption_Click()

  Dim hForm As FmSubQuali
  Dim hForm1 As FmVisualInput

  If cmboption.Text Then
    Select cmboption.Text
      Case "Single Selection", "Multiple Selection", "Text Table", "Dichotomous", "Fixed Components", "Left/Right Components"
        hForm = New FmSubQuali("Departmental Examination", txtexamname.Text, cmbtarget.Text, cmboption.Text)
        hForm.ShowModal
      Case "Visual Input", "ImageValue"
        hForm1 = New FmVisualInput(cmbtarget.Text, txtexamname.Text, "", cmboption.Text)
        hForm1.ShowModal
    End Select
  Endif

End

Public Sub txtexamname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnaddproc_Click()

  Dim hForm As FmAddVariableGrid

  hForm = New FmAddVariableGrid("fldprocname", "tblprocname")
  hForm.ShowModal
  cmbtarget.List = GetCategList()

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If Trim(txtdelim.Text) Then
    xdelim = Trim(txtdelim.Text)
  Else
    xdelim = ";"
  Endif

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    If cmbtarget.Text Then
      modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tbldeptexam", txtcsv.Text, xdelim, "flddept", cmbtarget.Text)
    Else
      modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tbldeptexam", txtcsv.Text, xdelim)
    Endif
    Balloon.Info(("Data Import completed"), btnupload)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End
