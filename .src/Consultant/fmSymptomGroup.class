' Gambas class file

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbtarget.Text = modBasic.$compID
    cmbtarget.Enabled = False
  Else
    cmbtarget.List = modBasic.$AllCompPerList
    cmbtarget.Enabled = True
  Endif
  cmbcategory.List = modMedicine.GetPathoCategoryList("Symptom")
  cmbcategory.Add("Null")

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub cmbtarget_Click()

  lblcomp.Text = modGeneral.GetCompNameFromCompID(cmbtarget.Text)

End

Public Sub cmbtarget_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbtarget)

End

Public Sub btnaddcategory_Click()

  Dim hForm As FmAddVariable1

  hForm = New FmAddVariable1("flclass", "fldcategory", "Symptom", "tblpathocategory")
  hForm.ShowModal
  cmbcategory.List = modMedicine.GetPathoCategoryList("Symptom")
  cmbcategory.Add("Null")

End

Private Function GetFlagValue(sBool As Boolean) As String

  Dim sVal As String

  If sBool = True Then
    sVal = "Flagged"
  Else If sBool = False Then
    sVal = "Unflagged"
  Endif
  Return sVal

End

Public Sub btnaddsymp_Click()

  Dim hForm As FmSelectList

  If cmbtarget.Text And If cmbcategory.Text Then
    hForm = New FmSelectList(("Select Complaints for the selected department"), "Complaints List", cmbtarget.Text & "|" & cmbcategory.Text)
    hForm.ShowModal
    FillComplaintGrid(cmbcategory.Text, cmbtarget.Text)
  Endif

End

Public Sub cmbcategory_Click()

  If cmbtarget.Text And If cmbcategory.Text Then
    FillComplaintGrid(cmbcategory.Text, cmbtarget.Text)
  Endif

End

Private Sub FillComplaintGrid(sCategory As String, sTarget As String)

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  Dim $rData As Result

  If cmbtarget.Text Then
    If sCategory = "Null" Then
      sql = "select fldid,fldflag,fldsymptom,fldflag from tblcomplaints where fldcomp=&1 and fldtype not in(select flclass from tblpathocategory where fldcategory like &2) ORDER BY fldsymptom"
      $rData = modDatabase.$myConn.Exec(sql, sTarget, "Symptom")
    Else
      sql = "select fldid,fldflag,fldsymptom,fldflag from tblcomplaints where fldcomp=&1 and fldtype=&2 ORDER BY fldsymptom"
      $rData = modDatabase.$myConn.Exec(sql, sTarget, sCategory)
    Endif

    grdsymp.Clear
    grdsymp.Columns.Count = $rData.Fields.Count
    grdsymp.Rows.Count = $rData.Count

    For Each $rData
      Column = 0
      For Each fld In $rData.Fields
        modGeneralMain.GridExplicitDecoration(grdsymp, $rData.Index, Column)
        If Column = 1 Then
          grdsymp[$rData.Index, Column].Picture = Picture[modMisc.SetGridCheckIcon($rData[fld.Name])]
          grdsymp[$rData.Index, Column].Text = ""
        Else
          grdsymp[$rData.Index, Column].Text = $rData[fld.Name]
        Endif
        Column = Column + 1
      Next
    Next
    grdsymp.Row = 0

    With grdsymp
      .Rows.Height = modBasic.$AppGridRowHeight
      .Columns[0].Width = 1
      .Columns[1].Width = 25 * modBasic.$AppWidthRatio
      .Columns[2].Width = grdsymp.Width - 75 * modBasic.$AppWidthRatio
      .Columns[3].Width = 1

      .Columns[2].Text = "Symptom"
    End With
  Endif

End

Public Sub grdsymp_Click()

  Dim Row As Integer
  Dim xval As String
  Dim res As Result

  If grdsymp.Column = 1 Then
    Row = grdsymp.Row
    xval = InputCombo("Alter Flag", grdsymp[grdsymp.Row, 2].Text, ["Flagged", "Unflagged"], GetFlagValue(grdsymp[grdsymp.Row, 3].Text), True)
    If xval Then
      res = modDatabase.$myConn.Edit("tblcomplaints", "fldid=&1", grdsymp[grdsymp.Row, 0].Text)
      If xval = "Flagged" Then
        res["fldflag"] = True
      Else If xval = "Unflagged" Then
        res["fldflag"] = False
      Endif
      res.Update
      If cmbtarget.Text And If cmbcategory.Text Then
        FillComplaintGrid(cmbcategory.Text, cmbtarget.Text)
      Endif
    Endif
    grdsymp.Row = Row
  Endif

End

Public Sub grdsymp_Menu()

  mnucomplaint.Popup

End

Public Sub mnudelcomplain_Click()

  If grdsymp.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblcomplaints", "fldid=&1", grdsymp[grdsymp.Row, 0].Text)
    If cmbtarget.Text And If cmbcategory.Text Then
      FillComplaintGrid(cmbcategory.Text, cmbtarget.Text)
    Endif
    Balloon.Delete(("Information deleted"), grdsymp)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
