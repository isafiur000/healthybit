' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbtarget.Text = modBasic.$compID
    cmbtarget.Enabled = False
    btnrefresh_Click()
  Else
    cmbtarget.List = modBasic.$AllCompPerList
    cmbtarget.Enabled = True
  Endif
  cmbgroupname.List = GetGroupList()
  cmbexamname.List = modMedicine.FillExamQualiOrQuantiCombo("%")

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnrefresh_Click()

  If cmbtarget.Text Then
    ShowDataGrid()
  Endif

End

Private Function GetGroupList() As String[]

  Dim xxx As String[]

  xxx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroupname) as col from tblgroupexam"))
  Return xxx

End

Private Sub ShowDataGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,fldgroupname,fldexamid from tblgroupexam where fldcomp=&1", cmbtarget.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 250 * modBasic.$AppWidthRatio
    .Columns[1].Text = "GroupName"
    .Columns[2].Text = "Examination"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub GridView1_Click()

  cmbgroupname.Text = GridView1[GridView1.Row, 1].Text
  cmbexamname.Text = GridView1[GridView1.Row, 2].Text

End

Public Sub btnOK_Click()

  Dim res As Result

  If cmbexamname.Text And If cmbtarget.Text And If cmbgroupname.Text Then
    res = modDatabase.$myConn.Create("tblgroupexam")
    res["fldgroupname"] = cmbgroupname.Text
    res["fldexamid"] = cmbexamname.Text
    res["fldcomp"] = cmbtarget.Text
    res.Update
    ShowDataGrid()
    cmbgroupname.List = GetGroupList()
    cmbexamname.Text = ""
  Endif

End

Public Sub btnCancel_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete("tblgroupexam", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    ShowDataGrid()
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tblgroupexam", txtcsv.Text, xdelim)
  Endif

End
