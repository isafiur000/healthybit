' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $tblpatlabtest As String
Private $tblpatlabsubtest As String

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, True)
  cmbdiagnogrp.List = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblsurveillance"))
  cmbtest.List = ["Blood Culture", "Bone Marrow Aspiration", "Both (RDT+Microscopy)", "ELISA", "FNAC", "Microscopy", "PCR", "RDT", "Slit Skin Smear", "Splenic Aspiration", "Stool Culture", "Virus Culture", "Other"]

  modHMISRep.$EWARSDistrictColl = New Collection
  modHMISRep.$EWARSMunicipalColl = New String[]
  dtfir.Value = Now()
  dtlast.Value = Now()
  rbnew.Value = True
  chkfinal.Value = True
  modAccount.PasInvoiceSetting(cmbfiscal, False)
  LoadTableNames()
  FillDistCollection()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Private Sub LoadTableNames()

  Dim res As Result

  If cmbfiscal.Text = "Current" Then
    $tblpatlabtest = "tblpatlabtest"
    $tblpatlabsubtest = "tblpatlabsubtest"
  Else
    res = modDatabase.$syConn.Exec("select fldpatlabtest,fldpatlabsubtest from tblfisclosing where fldindex=&1 and (fldstate=&2 or fldstate IS NULL)", cmbfiscal.Text, "Active")
    If res.Available Then
      If res["fldpatlabtest"] Then
        $tblpatlabtest = res["fldpatlabtest"]
      Else
        $tblpatlabtest = "tblpatlabtest"
      Endif
      If res["fldpatlabsubtest"] Then
        $tblpatlabsubtest = res["fldpatlabsubtest"]
      Else
        $tblpatlabsubtest = "tblpatlabsubtest"
      Endif
    Else
      $tblpatlabtest = "tblpatlabtest"
      $tblpatlabsubtest = "tblpatlabsubtest"
    Endif
  Endif

End

Public Sub cmbfiscal_Click()

  LoadTableNames()

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtfir.Value))
  If xx Then
    dtfir.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtneplast_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlast.Value))
  If xx Then
    dtlast.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Private Sub FillDistCollection()

  Dim sPath As String
  Dim xPath As String

  sPath = modHelpVariable.$docsDirectory &/ "EwarsDistrict.txt"
  modHMISRep.$EWARSDistrictColl = modString.GetCollectionFromCSV(sPath)

  xPath = modHelpVariable.$docsDirectory &/ "EWARSMunicipality.txt"
  modHMISRep.$EWARSMunicipalColl = modString.GetStringArrayFromFile(xPath)

End

Public Sub cmbtest_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtest)
  modFillContainer.RestrictComboToContent(cmbtest)

End

Public Sub btnshow_Click()

  FillGridView()

End

Private Sub FillGridView()

  Dim xFldList As String[]
  Dim codList As String[]
  Dim i As Integer

  xFldList = ["fldid", "fldencounterval", "fldencounterval", modDataRepo.SerialField(), "fldencounterval", "fldcode", "fldcodenew", "fldtype", "fldtime"]
  codList = modControlSub.GetDirectFillresultNoNull(modDatabase.$syConn.Exec("select fldcodenew as col from tblsurveillance where fldgroup=&1", Trim(cmbdiagnogrp.Text)))
  For i = 0 To codList.Count - 1
    codList[i] = "'" & codList[i] & "'"
  Next

  If chkfinal.Value = True Then
    If rbnew.Value = True Then
      $rData = modDatabase.$syConn.Exec("select " & xFldList.Join(",") & " from tblpatfindings where fldtime>=&1 and fldtime<=&2 and fldcodenew in(" & codList.Join(",") & ") and (fldtype=&3 or fldtype=&4) and fldsave=&5 and (fldflag=&6 or fldflag IS NULL)", modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), "Provisional Diagnosis", "Final Diagnosis", True, False)                       ''
    Else If rbflag.Value = True Then
      $rData = modDatabase.$syConn.Exec("select " & xFldList.Join(",") & " from tblpatfindings where fldtime>=&1 and fldtime<=&2 and fldcodenew in(" & codList.Join(",") & ") and (fldtype=&3 or fldtype=&4) and fldsave=&5 and fldflag=&6", modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), "Provisional Diagnosis", "Final Diagnosis", True, True)
    Endif
  Else
    If rbnew.Value = True Then
      $rData = modDatabase.$syConn.Exec("select " & xFldList.Join(",") & " from tblpatfindings where fldtime>=&1 and fldtime<=&2 and fldcodenew in(" & codList.Join(",") & ") and fldtype=&3 and fldsave=&4 and (fldflag=&5 or fldflag IS NULL)", modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), "Provisional Diagnosis", True, False)                       ''
    Else If rbflag.Value = True Then
      $rData = modDatabase.$syConn.Exec("select " & xFldList.Join(",") & " from tblpatfindings where fldtime>=&1 and fldtime<=&2 and fldcodenew in(" & codList.Join(",") & ") and fldtype=&3 and fldsave=&4 and fldflag=&5", modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), "Provisional Diagnosis", True, True)
    Endif
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 175 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 250 * modBasic.$AppWidthRatio
    .Columns[6].Width = 100 * modBasic.$AppWidthRatio
    .Columns[7].Width = 150 * modBasic.$AppWidthRatio
    .Columns[8].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "EncID"
    .Columns[2].Text = "Name"
    .Columns[3].Text = "Age"
    .Columns[4].Text = "Gender"
    .Columns[5].Text = "Diagnosis"
    .Columns[6].Text = "Code"
    .Columns[7].Text = "Category"
    .Columns[8].Text = "Date"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = GetGridViewValue(Column, $rData[$aMyFields[Column]])
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Private Function GetGridViewValue(Column As Integer, xVariable As Variant) As Variant

  Dim xxx As Variant

  If Column = 2 Then
    xxx = modPatient.GetPatientNameByEnc(xVariable, modDatabase.$syConn)
  Else If Column = 3 Then
    xxx = modMedReports.GetPatientPastAgeString("tblpatfindings", xVariable)
  Else If Column = 4 Then
    xxx = modPatient.GetPatientSex(xVariable, modDatabase.$syConn)
  Else If Column = 8 Then
    xxx = modReportVar.GetDateTimeReport(xVariable, gb.GeneralDate)
  Else
    xxx = xVariable
  Endif

  Return xxx

End

Public Sub mnudemog_Click()

  Dim hForm As FmPatdemograph

  If GridView1.Rows.Selection.Count
    hForm = New FmPatdemograph(GridView1[GridView1.Row, 1].Text, "Clinical")
    hForm.ShowModal
  Endif

End

Public Sub mnupatprofile_Click()

  Dim hForm As FmIndexForm

  If GridView1.Rows.Selection.Count
    hForm = New FmIndexForm(GridView1[GridView1.Row, 1].Text, True, True)
    hForm.Show()
  Endif

End

''========================== IMU API ==========================
Public Sub mnuapi_Click()

  Dim hForm As FmAPISetting

  hForm = New FmAPISetting("EWARS")
  hForm.ShowModal

End

Public Sub mnupload_Click()

  Dim hForm As CEwarsAPI
  Dim xout As String
  Dim xColl As Collection
  Dim xmsg As String
  Dim res As Result

  If rbnew.Value = True Then
    If GridView1.Rows.Selection.Count Then
      Inc Application.Busy
      hForm = New CEwarsAPI(GridView1[GridView1.Row, 0].Text, cmbtest.Text, cmbfiscal.Text)
      xout = hForm.EWARSOutput()
      If xout Then
        Try xColl = JSON.Decode(xout)
        If xColl Then
          xmsg = xColl["status"] & gb.NewLine & xColl["message"]
        Endif
      Endif
      Dec Application.Busy
      If xmsg Then
        If Message.Question(xmsg, ("Success"), ("Error")) = 1 Then
          res = modDatabase.$myConn.Edit("tblpatfindings", "fldid=&1", GridView1[GridView1.Row, 0].Text)
          res["fldflag"] = True
          res.Update
          FillGridView()
        Endif
      Endif
    Endif
  Endif

Catch
  Dec Application.Busy
  Message.Warning(Error.Text, ("OK"))

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, cmbtest.Text, modReportVar.GetDateTimeReport(dtfir.Value, gb.MediumDate) & " TO " & modReportVar.GetDateTimeReport(dtlast.Value, gb.MediumDate))

End

Public Sub mnubulk_Click()

  Dim Row As Integer
  Dim hForm As CEwarsAPI
  Dim xmsg As String
  Dim res As Result
  Dim sColl As Collection

  If rbnew.Value = True Then
    If Message.Question("Are you sure ?", ("No"), ("Yes")) = 2 Then
      For Row = 0 To GridView1.Rows.Count - 1

        Inc Application.Busy
        xmsg = ""
        hForm = New CEwarsAPI(GridView1[Row, 0].Text, cmbtest.Text, cmbfiscal.Text)
        xmsg = hForm.EWARSOutput()
        Dec Application.Busy
        If xmsg Then
          Try sColl = JSON.Decode(xmsg)
          If sColl And If sColl["response"]["status"] = "SUCCESS" Then
            res = modDatabase.$myConn.Edit("tblpatfindings", "fldid=&1", GridView1[Row, 0].Text)
            res["fldflag"] = True
            res.Update
          Else
            Message.Warning(xmsg, ("OK"))
          Endif
        Endif

      Next
      FillGridView()
    Endif
  Endif

Catch
  Dec Application.Busy
  Message.Warning(Error.Text, ("OK"))

End

Public Sub GridView1_Menu()

  If rbflag.Value = True Then
    mnuhide.Popup
  Endif

End

Public Sub mnuremoveflag_Click()

  Dim res As Result

  If rbflag.Value = True Then
    res = modDatabase.$myConn.Edit("tblpatfindings", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldflag"] = False
    res.Update
    FillGridView()
  Endif

End
