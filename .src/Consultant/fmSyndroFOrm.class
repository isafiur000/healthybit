' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbtarget.Text = modBasic.$compID
    cmbtarget.Enabled = False
  Else
    cmbtarget.List = modBasic.$AllCompPerList
    cmbtarget.Enabled = True
  Endif
  cmbcategory.List = modMedicine.GetPathoCategoryList("Syndrome")

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbtarget_Click()

  lblcomp.Text = modGeneral.GetCompNameFromCompID(cmbtarget.Text)

End

Public Sub cmbtarget_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbtarget)

End

Public Sub tlbtnadd_Click()

  Dim hForm As FmAddVariable1

  hform = New FmAddVariable1("flclass", "fldcategory", "Syndrome", "tblpathocategory")
  hform.ShowModal
  cmbcategory.List = modMedicine.GetPathoCategoryList("Syndrome")

End

Public Sub btnadd_Click()

  Dim res As Result

  If Not txtcode.Text Then
    txtcode.Text = "Other"
  Endif
  If cmbcategory.Text And If txtsyndrome.Text And If txtcode.Text Then
    If cmbtarget.Text Then
      res = modDatabase.$medConn.Create("tblsyndromes")
      res["fldsyndrome"] = Trim(txtsyndrome.Text)
      res["fldcategory"] = cmbcategory.Text
      res["fldsymcode"] = txtcode.Text
      res["fldcomp"] = cmbtarget.Text
      res.Update
      ShowGridData(cmbcategory.Text)
      txtsyndrome.Text = ""
      txtcode.Text = ""
      txtsearch.Text = ""
      Balloon.Info(("Information saved"), btnadd)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnrefresh_Click()

  ShowGridData(cmbcategory.Text)

End

Public Sub btndel_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    modDatabase.$medConn.Delete("tblsyndromes", "fldsyndrome=&1 and fldcomp=&2", GridView1[GridView1.Row, 0].Text, cmbtarget.Text)
    ShowGridData(cmbcategory.Text)
    txtsyndrome.Text = ""
    txtcode.Text = ""
    Balloon.Info(("Information deleted"), btndel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub ShowGridData(sType As String)

  If sType Then
    $rData = modDatabase.$medConn.Exec("select fldsyndrome,fldsymcode from tblsyndromes where fldcategory=&1 and fldcomp=&2", sType, cmbtarget.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    With GridView1
      .Columns[0].Width = 400 * modBasic.$AppWidthRatio
      .Columns[1].Width = 100 * modBasic.$AppWidthRatio
      .Columns[0].Text = "Syndrome"
      .Columns[1].Text = "ICD Group"
    End With
  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub txtsyndrome_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnaddicd_Click()

  Dim xx As String[]

  xx = ICDGridView("Select Disease")
  If xx Then
    txtsearch.Text = xx[0]
    txtcode.Text = xx[1]
  Endif

End
