' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbdepart.List = modGeneral.GetDepartmentsForOPDBoth()
  cmbweekday.List = ["AllDays", "Specific", "Sunday", "Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday", "%"]
  cmbmethod.List = ["AllParam", "Consult+Mode", "ConsultOnly", "DepartOnly"]
  cmbcomp.List = modBasic.$AllCompList
  cmbcomp.Text = modBasic.$compID
  cmbbillmode.List = modNonMedical.FillCashModeCombo()
  cmbbillmode.Add("%")
  dtconsult.Value = Now()
  dtstart.Value = ""
  dtend.Value = ""
  cmbweekday.SetFocus

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbcomp_Click()

  lbldepart.Text = modGeneral.GetCompNameFromCompID(cmbcomp.Text)

End

Public Sub btnconsult_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Consultant"), modBasic.$OPConsulUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif

End

Public Sub btnlist_Click()

  $rData = modDatabase.$myConn.Exec("select fldid,fldmethod,fldselect,flddate,fldbillingmode,flddept,flduserid,fldquota,fldreason from tbldepconsult")                                  ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Public Sub btnrefresh_Click()

  FilVarList()

End

Public Sub btnrefreshselect_Click()

  FillGridOptional()

End

Private Sub FillGridOptional()

  If cmbweekday.Text = "Specific" Then
    $rData = modDatabase.$myConn.Exec("select fldid,fldmethod,fldselect,flddate,fldbillingmode,flddept,flduserid,fldquota,fldwebquota,fldreason from tbldepconsult where fldcomp like &1 and fldselect like &2 and fldmethod like &3 and fldbillingmode like &4 and flddate>=&5 and flddate<=&6", cmbcomp.Text, "Specific", cmbmethod.Text, cmbbillmode.Text, modDate.StartSqlDate(dtconsult.Value), modDate.EndSqlDate(dtconsult.Value))                                         ''
  Else
    $rData = modDatabase.$myConn.Exec("select fldid,fldmethod,fldselect,flddate,fldbillingmode,flddept,flduserid,fldquota,fldwebquota,fldreason from tbldepconsult where fldcomp like &1 and fldselect like &2 and fldmethod like &3 and fldbillingmode like &4", cmbcomp.Text, cmbweekday.Text, cmbmethod.Text, cmbbillmode.Text)                                                                ''
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Private Sub FilVarList()

  '   $rData = modDatabase.$myConn.Exec("select fldid,fldbillingmode,flddept,flduserid,fldselect,flddate,fldquota,fldmethod,fldreason from tbldepconsult where fldcomp like &1", cmbcomp.Text)                                  ''
  $rData = modDatabase.$myConn.Exec("select fldid,fldmethod,fldselect,flddate,fldbillingmode,flddept,flduserid,fldquota,fldwebquota,fldreason from tbldepconsult where fldcomp like &1", cmbcomp.Text)                                  ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Private Sub ResizeGrid()

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    If cmbweekday.Text = "Specific" Or If cmbweekday.Text = "%" Then
      .Columns[3].Width = 120 * modBasic.$AppWidthRatio
    Else
      .Columns[3].Width = 50 * modBasic.$AppWidthRatio
    Endif
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 75 * modBasic.$AppWidthRatio
    .Columns[9].Width = 100 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Method"
    .Columns[2].Text = "Timing"
    .Columns[3].Text = "Date"
    .Columns[4].Text = "Mode"
    .Columns[5].Text = "Department"
    .Columns[6].Text = "Consultant"
    .Columns[7].Text = "Quota"
    .Columns[8].Text = "Quota"
    .Columns[9].Text = "Reason"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 6 Then
    GridView1.Data.Text = modGeneral.GetUserFullName($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result

  If Not cmbmethod.Text Then
    cmbmethod.Text = "AllParam"
  Endif

  If cmbweekday.Text And If cmbcomp.Text Then
    res = modDatabase.$myConn.Create("tbldepconsult")
    res["flduserid"] = btnconsult.Tag
    res["flddept"] = Trim(cmbdepart.Text)
    res["fldbillingmode"] = cmbbillmode.Text
    res["fldquota"] = txtquota.Value
    res["fldwebquota"] = txtwebquota.Value
    res["fldselect"] = cmbweekday.Text
    res["fldmethod"] = cmbmethod.Text
    res["fldcomp"] = cmbcomp.Text
    If cmbweekday.Text = "Specific" Then
      res["flddate"] = dtconsult.Value
    Endif
    res["fldfrom"] = dtstart.Value
    res["fldend"] = dtend.Value
    res["fldreason"] = cmbreason.Text
    res["xyz"] = False
    res.Update()
    FillGridOptional()
    BlankAll()
    Balloon.Info(("Information saved"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    cmbdepart.SetFocus
  Endif

End

Private Sub BlankAll()

  btnconsult.Tag = ""
  btnconsult.Text = ""
  cmbreason.Text = ""
  txtquota.Value = 0
  txtwebquota.Value = 0
  dtstart.Value = ""
  dtend.Value = ""

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tbldepconsult", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    FilVarList()
    Balloon.Delete(("Information deleted"), GridView1)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtconsult.Value))
  If xx Then
    dtconsult.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub GridView1_Click()

  Dim xx As Float
  Dim res As Result
  Dim Row As Integer

  Row = GridView1.Row
  If GridView1.Column = 7 Then
    xx = InputValue(("Change Quota allocation"), ("Change Value"), GridView1[GridView1.Row, 7].Text)
    If xx Then
      res = modDatabase.$myConn.Edit("tbldepconsult", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      res["fldquota"] = xx
      res.Update
      FilVarList()
    Endif
  Else If GridView1.Column = 8 Then
    xx = InputValue(("Change Web Quota allocation"), ("Change Value"), GridView1[GridView1.Row, 8].Text)
    If xx Then
      res = modDatabase.$myConn.Edit("tbldepconsult", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      res["fldwebquota"] = xx
      res.Update
      FilVarList()
    Endif
  Else
    res = modDatabase.$myConn.Exec("select fldselect,fldbillingmode,flddept,flduserid,fldmethod,fldcomp,fldquota,fldwebquota,fldfrom,fldend,fldreason from tbldepconsult where fldid=&1", GridView1[GridView1.Row, 0].Text)
    If res.Available Then
      cmbcomp.Text = res["fldcomp"]
      cmbmethod.Text = res["fldmethod"]
      cmbweekday.Text = res["fldselect"]
      cmbbillmode.Text = res["fldbillingmode"]
      cmbdepart.Text = res["flddept"]
      btnconsult.Tag = res["flduserid"]
      btnconsult.Text = modGeneral.GetUserFullName(btnconsult.Tag)
      cmbreason.Text = res["fldreason"]
      txtquota.Value = res["fldquota"]
      txtwebquota.Value = res["fldwebquota"]
      If res["fldfrom"] Then
        dtstart.Value = res["fldfrom"]
      Endif
      If res["fldend"] Then
        dtend.Value = res["fldend"]
      Endif
    Endif
  Endif
  GridView1.Row = Row

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "QUOTA ALLOCATION", modReportVar.GetDateTimeReport(dtconsult.Value, gb.MediumDate))

End

Public Sub cmbweekday_Click()

  If cmbweekday.Text = "Specific" Then
    dtconsult.Enabled = True
  Else
    dtconsult.Enabled = False
  Endif

End

Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim Row As Integer

  If Not cmbmethod.Text Then
    cmbmethod.Text = "AllParam"
  Endif

  If GridView1.Rows.Selection.Count > 0 Then
    If cmbweekday.Text And If cmbcomp.Text Then
      Row = GridView1.Row
      res = modDatabase.$myConn.Edit("tbldepconsult", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      res["flduserid"] = btnconsult.Tag
      res["flddept"] = Trim(cmbdepart.Text)
      res["fldbillingmode"] = cmbbillmode.Text
      res["fldquota"] = txtquota.Value
      res["fldwebquota"] = txtwebquota.Value
      res["fldselect"] = cmbweekday.Text
      res["fldmethod"] = cmbmethod.Text
      res["fldcomp"] = cmbcomp.Text
      If cmbweekday.Text = "Specific" Then
        res["flddate"] = dtconsult.Value
      Endif
      res["fldfrom"] = dtstart.Value
      res["fldend"] = dtend.Value
      res["fldreason"] = cmbreason.Text
      res["xyz"] = False
      res.Update()
      FillGridOptional()
      GridView1.Row = Row
      Balloon.Info(("Information saved"), btnedit)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnholiday_Click()

  Dim hForm As FmHoliday

  If cmbcomp.Text Then
    hForm = New FmHoliday(cmbcomp.Text)
    hForm.ShowModal
  Endif

End

Public Sub cmbmethod_Click()

  If cmbmethod.Text = "ConsultOnly" Then
    btnconsult.Enabled = True
    cmbbillmode.Text = "%"
    cmbbillmode.Enabled = False
    cmbdepart.Text = "%"
    cmbdepart.Enabled = False

  Else If cmbmethod.Text = "DepartOnly" Then
    btnconsult.Enabled = False
    cmbbillmode.Text = "%"
    cmbbillmode.Enabled = False
    cmbdepart.Enabled = True

  Else If cmbmethod.Text = "Consult+Mode" Then
    btnconsult.Enabled = True
    cmbbillmode.Enabled = True
    cmbdepart.Text = "%"
    cmbdepart.Enabled = False

  Else
    btnconsult.Enabled = True
    cmbbillmode.Enabled = True
    cmbdepart.Enabled = True

  Endif

End
