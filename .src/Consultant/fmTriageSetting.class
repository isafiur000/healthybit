' Gambas class file

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbtarget.List = modBasic.$AllCompPerList
  cmbsympcolor.List = ["Red", "Yellow", "Green", "Blue", "Black"]
  cmblabcolor.List = ["Red", "Yellow", "Green", "Blue", "Black"]
  cmbexamcolor.List = ["Red", "Yellow", "Green", "Blue", "Black"]

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub cmbtarget_Click()

  lblcomp.Text = modGeneral.GetCompNameFromCompID(cmbtarget.Text)

End

Public Sub cmbtarget_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbtarget)

End

Public Sub FillColorTree(GridView1 As GridView, sType As String, sColor As String, sComp As String)

  Dim res As Result
  Dim Column As Integer
  Dim fld As ResultField

  res = modDatabase.$myConn.Exec("select flid,fldchild,fldcategory,fldrelation,fldvalquali,flddiagnounit,fldbaserate,fldhitrate,fldfalserate from tbltriage where flddiagnotype=&1 and fldparent like &2 and fldcomp=&3", sType, sColor, sComp)                         ''
  GridView1.Clear()
  GridView1.Columns.Count = res.Fields.Count
  GridView1.Rows.Count = res.Count

  For Each res
    Column = 0
    For Each fld In res.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, res.Index, Column)
      GridView1[res.Index, Column].Text = res[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 200 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 50 * modBasic.$AppWidthRatio
    .Columns[4].Width = 200 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 50 * modBasic.$AppWidthRatio
    .Columns[8].Width = 50 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Variable"
    .Columns[3].Text = ""
    .Columns[4].Text = "Value"
    .Columns[5].Text = "Unit"
    .Columns[6].Text = "BaseRate"
    .Columns[7].Text = "HitRate"
    .Columns[8].Text = "False"
  End With

End

Public Sub btnrefrsymp_Click()

  If cmbsympcolor.Text And If cmbtarget.Text Then
    FillColorTree(grdsymptom, "Symptom", cmbsympcolor.Text, cmbtarget.Text)
  Endif

End

Public Sub btnrefrexam_Click()

  If cmbexamcolor.Text And If cmbtarget.Text Then
    FillColorTree(grdexam, "Exam", cmbexamcolor.Text, cmbtarget.Text)
  Endif

End

Public Sub btnrefrlab_Click()

  If cmblabcolor.Text And If cmbtarget.Text Then
    FillColorTree(grdtest, "Test", cmblabcolor.Text, cmbtarget.Text)
  Endif

End

Public Sub btnaddexam_Click()

  Dim hForm As FmEnterComboValue

  If cmbexamcolor.Text And If cmbtarget.Text Then
    hForm = New FmEnterComboValue("Triage", cmbexamcolor.Text, "Exam", cmbtarget.Text)
    hForm.ShowModal
    FillColorTree(grdexam, "Exam", cmbexamcolor.Text, cmbtarget.Text)
  Endif

End

Public Sub btnaddlab_Click()

  Dim hForm As FmEnterComboValue

  If cmblabcolor.Text And If cmbtarget.Text Then
    hForm = New FmEnterComboValue("Triage", cmblabcolor.Text, "Test", cmbtarget.Text)
    hForm.ShowModal
    FillColorTree(grdtest, "Test", cmblabcolor.Text, cmbtarget.Text)
  Endif

End

Public Sub btnaddsymp_Click()

  Dim hForm As FmEnterComboValue

  If cmbsympcolor.Text And If cmbtarget.Text Then
    hForm = New FmEnterComboValue("Triage", cmbsympcolor.Text, "Symptom", cmbtarget.Text)
    hForm.ShowModal
    FillColorTree(grdsymptom, "Symptom", cmbsympcolor.Text, cmbtarget.Text)
  Endif

End

Public Sub grdsymptom_Menu()

  mnuhidfile.Popup

End

Public Sub grdexam_Menu()

  mnuhidfile.Popup

End

Public Sub grdtest_Menu()

  mnuhidfile.Popup

End

Public Sub mnudelete_Click()

  Dim GridView1 As GridView
  Dim ComboBox1 As ComboBox
  Dim sType As String

  If TabPanel1.Index = 0 Then
    GridView1 = grdsymptom
    ComboBox1 = cmbsympcolor
    sType = "Symptom"
  Else If TabPanel1.Index = 1 Then
    GridView1 = grdexam
    ComboBox1 = cmbexamcolor
    sType = "Exam"
  Else If TabPanel1.Index = 2 Then
    GridView1 = grdtest
    ComboBox1 = cmblabcolor
    sType = "Test"
  Endif

  If GridView1.Rows.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tbltriage", "flid=&1", GridView1[GridView1.Row, 0].Text)
      If ComboBox1.Text And If cmbtarget.Text Then
        FillColorTree(GridView1, sType, ComboBox1.Text, cmbtarget.Text)
      Endif
    Endif
  Endif

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If Trim(txtdelim.Text) Then
    xdelim = Trim(txtdelim.Text)
  Else
    xdelim = ";"
  Endif

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    If cmbtarget.Text Then
      modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tbltriage", txtcsv.Text, xdelim, "fldcomp", cmbtarget.Text)
    Else
      modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tbltriage", txtcsv.Text, xdelim)
    Endif
    Balloon.Info(("Data Import completed"), btnupload)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnsettall_Click()

  Dim xPath As String

  If cmbtarget.Text Then
    Inc Application.Busy
    xPath = modMedicalReport.ShowTriageSettings(cmbtarget.Text)
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End
