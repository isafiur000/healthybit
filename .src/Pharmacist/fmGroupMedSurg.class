' Gambas class file

Private $rData As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

  cmbroute.List = modMedicine.ComboRoute()
  cmbdoseunit.List = modMedicine.$MedicineUnitList
  cmbfreq.List = modMedicine.FrequencyCombo()
  cmbgroup.List = MedGroupList()
  cmbmode.List = modNonMedical.FillCashModeCombo()
  cmbtestmode.List = ["Pre-Administration", "Post-Administration"]

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub btnrefresh_Click()

  If cmbgroup.Text Then
    FillDosingGrid()
    cmbroute.SetFocus
  Endif

End

Private Sub FillDosingGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  sql = "select fldid,fldroute,flditem,flddose,flddoseunit,fldfreq,fldday,fldqty,fldstart,fldadvice from tblproductgroup where fldmedgroup=&1 ORDER BY fldid DESC"
  $rData = modDatabase.$myConn.Exec(sql, cmbgroup.Text)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 75 * modBasic.$AppWidthRatio
    .Columns[2].Width = 300 * modBasic.$AppWidthRatio
    .Columns[3].Width = 50 * modBasic.$AppWidthRatio
    .Columns[4].Width = 50 * modBasic.$AppWidthRatio
    .Columns[5].Width = 50 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 50 * modBasic.$AppWidthRatio
    .Columns[8].Width = 50 * modBasic.$AppWidthRatio
    .Columns[9].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Route"
    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "Dose"
    .Columns[4].Text = "Unit"
    .Columns[5].Text = "Freq"
    .Columns[6].Text = "Days"
    .Columns[7].Text = "QTY"
    .Columns[8].Text = "Start"
    .Columns[9].Text = "Advice"
  End With

End

Public Sub GridView1_Click()

  cmbroute.Text = GridView1[GridView1.Row, 1].Text
  cmbmedicine.Text = GridView1[GridView1.Row, 2].Text
  txtdose.Value = GridView1[GridView1.Row, 3].Text
  cmbdoseunit.Text = GridView1[GridView1.Row, 4].Text
  cmbfreq.Text = GridView1[GridView1.Row, 5].Text
  txtday.Value = GridView1[GridView1.Row, 6].Text
  txtqty.Value = GridView1[GridView1.Row, 7].Text
  txtstart.Value = GridView1[GridView1.Row, 8].Text
  txtadvice.Text = GridView1[GridView1.Row, 9].Text

End

Public Sub tbtngroup_Click()

  Dim hForm As FmAddVariableGrid

  hform = New FmAddVariableGrid("fldmedgroup", "tblmedgroup")
  hform.ShowModal
  cmbgroup.List = MedGroupList()

End

Public Sub cmbgroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbgroup)
  modFillContainer.RestrictComboToContent(cmbgroup)

End

Public Sub cmbroute_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbroute)
  modFillContainer.RestrictComboToContent(cmbroute)

End

Public Sub cmbdoseunit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdoseunit)
  modFillContainer.RestrictComboToContent(cmbdoseunit)

End

Public Sub cmbfreq_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbfreq)
  modFillContainer.RestrictComboToContent(cmbfreq)

End

Private Function MedGroupList() As String[]

  Dim res As Result
  Dim xx As String[]

  res = modDatabase.$myConn.Exec("select fldmedgroup as col from tblmedgroup")
  xx = modControlSub.GetDirectFillresult(res)

  Return xx

End

Public Sub cmbmedicine_GotFocus()

  Dim res As Result

  If Not cmbroute.Text Then
    cmbroute.SetFocus

  Else If cmbroute.Text Then
    If Not cmbmedicine.Text Then
      res = modStock.ItemListForPurchaseResult(cmbroute.Text, "Generic")
      cmbmedicine.Text = GridViewNew("Select Particulars", modControlSub.GetDirectFillresult(res), False, cmbmedicine)
      txtdose.SetFocus
    Endif
  Endif

End

Public Sub tbtnadd_Click()

  Dim res As Result

  If cmbgroup.Text Then
    If cmbroute.Text And If cmbmedicine.Text And If txtqty.Value Then
      res = modDatabase.$myConn.Create("tblproductgroup")
      res["fldmedgroup"] = cmbgroup.Text
      res["fldroute"] = cmbroute.Text
      res["flditem"] = cmbmedicine.Text
      res["flddose"] = txtdose.Value
      res["flddoseunit"] = cmbdoseunit.Text
      res["fldfreq"] = cmbfreq.Text
      res["fldday"] = txtday.Value
      res["fldqty"] = txtqty.Value
      res["fldstart"] = txtstart.Value
      res["fldadvice"] = Trim(txtadvice.Text)
      res.Update
      ClearAllContr()
      FillDosingGrid()
      cmbroute.SetFocus
    Endif
  Endif

End

Public Sub tbtnadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    tbtnadd_Click()
  Endif

End

Private Sub ClearAllContr()

  cmbmedicine.Text = ""
  cmbroute.Text = ""
  txtdose.Value = 0
  cmbdoseunit.Text = ""
  cmbfreq.Text = ""
  txtday.Value = 0
  txtqty.Value = 0
  txtstart.Value = 0
  txtadvice.Text = ""

End

Public Sub txtdose_GotFocus()

  If cmbmedicine.Text Then
    If modStockSub.GetComboControlGeneric(cmbroute.Text, cmbmedicine.Text) = False Then
      cmbmedicine.Text = ""
      Balloon.Warning(("Item not listed"), cmbmedicine)
      Balloon.Delay = modBasic.$BalloonDelay
      cmbroute.SetFocus
    Endif
  Endif

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblproductgroup", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    FillDosingGrid()
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count > 0 Then
    res = modDatabase.$myConn.Edit("tblproductgroup", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldroute"] = cmbroute.Text
    res["flditem"] = cmbmedicine.Text
    res["flddose"] = txtdose.Value
    res["flddoseunit"] = cmbdoseunit.Text
    res["fldfreq"] = cmbfreq.Text
    res["fldday"] = txtday.Value
    res["fldqty"] = txtqty.Value
    res["fldstart"] = txtstart.Value
    res["fldadvice"] = Trim(txtadvice.Text)
    res.Update
    FillDosingGrid()
    Balloon.Info(("Information saved"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub cmbroute_Click()

  cmbmedicine.Clear

End

Public Sub EnableDosing(sbool As Boolean)

  txtdose.Enabled = sbool
  cmbdoseunit.Enabled = sbool
  cmbfreq.Enabled = sbool
  txtday.Enabled = sbool

End

Public Sub cmbroute_LostFocus()

  If cmbroute.Text = "suture" Or cmbroute.Text = "msurg" Or cmbroute.Text = "ortho" Or cmbroute.Text = "extra" Then
    EnableDosing(False)
  Else
    EnableDosing(True)
  Endif

End

Public Sub btnexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, cmbgroup.Text, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btnlist_Click()

  Dim xPath As String

  Inc Application.Busy
  xPath = modRepoMaster.ShowPharmacyGroupListHTML()
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End

''=================== LABORATORY ====================
Public Sub cmbmode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmode)
  modFillContainer.RestrictComboToContent(cmbmode)

End

Public Sub cmbmode_Click()

  cmbtest.Clear()
  If cmbmode.Text Then
    cmbtest.List = modNonMedical.NonStockBillActiveItemArray("Diagnostic Tests", cmbmode.Text)
  Endif

End

Public Sub cmbtest_GotFocus()

  Dim xLst As String[]

  If cmbmode.Text Then
    If Not cmbtest.Text Then
      If modBasic.$ItemListFormat = "GridView" Then
        xLst = modNonMedical.NonStockBillingItemArray("Diagnostic Tests", cmbmode.Text)
        If xLst.Count Then
          cmbtest.Text = GridViewNew("Select Particulars", xLst, False, cmbtest)
        Endif
      Endif
    Endif
  Endif

End
