' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

  cmbtarget.List = modBasic.$AllCompPerList
  cmbtarget.Text = modBasic.$compID
  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbtarget.Enabled = False
  Endif

  cmbroute.List = modMedicine.ComboRoute()
  cmbdoseunit.List = modMedicine.$MedicineUnitList
  cmbfreq.List = modMedicine.FrequencyCombo()
  cmbgroup.List = modMedicine.GetMedicineProcolsList()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub cmbtarget_Click()

  lblcomp.Text = modGeneral.GetCompNameFromCompID(cmbtarget.Text)

End

Public Sub btnrefresh_Click()

  If cmbgroup.Text And If cmbtarget.Text Then
    FillDosingGrid()
    cmbcategory.SetFocus
  Endif

End

Private Sub FillDosingGrid()

  Dim sql As String

  sql = "select fldid,fldroute,flditem,flddose,flddoseunit,fldfreq,fldday,fldqty,fldstart,fldadvice,fldcategory from tblproductgroup where fldmedgroup=&1 and fldcomp=&2 ORDER BY fldid DESC"
  $rData = modDatabase.$myConn.Exec(sql, cmbgroup.Text, cmbtarget.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 75 * modBasic.$AppWidthRatio
    .Columns[2].Width = 300 * modBasic.$AppWidthRatio
    .Columns[3].Width = 50 * modBasic.$AppWidthRatio
    .Columns[4].Width = 50 * modBasic.$AppWidthRatio
    .Columns[5].Width = 50 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 50 * modBasic.$AppWidthRatio
    .Columns[8].Width = 50 * modBasic.$AppWidthRatio
    .Columns[9].Width = 150 * modBasic.$AppWidthRatio
    .Columns[10].Width = 100 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Route"
    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "Dose"
    .Columns[4].Text = "Unit"
    .Columns[5].Text = "Freq"
    .Columns[6].Text = "Days"
    .Columns[7].Text = "QTY"
    .Columns[8].Text = "Start"
    .Columns[9].Text = "Advice"
    .Columns[10].Text = "Type"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Click()

  cmbroute.Text = GridView1[GridView1.Row, 1].Text
  cmbmedicine.Text = GridView1[GridView1.Row, 2].Text
  txtdose.Value = GridView1[GridView1.Row, 3].Text
  cmbdoseunit.Text = GridView1[GridView1.Row, 4].Text
  cmbfreq.Text = GridView1[GridView1.Row, 5].Text
  txtday.Value = GridView1[GridView1.Row, 6].Text
  txtqty.Value = GridView1[GridView1.Row, 7].Text
  txtstart.Value = GridView1[GridView1.Row, 8].Text
  txtadvice.Text = GridView1[GridView1.Row, 9].Text
  cmbcategory.Text = GridView1[GridView1.Row, 10].Text

End

Public Sub cmbgroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbgroup)
  modFillContainer.RestrictComboToContent(cmbgroup)

End

Public Sub cmbroute_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbroute)
  modFillContainer.RestrictComboToContent(cmbroute)

End

Public Sub cmbdoseunit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdoseunit)
  modFillContainer.RestrictComboToContent(cmbdoseunit)

End

Public Sub cmbfreq_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbfreq)
  modFillContainer.RestrictComboToContent(cmbfreq)

End

Public Sub cmbmedicine_GotFocus()

  Dim res As Result

  If Not cmbroute.Text Then
    cmbroute.SetFocus

  Else If cmbroute.Text Then
    If Not cmbmedicine.Text Then
      res = modStock.ItemListForPurchaseResult(cmbroute.Text, "Generic")
      cmbmedicine.Text = GridViewNew("Select Particulars", modControlSub.GetDirectFillresult(res), False, cmbmedicine)
      txtdose.SetFocus
    Endif
  Endif

End

Public Sub tbtnadd_Click()

  Dim res As Result

  If cmbgroup.Text Then
    If cmbroute.Text And If cmbmedicine.Text And If txtqty.Value Then
      res = modDatabase.$myConn.Create("tblproductgroup")
      res["fldmedgroup"] = cmbgroup.Text
      res["fldcomp"] = cmbtarget.Text
      res["fldcategory"] = cmbcategory.Text
      res["fldroute"] = cmbroute.Text
      res["flditem"] = cmbmedicine.Text
      res["flddose"] = txtdose.Value
      res["flddoseunit"] = cmbdoseunit.Text
      res["fldfreq"] = cmbfreq.Text
      res["fldday"] = txtday.Value
      res["fldqty"] = txtqty.Value
      res["fldstart"] = txtstart.Value
      res["fldadvice"] = Trim(txtadvice.Text)
      res.Update
      ClearAllContr()
      FillDosingGrid()
      cmbgroup.List = modMedicine.GetMedicineProcolsList()
      cmbroute.SetFocus
    Endif
  Endif

End

Public Sub tbtnadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    tbtnadd_Click()
  Endif

End

Private Sub ClearAllContr()

  cmbmedicine.Text = ""
  cmbroute.Text = ""
  txtdose.Value = 0
  cmbdoseunit.Text = ""
  cmbfreq.Text = ""
  txtday.Value = 0
  txtqty.Value = 0
  txtstart.Value = 0
  txtadvice.Text = ""

End

Public Sub txtdose_GotFocus()

  If cmbmedicine.Text Then
    If modStockSub.GetComboControlGeneric(cmbroute.Text, cmbmedicine.Text) = False Then
      cmbmedicine.Text = ""
      Balloon.Warning(("Item not listed"), cmbmedicine)
      Balloon.Delay = modBasic.$BalloonDelay
      cmbroute.SetFocus
    Endif
  Endif

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblproductgroup", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    FillDosingGrid()
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count > 0 Then
    res = modDatabase.$myConn.Edit("tblproductgroup", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldcategory"] = cmbcategory.Text
    res["fldroute"] = cmbroute.Text
    res["flditem"] = cmbmedicine.Text
    res["flddose"] = txtdose.Value
    res["flddoseunit"] = cmbdoseunit.Text
    res["fldfreq"] = cmbfreq.Text
    res["fldday"] = txtday.Value
    res["fldqty"] = txtqty.Value
    res["fldstart"] = txtstart.Value
    res["fldadvice"] = Trim(txtadvice.Text)
    res.Update
    FillDosingGrid()
    Balloon.Info(("Information saved"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub cmbroute_Click()

  cmbmedicine.Clear

End

Public Sub EnableDosing(sbool As Boolean)

  txtdose.Enabled = sbool
  cmbdoseunit.Enabled = sbool
  cmbfreq.Enabled = sbool
  txtday.Enabled = sbool

End

Public Sub cmbroute_LostFocus()

  If cmbroute.Text = "suture" Or cmbroute.Text = "msurg" Or cmbroute.Text = "ortho" Or cmbroute.Text = "extra" Then
    EnableDosing(False)
  Else
    EnableDosing(True)
  Endif

End

Public Sub btnexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, cmbgroup.Text, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btnlist_Click()

  Dim xPath As String

  Inc Application.Busy
  If cmbtarget.Text Then
    xPath = modRepoMaster.ShowPharmacyGroupListHTML(cmbtarget.Text)
  Endif
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End
