' Gambas class file

Private $rData As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbroute.List = modMedicine.ComboRoute()
  If modHelpVariable.$LogInCategory <> "Dispensar" Then
    cmbcomp.List = modBasic.$AllCompPerList
  Else
    cmbcomp.Add(modBasic.$compID)
    cmbcomp.Text = modBasic.$compID
    cmbcomp.Enabled = False
  Endif
  lblrupee.Text = modBasic.$currency
  cmbcomp.SetFocus

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Private Sub BlankCOntrol()

  txtbatch.Text = ""
  dtexpiry.Value = ""
  txtrate.Value = 0
  txtorder.Value = 0
  txtstockno.Value = 0

End

Public Sub cmbroute_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbroute)
  modFillContainer.RestrictComboToContent(cmbroute)

End

Public Sub cmbcomp_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcomp)
  modFillContainer.RestrictComboToContent(cmbcomp)

End

Public Sub cmbcomp_Click()

  cmbroute.Text = ""

End

Public Sub cmbroute_Click()

  If cmbcomp.Text Then
    txtsearch.Text = ""
    cmbitem.List = modControlSub.GetDirectFillresult(modStock.FillMedicinesTransferComboResult(cmbroute.Text, "Generic", cmbcomp.Text))
  Endif

End

Public Sub btnrefresh_Click()

  If cmbcomp.Text Then
    If cmbroute.Text Then
      txtsearch.Text = ""
      cmbitem.List = modControlSub.GetDirectFillresult(modStock.FillMedicinesTransferComboResult(cmbroute.Text, "Generic", cmbcomp.Text))
    Endif
  Endif

End

Public Sub txtsearch_Change()

  If cmbcomp.Text Then
    cmbitem.List = modControlSub.GetDirectFillresult(GetFillMedicinesTransferComboResult(cmbroute.Text, "Generic", txtsearch.Text & "%", cmbcomp.Text))
  Endif

End

Public Sub cmbitem_Click()

  If cmbcomp.Text Then
    If cmbroute.Text Then
      ShowGridData(True)
    Endif
  Endif

End

Public Sub cmbitem_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    cmbitem_Click()
  Endif

End

Private Sub ShowGridData(available As Boolean)

  Dim Column As Integer
  Dim fld As ResultField

  If available = True Then
    $rData = modDatabase.$myConn.Exec("select fldstockno,fldbatch,fldexpiry,fldqty,fldsellpr,fldstatus from tblentry where fldstockid=&1 and fldcomp like &2 and fldqty>&3 and fldstatus>&4", cmbitem.Text, cmbcomp.Text, 0, 0)                                                    ''
  Else If available = False Then
    $rData = modDatabase.$myConn.Exec("select fldstockno,fldbatch,fldexpiry,fldqty,fldsellpr,fldstatus from tblentry where fldstockid=&1 and fldcomp like &2 and fldqty>&3", cmbitem.Text, cmbcomp.Text, 0)                                                      ''
  Endif

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      If Column = 2 Then
        GridView1[$rData.Index, Column].Text = modReportVar.GetDateTimeReport($rData["fldexpiry"], gb.MediumDate)
      Else
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[5].Width = 50 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Code"
    .Columns[1].Text = "Batch"
    .Columns[2].Text = "Expiry"
    .Columns[3].Text = "QTY"
    .Columns[4].Text = "SellPrice"
    .Columns[5].Text = "Order"
  End With

  BlankCOntrol()

End

Public Sub btnall_Click()

  If cmbcomp.Text Then
    If cmbroute.Text Then
      ShowGridData(False)
    Endif
  Endif

End

Public Sub GridView1_Click()

  Dim res As Result

  txtstockno.Value = GridView1[GridView1.Row, 0].Text
  res = modDatabase.$myConn.Exec("select fldbatch,fldexpiry,fldqty,fldsellpr,fldstatus from tblentry where fldstockno=&1", txtstockno.Value)
  If res.Available Then
    txtbatch.Text = res["fldbatch"]
    dtexpiry.Value = res["fldexpiry"]
    txtrate.Value = res["fldsellpr"]
    txtorder.Value = res["fldstatus"]
  Endif

End

Public Sub GridView1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    GridView1_Click()
  Endif

End

Public Sub btnupdate_Click()

  Dim res As Result

  If txtbatch.Text And If dtexpiry.Value Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Edit("tblentry", "fldstockno=&1", txtstockno.Value)
      res["fldbatch"] = Trim(txtbatch.Text)
      res["fldexpiry"] = dtexpiry.Value
      If modHelpVariable.$LogInCategory <> "Dispensar" Then
        res["fldsellpr"] = txtrate.Value
      Endif
      res["fldstatus"] = txtorder.Value
      res["xyz"] = False
      res.Update()
      ShowGridData(True)
      Balloon.Info(("Information saved"), btnupdate)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Else
    Message.Warning("Incomplete data", ("OK"))
  Endif

End

Public Sub btnupdate_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnupdate_Click()
  Endif

End

Public Sub btnstatus_Click()

  Dim xx As String

  xx = "STATUS: " & modStock.GetActiveStatusItem(cmbroute.Text, cmbitem.Text)
  Message.Info(xx, ("OK"))

End

Public Sub txtbatch_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Private Function GetFillMedicinesTransferComboResult(txtroute As String, BrandOrGeneric As String, textToCursor As String, FromComp As String) As Result

  Dim sql As String
  Dim res As Result

  Select txtroute
    Case "oral", "liquid", "fluid", "injection", "resp", "topical", "eye/ear", "anal/vaginal"
      If BrandOrGeneric = "Generic" Then '''for generic
        sql = "select distinct(tblentry.fldstockid) as col from tblentry where lower(tblentry.fldstockid) like &1 and tblentry.fldcomp=&2 and tblentry.fldqty>&3 and tblentry.fldstockid in(select tblmedbrand.fldbrandid From tblmedbrand where tblmedbrand.fldactive=&4 and tblmedbrand.flddrug in(select tbldrug.flddrug From tbldrug where tbldrug.fldroute=&5)) ORDER BY tblentry.fldstockid ASC"
      Else If BrandOrGeneric = "Brand" Then '''for brand
        sql = "select tblmedbrand.fldbrand as col from (tblmedbrand inner join tblentry on tblentry.fldstockid=tblmedbrand.fldbrandid) inner join tbldrug on tblmedbrand.flddrug=tbldrug.flddrug where lower(tblmedbrand.fldbrand) like &1 and tblentry.fldcomp=&2 and tblentry.fldqty>&3 and tblmedbrand.fldactive=&4 and tbldrug.fldroute=&5 ORDER BY tblmedbrand.fldbrand ASC"
      Endif
      res = modDatabase.$myConn.Exec(sql, LCase(textToCursor) & "%", FromComp, 0, "Active", txtroute)

    Case "suture", "msurg", "ortho"
      sql = "select distinct(tblentry.fldstockid) as col from tblentry where lower(tblentry.fldstockid) like &1 and tblentry.fldcomp=&2 and tblentry.fldqty>&3 and tblentry.fldstockid in(Select tblsurgbrand.fldbrandid From tblsurgbrand where tblsurgbrand.fldactive=&4 and tblsurgbrand.fldsurgid in(select tblsurgicals.fldsurgid from tblsurgicals where tblsurgicals.fldsurgcateg=&5)) ORDER BY tblentry.fldstockid ASC"
      res = modDatabase.$myConn.Exec(sql, LCase(textToCursor) & "%", FromComp, 0, "Active", txtroute)

    Case "extra"
      sql = "select tblentry.fldstockid as col from tblentry where lower(tblentry.fldstockid) like &1 and tblentry.fldcomp=&2 and tblentry.fldqty>&3 and tblentry.fldstockid in(Select tblextrabrand.fldbrandid From tblextrabrand where tblextrabrand.fldactive=&4) ORDER BY tblentry.fldstockid ASC"
      res = modDatabase.$myConn.Exec(sql, LCase(textToCursor) & "%", FromComp, 0, "Active")

  End Select

  Return res

End

Public Sub mnuexpire_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    Inc Application.Busy
    modStockSub.GetCompwiseExpiredUpdate()
    Dec Application.Busy
    Balloon.Info(("Update process complete"), mnubulk)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub mnuupdate_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    Inc Application.Busy
    modStockSub.GetCompwiseOutOrderUpdate()
    Dec Application.Busy
    Balloon.Info(("Update process complete"), mnubulk)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
