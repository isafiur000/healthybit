' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  FillWordCode()
  BlankAllBox()
  SHowGridDire()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Private Sub BlankAllBox()

  cmbword.Text = ""
  txtengword.Text = ""
  txtlocalword.Text = ""

End

Private Sub FillWordCode()

  Dim sql As String
  Dim res As Result

  If rbpart.Value = True Then
    cmbword.List = ["Every", "Difference", "Hour", "Day", "Morning", "Noon", "Evening"]

  Else If rbprec.Value = True Then
    cmbword.List = ["Two-hourly", "Hourly", "stat", "PRN", "SOS", "AM", "HS", "Pre", "Post", "Alt day", "Third day", "Fourth day", "Weekly", "Twice weekly", "Thrice weekly", "Biweekly", "Triweekly", "Monthly", "Yearly", "Tapering"]                                                                  ''

  Else If rbdosage.Value = True Then
    sql = "select distinct(fldvolunit) as col from tblmedbrand"
    res = modDatabase.$myConn.Exec(sql)
    cmbword.List = modControlSub.GetDirectFillresult(res)

  Endif

End

Public Sub btnsave1_Click()

  Dim res As Result

  res = modDatabase.$myConn.Create("tbllocallabel")
  res["fldengcode"] = cmbword.Text
  res["fldengdire"] = Trim(txtengword.Text)
  res["fldfont"] = txtfont.Value
  res["fldlocaldire"] = Trim(txtlocalword.Text)
  res.Update()
  BlankAllBox()
  SHowGridDire()
  Balloon.Info(("Information saved"), btnsave1)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit("tbllocallabel", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldengcode"] = cmbword.Text
    res["fldengdire"] = Trim(txtengword.Text)
    res["fldfont"] = txtfont.Value
    res["fldlocaldire"] = Trim(txtlocalword.Text)
    res.Update()
    SHowGridDire()
    GridView1.Row = Row
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btndel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tbllocallabel", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    SHowGridDire()
    Balloon.Delete(("Information deleted"), btndel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub SHowGridDire()

  Dim sql As String

  sql = "select fldid,fldengcode,fldengdire,fldfont,fldlocaldire from tbllocallabel"
  $rData = modDatabase.$myConn.Exec(sql)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  modGridView.SetAccordingToFontCol(GridView1, 3, 4)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 250 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 200 * modBasic.$AppWidthRatio
    .Columns[1].Text = "Code"
    .Columns[2].Text = "English"
    .Columns[3].Text = "Font"
    .Columns[4].Text = "Local"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Click()

  cmbword.Text = GridView1[GridView1.Row, 1].Text
  txtengword.Text = GridView1[GridView1.Row, 2].Text
  txtfont.Value = GridView1[GridView1.Row, 3].Text
  txtlocalword.Text = GridView1[GridView1.Row, 4].Text

End

Public Sub rbpart_Click()

  FillWordCode()

End

Public Sub rbprec_Click()

  FillWordCode()

End

Public Sub rbdosage_Click()

  FillWordCode()

End

Public Sub txtlocalword_GotFocus()

  If txtfont.Value Then
    txtlocalword.Font = Font[txtfont.Value]
  Endif

End

Public Sub btnexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "MEDICINE LABELING", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End
