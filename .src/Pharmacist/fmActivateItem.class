' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $strType As String
Private $tbl As String
Private $gridPict As Picture

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  rbgeneric.Value = True
  MenuButton1.Menu = "mnualllist"
  $gridPict = Picture["icon:/small/access"]

  rbmed.Value = True
  rbmed_Click()
  txtname.SetFocus

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Private Function RouteList() As String[]

  Dim xx As String[]

  If rbmed.Value = True Then
    xx = modMedicine.RouteMedicine()
  Else If rbsurg.Value = True Then
    xx = ["suture", "msurg", "ortho"]
  Else If rbextra.Value = True Then
    xx = ["extra"]
  Endif
  Return xx

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Endif

End

Public Sub txtname_Change()

  ShowStockGrid()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Private Sub ShowStockGrid()

  If cmbroute.Text Then
    If rbmed.Value = True Then
      If rbgeneric.Value = True Then
        $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblmedbrand where lower(fldbrandid) like &1 and flddrug in(select flddrug from tbldrug where fldroute=&2)", LCase(txtname.Text) & "%", cmbroute.Text)                                           ''
      Else If rbbrand.Value = True Then
        $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblmedbrand where lower(fldbrand) like &1 and flddrug in(select flddrug from tbldrug where fldroute=&2) ORDER BY fldbrand", LCase(txtname.Text) & "%", cmbroute.Text)
      Endif
    Else If rbsurg.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblsurgbrand where lower(fldbrandid) like &1 and fldsurgid in(select fldsurgid from tblsurgicals where fldsurgcateg=&2)", LCase(txtname.Text) & "%", cmbroute.Text)
    Else If rbextra.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblextrabrand where lower(fldbrandid) like &1", LCase(txtname.Text) & "%")
    Endif

  Else
    If rbmed.Value = True Then
      If rbgeneric.Value = True Then
        $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblmedbrand where lower(fldbrandid) like &1", LCase(txtname.Text) & "%")                                           ''
      Else If rbbrand.Value = True Then
        $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblmedbrand where lower(fldbrand) like &1 ORDER BY fldbrand", LCase(txtname.Text) & "%")
      Endif
    Else If rbsurg.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblsurgbrand where lower(fldbrandid) like &1", LCase(txtname.Text) & "%")
    Else If rbextra.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldbrandid,fldbrandid,fldbrand,fldactive,fldbrandid from tblextrabrand where lower(fldbrandid) like &1", LCase(txtname.Text) & "%")
    Endif

  Endif

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 300 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 25 * modBasic.$AppWidthRatio
    .Columns[0].Text = "Type"
    .Columns[1].Text = "Generic Name"
    .Columns[2].Text = "Brand Name"
    .Columns[3].Text = "Status"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Text = modMedicine.GetRouteFromItem($rData["fldbrandid"], $strType)
  Else If Column = 4 Then
    GridView1.Data.Picture = $gridPict
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub rbmed_Click()

  $strType = "Medicines"
  $tbl = "tblmedbrand"
  cmbroute.Clear
  cmbroute.List = RouteList()
  ShowStockGrid()

End

Public Sub rbsurg_Click()

  $strType = "Surgicals"
  $tbl = "tblsurgbrand"
  cmbroute.Clear
  cmbroute.List = RouteList()
  ShowStockGrid()

End

Public Sub rbextra_Click()

  $strType = "Extra Items"
  $tbl = "tblextrabrand"
  cmbroute.Clear
  cmbroute.List = RouteList()
  ShowStockGrid()

End

Public Sub cmbroute_Click()

  ShowStockGrid()
  txtname.SetFocus

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $strType, "Total Quantity")

End

Public Sub btnactivate_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit($tbl, "fldbrandid=&1", GridView1[GridView1.Row, 1].Text)
    res["fldactive"] = "Active"
    res.Update()
    ShowStockGrid()
    GridView1.Row = Row
  Endif

End

Public Sub btndeactive_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit($tbl, "fldbrandid=&1", GridView1[GridView1.Row, 1].Text)
    res["fldactive"] = "Inactive"
    res.Update()
    ShowStockGrid()
    GridView1.Row = Row
  Endif

End

Public Sub btnactive_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Exec("update " & $tbl & " set fldactive='Active'")
    ShowStockGrid()
  Endif

End

Public Sub btndeact_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Exec("update " & $tbl & " set fldactive='Inactive'")
    ShowStockGrid()
  Endif

End

Public Sub mnuenablestock_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Edit($tbl, "fldbrandid in(select fldstockid from tblentry where fldqty>&1)", 0)
    If res.Available Then
      Inc Application.Busy
      For Each res
        res["fldactive"] = "Active"
        res.Update()
      Next
      Dec Application.Busy
      ShowStockGrid()
    Endif
  Endif

End

Public Sub btnactone_Click()

  Dim res1 As Result
  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    Inc Application.Busy
    If $tbl = "tblmedbrand" Then
      res1 = modDatabase.$myConn.Exec("select distinct(flddrug) as col from tblmedbrand")
      If res1.Available Then
        For Each res1
          res = modDatabase.$myConn.Edit($tbl, "flddrug=&1", res1!col)
          res["fldactive"] = "Active"
          res.Update()
        Next
      Endif

    Else If $tbl = "tblsurgbrand" Then
      res1 = modDatabase.$myConn.Exec("select distinct(fldsurgid) as col from tblsurgbrand")
      If res1.Available Then
        For Each res1
          res = modDatabase.$myConn.Edit($tbl, "fldsurgid=&1", res1!col)
          res["fldactive"] = "Active"
          res.Update()
        Next
      Endif

    Else If $tbl = "tblextrabrand" Then
      res1 = modDatabase.$myConn.Exec("select distinct(fldextraid) as col from tblextrabrand")
      If res1.Available Then
        For Each res1
          res = modDatabase.$myConn.Edit($tbl, "fldextraid=&1", res1!col)
          res["fldactive"] = "Active"
          res.Update()
        Next
      Endif

    Endif
    Dec Application.Busy
    ShowStockGrid()
  Endif

End

Public Sub mnudelete_Click()

  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      If $tbl = "tblmedbrand" Then
        modDatabase.$myConn.Delete("tblmedbrand", "fldbrandid=&1", GridView1[GridView1.Row, 1].Text)
      Else If $tbl = "tblsurgbrand" Then
        modDatabase.$myConn.Delete("tblsurgbrand", "fldbrandid=&1", GridView1[GridView1.Row, 1].Text)
      Else If $tbl = "tblextrabrand" Then
        modDatabase.$myConn.Delete("tblextrabrand", "fldbrandid=&1", GridView1[GridView1.Row, 1].Text)
      Endif
    Endif
    ShowStockGrid()
    Try GridView1.Row = Row
  Endif

End

Public Sub GridView1_Click()

  Dim hForm As FRequest
  Dim sql As String
  Dim res As Result

  If GridView1.Column = 4 Then
    sql = "select fldstockno,fldstockid,fldbatch,fldexpiry,fldqty,fldsellpr,fldstatus,fldcomp from tblentry where fldstockid=&1 and fldqty>&2"
    res = modDatabase.$syConn.Exec(sql, GridView1[GridView1.Row, 1].Text, 0)
    If res.Available Then
      hForm = New FRequest(res, GridView1[GridView1.Row, 1].Text, "StockDB", False)
      hForm.ShowModal
    Endif
  Endif

End
