' Gambas class file

Private $sTitle As String
Private $ItemList As String[]
Private $FullList As String[]
Private $xControl As Control
Private $sHeight As Integer

Private $aMyFields As String[]
Private $sValue As String

Public Sub Run(Prompt As String, ItemList As String[], FullList As String[], Optional xControl As Control, Optional sHeight As Integer) As String

  $sTitle = Prompt
  $ItemList = ItemList
  $FullList = FullList

  If xControl Then
    $xControl = xControl
  Endif
  If sHeight Then
    $sHeight = sHeight
  Endif
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $sTitle
  If modBasic.$ItemListModalCentre = "Yes" Then
  Else
    If $xControl Then
      Me.Left = $xControl.ScreenX
      Me.Top = $xControl.ScreenY
      Me.Width = $xControl.Width  'Max($xControl.Width, Me.Width)
      Me.Border = False
    Endif
    If $sHeight Then
      Me.Height = $sHeight
    Endif
  Endif

  lstletter.List = modString.GetFirstLetterArray($FullList)
  chkleftmain.Value = modBasic.$SearchBothSide
  FillItemGrid()
  txtname.SetFocus

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub FillItemGrid()

  GridView1.Clear()
  $aMyFields = modString.SelectStringArrayToText(txtname.Text, $FullList, chkleftmain.Value)
  GridView1.Columns.Count = 2
  GridView1.Rows.Count = $aMyFields.Count

  GridView1.Rows.Height = modBasic.$AppGridRowHeight
  GridView1.Columns[0].Width = 25 * modBasic.$AppWidthRatio
  GridView1.Columns[1].Expand = True
  GridView1.Row = 0

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    If $ItemList.Exist($aMyFields[Row]) = True Then
      GridView1.Data.Picture = Picture["icons/null.svg"]
    Else
      GridView1.Data.Picture = Picture["icons/true.svg"]
    Endif
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $aMyFields[Row]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub txtname_Change()

  FillItemGrid()

End

Public Sub lstletter_Click()

  chkleftmain.Value = False
  txtname.Text = lstletter.Text
  GridView1.SetFocus

End

Public Sub GridView1_KeyRelease()

  If GridView1.Rows.Count > 0 Then
    If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
      $sValue = GridView1[GridView1.Row, 1].Text
      Me.Close(True)
    Endif
  Endif

End

Public Sub btnok_Click()

  If GridView1.Rows.Count > 0 Then
    $sValue = GridView1[GridView1.Row, 1].Text
    Me.Close(True)
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Else If Key.Code = Key.Up Then
    GridView1.Row = GridView1.Rows.Count - 1
    GridView1.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If GridView1.Rows.Count > 0 Then
      $sValue = GridView1[GridView1.Row, 1].Text
      Me.Close(True)
    Endif
  Endif

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtname_Click()

  txtname.Text = ""
  FillItemGrid()

End

Public Sub Form_Deactivate()

  Me.Close()

End
