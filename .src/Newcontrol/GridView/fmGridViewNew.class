' Gambas class file

Private $sTitle As String
Private $ItemList As String[]
Private $xControl As Control
Private $sHeight As Integer

Private $aMyFields As String[]
Private $sValue As String
Private $CustText As Boolean

Public Sub Run(Prompt As String, ItemList As String[], CustomText As Boolean, Optional xControl As Control, Optional sHeight As Integer) As String

  $sTitle = Prompt
  $ItemList = ItemList
  $CustText = CustomText

  If xControl Then
    $xControl = xControl
  Endif
  If sHeight Then
    $sHeight = sHeight
  Endif
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $sTitle
  If modBasic.$ItemListModalCentre = "Yes" Then
  Else
    If $xControl Then
      Me.Left = $xControl.ScreenX
      Me.Top = $xControl.ScreenY
      Me.Width = $xControl.Width  'Max($xControl.Width, Me.Width)
      Me.Border = False
    Endif
    If $sHeight Then
      Me.Height = $sHeight
    Endif
  Endif

  lstletter.List = modString.GetFirstLetterArray($ItemList)
  chkleftmain.Value = modBasic.$SearchBothSide
  FillItemGrid()
  txtname.SetFocus

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub FillItemGrid()

  GridView1.Clear()
  $aMyFields = modString.SelectStringArrayToText(txtname.Text, $ItemList, chkleftmain.Value)
  GridView1.Columns.Count = 1
  GridView1.Rows.Count = $aMyFields.Count

  GridView1.Rows.Height = modBasic.$AppGridRowHeight
  GridView1.Row = 0

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $aMyFields[Row]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub txtname_Change()

  FillItemGrid()

End

Public Sub lstletter_Click()

  chkleftmain.Value = False
  txtname.Text = lstletter.Text
  GridView1.SetFocus

End

Public Sub GridView1_KeyRelease()

  If GridView1.Rows.Count > 0 Then
    If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
      $sValue = GridView1[GridView1.Row, 0].Text
      Me.Close(True)
    Endif
  Endif

End

Public Sub btnok_Click()

  If GridView1.Rows.Count > 0 Then
    $sValue = GridView1[GridView1.Row, 0].Text
    Me.Close(True)
  Else
    If $CustText = True Then
      If Trim(txtname.Text) Then
        $sValue = Trim(txtname.Text)
        Me.Close(True)
      Endif
    Endif
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Else If Key.Code = Key.Up Then
    GridView1.Row = GridView1.Rows.Count - 1
    GridView1.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If GridView1.Rows.Count > 0 Then
      $sValue = GridView1[GridView1.Row, 0].Text
      Me.Close(True)
    Else
      If $CustText = True Then
        If Trim(txtname.Text) Then
          $sValue = Trim(txtname.Text)
          Me.Close(True)
        Endif
      Endif
    Endif
  Endif

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtname_Click()

  txtname.Text = ""
  FillItemGrid()

End

Public Sub Form_Deactivate()

  Me.Close()

End
