' Gambas class file

Private $dictPath As String

Private $sValue As String

Public Sub Run(dictPath As String) As String

  $dictPath = dictPath

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  txtselect.SetFocus
  lstletter.List = modString.GetFirstLetterArray(GetChapterList($dictPath))
  DisplayDictgrid()

End

Private Sub GetChapterList(xdictPath As String) As String[]

  Dim xxx As String[]
  Dim ConfFile As Settings
  Dim xtitle As String
  Dim xkey As String

  xxx = New String[]
  ConfFile = New Settings(xdictPath)
  For Each xtitle In ConfFile.Keys
    If xtitle = "Dictionary" Then
      For Each xkey In ConfFile.Keys[xtitle]
        If LCase(xkey) Like LCase(txtselect.Text) & "*" Then
          xxx.Add(xkey)
        Endif
      Next
    Endif
  Next
  Return xxx

End

Private Sub DisplayDictgrid()

  If Exist($dictPath) Then
    ListBox1.List = GetChapterList($dictPath)
    ListBox1.Index = 0
  Endif

End

Public Sub lstletter_Click()

  txtselect.Text = lstletter.Text

End

Public Sub txtselect_Change()

  DisplayDictgrid()

End

Public Sub txtselect_KeyRelease()

  If Key.Code = Key.Down Then
    ListBox1.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    $sValue = modControlSub.ShowSettingValue($dictPath, "Dictionary/" & ListBox1.Text)
    Me.Close(True)
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If ListBox1.Text Then
      $sValue = modControlSub.ShowSettingValue($dictPath, "Dictionary/" & ListBox1.Text)
      Me.Close(True)
    Else
      Me.Close
    Endif
  Else If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub btnok_Click()

  If ListBox1.Text Then
    $sValue = modControlSub.ShowSettingValue($dictPath, "Dictionary/" & ListBox1.Text)
    Me.Close(True)
  Else
    Me.Close
  Endif

End

Public Sub txtselect_Click()

  txtselect.Text = ""

End

Public Sub btnclose_Click()

  Me.Close

End
