' Gambas class file

Private $sValue As String
Private $sCol As String[]
Private $sStr As String

Private aObj As New Object[10]
Private aObk As New Object[10]

Public Sub Run(sColumn As String[], sTitle As String, sText As String) As String

  Dim xx As String
  Dim i As Integer
  Dim xcol As String[]
  Dim AppWidth As Float
  Dim AppHeight As Float

  AppWidth = modBasic.$AppScaleFactor
  AppHeight = modBasic.$AppScaleFactor
  lblname.Text = sTitle
  $sCol = sColumn
  $sStr = sText

  i = 1
  xcol = New String[]
  For Each xx In $sCol
    aObj[i] = New Label(Frame1) As "LabelGroup"
    aObk[i] = New TextBox(Frame1) As "TextGroup"
    With aObj[i]
      .X = 5 * AppWidth
      .Y = ((i - 1) * 25 + 5) * AppHeight
      .Width = 125 * AppWidth
      .Height = 25 * AppHeight
      .Text = xx
      .Tag = i
    End With
    xcol.Add(xx)

    With aObk[i]
      .X = 150 * AppWidth
      .Y = ((i - 1) * 25 + 5) * AppHeight
      .Width = 300 * AppWidth
      .Height = 25 * AppHeight
      .Text = ""
      .Tag = i
    End With
    i = i + 1
  Next

  If $sStr = "" Then
    $sStr = xcol.Join(";")
  Endif
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  DisplayAgegrid()

End

Private Sub DisplayAgegrid()

  Dim $ageFile As String[]
  Dim sline As String
  Dim sCol As String[]
  Dim Row As Integer
  Dim Column As Integer

  GridView1.Clear
  GridView1.Columns.Count = $sCol.Count
  GridView1.Rows.Count = Split($sStr, gb.NewLine).Count - 1

  Row = 0
  $ageFile = Split($sStr, gb.NewLine)
  For Each sline In $ageFile
    sCol = Split(sline, ";")
    If sline = $ageFile[0] Then
    Else
      For Column = 0 To sCol.Count - 1
        GridView1[Row - 1, Column].Text = sCol[Column]
      Next
    Endif
    Row = Row + 1
  Next

  For Column = 0 To $sCol.Count - 1
    GridView1.Columns[Column].Text = $sCol[Column]
    Try GridView1.Columns.Width = CInt(Floor(Frame1.Width / sCol.Count))
  Next

End

Public Sub GridView1_Click()

  Dim Column As Integer

  For Column = 0 To GridView1.Columns.Count - 1
    aObk[Column + 1].Text = GridView1[GridView1.Row, Column].Text
  Next

End

Private Function GetTableData() As String

  Dim Row As Integer
  Dim Column As Integer
  Dim sCol As String[]
  Dim sRow As String[]
  Dim xval As String

  sRow = New String[]
  sRow.Add($sCol.Join(";"))
  For Row = 0 To GridView1.Rows.Count - 1
    sCol = New String[]
    For Column = 0 To GridView1.Columns.Count - 1
      sCol.Add(GridView1[Row, Column].Text)
    Next
    sRow.Add(sCol.Join(";"))
  Next
  xval = sRow.Join(gb.NewLine)
  Return xval

End

Public Sub btnapply_Click()

  $sValue = GetTableData()
  Me.Close(True)

End

Public Sub btnadd_Click()

  Dim xx As String[]
  Dim i As Integer

  xx = New String[]
  For i = 1 To $sCol.Count
    xx.Add(aObk[i].Text)
  Next
  $sStr = GetTableData() & gb.NewLine & xx.Join(";")
  DisplayAgegrid()

End

Public Sub btnedit_Click()

  Dim Row As Integer
  Dim Column As Integer
  Dim sCol As String[]
  Dim sRow As String[]

  sRow = New String[]
  sRow.Add($sCol.Join(";"))
  For Row = 0 To GridView1.Rows.Count - 1
    sCol = New String[]
    If Row = GridView1.Row Then
      For Column = 0 To GridView1.Columns.Count - 1
        sCol.Add(aObk[Column + 1].Text)
      Next
    Else
      For Column = 0 To GridView1.Columns.Count - 1
        sCol.Add(GridView1[Row, Column].Text)
      Next
    Endif
    sRow.Add(sCol.Join(";"))
  Next
  $sStr = sRow.Join(gb.NewLine)
  DisplayAgegrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close()
  Endif

End
