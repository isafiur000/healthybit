' Gambas class file

Private $himage As New Image
Private $sValue As String
Private $sMode As String
Private $Resol As String
Private $Bright As Integer
Private $xComm As String
Private $xQuote As String

Public Sub Run() As String

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  Dim xmode As String
  Dim xbright As String

  cmbscanner.Text = modSettings.ShowSettingFromFIle("ScannerDevice/Default")
  $xQuote = modSettings.ShowSettingFromFIle("ScannerDevice/QuoteDevice")
  xmode = modSettings.ShowSettingFromFIle("ScannerDevice/ScanMode")
  If xmode Then
    $sMode = xmode
  Else
    $sMode = "Color"
  Endif
  $Resol = modSettings.ShowSettingFromFIle("ScannerDevice/Resolution")
  xbright = modSettings.ShowSettingFromFIle("ScannerDevice/Brightness")
  If xbright Then
    $Bright = CInt(xbright)
  Endif
  $xComm = modSettings.ShowSettingFromFIle("ScannerDevice/Command")

End

Public Sub btnload_Click()

  cmbscanner.List = modApplication.ScannerList()

End

Public Sub btnscanhelp_Click()

  Dim xx As String
  Dim yy As String

  xx = modApplication.ScannerListString()
  If xx Then
    yy = GetTextArea("Scanner List", xx)
  Endif

End

Public Sub btnscanner_Click()

  Dim xdevice As String
  Dim sPath As String
  Dim arcmd As String[]

  If cmbscanner.Text Then
    ProgressBar1.Value = 0

    If $xQuote = "Yes" Then
      xdevice = "'" & cmbscanner.Text & "'"
    Else
      xdevice = cmbscanner.Text
    Endif

    Inc Application.Busy
    sPath = Temp() & ".png"
    ' arcmd = ["scanimage", "-d", cmbscanner.Text, "--mode=" & $sMode, "--format=png", "-o", sPath]
    arcmd = ["scanimage", "-d", xdevice, "--mode=" & $sMode, "--format=png"]
    If $Bright Then
      arcmd.Add("--brightness")
      arcmd.Add(CStr($Bright))
    Endif
    If $Resol Then
      arcmd.Add("--resolution")
      arcmd.Add($Resol)
    Endif
    arcmd.Add("-o")
    arcmd.Add(sPath)
    modBasic.DebugString("Execute " & arcmd.Join(Space(1)))
    If $xComm = "Shell" Then
      Shell arcmd.Join(Space(1)) Wait
    Else
      Exec arcmd Wait
    Endif
    $himage = Image.Load(sPath)
    Dec Application.Busy

    DrawingArea1.Refresh()
    ProgressBar1.Value = 1
    Wait

    $sValue = sPath
  Endif

Catch
  Dec Application.Busy
  ProgressBar1.Value = 0
  Message.Warning("Please Reset Scanner Path", ("OK"))

End

Public Sub DrawingArea1_Draw()

  Draw.Image($himage, 0, 0)

End

Public Sub btnclose_Click()

  Me.Close(True)

End

Public Sub btnsave_Click()

  If cmbscanner.Text Then
    modSettings.SaveSettingsToFile("ScannerDevice/Default", cmbscanner.Text)
  Endif

End
