' Gambas class file

Private $sValue As String[]
Private $sImage As String
Private $PenColor As Integer
Private $PenWidth As Integer
Private pointArr As Collection

Private pointw_x As Float[]
Private pointw_y As Float[]
Private strList As String[]
Private sInsert As String

Public Sub Run(Optional sImage As String) As String[]

  If sImage Then
    $sImage = sImage
  Else
    $sImage = ""
  Endif
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  $PenColor = Color.Red
  $PenWidth = 3

  pointArr = New Collection
  pointw_x = New Float[]
  pointw_y = New Float[]
  strList = New String[]
  cmbimage.List = LoadImageTypes()
  cmbimage.Text = ""
  If $sImage Then
    LoadPicture($sImage)
  Endif
  btnrefresh.SetFocus

End

Public Sub coldatacurve_Change()

  If coldatacurve.Value Then
    $PenColor = coldatacurve.Value
  Endif

End

Public Sub txtickval_Change()

  If txtickval.Value Then
    $PenWidth = txtickval.Value
  Endif

End

Public Sub DrawingArea1_MouseMove()

  Dim xx As Float
  Dim yy As Float
  Dim aColl As Collection

  xx = Mouse.X
  yy = Mouse.Y
  aColl = New Collection
  aColl.Add(xx, "X")
  aColl.Add(yy, "Y")
  aColl.Add($PenColor, "Color")
  aColl.Add($PenWidth, "Width")
  Try pointArr.Add(aColl, CStr(xx) & ":" & CStr(yy))

  DrawingArea1.Refresh

End

Public Sub DrawingArea1_Draw()

  Dim i As Integer
  Dim j As Integer

  For i = 0 To pointArr.Keys.Count - 1
    Paint.Brush = Paint.Color(pointArr[pointArr.Keys[i]]["Color"])
    Paint.LineWidth = pointArr[pointArr.Keys[i]]["Width"]
    If i = 0 Then
      Paint.MoveTo(pointArr[pointArr.Keys[i]]["X"], pointArr[pointArr.Keys[i]]["Y"])
    Else
      If Abs(pointArr[pointArr.Keys[i]]["X"] - pointArr[pointArr.Keys[i - 1]]["X"]) > 20 Or If Abs(pointArr[pointArr.Keys[i]]["Y"] - pointArr[pointArr.Keys[i - 1]]["Y"]) > 20 Then                   ''
        Paint.MoveTo(pointArr[pointArr.Keys[i]]["X"], pointArr[pointArr.Keys[i]]["Y"])
      Else
        Paint.LineTo(pointArr[pointArr.Keys[i]]["X"], pointArr[pointArr.Keys[i]]["Y"])
      Endif
    Endif
  Next
  Paint.Stroke()

  For j = 0 To pointw_x.Max
    Paint.DrawText(strList[j], pointw_x[j], pointw_y[j])
  Next

  ' For i = 0 To pointsArr_x.Max
  '   If txtickval.Value Then
  '     Paint.Arc(pointsArr_x[i], pointsArr_y[i], txtickval.Value)
  '   Else
  '     Paint.Arc(pointsArr_x[i], pointsArr_y[i], 3)
  '   Endif
  '   Paint.Fill
  ' Next

End

Public Sub btnclear_Click()

  pointArr.Clear()
  DrawingArea1.Clear()
  DrawingArea1.Refresh()

End

Public Sub btnsave_Click()

  Dim xpict As Picture
  Dim xpath As String

  Inc Application.Busy
  xpict = Desktop.Screenshot(Me.X, Me.Y + Panel1.Height, DrawingArea1.Width, DrawingArea1.Height)
  xpath = Temp() & ".png"
  xpict.Save(xpath)
  Dec Application.Busy

  If Exist(xpath) Then
    $sValue = [xpath, cmbimage.Text & "-" & modString.GetDateString(Now())]
    Me.Close(True)
  Endif

End

Public Sub btnexit_Click()

  Me.Close

End

''========================= Text Insert =============================
Public Sub btninserttext_Click()

  Dim xtext As String

  xtext = InputBox("Write String to insert", "Draw Text", "")
  If xtext Then
    sInsert = xtext
  Endif

End

Public Sub DrawingArea1_DblClick()

  If sInsert Then
    pointw_x.Add(Mouse.X)
    pointw_y.Add(Mouse.Y)
    strList.Add(sInsert)
    DrawingArea1.Refresh
    sInsert = ""
  Endif

End

''================== background image ===============================
Public Sub btnimageadd_Click()

  fmImageList.Close
  fmImageList.ShowModal
  cmbimage.List = LoadImageTypes()

End

Private Function LoadImageTypes() As String[]

  Dim i As Integer
  Dim xxx As String[]
  Dim xPath As String

  xxx = New String[]
  For i = 1 To 20
    xPath = modSettings.ShowSettingFromFIle("DrawBackground" & CStr(i) & "/Name")
    If xPath Then
      xxx.Add(xPath)
    Endif
  Next
  Return xxx

End

Public Sub btnrefresh_Click()

  Dim xpath As String
  Dim i As Integer

  If cmbimage.Text Then
    For i = 1 To 20
      If modSettings.ShowSettingFromFIle("DrawBackground" & CStr(i) & "/Name") = cmbimage.Text Then
        xpath = modSettings.ShowSettingFromFIle("DrawBackground" & CStr(i) & "/Path")
        LoadPicture(xpath)
        Break
      Endif
    Next
  Else
    If $sImage Then
      LoadPicture($sImage)
    Endif
  Endif

End

Private Sub LoadPicture(xpath As String)

  If xpath And If Exist(xpath) Then
    If chkstretch.Value = True Then
      PictureBox1.Picture = Image.Load(xpath).Stretch(PictureBox1.Width, PictureBox1.Height).Picture
    Else
      PictureBox1.Picture = Image.Load(xpath).Picture
    Endif
    DrawingArea1.Background = Color.Transparent
  Endif

End

Public Sub cmbimage_GotFocus()

  If modBasic.$TabletModeEnable = "Yes" Then
    If Not cmbimage.Text Then
      cmbimage.Popup()
    Endif
  Endif

End
