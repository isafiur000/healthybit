' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $sValue As Variant[]

Public Sub Run(sTitle As String) As Variant[]

  Me.Title = sTitle
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  chkleftmain.Value = modBasic.$SearchBothSide
  modSettings.ShowCheckBox(chkgroup, "ClinicForms/ICDGroups")
  chkboth.Value = True

  $sValue = New Variant[]
  FillICDGrid()
  txtname.SetFocus

End

Public Sub chkgroup_Click()

  modSettings.EnterCheckSetting(chkgroup, "ClinicForms/ICDGroups")

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub FillICDGrid()

  Dim xstr As String
  Dim xgrp As String

  If chkboth.Value = True Then
    xstr = " and fldoldcode IS NOT NULL"
  Else
    xstr = ""
  Endif
  If chkgroup.Value = True Then
    xgrp = " and fldicdcode NOT LIKE &2"
  Else
    xgrp = ""
  Endif

  If txtcode.Text Then
    $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease where lower(fldicdcode) like &1" & xstr & xgrp, LCase(txtcode.Text) & "%", "%.%")
  Else If txtname.Text Then
    If chkleftmain.Value = True Then
      $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease where lower(fldicdnme) like &1" & xstr & xgrp, "%" & LCase(txtname.Text) & "%", "%.%")
    Else
      $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease where lower(fldicdnme) like &1" & xstr & xgrp, LCase(txtname.Text) & "%", "%.%")
    Endif

  Else
    If chkgroup.Value = True Then
      If chkboth.Value = True Then
        $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease where fldoldcode IS NOT NULL and fldicdcode NOT LIKE &1", "%.%")
      Else
        $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease where fldicdcode NOT LIKE &1", "%.%")
      Endif
    Else
      If chkboth.Value = True Then
        $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease where fldoldcode IS NOT NULL")
      Else
        $rData = modDatabase.$icdConn.Exec("select fldicdcode,fldicdnme,fldicdcode,fldoldcode from tblicddisease")
      Endif
    Endif

  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 25 * modBasic.$AppWidthRatio
    .Columns[1].Width = GridView1.Width - 145 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1

    .Columns[1].Text = "Particulars"
    .Columns[2].Text = "Code"
  End With
  GridView1.Row = 0

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Picture = Picture["icons/unchecked.png"]
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
    GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
    GridView1.Data.WordWrap = True
  Endif

End

Private Sub GetCheckUncheckGrid()

  If GridView1[GridView1.Row, 0].Picture = Picture["icons/unchecked.png"] Then
    GridView1[GridView1.Row, 0].Picture = Picture["icons/checked.png"]
  Else If GridView1[GridView1.Row, 0].Picture = Picture["icons/checked.png"] Then
    GridView1[GridView1.Row, 0].Picture = Picture["icons/unchecked.png"]
  Endif

End

Public Sub GridView1_Click()

  If GridView1.Rows.Count > 0 Then
    GetCheckUncheckGrid()
  Endif

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Endif

End

Public Sub txtname_Change()

  txtcode.Text = ""
  If txtname.Text Then
    FillICDGrid()
  Endif

End

Public Sub txtcode_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Endif

End

Public Sub txtcode_Change()

  txtname.Text = ""
  If txtcode.Text Then
    FillICDGrid()
  Endif

End

Public Sub txtname_Click()

  txtname.Text = ""
  FillICDGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtcode_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub btninsert_Click()

  Dim Row As Integer
  Dim xxx As String[]

  For Row = 0 To GridView1.Rows.Count - 1
    If GridView1[Row, 0].Picture = Picture["icons/checked.png"] Then
      xxx = New String[]
      xxx.Add(GridView1[Row, 1].Text)
      xxx.Add(GridView1[Row, 2].Text)
      xxx.Add(GridView1[Row, 3].Text)
      $sValue.Add(xxx)
      GridView1[Row, 0].Picture = Picture["icons/false.svg"]
    Endif
  Next
  Balloon.Info(("Information added"), GridView1)
  Balloon.Delay = modBasic.$BalloonDelay
  txtname.SetFocus

End

Public Sub btnok_Click()

  Me.Close(True)

End

Public Sub chkall_Click()

  Dim i As Integer

  If chkall.Value = True Then
    For i = 0 To GridView1.Rows.Count - 1
      GridView1[i, 0].Picture = Picture["icons/checked.png"]
    Next
  Else If chkall.Value = False Then
    For i = 0 To GridView1.Rows.Count - 1
      GridView1[i, 0].Picture = Picture["icons/unchecked.png"]
    Next
  Endif

End
