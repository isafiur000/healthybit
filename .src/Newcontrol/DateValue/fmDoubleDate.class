' Gambas class file

Private $sValue As Date[]
Private $MonLimit As Integer

Public Sub Run(Prompt As String, Title As String, sTime As Date[], Optional sMonthLimit As Integer) As Date[]

  lblitem.Text = Prompt
  If Title Then
    Me.Title = Title
  Else
    Me.Title = Application.ActiveWindow.Title
  Endif
  dtfirst.Value = sTime[0]
  dtlast.Value = sTime[1]
  If sMonthLimit Then
    $MonLimit = sMonthLimit
  Else
    Panel2.Visible = True
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbfiscal.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldlabel) as col from tblfiscalid"))
  dtfirst.SetFocus

End

Public Sub cmbfiscal_Click()

  Dim res As Result

  If cmbfiscal.Text Then
    res = modDatabase.$myConn.Exec("select fldfrom,fldend from tblfiscalid where fldlabel=&1", cmbfiscal.Text)
    If res.Available Then
      res.MoveFirst
      dtfirst.Value = res["fldfrom"]
      dtlast.Value = res["fldend"]
    Endif
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtfirst.Value))
  If xx Then
    dtfirst.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtneplast_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlast.Value))
  If xx Then
    dtlast.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnclear_Click()

  dtfirst.Value = ""
  dtlast.Value = ""

End

Public Sub btnsave_Click()

  Dim xx As Date[]

  If $MonLimit Then
    If DateDiff(dtfirst.Value, dtlast.Value, gb.Month) > $MonLimit Then
      dtlast.Value = DateAdd(dtfirst.Value, gb.Month, $MonLimit)
    Endif
  Endif

  If dtfirst.Value And If dtlast.Value Then
    xx = New Date[]
    xx.Add(dtfirst.Value)
    xx.Add(dtlast.Value)
    $sValue = xx
    Me.Close(True)
  Endif

End
