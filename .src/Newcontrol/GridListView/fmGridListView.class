' Gambas class file

Private $sTitle As String
Private $ItemList As String[]
Private $xControl As Control
Private $sHeight As Integer

Private $aMyFields As String[]
Private $sValue As String[]

Public Sub Run(Prompt As String, ItemList As String[], Optional xControl As Control, Optional sHeight As Integer) As String[]

  $sTitle = Prompt
  $ItemList = ItemList

  If xControl Then
    $xControl = xControl
  Endif
  If sHeight Then
    $sHeight = sHeight
  Endif
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $sTitle
  If modBasic.$ItemListModalCentre = "Yes" Then
  Else
    If $xControl Then
      Me.Left = $xControl.ScreenX
      Me.Top = $xControl.ScreenY
      Me.Width = $xControl.Width  'Max($xControl.Width, Me.Width)
      Me.Border = False
    Endif
    If $sHeight Then
      Me.Height = $sHeight
    Endif
  Endif

  $sValue = New String[]
  lstletter.List = modString.GetFirstLetterArray($ItemList)
  chkleftmain.Value = modBasic.$SearchBothSide
  FillItemGrid()
  txtname.SetFocus

End

Public Sub chksort_Click()

  txtname.Text = ""
  $ItemList.Sort()
  lstletter.List = modString.GetFirstLetterArray($ItemList)
  chkleftmain.Value = modBasic.$SearchBothSide
  chksort.Enabled = False
  FillItemGrid()
  txtname.SetFocus

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub txtname_Change()

  FillItemGrid()

End

Public Sub lstletter_Click()

  chkleftmain.Value = False
  txtname.Text = lstletter.Text

End

Public Sub txtname_LostFocus()

  GridView1.SetFocus

End

Public Sub FillItemGrid()

  GridView1.Clear()
  $aMyFields = modString.SelectStringArrayToText(txtname.Text, $ItemList, chkleftmain.Value)
  GridView1.Columns.Count = 2
  GridView1.Rows.Count = $aMyFields.Count

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 25 * modBasic.$AppWidthRatio
    .Columns[1].Width = GridView1.Width - (35 * modBasic.$AppWidthRatio)
  End With
  GridView1.Row = 0

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Picture = Picture["icons/unchecked.png"]
    GridView1.Data.Text = ""
  Else If Column = 1 Then
    GridView1.Data.Text = $aMyFields[Row]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Private Sub GetCheckUncheckGrid()

  If GridView1[GridView1.Row, 0].Picture = Picture["icons/unchecked.png"] Then
    GridView1[GridView1.Row, 0].Picture = Picture["icons/checked.png"]
  Else If GridView1[GridView1.Row, 0].Picture = Picture["icons/checked.png"] Then
    GridView1[GridView1.Row, 0].Picture = Picture["icons/unchecked.png"]
  Endif

End

Public Sub GridView1_Click()

  If GridView1.Rows.Count > 0 Then
    GetCheckUncheckGrid()
  Endif

End

Public Sub GridView1_KeyRelease()

  If Key.Code = Key.Space Then
    GetCheckUncheckGrid()
  Endif

End

Public Sub Form_KeyRelease()

  Dim i As Integer

  If Key.Code = Key.Insert Then
    For i = 0 To GridView1.Rows.Count - 1
      If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
        $sValue.Add(GridView1[i, 1].Text)
        GridView1[i, 0].Picture = Picture["icons/false.svg"]
      Endif
    Next
    Balloon.Info(("Information added"), GridView1)
    Balloon.Delay = modBasic.$BalloonDelay
    txtname.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    Me.Close(True)
  Else If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub btninsert_Click()

  Dim i As Integer

  For i = 0 To GridView1.Rows.Count - 1
    If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
      $sValue.Add(GridView1[i, 1].Text)
      GridView1[i, 0].Picture = Picture["icons/false.svg"]
    Endif
  Next
  Balloon.Info(("Information added"), GridView1)
  Balloon.Delay = modBasic.$BalloonDelay
  lstletter.SetFocus

End

Public Sub btnok_Click()

  Me.Close(True)

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Else If Key.Code = Key.Up Then
    GridView1.Row = GridView1.Rows.Count - 1
    GridView1.SetFocus
  Endif

End

Public Sub chkall_Click()

  Dim i As Integer

  If chkall.Value = True Then
    For i = 0 To GridView1.Rows.Count - 1
      GridView1[i, 0].Picture = Picture["icons/checked.png"]
    Next
  Else If chkall.Value = False Then
    For i = 0 To GridView1.Rows.Count - 1
      GridView1[i, 0].Picture = Picture["icons/unchecked.png"]
    Next
  Endif

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtname_Click()

  txtname.Text = ""
  FillItemGrid()

End

Public Sub Form_Deactivate()

  Me.Close()

End
