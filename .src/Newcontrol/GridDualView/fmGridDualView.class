' Gambas class file

Private $sList As String[]
Private $GridDataList As String[]
Private $GridWorkList As String[]
Private $sValue As String[]

Public Sub Run(sTitle As String, sList As String[]) As String[]

  Me.Title = sTitle
  $sList = sList
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  $sValue = New String[]
  $GridDataList = $sList
  lstletter.List = modString.GetFirstLetterArray($sList)
  $GridWorkList = $GridDataList
  chkleftmain.Value = modBasic.$SearchBothSide
  FillICDGrid()
  txtname.SetFocus

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub FillICDGrid()

  GridView1.Clear()
  GridView1.Columns.Count = 3
  GridView1.Rows.Count = $GridWorkList.Count

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 25 * modBasic.$AppWidthRatio
    .Columns[1].Width = GridView1.Width - (135 * modBasic.$AppWidthRatio)
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[1].Text = "Particulars"
    .Columns[2].Text = "Code"
  End With
  GridView1.Row = 0

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  Dim sLip As String[]

  sLip = Split($GridWorkList[Row], "|")
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Picture = Picture["icons/unchecked.png"]
  Else If Column = 1 Then
    GridView1.Data.Text = sLip[0]
    GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
    GridView1.Data.WordWrap = True
  Else If Column = 2 Then
    GridView1.Data.Text = sLip[1]
  Endif

End

Private Sub GetCheckUncheckGrid()

  If GridView1[GridView1.Row, 0].Picture = Picture["icons/unchecked.png"] Then
    GridView1[GridView1.Row, 0].Picture = Picture["icons/checked.png"]
  Else If GridView1[GridView1.Row, 0].Picture = Picture["icons/checked.png"] Then
    GridView1[GridView1.Row, 0].Picture = Picture["icons/unchecked.png"]
  Endif

End

Public Sub GridView1_Click()

  If GridView1.Rows.Count > 0 Then
    GetCheckUncheckGrid()
  Endif

End

Public Sub GridView1_KeyRelease()

  If Key.Code = Key.Space Then
    GetCheckUncheckGrid()
  Endif

End

Private Sub SortSelectedRows()

  Dim xx As String
  Dim asx As String[]
  Dim xfinal As String[]

  xfinal = New String[]
  If $GridDataList Then
    For Each xx In $GridDataList
      asx = Split(xx, "|")
      If txtcode.Text Then
        If LCase(asx[1]) Like LCase(txtcode.Text) & "*" Then
          xfinal.Add(asx[0] & "|" & asx[1])
        Endif
      Else If txtname.Text Then
        If chkleftmain.Value Then
          If LCase(asx[0]) Like "*" & LCase(txtname.Text) & "*" Then
            xfinal.Add(asx[0] & "|" & asx[1])
          Endif
        Else
          If LCase(asx[0]) Like LCase(txtname.Text) & "*" Then
            xfinal.Add(asx[0] & "|" & asx[1])
          Endif
        Endif
      Endif
    Next
  Endif
  If xfinal Then
    xfinal.Sort()
  Endif
  $GridWorkList = xfinal
  FillICDGrid()

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Endif

End

Public Sub txtname_Change()

  txtcode.Text = ""
  If txtname.Text Then
    If chkleftmain.Value = False Then
      txtname.Text = String.UCaseFirst(txtname.Text)
    Endif
    SortSelectedRows()
  Else
    $GridWorkList = $GridDataList
    FillICDGrid()
  Endif

End

Public Sub lstletter_Click()

  chkleftmain.Value = False
  txtname.Text = lstletter.Text
  SortSelectedRows()

End

Public Sub txtcode_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Endif

End

Public Sub txtcode_Change()

  txtname.Text = ""
  If txtcode.Text Then
    txtcode.Text = UCase(txtcode.Text)
    SortSelectedRows()
  Else
    $GridWorkList = $GridDataList
    FillICDGrid()
  Endif

End

Public Sub txtname_Click()

  txtname.Text = ""
  $GridWorkList = $GridDataList
  FillICDGrid()

End

Public Sub Form_KeyRelease()

  Dim i As Integer

  If Key.Code = Key.Insert Then
    For i = 0 To GridView1.Rows.Count - 1
      If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
        $sValue.Add(GridView1[i, 1].Text)
        GridView1[i, 0].Picture = Picture["icons/false.svg"]
      Endif
    Next
    Balloon.Info(("Information added"), GridView1)
    Balloon.Delay = modBasic.$BalloonDelay
    txtname.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    Me.Close(True)
  Else If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub btninsert_Click()

  Dim i As Integer

  For i = 0 To GridView1.Rows.Count - 1
    If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
      $sValue.Add(GridView1[i, 1].Text)
      GridView1[i, 0].Picture = Picture["icons/false.svg"]
    Endif
  Next
  Balloon.Info(("Information added"), GridView1)
  Balloon.Delay = modBasic.$BalloonDelay
  lstletter.SetFocus

End

Public Sub btnok_Click()

  Me.Close(True)

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtcode_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End
