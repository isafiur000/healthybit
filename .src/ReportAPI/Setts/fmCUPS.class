' Gambas class file

Private $sValue As String

Public Sub Run() As String

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  Me.Center
  cmbmediasize.List = ["Letter", "Legal", "A4", "A5", "COM10", "DL", "Custom"]
  cmbmediaunit.List = ["in", "cm", "mm"]
  cmborientation.List = ["portrait", "landscape"]
  cmbrotation.List = ["no rotation", "90 degrees", "270 degrees", "180 degrees"]
  cmbsideprint.List = ["one-sided", "two-sided-short-edge", "two-sided-long-edge"]
  cmbpageborder.List = ["double", "double-thick", "single", "single-thick"]
  cmbscaletofit.List = ["fit-to-page"]

End

Public Sub btnsave_Click()

  Dim xcmd As String[]
  Dim xmedia As String

  xcmd = New String[]
  If cmbmediasize.Text Then
    If cmbmediasize.Text = "Custom" Then
      xmedia = "-o media=Custom." & txtmediawidth.Text & "x" & txtmedialength.Text & cmbmediaunit.Text
    Else
      xmedia = "-o media=" & cmbmediasize.Text
    Endif
    xcmd.Add(xmedia)
  Endif
  If cmborientation.Text Then
    xcmd.Add("-o " & cmborientation.Text)
  Endif
  If cmbrotation.Text Then
    If cmbrotation.Text = "no rotation" Then
      xcmd.Add("-o orientation-requested=3")
    Else If cmbrotation.Text = "90 degrees" Then
      xcmd.Add("-o orientation-requested=4")
    Else If cmbrotation.Text = "270 degrees" Then
      xcmd.Add("-o orientation-requested=5")
    Else If cmbrotation.Text = "180 degrees" Then
      xcmd.Add("-o orientation-requested=6")
    Endif
  Endif
  If cmbsideprint.Text Then
    xcmd.Add("-o sides=" & cmbsideprint.Text)
  Endif
  If txtpagerange.Text Then
    xcmd.Add("-o page-ranges=" & txtpagerange.Text)
  Endif
  If cmbpageborder.Text Then
    xcmd.Add("-o page-border=" & cmbpageborder.Text)
  Endif
  If cmbscaletofit.Text Then
    xcmd.Add("-o fit-to-page")
  Endif
  $sValue = xcmd.Join(Space(1))
  Me.Close(True)

End
