' Gambas class file

Private $sLink As String
Private $encid As String
Private $sType As String

Private $encList As String[]
Private $sFind As String
Private $sGet As String
Private $ssL As String
Private xGridData As Variant[]

Public Sub _new(sLink As String, encid As String, sType As String)

  $sLink = sLink
  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  If $sLink Then
    SidePanel1.Hidden = True
    Inc Application.Busy
    WebView1.Url = $sLink
    If modBasic.$WebZoomValue Then
      Slider1.Value = modBasic.$WebZoomValue * 100
      WebView1.Zoom = Slider1.Value / 100
    Endif
    Dec Application.Busy
  Else
    SidePanel1.Hidden = False
  Endif

  If $sType = "PACS" Then
    btnsource.Enabled = True
    LoadPacsNodes()
  Endif

End

Public Sub WebView1_Load()

  WebView1.Enabled = True

End

Public Sub Slider1_Change()

  Inc Application.Busy
  WebView1.Zoom = Slider1.Value / 100
  Dec Application.Busy

End

Public Sub btnarchive_Click()

  If SidePanel1.Hidden = True Then
    SidePanel1.Hidden = False
  Else If SidePanel1.Hidden = False Then
    SidePanel1.Hidden = True
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub Form_KeyRelease()

  If Key.Control And If Key.Code = Key["X"] Then
    Me.Close()
  Else If Key.Control And If Key.Code = Key["P"] Then

  Else If Key.Control And If Key.Code = Key["A"] Then
    btnarchive_Click()
  Endif

End

''======================== PACS ==========================
Private Sub LoadPacsNodes()

  cmbserver.List = modMisc.GetPacsServerList()
  If MMain.$SISHAppMode = "HIS" Then
    cmbserver.Add("REPO")
  Endif

  If cmbserver.List.Count = 1 Then
    cmbserver.Index = 0
  Endif

End

Public Sub btnsource_Click()

  If SidePanel1.Hidden = True Then
    SidePanel1.Hidden = False
  Else If SidePanel1.Hidden = False Then
    SidePanel1.Hidden = True
  Endif

End

Public Sub cmbserver_Click()

  ShowPacsData()

End

Private Sub ShowPacsData()

  Dim xfind As String
  Dim xget As String
  Dim xssl As String

  If cmbserver.Text Then
    GridView1.Clear()
    GridView2.Clear()
    If cmbserver.Text = "REPO" Then
      modRepository.ReadRepoConfig()
      modPACS.GetRemotePacsSetting()
      xfind = modSettings.ShowSettingFromFIle("Repository/FindCommand")
      xget = modSettings.ShowSettingFromFIle("Repository/GetCommand")
      xssl = modSettings.ShowSettingFromFIle("Repository/UseSSL")
      If xfind Then
        $sFind = xfind
      Else
        $sFind = "dcmtk"
      Endif
      If xget Then
        $sGet = xget
      Else
        $sGet = "Orthanc"
      Endif
      If xssl Then
        $ssL = xssl
      Else
        $ssL = "Disable"
      Endif
    Else
      MMain.InitialAppMode()
      modPACS.GetPacsSetting(cmbserver.Text)
      $sFind = modSettings.ShowSettingFromFIle(cmbserver.Text & "/" & "FindCommand")
      $sGet = modSettings.ShowSettingFromFIle(cmbserver.Text & "/" & "GetCommand")
      $ssL = modSettings.ShowSettingFromFIle(cmbserver.Text & "/" & "UseSSL")
    Endif

    ' If modPACS.$sNode Then
    '   lblip.Text = modPACS.$sNode
    ' Else
    '   lblip.Text = modPACS.$sHost
    ' Endif

    $encList = [$encid]
    If modPACS.$sHost And If modPACS.$sAET And If modPACS.$sPPort Then
      FillStudyData()
    Endif
  Endif

End

Private Function GetStudyData() As Variant[]

  Dim Row As Integer
  Dim xcoll As Collection
  Dim aList As Variant[]

  Dim xVar As Variant[]
  Dim aColl As Collection
  Dim encid As String

  xVar = New Variant[]
  If $sFind = "Orthanc" Then
    For Each encid In $encList
      xcoll = modPACS.GetOrthancValueJSON("patients", modPACS.GetOrthancString(encid))
      If xcoll And If xcoll.Count Then
        aList = xcoll["Studies"]
        For Row = 0 To aList.Count - 1
          aColl = New Collection
          aColl.Add("", CStr(0))
          aColl.Add(xcoll["MainDicomTags"]["PatientID"], CStr(1))
          aColl.Add("", CStr(2))
          aColl.Add(xcoll["MainDicomTags"]["PatientName"], CStr(3))
          aColl.Add(xcoll["MainDicomTags"]["PatientSex"], CStr(4))
          aColl.Add("", CStr(5))
          aColl.Add(aList[Row], CStr(6))
          xVar.Add(aColl)
        Next
      Endif
    Next

  Else If $sFind = "Dicomweb" Then
    For Each encid In $encList
      aList = modPACS.GetWADOFindStudyJSON(encid)
      If aList And If aList.Count Then
        For Row = 0 To aList.Count - 1
          aColl = New Collection
          aColl.Add(aList[Row]["00080020"]["Value"][0], CStr(0))
          aColl.Add(aList[Row]["00100020"]["Value"][0], CStr(1))
          aColl.Add("", CStr(2))
          aColl.Add(aList[Row]["00100010"]["Value"][0]["Alphabetic"], CStr(3))
          aColl.Add(aList[Row]["00100040"]["Value"][0], CStr(4))
          aColl.Add("", CStr(5))
          aColl.Add(aList[Row]["0020000D"]["Value"][0], CStr(6))
          xVar.Add(aColl)
        Next
      Endif
    Next

  Else
    For Each encid In $encList
      aList = modPACS.GetCFindStudyValue(encid)
      If aList And If aList.Count Then
        For Row = 0 To aList.Count - 1
          aColl = New Collection
          aColl.Add(aList[Row]["StudyDate"], CStr(0))
          aColl.Add(aList[Row]["PatientID"], CStr(1))
          aColl.Add("", CStr(2))
          aColl.Add(aList[Row]["PatientName"], CStr(3))
          aColl.Add(aList[Row]["PatientSex"], CStr(4))
          aColl.Add("", CStr(5))
          aColl.Add(aList[Row]["StudyInstanceUID"], CStr(6))
          xVar.Add(aColl)
        Next
      Endif
    Next

  Endif

  Return xVar

End

Private Sub FillStudyData()

  Dim xFinal As Variant[]

  xFinal = GetStudyData()
  GridView1.Clear
  GridView1.Columns.Count = 7
  GridView1.Rows.Count = xFinal.Count
  LoadVariableToGrid(xFinal, GridView1)
  ResizeFirstGrid()

End

Private Sub LoadVariableToGrid(xFinal As Variant[], xGridView As GridView)

  Dim xRowVal As Collection
  Dim Row As Integer
  Dim Column As Integer

  xGridData = xFinal
  For Row = 0 To xFinal.Count - 1
    xRowVal = xFinal[Row]
    For Column = 0 To xGridView.Columns.Count - 1
      xGridView[Row, Column].Text = xRowVal[CStr(Column)]
    Next
  Next

End

Private Sub ResizeFirstGrid()

  With GridView1
    .Rows.Height = 1.25 * modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 1
    .Columns[2].Width = 1
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 50 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[6].Width = 250 * modBasic.$AppWidthRatio ''StudyUID

    ' .Columns[0].Text = "Date"
    ' .Columns[1].Text = "EncID"
    .Columns[3].Text = "Name"
    .Columns[4].Text = "Sex"
    .Columns[6].Text = "StudyInstanceUID"
  End With
  GridView1.Row = 0

End

Public Sub GridView1_Click()

  Dim Row As Integer
  Dim xcoll As Collection
  Dim aList As Variant[]

  ' $encid = GridView1[GridView1.Row, 1].Text
  ' txtstudycode.Text = GridView1[GridView1.Row, 6].Text
  GridView2.Clear
  GridView2.Columns.Count = 4
  If $sFind = "Orthanc" Then
    xcoll = modPACS.GetOrthancValueJSON("studies", GridView1[GridView1.Row, 6].Text)
    If xcoll And If xcoll.Count Then
      aList = xcoll["Series"]
      GridView2.Rows.Count = aList.Count
      For Row = 0 To aList.Count - 1
        GridView2[Row, 0].Text = ""
        GridView2[Row, 1].Text = GridView1[GridView1.Row, 6].Text
        GridView2[Row, 2].Text = ""
        GridView2[Row, 3].Text = aList[Row]
      Next
    Endif

  Else If $sFind = "Dicomweb" Then
    aList = modPACS.GetWADOFindSeriesJSON($encid, GridView1[GridView1.Row, 6].Text)
    If aList And If aList.Count Then
      GridView2.Rows.Count = aList.Count
      For Row = 0 To aList.Count - 1
        GridView2[Row, 0].Text = aList[Row]["00080060"]["Value"][0]
        GridView2[Row, 1].Text = GridView1[GridView1.Row, 6].Text
        GridView2[Row, 2].Text = ""
        GridView2[Row, 3].Text = aList[Row]["0020000E"]["Value"][0]
      Next
    Endif

  Else
    aList = modPACS.GetCFindSeriesValue($encid, GridView1[GridView1.Row, 6].Text)
    If aList And If aList.Count Then
      GridView2.Rows.Count = aList.Count
      For Row = 0 To aList.Count - 1
        GridView2[Row, 0].Text = aList[Row]["Modality"]  ''GetDicomValue(FileList[Row], "Modality")
        GridView2[Row, 1].Text = GridView1[GridView1.Row, 6].Text
        GridView2[Row, 2].Text = ""
        GridView2[Row, 3].Text = aList[Row]["SeriesInstanceUID"]  'GetDicomValue(FileList[Row], "SeriesInstanceUID")
      Next
    Endif

  Endif
  ResizeSecondGridView()

End

Private Sub ResizeSecondGridView()

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 1 ''Study UID
    .Columns[2].Width = 1
    .Columns[3].Width = 500 * modBasic.$AppWidthRatio ''series UID

    ' .Columns[0].Text = "Modality"
    .Columns[3].Text = "SeriesInstanceUID"
  End With
  GridView2.Row = 0

End

Public Sub btnwebview_Click()

  Dim xLink As String

  If GridView2.Rows.Selection.Count Then
    xLink = modPACS.DicomSeriesWebView($sFind, $encid, GridView2[GridView2.Row, 1].Text, GridView2[GridView2.Row, 3].Text)
    WebView1.Url = xLink
  Endif

End
