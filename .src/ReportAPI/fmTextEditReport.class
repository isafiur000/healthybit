' Gambas class file

Private $txtbuttFile As ButtonBox

Public Sub _new(txtbuttFile As ButtonBox)

  $txtbuttFile = txtbuttFile
  If Exist($txtbuttFile.Text) Then
    TextEdit1.RichText = File.Load($txtbuttFile.Text)
    txtfolder.Text = File.Dir($txtbuttFile.Text)
    txtfile.Text = File.Name($txtbuttFile.Text)
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  FillListView()

End

Public Sub txtfolder_Click()

  If Dialog.SelectDirectory() Then Return
  txtfolder.Text = Dialog.Path

End

Public Sub btnsave_Click()

  Dim newFile As String

  newFile = txtfolder.Text &/ txtfile.Text
  If Exist(newFile) Then
    Kill newFile
  Endif

  File.Save(newFile, TextEdit1.RichText)
  $txtbuttFile.Text = newFile
  Balloon.Info(("Template saved"), btnsave)
  Balloon.Delay = modBasic.$BalloonDelay

End

''========================= Variables =============================
Private Sub FillListView()

  Dim xList As String[] = ["General Param", "Patient Param", "Encounter Param", "Obstetrics Param", "Consultation Param", "Delivery Param", "Procedure Param", "Clinical Param", "Examination", "Supplier Details", "Purchase Entry", "Purchase Order", "Store Request", "Store Transfer"]
  Dim xval As String
  Dim yList As Variant[]
  Dim yval As Variant
  Dim icon1 As Picture
  Dim icon2 As Picture

  icon1 = Picture["icons/coll3.png"]
  icon2 = Picture["icons/coll4.png"]
  For Each xval In xList
    TreeView1.Add(xval, xval, icon1)
    yList = modReportVar.GetReportVarCollection(xval)
    For Each yval In yList
      TreeView1.Add(yval[0], yval[1], icon2, xval)
    Next
  Next

End

Public Sub TreeView1_Click()

  If TreeView1.Current.Picture = Picture["icons/coll4.png"] Then
    TextEdit1.Text = TextEdit1.Text & TreeView1.Current.Key
  Endif

End

Public Sub btndiagno_Click()

  TextEdit1.Text = TextEdit1.Text & VarDiagnostic()

End
