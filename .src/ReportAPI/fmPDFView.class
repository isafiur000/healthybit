' Gambas class file

Private $hPDF As New PdfDocument
Private $sLink As String

Public Sub _new(sLink As String, sLock As Boolean)

  $sLink = sLink
  If sLock = True Then
    btnprint.Enabled = False
  Endif

End

Public Sub Form_Open()

  cmbview.List = ["None", "Horizontal", "Vertical", "Row", "Column", "Fill"]

  $hPDF.Open($sLink)
  DocumentView1.Count = $hPDF.Count
  txtpageval.MaxValue = $hPDF.Count
  docviewleft.Count = 0
  docviewleft.DocHeight = $hPDF[1].Height / 0.72
  docviewleft.DocWidth = $hPDF[1].Width / 0.72
  DocumentView1.DocWidth = $hPDF[1].Width / 0.72
  DocumentView1.DocHeight = $hPDF[1].Height / 0.72

  cmbview.Text = "None"
  DocumentView1.Arrangement = 0

End

Public Sub DocumentView1_Layout(Page As Integer)

  Last.Layout.Height = $hPDF[Page + 1].Height / 0.72
  Last.Layout.Width = $hPDF[Page + 1].Width / 0.72

End

Public Sub DocumentView1_Draw(Page As Integer, Width As Integer, Height As Integer)

  If Page > $hPDF.Count - 1 Then Return

  $hPDF.Zoom = Last.Zoom / 0.72
  Draw.Image($hPDF[Page + 1].Image, 0, 0)

End

Public Sub DocumentView1_Zoom()

  Object.Lock(SliderBox1)
  SliderBox1.Value = Last.Zoom * 100
  Object.Unlock(SliderBox1)

End

Public Sub SliderBox1_Change()

  DocumentView1.Zoom = SliderBox1.Value / 100

End

Public Sub cmbview_Click()

  DocumentView1.Arrangement = cmbview.Index
  txtpageval.Value = 1

End

Public Sub docviewleft_Draw(Page As Integer, (Width) As Integer, (Height) As Integer)

  $hPDF.Zoom = Last.Zoom / 0.72
  Draw.Image($hPDF[Page + 1].Image, 0, 0)

End

Public Sub DocumentView1_MouseDown()

  Print DocumentView1.Find(Mouse.x, Mouse.y)

End

Public Sub txtspinvalue_Change()

  DocumentView1.Column = txtspinvalue.Value
  DocumentView1.Refresh

End

Public Sub txtpageval_Change()

  DocumentView1[txtpageval.Value - 1].EnsureVisible

End

Public Sub btnprint_Click()

  modPrint.DefaultPrinting($sLink, "ReportSize")

End
