' Gambas class file

Private $encid As String
Private $rData As Result
Private AppFactor As Float

Private aWebFrame As New Object[50]
Private aNameLabel As New Object[50]
Private aHorizPanel As New Object[50]
Private aIndexVal As New Object[50]
Private aRegmLabel As New Object[50]
Private aQuantiBox As New Object[50]
Private aUnitLabel As New Object[50]
Private aDoseLabel As New Object[50]
Private aCountBox As New Object[50]
Private aWeighBox As New Object[50]
Private aBlankBox As New Object[50]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  txtname.Text = modPatient.GetPatientNameByEnc($encid)
  AppFactor = modBasic.$AppScaleFactor
  LoadControlsWeb()

End

Private Sub LoadControlsWeb()

  Dim i As Integer
  Dim sql As String
  Dim xunit As String

  i = 0
  sql = "select fldid,fldroute,flditem,flddose,fldfreq,flddays,fldstatus from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstarttime<=&3 and fldendtime>=&4 and fldroute<>&5 and flditemtype=&6 and fldcurval=&7"
  $rData = modDatabase.$myConn.Exec(sql, $encid, True, Now(), DateAdd(Now(), gb.Hour, -3), "fluid", "Medicines", "Continue")
  If $rData.Available Then
    Frame1.Height = ($rData.Count * 85 + 30) * AppFactor

    For Each $rData
      If i < 49 Then

        aWebFrame[i] = New Panel(Frame1)
        aNameLabel[i] = New TextLabel(aWebFrame[i])
        aHorizPanel[i] = New HBox(aWebFrame[i])
        aIndexVal[i] = New ValueBox(aHorizPanel[i])
        aRegmLabel[i] = New Label(aHorizPanel[i])
        aUnitLabel[i] = New Label(aHorizPanel[i])
        aQuantiBox[i] = New ValueBox(aHorizPanel[i])
        aDoseLabel[i] = New Label(aHorizPanel[i])
        aCountBox[i] = New ValueBox(aHorizPanel[i])
        aWeighBox[i] = New ValueBox(aHorizPanel[i])
        aBlankBox[i] = New Label(aHorizPanel[i])

        With aWebFrame[i]
          .Arrangement = Arrange.Vertical
          .Height = 75 * AppFactor
        End With

        With aNameLabel[i]
          .Height = 30 * AppFactor
          .Text = "<b>" & $rData["flditem"] & "</b>"
          .Tag = i
        End With

        With aHorizPanel[i]
          .Height = 30 * AppFactor
        End With

        xunit = modPharmLabel.GetDosageFormForLabel($rData["flditem"], "Inpatient")
        With aIndexVal[i]
          .Width = 25 * AppFactor
          .Height = 30 * AppFactor
          .Value = $rData["fldid"]
          .Visible = False
          .Tag = i
        End With
        With aRegmLabel[i]
          .Expand = True
          .Height = 30 * AppFactor
          .Text = $rData["fldroute"] & Space(2) & $rData["flddose"] & " " & modMedConstant.GetMedicineDoseUnit($rData["flditem"]) & " X " & $rData["fldfreq"] & " X " & $rData["flddays"] & " day(s)"
          .Tag = $rData["fldfreq"]
        End With
        With aUnitLabel[i]
          .Width = 50 * AppFactor
          .Height = 30 * AppFactor
          .Border = Border.Plain
          .Text = Round($rData["flddose"] / modMedConstant.GetDrugInitialStrength($rData["flditem"]), -2)
          .Tag = i
        End With
        With aDoseLabel[i]
          .Width = 75 * AppFactor
          .Height = 30 * AppFactor
          .Border = Border.Plain
          .Text = xunit
          .Tag = i
        End With
        With aQuantiBox[i]
          .Width = 75 * AppFactor
          .Height = 30 * AppFactor
          .Tag = i
        End With
        With aCountBox[i]
          .Width = 50 * AppFactor
          .Height = 30 * AppFactor
          .Enabled = False
          .Value = modPharmacy.TotalDoseCount($rData["fldid"], Now())
          .Tag = i
        End With
        With aWeighBox[i]
          .Width = 75 * AppFactor
          .Height = 30 * AppFactor
          .Enabled = False
          .Value = modPharmacy.TotalDoseAmount($rData["fldid"], Now())
          .Tag = i
        End With
        With aBlankBox[i]
          .Width = 25 * AppFactor
          .Height = 30 * AppFactor
        End With

      Endif
      i = i + 1
    Next
  Endif

End

Public Sub btnsaveall_Click()

  Dim i As Integer
  Dim res As Result

  For i = 0 To $rData.Count - 1
    If i < 49 Then
      If aIndexVal[i].Value And If aQuantiBox[i].Value Then

        modPharmSub.InsertNurDosing(aIndexVal[i].Value, $encid, aQuantiBox[i].Value, aDoseLabel[i].Text, "")
        If aRegmLabel[i].Tag = "stat" Then
          res = modDatabase.$myConn.Edit("tblpatdosing", "fldid=&1", aIndexVal[i].Value)
          res["fldcurval"] = "Completed"
          res.Update
        Endif
        aQuantiBox[i].Value = 0
        aQuantiBox[i].ReadOnly = True

      Endif
    Endif
  Next
  Balloon.Info(("Information saved"), btnsaveall)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnclose_Click()

  Me.Close()

End
