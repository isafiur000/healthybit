' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  cmbnote.List = ["Progress Note", "Clinicians Note", "Nurses Note", "Transfer Note"]
  txtname.Text = modPatient.GetPatientNameByEnc($encid)
  cmbnote.SetFocus

End

Public Sub cmbnote_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbnote)
  modFillContainer.RestrictComboToContent(cmbnote)

End

Public Sub btnhelpplanfinal_Click()

  txtnote.RichText = txtnote.RichText & modCloudAI.GetPatCloudAIResponse($encid, txtnote.Text)

End

Public Sub btntemplpplanfinal_Click()

  If cmbnote.Text Then
    txtnote.RichText = txtnote.RichText & DictionaryVIew(cmbnote.Text)
  Endif

End

Public Sub btnsaveall_Click()

  If $encid Then
    If cmbnote.Text Then
      If txtnote.Text Then
        modPatientGeneral.AddExamGeneralQualiData($encid, "Notes", cmbnote.Text, txtimpression.Text, txtnote.RichText, False)               ''
        Balloon.Info(("Information saved"), btnsaveall)
        Balloon.Delay = modBasic.$BalloonDelay
        txtnote.RichText = ""
        txtimpression.Text = ""
      Endif
    Endif
  Endif

End

Public Sub btnclose_Click()

  Me.Close

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["S"] And If Key.Control Then
    btnsaveall_Click()
  Endif

End

Public Sub btnshownote_Click()

  SHowNotesGrid()

End

Private Sub SHowNotesGrid()

  Dim sql As String
  Dim xType As String

  If rbprogress.Value = True Then
    xType = "Progress Note"
  Else If rbclinician.Value = True Then
    xType = "Clinicians Note"
  Else If rbnurses.Value = True Then
    xType = "Nurses Note"
  Else If rbtransfer.Value = True Then
    xType = "Transfer Note"
  Endif

  sql = "select fldtime,flddetail,fldreportquali,flduserid from tblexamgeneral where fldencounterval=&1 and fldinput=&2 and flditem=&3 ORDER BY fldtime DESC"
  $rData = modDatabase.$myConn.Exec(sql, $encid, "Notes", xType)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 400 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Date"
    .Columns[1].Text = "Description"
    .Columns[2].Text = "Impression"
    .Columns[3].Text = "User"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 1 Then
    GridView1.Data.RichText = $rData[$aMyFields[Column]]
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.RichText, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End
