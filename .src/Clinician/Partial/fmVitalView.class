' Gambas class file

Private $encid As String
Private $PatientNum As String
Private $sVitalsVar As Variant[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  $PatientNum = modPatient.GetPatientNoByEnc($encid)
  $sVitalsVar = modPathoSub.GetAllEssentialExams($encid)
  FillDailyGrid()

End

Public Sub btngridrefresh_Click()

  FillDailyGrid()

End

Public Sub RefreshValues()

  FillDailyGrid()

End

Private Sub FillDailyGrid()

  Dim xfir As Date
  Dim xlast As Date

  Dim i As Integer
  Dim j As Integer

  GridView2.Clear()
  GridView2.Rows.Count = $sVitalsVar.Count
  If rbdaily.Value = True Then
    xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, -12))
    xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))
    GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Hour) + 1
  Else If rbmonthly.Value = True Then
    xfir = modDate.StartSqlDate(DateAdd(Now(), gb.Day, -12))
    xlast = modDate.StartSqlDate(DateAdd(Now(), gb.Day, 1))
    GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Day) + 1
  Else If rbyearly.Value = True Then
    xfir = modDate.StartSqlMonth(DateAdd(Now(), gb.Month, -12))
    xlast = modDate.StartSqlMonth(DateAdd(Now(), gb.Month, 1))
    GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Month) + 1
  Endif

  For i = 0 To $sVitalsVar.Count - 1
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        GridView2[i, j].Text = $sVitalsVar[i]["ExamName"]
      Else
        If rbdaily.Value = True Then
          GridView2[i, j].Text = GetExamValueByDate($PatientNum, $sVitalsVar[i]["ExamName"], DateAdd(xfir, gb.Hour, j))
        Else If rbmonthly.Value = True Then
          GridView2[i, j].Text = GetExamValueByDate($PatientNum, $sVitalsVar[i]["ExamName"], DateAdd(xfir, gb.Day, j))
        Else If rbyearly.Value = True Then
          GridView2[i, j].Text = GetExamValueByDate($PatientNum, $sVitalsVar[i]["ExamName"], DateAdd(xfir, gb.Month, j))
        Endif
      Endif
    Next
  Next

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = 150 * modBasic.$AppWidthRatio
        .Columns[j].Text = "Examination"
      Else
        .Columns[j].Width = 64 * modBasic.$AppWidthRatio
        If rbdaily.Value = True Then
          .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd(xfir, gb.Hour, j), gb.ShortTime)
        Else If rbmonthly.Value = True Then
          .Columns[j].Text = modDate.GetDateOnlyInFormatForAll(DateAdd(xfir, gb.Day, j), modBasic.$DateFormat, "DateOnly")
        Else If rbyearly.Value = True Then
          .Columns[j].Text = modDate.GetDateOnlyInFormatForAll(DateAdd(xfir, gb.Month, j), modBasic.$DateFormat, "YearMonth")
        Endif
      Endif
    Next
  End With
  GridView2.Scroll(GridView2.X + GridView2.Width, GridView2.Y)

End

Private Function GetExamValueByDate(patNo As String, xexam As String, xDate As Date) As String

  Dim res As Result
  Dim xx As String
  Dim xfir As Date
  Dim xlast As Date

  If rbdaily.Value = True Then
    xfir = modDate.StartSqlHour(xDate)
    xlast = modDate.EndSqlHour(xDate)
  Else If rbmonthly.Value = True Then
    xfir = modDate.StartSqlDate(xDate)
    xlast = modDate.EndSqlDate(xDate)
  Else If rbyearly.Value = True Then
    xfir = modDate.StartSqlMonth(xDate)
    xlast = modDate.EndSqlMonth(xDate)
  Endif

  res = modDatabase.$myConn.Exec("select tblpatientexam.fldrepquali as fldrepquali,tblpatientexam.fldrepquanti as fldrepquanti from tblpatientexam inner join tblencounter on tblpatientexam.fldencounterval=tblencounter.fldencounterval  where tblencounter.fldpatientval=&1 and tblpatientexam.fldsave=&2 and tblpatientexam.fldhead=&3 and tblpatientexam.fldtime>=&4 and tblpatientexam.fldtime<=&5", patNo, True, xexam, xfir, xlast)
  If res.Available Then
    res.MoveFirst
    If res["fldrepquanti"] Then
      xx = CStr(res["fldrepquanti"])
    Else
      xx = res["fldrepquali"]
    Endif
  Else
    xx = ""
  Endif
  Return xx

End
