' Gambas class file

Private $encid As String
Private $BillMode As String
Private $sPackage As String
Private $xFinClear As Boolean
Private $deptGroup As String

Private $rData As Result
Private $aMyFields As String[]
Private $ConsList As String[]

Public Sub _new(encid As String, DiscType As String)

  $encid = encid
  $sPackage = DiscType

End

Public Sub Form_Open()

  cmbdisctype.Text = $sPackage
  $BillMode = modNonMedical.GetDiscBindBillMode($sPackage)
  If Not $BillMode Then
    $BillMode = modpatient.GetPatBillingMode($encid)
  Endif

  $xFinClear = modNonMedical.GetFinanceClearance($encid)
  $deptGroup = modSettings.ShowSettingFromFIle("RegistrationForm/DefaultDeptGroup")
  rbconsult.Value = True
  GetDepartmentList()

  btnconsult.Tag = modBillings.GetPayableUserSetting("Service", $encid)
  If btnconsult.Tag Then
    btnconsult.Text = modGeneral.GetUserFullName(btnconsult.Tag)
  Endif
  If modBasic.$PayableLockEntry = "Yes" Then
    btnconsult.Enabled = False
  Endif
  dtconsult.Value = Now()
  FillLabtable()
  If $xFinClear = True Then
    btnsubOK.Enabled = False
  Endif

End

Public Sub rbconsult_Click()

  GetDepartmentList()

End

Public Sub rbopvisit_Click()

  GetDepartmentList()

End

Private Sub GetDepartmentList()

  If $deptGroup Then
    If rbconsult.Value = True Then
      $ConsList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tbldeptgroup where fldgroupname=&1 and flditemname in(select flddept from tbldepartment where fldcateg=&2)", $deptGroup, "Consultation"))   ''
      cmbconsult.List = $ConsList
    Else If rbopvisit.Value = True Then
      $ConsList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tbldeptgroup where fldgroupname=&1 and flditemname in(select flddept from tbldepartment where fldcateg=&2)", $deptGroup, "OPD Visit"))
      cmbconsult.List = $ConsList
    Endif

  Else
    If rbconsult.Value = True Then
      $ConsList = modGeneral.GetDepartForConsultOnly()
      cmbconsult.List = $ConsList
    Else If rbopvisit.Value = True Then
      $ConsList = modGeneral.GetDepartForOPVisitOnly()
      cmbconsult.List = $ConsList
    Endif

  Endif

End

''==================== New consult ========================
Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

Public Sub btnconsult_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Consultant"), modBasic.$OPConsulUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif

End

Public Sub btnsms_Click()

  If btnconsult.Tag Then
    modDevice.SendMessageToRegistPhysician($encid, btnconsult.Tag)
  Endif

End

Public Sub txtsearch_Change()

  cmbconsult.List = modString.SelectStringArrayToText(txtsearch.Text, $ConsList, chkleft.Value)

End

Public Sub cmbconsult_Click()

  txtdepart.Text = cmbconsult.Text
  dtconsult.Value = Now()
  txtpresummary.RichText = ""
  btnconsult.Tag = ""
  btnconsult.Text = ""

End

Public Sub btnsubOK_Click()

  Dim billItem As String

  Dim xrefer As String
  Dim xpayble As String
  Dim xgo As Boolean

  If txtdepart.Text Then
    If $ConsList.Exist(txtdepart.Text) Then
      xrefer = modBillings.GetReferralUserSetting("Service", $encid)
      If btnconsult.Tag Then
        xpayble = btnconsult.Tag
      Else
        xpayble = modBillings.GetPayableUserSetting("Service", $encid)
      Endif

      Inc Application.Busy
      If modBasic.$ClinConsultComment = "Essential" Then
        If txtpresummary.RichText Then
          xgo = True
        Else
          xgo = False
        Endif
      Else
        xgo = True
      Endif

      If xgo = True Then
        If modGeneral.GetCategoryFromDept(txtdepart.Text) = "Consultation" Then
          modPatientSub.AddConsultData($encid, txtdepart.Text, dtconsult.Value, $BillMode, "IP Consultation", btnconsult.Tag, txtpresummary.RichText)
        Else
          modPatientSub.AddOPVisitData($encid, txtdepart.Text, dtconsult.Value, $BillMode, "IP Consultation", txtpresummary.RichText)
        Endif

        If modBasic.$AutoBillConsult Then
          billItem = modNonMedical.GetIPDepartConsultRate(txtdepart.Text, cmbdisctype.Text)
          If billItem Then
            If modBasic.$AutoBillConsult = "Standard" Then
            Else If modBasic.$AutoBillConsult = "Full" Then
              modBillings.GetAutoBillingEntry($encid, cmbdisctype.Text, "Service", billItem, 1, "Done", 0, True, False, xpayble, xrefer)
            Else If modBasic.$AutoBillConsult = "Partial" Then
              modBillings.GetAutoBillingEntry($encid, cmbdisctype.Text, "Service", billItem, 1, "Punched", 0, False, False, xpayble, xrefer)
            Endif
          Endif
        Endif
      Endif
      FillLabtable()
      Dec Application.Busy

    Endif
  Endif

End

Public Sub FillLabtable()

  Dim sql As String
  Dim xFldList1 As String[] = ["fldid", "fldconsultname", "fldencounterval", "fldconsulttime", "fldid", "fldpresummary"]
  Dim xFldList2 As String[] = ["fldid", "fldconsultname", "fldencounterval", "fldconsulttime", "fldid", "fldpresummary"]

  xFldList1.Add(Quote("tblconsult"))
  xFldList2.Add(Quote("tblopvisit"))
  sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus=&2 UNION ALL select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus=&2"                                             ''
  $rData = modDatabase.$myConn.Exec(sql, $encid, "Planned")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 250 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 25 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[6].Width = 1

    .Columns[1].Text = "Consultation"
    .Columns[3].Text = "DateTime"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 4 Then
    GridView1.Data.Picture = Picture["icon:/tiny/cancel"]
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Click()

  Dim res As Result

  If GridView1.Column = 4 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete(GridView1[GridView1.Row, 6].Text, "fldid=&1", GridView1[GridView1.Row, 0].Text)
      FillLabtable()
    Endif
  Else

    If GridView1[GridView1.Row, 6].Text = "tblconsult" Then
      res = modDatabase.$myConn.Exec("select fldconsultname,fldconsulttime,fldpresummary,flduserid from tblconsult where fldid=&1", GridView1[GridView1.Row, 0].Text)
      If res.Available Then
        txtdepart.Text = res["fldconsultname"]
        dtconsult.Value = res["fldconsulttime"]
        txtpresummary.RichText = res["fldpresummary"]
        btnconsult.Tag = res["flduserid"]
        If res["flduserid"] Then
          btnconsult.Tag = res["flduserid"]
          btnconsult.Text = modGeneral.GetUserFullName(res["flduserid"])
        Else
          btnconsult.Tag = ""
          btnconsult.Text = ""
        Endif
      Endif
    Else If GridView1[GridView1.Row, 6].Text = "tblopvisit" Then
      res = modDatabase.$myConn.Exec("select fldconsultname,fldconsulttime,fldpresummary from tblconsult where fldid=&1", GridView1[GridView1.Row, 0].Text)
      If res.Available Then
        txtdepart.Text = res["fldconsultname"]
        dtconsult.Value = res["fldconsulttime"]
        txtpresummary.RichText = res["fldpresummary"]
        btnconsult.Tag = ""
        btnconsult.Text = ""
      Endif
    Endif

  Endif

End
