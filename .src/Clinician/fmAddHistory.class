' Gambas class file

Private $sValue As String
Private $sType As String
Private $sVariable As String
Private $sTable As String

Private AppFactor As Float
Private $defValue As String
Private $SelVar As Variant[]

Private aHorzPanel As New Object[50]
Private aNameLabel As New Object[50]
Private aOptionBox As New Object[50]
Private aComboBox As New Object[50]
Private aButonBox As New Object[50]
Private aSpaceBox As New Object[50]

Public Sub Run(sType As String, sVariable As String) As String

  $sType = sType
  $sVariable = sVariable

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If $sType = "History" Then
    $sTable = "tblhistory"
  Else If $sType = "Discharge" Then
    $sTable = "tbldischarge"
  Endif
  cmbformat.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec(Subst("select distinct(fldformat) as col from &1", $sTable) & " where flditem=&1", $sVariable))
  cmbformat.Text = "Common"

End

Public Sub btnfill_Click()

  Dim xht As Integer

  If Frame1.Children.Count Then
    Frame1.Children.Clear()
  Endif
  If cmbformat.Text Then
    $SelVar = New Variant[]
    ShowControls()

    xht = ($SelVar.Count * 35 + 30) * AppFactor
    If Frame1.Height < xht Then
      Frame1.Height = xht
    Endif
  Endif

End

Private Sub ShowControls()

  Dim res As Result
  Dim xVar As Variant[]
  Dim i As Integer

  AppFactor = modBasic.$AppScaleFactor
  res = modDatabase.$myConn.Exec(Subst("select fldid,flddetail,fldoptions from &1", $sTable) & " where flditem=&1 and fldformat like &2", $sVariable, cmbformat.Text)     ''
  If res.Available Then
    res.MoveFirst
    $defValue = res["flddetail"]
    If res["fldoptions"] Then
      Try xVar = JSON.Decode(res["fldoptions"])
      If xVar Then
        $SelVar = xVar

        For i = 0 To $SelVar.Count - 1
          If i < 49 Then
            aHorzPanel[i] = New Panel(Frame1)
            aNameLabel[i] = New TextLabel(aHorzPanel[i])
            aOptionBox[i] = New ComboBox(aHorzPanel[i])
            aComboBox[i] = New ComboBox(aHorzPanel[i])
            aButonBox[i] = New Button(aHorzPanel[i]) As "ButtonGroup"
            aSpaceBox[i] = New Label(aHorzPanel[i])

            With aHorzPanel[i]
              .Arrangement = Arrange.Horizontal
              .Height = 25 * AppFactor
              .Border = Border.Plain
            End With

            With aNameLabel[i]
              .Expand = True
              .Text = $SelVar[i]["0"]
              .Tag = i
              .Wrap = True
            End With

            With aOptionBox[i]
              .Width = 75 * AppFactor
              .List = ["=", ":", "is"]
              .ReadOnly = False
              .Tag = i
            End With

            With aComboBox[i]
              .Width = 200 * AppFactor
              .List = Split($SelVar[i]["1"], ";")
              .Tag = i
            End With

            With aButonBox[i]
              .Width = 24 * AppFactor
              .Picture = Picture["icon:/small/download"]
              .Tag = i
            End With

            With aSpaceBox[i]
              .Width = 25 * AppFactor
            End With

          Endif
        Next

      Endif
    Endif
  Endif

End

Public Sub ButtonGroup_Click()

  Dim i As Integer
  Dim xstr As String

  i = Last.Tag
  If aOptionBox[i].Text Then
    xstr = aNameLabel[i].Text & Space(1) & aOptionBox[i].Text & Space(1) & aComboBox[i].Text
  Else
    xstr = aNameLabel[i].Text & Space(1) & aComboBox[i].Text
  Endif

  If txthistory.Text Then
    txthistory.RichText = txthistory.RichText & xstr
  Else
    txthistory.RichText = xstr
  Endif

End

Public Sub btnselection_Click()

  Dim i As Integer
  Dim xList As String[]

  xList = New String[]
  For i = 0 To $SelVar.Count - 1
    If aComboBox[i].Text Then
      xList.Add(aNameLabel[i].Text & Space(1) & aComboBox[i].Text)
    Endif
  Next

  If xList.Count Then
    If txthistory.Text Then
      If txthistory.RichMode = True Then
        txthistory.RichText = txthistory.RichText & "<br>" & xList.Join("<br>")
      Else
        txthistory.RichText = txthistory.RichText & gb.NewLine & xList.Join(gb.NewLine)
      Endif
    Else
      If txthistory.RichMode = True Then
        txthistory.RichText = xList.Join("<br>")
      Else
        txthistory.RichText = xList.Join(gb.NewLine)
      Endif
    Endif
  Endif

End

Public Sub btndefault_Click()

  If txthistory.Text Then
    If txthistory.RichMode = True Then
      txthistory.RichText = txthistory.RichText & "<br>" & $defValue
    Else
      txthistory.RichText = txthistory.RichText & gb.NewLine & $defValue
    Endif
  Else
    txthistory.RichText = $defValue
  Endif

End

Public Sub btnsave_Click()

  $sValue = txthistory.RichText
  Me.Close(True)

End
