' Gambas class file

Private $encid As String
Private $ExamList As String[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  $ExamList = modClinic.GetFullVitalList($encid)
  FillDailyGrid()

End

''==================== table =======================
Private Sub FillDailyGrid()

  Dim xfir As Date
  Dim xlast As Date

  Dim i As Integer
  Dim j As Integer

  xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, -18))
  xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))

  GridView2.Clear()
  GridView2.Rows.Count = $ExamList.Count
  GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Hour) + 1

  For i = 0 To $ExamList.Count - 1
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        GridView2[i, j].Text = $ExamList[i]
      Else
        GridView2[i, j].Text = GetExamValueByDate($encid, $ExamList[i], DateAdd(xfir, gb.Hour, j))
      Endif
    Next
  Next

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = 150 * modBasic.$AppWidthRatio
        .Columns[j].Text = "Examination"
      Else
        .Columns[j].Width = 50 * modBasic.$AppWidthRatio
        .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd(xfir, gb.Hour, j), gb.ShortTime)
      Endif
    Next
  End With
  GridView2.Scroll(GridView2.X + GridView2.Width, GridView2.Y)

End

Private Function GetExamValueByDate(encid As String, xexam As String, xDate As Date) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldrepquanti from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 and fldtime>=&4 and fldtime<=&5", encid, True, xexam, modDate.StartSqlHour(xDate), modDate.EndSqlHour(xDate))
  If res.Available Then
    res.MoveFirst
    If res["fldrepquanti"] Then
      xx = CStr(res["fldrepquanti"])
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End
