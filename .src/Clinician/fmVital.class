' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $encid As String
Private $sVitalsVar As Variant[]

Private $sType As String
Private $sSysCons As String
Private $sOption As String

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  Dim hForm As FmQuantiEntry

  modGeneralMain.ArrangeFormCentre(Me, "False")
  hForm = New FmQuantiEntry(txtexamval, pnlquanti)
  modBasic.LoadClinicalChartSetting()
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtcolor.Background = modPatient.GetPatientColorStatic($encid)
  $sVitalsVar = modPathoSub.GetAllEssentialExams($encid)
  ListBox1.List = modString.GetListFromVariant($sVitalsVar, "ExamName", True)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key.Up Then
    ListBox1.SetFocus
  Else If Key.Code = Key.Down Then
    ListBox1.SetFocus
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
  Else If TabPanel1.Index = 1 Then
    FillDailyGrid()
  Endif

End

Public Sub ListBox1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    ListBox1_Click()
  Endif

End

Public Sub ListBox1_Click()

  Dim xlimit As Float[]
  Dim sColl As Collection

  txtexamval.Value = 0
  txtquali.Clear
  txtquali.Text = ""
  $sType = ""
  $sSysCons = ""
  $sOption = ""

  For Each sColl In $sVitalsVar
    If sColl["ExamName"] = ListBox1.Text Then
      lblexamname.Text = sColl["ExamName"]
      $sType = sColl["DataType"]
      $sSysCons = sColl["SysConstant"]
      $sOption = sColl["OptionType"]
      If sColl["DataType"] = "Quantitative" Then
        txtexamval.Enabled = True
        txtexamval.Visible = True
        lblunit.Visible = True
        txtquali.Enabled = False
        txtquali.Visible = False
        If modBasic.$TabletModeEnable = "Yes" Then
          pnlquanti.Visible = True
        Else
          pnlquanti.Visible = False
        Endif
        xlimit = modClinic.GetBothQuantiExamVal(sColl["ExamName"], $encid)
        txtmin.Value = xlimit[0]
        txtmax.Value = xlimit[1]
        lblunit.Text = modClinic.GetExamUnit(sColl["ExamName"], $encid)
        FillVitalExamChart(PictureBox1, $encid, sColl["ExamName"])
      Else If sColl["DataType"] = "Qualitative" Then
        txtexamval.Enabled = False
        txtexamval.Visible = False
        lblunit.Visible = False
        txtquali.Enabled = True
        txtquali.Visible = True
        pnlquanti.Visible = False
        FillComboBox()
      Endif
      ShowExamGrid(sColl["ExamName"])
      Break
    Endif
  Next

End

Public Sub btnsave_Click()

  If lblexamname.Text Then
    If $sType = "Quantitative" Then
      If txtmin.Value = 0 And If txtmax.Value = 0 Then
        chkabnormal.Value = False
      Else
        If txtexamval.Value < txtmin.Value Or If txtexamval.Value > txtmax.Value Then
          chkabnormal.Value = True
        Else
          chkabnormal.Value = False
        Endif
      Endif
      modClinSub.AddQuantiData($encid, "", lblexamname.Text, $sOption, txtexamval.Value, chkabnormal.Value, "Essential Examinations", $sSysCons)
      FillVitalExamChart(PictureBox1, $encid, lblexamname.Text)

    Else If $sType = "Qualitative" Then
      If txtquali Then
        modClinSub.AddClinicExam($encid, "", lblexamname.Text, $sOption, txtquali.Text, txtexamval.Value, chkabnormal.Value, "Essential Examinations", $sSysCons, "Regular")
      Endif
    Endif
    ShowExamGrid(ListBox1.Text)
    FillDailyGrid()
    txtquali.Clear()
    txtquali.Text = ""
    txtexamval.Value = 0
    pnlquanti.Visible = False

  Endif

End

Public Sub btnsave_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnsave_Click()
    ListBox1.SetFocus
  Endif

End

Public Sub btnsave_GotFocus()

  btnsave.Font.Bold = True

End

Public Sub btnsave_LostFocus()

  btnsave.Font.Bold = False

End

Private Sub ShowExamGrid(sExam As String)

  $rData = modDatabase.$myConn.Exec("select fldid,fldid,fldtime,fldtype,fldhead,fldabnormal,fldid,fldsave from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 ORDER by fldid DESC", $encid, True, sExam)                                                                                 ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 25
    .Columns[2].Width = 120 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1
    .Columns[4].Width = 200 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 1

    .Columns[2].Text = "DateTime"
    .Columns[4].Text = "Examination"
    .Columns[6].Text = "Observation"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 5 Then
    GridView1.Data.Picture = Picture[modMisc.GetGridIcon($rData[$aMyFields[Column]])]
    GridView1.Data.Text = ""
  Else If Column = 6 Then
    GridView1.Data.RichText = modClinic.GetExamValueString($encid, $rData[$aMyFields[Column]], False)
  Else If Column = 2 Then
    GridView1.Data.Text = modPatient.GetDayTimeFromAdmission($encid, $rData[$aMyFields[Column]])
  Else If Column = 1 Then
    GridView1.Data.Background = modPathoSub.GetPatientColorByTriage($rData[$aMyFields[Column]])
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Sub FillVitalExamChart(drw As DrawingArea, encid As String, strHead As String)

  Dim maxval As Float
  Dim minval As Float
  Dim normval As Float
  Dim sql As String
  Dim rData As Result
  Dim dat As Date
  Dim xlimit As Float[]

  Dim pointsArr_x As Float[]
  Dim pointsArr_y As Float[]

  drw.Clear
  dat = DateAdd(Now(), gb.day, 0 - CInt(SpinBox1.Value))
  pointsArr_x = New Float[]
  pointsArr_y = New Float[]
  sql = "select fldrepquanti,fldtime from tblpatientexam where fldencounterval=&1 and fldhead=&2 and fldtype=&3 and fldsave=&4 and fldtime>&5"
  rData = modDatabase.$myConn.Exec(sql, encid, strHead, "Quantitative", True, modDate.StartSqlDate(dat))
  If rData.Available Then
    xlimit = modClinic.GetBothQuantiExamVal(strHead, encid)
    minval = xlimit[0]
    maxval = xlimit[1]
    normval = modClinic.GetNormalQuantiExamVal(strHead, encid)
    For Each rData
      pointsArr_x.Add(rData!fldtime)
      pointsArr_y.Add(rData!fldrepquanti)
    Next

    If pointsArr_x.Count Then
      modChart.DrawClinicalChart(drw, encid, pointsArr_x, pointsArr_y, maxval, minval, normval)
    Endif

  Endif

End

Public Sub GridView1_Click()

  Dim xquantival As Variant[]
  Dim yqualival As Variant[]
  Dim xlimit As Float[]
  Dim Row As Integer
  Dim xx As String

  Row = GridView1.Row
  If GridView1.Column = 6 Then
    If $sType = "Quantitative" Then
      xlimit = modClinic.GetBothQuantiExamVal(GridView1[GridView1.Row, 4].Text, $encid)
      xquantival = GetQuantiValues("Exam", $encid, GridView1[GridView1.Row, 4].Text, xlimit[0], xlimit[1], modClinic.GetExamValueByID(GridView1[GridView1.Row, 0].Text))                                       '
      If xquantival Then
        modClinSub.UpdateQuantiData(GridView1[GridView1.Row, 0].Text, xquantival[0], xquantival[1])
        FillVitalExamChart(PictureBox1, $encid, lblexamname.Text)
      Endif
    Else If $sType = "Qualitative" Then
      If $sOption = "Clinical Scale" Then
        yqualival = modExamOption.GetClinScalePopUp("Exam", GridView1[GridView1.Row, 4].Text)
        If yqualival Then
          modClinSub.UpdateQualiQuantiData(GridView1[GridView1.Row, 0].Text, yqualival[1], CFloat(yqualival[0]), False)
        Endif
      Else If $sOption = "RichText Area" Then
        yqualival = GetQualiRich(GridView1[GridView1.Row, 4].Text, modClinic.GetExamValueString($encid, GridView1[GridView1.Row, 0].Text, False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData(GridView1[GridView1.Row, 0].Text, yqualival[0], yqualival[1], yqualival[2])
        Endif
      Else
        yqualival = GetQualiValues(GridView1[GridView1.Row, 4].Text, modClinic.GetExamValueString($encid, GridView1[GridView1.Row, 0].Text, False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData(GridView1[GridView1.Row, 0].Text, yqualival[0], yqualival[1], yqualival[2])
        Endif
      Endif
    Endif
    ShowExamGrid(ListBox1.Text)

  Else If GridView1.Column = 5 Then
    xx = InputCombo("Select Flag for the observation", "Change Flag", ["Normal", "Abnormal"], modMisc.GetIconValue(GridView1[GridView1.Row, 5].Picture), True)
    If xx Then
      modClinSub.UpdateExamFlag(GridView1[GridView1.Row, 0].Text, xx)
      ShowExamGrid(ListBox1.Text)
    Endif

  Endif
  GridView1.Row = Row

End

Public Sub btnrefresh_Click()

  FillVitalExamChart(PictureBox1, $encid, ListBox1.Text)

End

Private Sub FillComboBox()

  Dim xscle As String[]

  If txtquali.Count Then
    txtquali.Clear()
  Endif
  txtquali.Text = ""
  If lblexamname.Text Then
    If $sOption = "Single Selection" Or If $sOption = "Dichotomous" Then
      txtquali.List = modAllExam.SelectExamOptionList("Exam", lblexamname.Text)
      If modBasic.$TabletModeEnable = "Yes" Then
        txtquali.Popup()
      Endif
    Else If $sOption = "Clinical Scale" Then
      xscle = modExamOption.GetClinScalePopUp("Exam", lblexamname.Text)
      If xscle Then
        txtexamval.Value = CFloat(xscle[0])
        txtquali.Text = xscle[1]
        txtexamval.Visible = True
        txtquali.Visible = False
      Endif
    Else If $sOption = "Visual Input" Then
      txtquali.Text = modExamOption.GetVisualDataExamVal(lblexamname.Text)
    Else
      If modBasic.$DisplayQualiArea = "Yes" Then
        txtquali.Text = GetTextArea(lblexamname.Text, "")
      Endif
    Endif
  Endif
  txtquali.SetFocus

End

Public Sub GridView1_Menu()

  mnufile.Popup

End

Public Sub mnudel_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Edit("tblpatientexam", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then
        res["fldsave"] = False
        res["flduserid"] = modBasic.$lbluser
        res.Update
        ShowExamGrid(ListBox1.Text)
      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif
    Endif
  Endif

End

Public Sub btncolor_Click()

  Dim xcol As String

  xcol = InputColor("Triage", txtcolor.Background)
  If xcol Then
    txtcolor.Background = CInt(xcol)
    modPatient.SetPatientColor($encid, xcol)
  Endif

End

''==================== table =======================
Private Sub FillDailyGrid()

  Dim xfir As Date
  Dim xlast As Date

  Dim i As Integer
  Dim j As Integer

  xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, -18))
  xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))

  GridView2.Clear()
  GridView2.Rows.Count = $sVitalsVar.Count
  GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Hour) + 1

  For i = 0 To ListBox1.List.Count - 1
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        GridView2[i, j].Text = $sVitalsVar[i]["ExamName"]
      Else
        GridView2[i, j].Text = GetExamValueByDate($encid, $sVitalsVar[i]["ExamName"], DateAdd(xfir, gb.Hour, j))
      Endif
    Next
  Next

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = 150 * modBasic.$AppWidthRatio
        .Columns[j].Text = "Examination"
      Else
        .Columns[j].Width = 50 * modBasic.$AppWidthRatio
        .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd(xfir, gb.Hour, j), gb.ShortTime)
      Endif
    Next
  End With
  GridView2.Scroll(GridView2.X + GridView2.Width, GridView2.Y)

End

Private Function GetExamValueByDate(encid As String, xexam As String, xDate As Date) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldrepquanti from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 and fldtime>=&4 and fldtime<=&5", encid, True, xexam, modDate.StartSqlHour(xDate), modDate.EndSqlHour(xDate))
  If res.Available Then
    res.MoveFirst
    If res["fldrepquanti"] Then
      xx = CStr(res["fldrepquanti"])
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End
