' Gambas class file

Private $encid As String
Private $BillMode As String
Private $sPackage As String
Private $xFinClear As Boolean
Private $tot As Integer
Private $totRecord As Integer
Private $rData As Result
Private $aMyFields As String[]

Private aObj1 As New Object[50]
Private aObj2 As New Object[50]
Private aObj3 As New Object[50]
Private aObj4 As New Object[50]

Public Sub _new(encid As String, DiscType As String)

  $encid = encid
  $sPackage = DiscType

End

Public Sub Form_Open()

  Dim xstatus As String

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbdisctype.List = modBasic.$BillDiscountCash
  If cmbdisctype.List.Count = 0 Then
    cmbdisctype.Add($sPackage)
  Endif
  cmbdisctype.Text = $sPackage
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  txtpatientaddress.Text = modPatient.GetPatientAddressByEnc($encid)
  $BillMode = modNonMedical.GetDiscBindBillMode($sPackage)
  If Not $BillMode Then
    $BillMode = modpatient.GetPatBillingMode($encid)
  Endif

  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)
  $xFinClear = modNonMedical.GetFinanceClearance($encid)
  cmbequip.List = modNonMedical.NonStockBillActiveItemArray("Equipment", $BillMode)

  btnrefer.Tag = modBillings.GetReferralUserSetting("Equipment", $encid)
  If btnrefer.Tag Then
    btnrefer.Text = modGeneral.GetUserFullName(btnrefer.Tag)
  Endif
  If modBasic.$ReferralLockEntry = "Yes" Then
    btnrefer.Enabled = False
  Endif
  $tot = 0
  FillFrame()
  CompleteGrid()
  If $xFinClear = True Then
    btnadd.Enabled = False
  Endif
  cmbdisctype.SetFocus

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub cmbdisctype_Click()

  If cmbdisctype.Text Then
    $BillMode = modNonMedical.GetDiscBindBillMode(cmbdisctype.Text)
    If Not $BillMode Then
      $BillMode = modpatient.GetPatBillingMode($encid)
    Endif
    cmbequip.List = modNonMedical.NonStockBillActiveItemArray("Equipment", $BillMode)
  Endif

End

Public Sub cmbdisctype_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdisctype)
  modFillContainer.RestrictComboToContent(cmbdisctype)

End

Private Sub FillFrame()

  Dim i As Integer
  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldid,flditem,fldfirsttime from tblpattiming where fldencounterval=&1 and fldtype=&2 and fldfirstsave=&3 and fldsecondsave=&4", $encid, "Equipment", True, False)                                                                   ''
  $totRecord = res.Count
  If res.Available = True Then
    i = 0
    For Each res
      FillObjectInFrame(i, res!flditem, False, res!fldfirsttime, "", res!fldid)
      i = i + 1
    Next
    $tot = i - 1
  Endif

End

Public Sub btnadd_Click()

  If modNonMedical.GetControlServCombo(cmbequip.Text) = False Then
    cmbequip.Text = ""
    Balloon.Warning(("Item not listed"), cmbequip)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

  If cmbequip.Text Then
    $tot = $tot + 1
    FillObjectInFrame($tot, cmbequip.Text, True, "", "", 0)
  Endif

End

Private Sub FillObjectInFrame(i As Integer, txtitem As String, optnew As Boolean, txt1 As Date, txt2 As Date, id As Long)

  Dim AppFactor As Float

  AppFactor = modBasic.$AppScaleFactor
  aObj1[i] = New Label(Frame1) As "Labelgroup"
  aObj2[i] = New ButtonBox(Frame1) As "StartButton"
  aObj3[i] = New ButtonBox(Frame1) As "StopButton"
  aObj4[i] = New ValueBox(Frame1) As "ValueGroup"

  ' Frame1.Width = 775 * AppFactor

  With aObj1[i]
    .X = 5 * AppFactor
    .Y = i * 30 * AppFactor
    .Width = 300 * AppFactor
    .Height = 25 * AppFactor
    .Text = txtitem
    .Tag = i
    ' .Font.Bold = True
    .Border = 2
  End With

  With aObj2[i]
    .X = 325 * AppFactor
    .Y = i * 30 * AppFactor
    .Width = 200 * AppFactor
    .Height = 25 * AppFactor
    .Text = modReportVar.GetDateTimeReport(txt1, gb.GeneralDate)
    .Picture = Picture["icon:/small/play"]
    .Tag = i
    If optnew = True Then
      .Enabled = True
    Else If optnew = False Then
      .Enabled = False
    Endif
  End With

  With aObj3[i]
    .X = 550 * AppFactor
    .Y = i * 30 * AppFactor
    .Width = 200 * AppFactor
    .Height = 25 * AppFactor
    .Text = modReportVar.GetDateTimeReport(txt2, gb.GeneralDate)
    .Picture = Picture["icon:/small/stop"]
    .Tag = i
    If optnew = True Then
      .Enabled = False
    Else If optnew = False Then
      .Enabled = True
    Endif
  End With

  With aObj4[i]
    .X = 750 * AppFactor
    .Y = i * 30 * AppFactor
    .Width = 25 * AppFactor
    .Height = 25 * AppFactor
    .value = id
    .Visible = False
    .Tag = i
  End With

End

Public Sub StartButton_Click()

  Dim j As Integer
  Dim xidno As Long

  j = Last.Tag
  aObj2[j].Text = modReportVar.GetDateTimeReport(Now(), gb.GeneralDate)
  xidno = modPatientGeneral.AddPatientTableFirstTime($encid, "Equipment", cmbdisctype.Text, aObj1[j].Text, "", Now(), "")
  aObj2[j].Enabled = False
  aObj3[j].Enabled = True

End

Public Sub StopButton_Click()

  Dim j As Integer
  Dim hr As Float
  Dim unitrate As String
  Dim res As Result

  Dim xrefer As String
  Dim xpayble As String

  If btnrefer.Tag Then
    xrefer = btnrefer.Tag
  Else
    xrefer = modBillings.GetReferralUserSetting("Equipment", $encid)
  Endif
  xpayble = modBillings.GetPayableUserSetting("Equipment", $encid)

  Inc Application.Busy
  j = Last.Tag

  If aObj4[j].Value > 0 Then
    aObj3[j].Text = modReportVar.GetDateTimeReport(Now(), gb.GeneralDate)

    modDatabase.$myConn.Begin
    modPatientGeneral.UpdatePatientTableSecondTime(aObj4[j].Value, "")

    If modBasic.$AutoBillEquipment = "Standard" Or If modBasic.$AutoBillEquipment = "Full" Or If modBasic.$AutoBillEquipment = "Partial" Then
      res = modDatabase.$myConn.Exec("select flditem,fldfirsttime,fldsecondtime,flddisctype from tblpattiming where fldid=&1", aObj4[j].Value)
      If res.Available Then
        unitrate = modNonMedical.GetBillingTargeDept(aObj1[j].Text, "Equipment")
        hr = modPatPatho.GetEquipmentUseDuration($encid, aObj1[j].Text, unitrate, res["fldfirsttime"], res["fldsecondtime"])
        If hr > 0 Then
          modBillings.EnterBillingForInterval(modBasic.$AutoBillEquipment, "Equipment", $encid, res["flddisctype"], aObj1[j].Text, hr, aObj4[j].Value, xpayble, xrefer)
        Endif
      Endif
    Endif

    modDatabase.$myConn.Commit
    aObj3[j].Enabled = False
    aObj2[j].Enabled = True
  Endif
  Dec Application.Busy
  CompleteGrid()

Catch
  modDatabase.$myConn.Rollback
  Dec Application.Busy
  modHelpVariable.CreateErrorReport()

End

Public Sub Form_Resize()

  If Frame1.Height < $totRecord * 30 Then
    Frame1.Height = $totRecord * 30
  Endif

End

Public Sub btnrefer_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Payable User"), modBasic.$PayUserList)
  If xMedUser And If xMedUser.Count Then
    btnrefer.Tag = xMedUser[0]
    btnrefer.Text = xMedUser[1]
  Else
    btnrefer.Tag = ""
    btnrefer.Text = ""
  Endif

End

Private Sub CompleteGrid()

  Dim sql As String

  sql = "select fldid,flditem,fldfirsttime,fldsecondtime,fldid,fldfirstuserid,fldseconduserid from tblpattiming where fldencounterval=&1 and fldtype=&2"
  $rData = modDatabase.$myConn.Exec(sql, $encid, "Equipment")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 250 * modBasic.$AppWidthRatio
    .Columns[2].Width = 175 * modBasic.$AppWidthRatio
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 100 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Equipment"
    .Columns[2].Text = "Start"
    .Columns[3].Text = "Close"
    .Columns[4].Text = "Interval"
    .Columns[5].Text = "User1"
    .Columns[6].Text = "User2"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 2 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 4 Then
    GridView1.Data.Text = modPatPatho.GetEquipServiceInterval($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnrefer_Change()

  If btnrefer.Text = "" Then
    btnrefer.Tag = ""
  Endif

End

Public Sub cmbequip_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbequip)
  modFillContainer.RestrictComboToContent(cmbequip)

End
