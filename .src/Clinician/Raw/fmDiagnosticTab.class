' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]

Private xFinal As Variant[]
Private $ExamList As String[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  rbmetric.Value = True
  rblabs.Value = True
  rbdaterepo.Value = True
  If modBasic.$WebZoomValue Then
    Slider1.Value = modBasic.$WebZoomValue * 100
    WebView1.Zoom = Slider1.Value / 100
  Endif

End

Public Sub Slider1_Change()

  Inc Application.Busy
  WebView1.Zoom = Slider1.Value / 100
  Dec Application.Busy

End

Private Function LabUnitForm() As String

  Dim xx As String

  If rbsi.Value = True Then
    xx = "SI"
  Else
    xx = "Metric"
  Endif
  Return xx

End

Public Sub rbdaterepo_Click()

  btnshowexams_Click()

End

Public Sub rbnamerepo_Click()

  btnshowexams_Click()

End

''======================= List ============================
Private Sub ShowGridViewList()

  If rbnamerepo.Value = True Then
    If rblabs.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldtestid,fldtestid as col from tblpatlabtest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY fldtestid ORDER BY fldtestid", $encid, "Reported", "Verified", "Visible")
    Else If rbradio.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldtestid,fldtestid as col from tblpatradiotest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY fldtestid ORDER BY fldtestid", $encid, "Reported", "Verified", "Visible")
    Else If rbexam.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldhead,fldhead as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval IS NULL GROUP BY fldhead ORDER BY fldhead", $encid, True)
    Else If rbstruct.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldinput,fldinput as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval like &3 GROUP BY fldinput ORDER BY fldinput", $encid, True, "%")
    Endif

  Else If rbdaterepo.Value = True Then
    If rblabs.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime_sample),DATE(fldtime_sample) as col from tblpatlabtest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY DATE(fldtime_sample) ORDER BY fldtime_sample", $encid, "Reported", "Verified", "Visible")
    Else If rbradio.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime_report),DATE(fldtime_report) as col from tblpatradiotest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY DATE(fldtime_report) ORDER BY fldtime_report", $encid, "Reported", "Verified", "Visible")
    Else If rbexam.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime),DATE(fldtime) as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval IS NULL GROUP BY DATE(fldtime) ORDER BY fldtime", $encid, True)
    Else If rbstruct.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime),DATE(fldtime) as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval like &3 GROUP BY DATE(fldtime) ORDER BY fldtime", $encid, True, "%")
    Endif

  Endif

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Expand = True
    .Columns[1].Width = 1
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    If rbnamerepo.Value = True Then
      GridView1.Data.Text = $rData[$aMyFields[Column]]
    Else If rbdaterepo.Value = True Then
      GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
    Endif
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Click()

  Dim xformat As String

  If rbdaterepo.Value = True Then
    xformat = "Date"
  Else If rbnamerepo.Value = True Then
    xformat = "Name"
  Endif

  If GridView1.Rows.Selection.Count Then
    If rblabs.Value = True Then
      WebView1.Html = modDiagnosticReport.GetLaboratoryPanelReport($encid, xformat, LabUnitForm(), GridView1[GridView1.Row, 1].Text)
    Else If rbradio.Value = True Then
      WebView1.Html = modDiagnosticReport.GetRadiologyPanelReport($encid, xformat, GridView1[GridView1.Row, 1].Text)
    Else If rbexam.Value = True Then
      WebView1.Html = modDiagnosticReport.GetExaminationPanelReport($encid, "Physician Examinations", xformat, GridView1[GridView1.Row, 1].Text)
    Else If rbstruct.Value = True Then
      WebView1.Html = modDiagnosticReport.GetStructuredPanelReport($encid, xformat, GridView1[GridView1.Row, 1].Text)
    Endif
  Endif

End

''=============================== EXAMS ====================
Public Sub btnshowexams_Click()

  Dim xformat As String

  If rbdaterepo.Value = True Then
    xformat = "Date"
  Else If rbnamerepo.Value = True Then
    xformat = "Name"
  Endif

  If $encid Then
    ShowGridViewList()
    If rblabs.Value = True Then
      WebView1.Visible = True
      GridView2.Visible = False
      WebView1.Html = modDiagnosticReport.GetLaboratoryPanelReport($encid, xformat, LabUnitForm())
    Else If rbradio.Value = True Then
      WebView1.Visible = True
      GridView2.Visible = False
      WebView1.Html = modDiagnosticReport.GetRadiologyPanelReport($encid, xformat)
    Else If rbexam.Value = True Then
      WebView1.Visible = True
      GridView2.Visible = False
      WebView1.Html = modDiagnosticReport.GetExaminationPanelReport($encid, "Physician Examinations", xformat)
    Else If rbstruct.Value = True Then
      WebView1.Visible = True
      GridView2.Visible = False
      WebView1.Html = modDiagnosticReport.GetStructuredPanelReport($encid, xformat)
    Else If rbvitals.Value = True Then
      WebView1.Visible = False
      GridView2.Visible = True
      $ExamList = modString.GetListFromCompareVariant(modFixPatho.$ExaminationCompVar, "fldexamid", "fldcategory", "Essential Examinations", True)
      FillDailyGrid()
    Endif
  Endif

End

''----------------------------- Vitals ------------------------------
Private Sub FillDailyGrid()

  Dim xRowVal As Collection
  Dim xfir As Date
  Dim xlast As Date

  Dim i As Integer
  Dim j As Integer

  xFinal = New Variant[]

  xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, -18))
  xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))

  GridView2.Clear()
  GridView2.Rows.Count = $ExamList.Count
  GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Hour) + 1

  For i = 0 To $ExamList.Count - 1
    xRowVal = New Collection
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        xRowVal.Add($ExamList[i], CStr(j))
      Else
        xRowVal.Add(GetExamValueByDate($encid, $ExamList[i], DateAdd(xfir, gb.Hour, j)), CStr(j))
      Endif
    Next
    xFinal.Add(xRowVal)
  Next

  With GridView2
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = 150 * modBasic.$AppWidthRatio
        .Columns[j].Text = "Examination"
      Else
        .Columns[j].Width = 50 * modBasic.$AppWidthRatio
        .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd(xfir, gb.Hour, j), gb.ShortTime)
      Endif
    Next
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  modGeneralMain.GridViewDecoration(GridView2, Row)
  If xFinal.Count Then
    GridView2.Data.Text = xFinal[Row][CStr(Column)]
  Endif

End

Private Function GetExamValueByDate(encid As String, xexam As String, xDate As Date) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldrepquanti from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 and fldtime>=&4 and fldtime<=&5", encid, True, xexam, modDate.StartSqlHour(xDate), modDate.EndSqlHour(xDate))
  If res.Available Then
    res.MoveFirst
    If res["fldrepquanti"] Then
      xx = CStr(res["fldrepquanti"])
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End

Public Sub btnopenweb_Click()

  If modGeneralMain.$RightAdView Then
    modGeneralMain.$RightAdView.SetHtml(WebView1.Html)
    modGeneralMain.$RightAdView.Refresh()
  Endif

End
