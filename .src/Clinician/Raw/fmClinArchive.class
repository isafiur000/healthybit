' Gambas class file

Private $patId As String
Private $tblpatreport As String

Private $rData As Result
Private $aMyFields As String[]
Private $rData3 As Result
Private $aMyFields3 As String[]

Private $hPDF As PdfDocument

Public Sub _new(patId As String)

  $patId = patId

End

Public Sub Form_Open()

  cmbsource.List = ["All Categories", "Diagnostic Tests", "Radio Diagnostics", "General Reports", "Scanned Images", "Past Documents"]
  cmbsource.Text = "Scanned Images"
  modAccount.PasInvoiceSetting(cmbfiscal, False)
  LoadTableNames()

  ShowEncounterList()

End

Private Sub LoadTableNames()

  Dim res As Result

  If cmbfiscal.Text = "Current" Then
    $tblpatreport = "tblpatreport"
  Else
    res = modDatabase.$syConn.Exec("select fldpatreport from tblfisclosing where fldindex=&1 and (fldstate=&2 or fldstate IS NULL)", cmbfiscal.Text, "Active")
    If res.Available Then
      If res["fldpatreport"] Then
        $tblpatreport = res["fldpatreport"]
      Else
        $tblpatreport = "tblpatreport"
      Endif
    Else
      $tblpatreport = "tblpatreport"
    Endif
  Endif

End

Public Sub cmbfiscal_Click()

  LoadTableNames()

End

Private Sub ShowEncounterList()

  $rData = modDatabase.$myConn.Exec("select fldregdate,fldencounterval from tblencounter where fldpatientval=&1 and fldencounterval in(select fldencounterval from " & $tblpatreport & ") ORDER BY fldregdate DESC", $patId)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields, -1)
  With GridView1
    .Columns[0].Width = 125 * modBasic.$AppWidthRatio
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[0].Text = "Date"
    .Columns[1].Text = "Encounter"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Click()

  If GridView1.Rows.Selection.Count Then
    txtnewencid.Text = GridView1[GridView1.Row, 1].Text
    If txtnewencid.Text Then
      ShowDocuments(txtnewencid.Text)
    Endif
  Endif

End

Private Sub ShowDocuments(encid As String)

  If cmbsource.Text = "All Categories" Then
    $rData3 = modDatabase.$syConn.Exec("select fldid,fldtime,fldcateg,fldtitle,flddetail,fldsave,fldlink,fldencounterval,flvisible,fldtime from " & $tblpatreport & " where fldencounterval like &1 and (flvisible=&2 or flvisible IS NULL)", encid, "Visible")
  Else
    $rData3 = modDatabase.$syConn.Exec("select fldid,fldtime,fldcateg,fldtitle,flddetail,fldsave,fldlink,fldencounterval,flvisible,fldtime from " & $tblpatreport & " where fldencounterval like &1 and fldcateg like &2 and (flvisible=&3 or flvisible IS NULL)", encid, cmbsource.Text, "Visible")
  Endif
  $aMyFields3 = New String[]
  modGridView.ReadSmallData(GridView3, $rData3, $aMyFields3)

  With GridView3
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 200 * modBasic.$AppWidthRatio
    .Columns[4].Width = 250 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[6].Width = 1
    .Columns[7].Width = 1
    .Columns[8].Width = 1
    .Columns[9].Width = 1

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Title"
    .Columns[4].Text = "Comment"
  End With

End

Public Sub GridView3_Data(Row As Integer, Column As Integer)

  $rData3.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView3, Row)
  If Column = 1 Then
    GridView3.Data.Text = modReportVar.GetDateTimeReport($rData3[$aMyFields3[Column]], gb.GeneralDate)
  Else
    GridView3.Data.Text = $rData3[$aMyFields3[Column]]
  Endif

End

Public Sub GridView3_Click()

  Dim xFPath As String

  If GridView3.Rows.Selection.Count Then
    lbldate.Text = "DATE: " & GridView3[GridView3.Row, 1].Text
    txttitle.Text = GridView3[GridView3.Row, 3].Text
    txtcomment.Text = GridView3[GridView3.Row, 4].Text
    xFPath = modImage.GetBlobFileData(GridView3[GridView3.Row, 0].Text, $tblpatreport)
    If Exist(xFPath) Then

      Select Case Lower(File.Ext(xFPath))
        Case "bmp", "jpg", "jpeg", "png", "gif", "svg", "tiff"
          DisplayImage(xFPath)
        Case Else
          DisplayPDF(xFPath)
      End Select

    Endif
  Endif

End

Private Sub DisplayImage(sPath As String)

  PictureBox1.Visible = True
  docviewleft.Visible = False
  DocumentView1.Visible = False
  PictureBox1.Image = Image.Load(sPath)
  PictureBox1.Zoom = SliderBox1.Value / 100
  ' modImage.StretchtPictureToBox(PictureBox1, sPath)

End

Private Sub DisplayPDF(sPath As String)

  PictureBox1.Visible = False
  docviewleft.Visible = True
  DocumentView1.Visible = True
  If $hPDF And If $hPDF.Count Then
    $hPDF.Close()
  Endif

  $hPDF = New PdfDocument
  $hPDF.Open(sPath)
  DocumentView1.Count = $hPDF.Count
  docviewleft.Count = 0
  docviewleft.DocHeight = $hPDF[1].Height / 0.72
  docviewleft.DocWidth = $hPDF[1].Width / 0.72
  DocumentView1.DocWidth = $hPDF[1].Width / 0.72
  DocumentView1.DocHeight = $hPDF[1].Height / 0.72

  DocumentView1.Arrangement = 0
  DocumentView1.Refresh()

End

Public Sub DocumentView1_Layout(Page As Integer)

  Last.Layout.Height = $hPDF[Page + 1].Height / 0.72
  Last.Layout.Width = $hPDF[Page + 1].Width / 0.72

End

Public Sub DocumentView1_Draw(Page As Integer, Width As Integer, Height As Integer)

  If Page > $hPDF.Count - 1 Then Return

  $hPDF.Zoom = Last.Zoom / 0.72
  Draw.Image($hPDF[Page + 1].Image, 0, 0)

End

Public Sub DocumentView1_MouseDown()

  Print DocumentView1.Find(Mouse.x, Mouse.y)

End

Public Sub DocumentView1_Zoom()

  Object.Lock(SliderBox1)
  SliderBox1.Value = Last.Zoom * 100
  Object.Unlock(SliderBox1)

End

Public Sub SliderBox1_Change()

  DocumentView1.Zoom = SliderBox1.Value / 100
  PictureBox1.Zoom = SliderBox1.Value / 100

End

Public Sub btnview_Click()

  Dim indexLst As Long[]
  Dim fileList As String[]
  Dim xFPath As String
  Dim sPath As String

  If GridView3.Rows.Selection.Count > 0 Then
    xFPath = modImage.GetBlobFileData(GridView3[GridView3.Row, 0].Text, $tblpatreport)
    If Exist(xFPath) Then
      modDevice.DesktopOpenFile(xFPath)
    Endif
  Endif

End
