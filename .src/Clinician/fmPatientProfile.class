' Gambas class file

Public $PatientEncounter As String
Public $PatientComp As String
Private $ParentForm As String
Private $UserRestrict As String[]

Public Sub _new(ParentForm As String)

  $ParentForm = ParentForm

End

Public Sub Form_Open()

  modGeneralMain.DesignTextLabel(Me)
  cmbpatheightunit.List = ["cm", "inch", "foot"]
  $UserRestrict = modBasic.$ClinicDisableCompo
  If MMain.$SISHAppMode = "HIS" Then
    txtbedno.ReadOnly = True
  Endif
  modGeneralMain.SetEncIDPrefix(txtencid)
  txtencid.SetFocus

End

Public Sub Form_Close()

  If txtencid.Text Then
    modPatientSub.ReleaseLockEncounter(Trim(txtencid.Text), modHelpVariable.$LogInCategory)
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["O"] And If Key.Control Then
    btnwebcam_Click()
  Else If Key.Code = Key.F1 Then
    If Not txtencid.Text Or If txtencid.Text = modBasic.$EncIdPrefix Then
      GetF1Code()
    Endif
  Else If Key.Code = Key.F2 Then
    If Not txtencid.Text Or If txtencid.Text = modBasic.$EncIdPrefix Then
      GetF2Code()
    Endif
  Else If Key.Code = Key["F"] And If Key.Control Then
    txtencid.Text = GetEncid()
  Endif

End

Private Sub GetF1Code()

  If $ParentForm = "fmPatPharmacy" Then
    If modBasic.$regF1Dispensing = "Yes" Or If modBasic.$regF1Dispensing = "Partial" Then                                                                                                                             ''
      txtencid.Text = RegistrationNewForm()
    Else If modBasic.$regF1Dispensing = "Full" Then
      txtencid.Text = RegistrationForm(False)
    Endif
  Else If $ParentForm = "fmSamplingFir" Then
    If modBasic.$regF1Sampling = "Yes" Or If modBasic.$regF1Sampling = "Partial" Then
      txtencid.Text = RegistrationNewForm()
    Else If modBasic.$regF1Sampling = "Full" Then
      txtencid.Text = RegistrationForm(False)
    Endif
    ''for old
  Else If $ParentForm = "fmPatPharmacyOld" Then
    If modBasic.$regF1Dispensing = "Yes" Or If modBasic.$regF1Dispensing = "Partial" Then                                                                                                                             ''
      txtencid.Text = RegistrationNewForm()
    Else If modBasic.$regF1Dispensing = "Full" Then
      txtencid.Text = RegistrationForm(False)
    Endif

  Endif

End

Public Sub GetF2Code()

  Dim sOutstr As String

  If $ParentForm = "fmPatPharmacy" Then
    If modBasic.$DispPrescibeSource = "RemoteDB" Then
      If modDatabase.$pharmConn.Opened Then
        txtencid.Text = PharmacyRequest(modDatabase.$pharmConn)
      Endif
    Else
      If modGlobalSetting.ShowSettingFromDB("GeneralSettings/OnlineAccess_DispensingForm") = "Yes" Then
        sOutstr = RemoteRequest("Pharmacy")
        If sOutstr Then
          txtencid.Text = Split(sOutstr, "@")[0]
          $PatientComp = Split(sOutstr, "@")[1]
        Endif
      Endif
    Endif
  Else If $ParentForm = "fmProcedure" Then
    If modGlobalSetting.ShowSettingFromDB("GeneralSettings/OnlineAccess_ProcedureForm") = "Yes" Then
      sOutstr = RemoteRequest("Procedure")
      If sOutstr Then
        txtencid.Text = Split(sOutstr, "@")[0]
        $PatientComp = Split(sOutstr, "@")[1]
      Endif
    Endif
  Else If $ParentForm = "fmExtProcedure" Then
    ''for old
  Else If $ParentForm = "fmPatPharmacyOld" Then
    If modGlobalSetting.ShowSettingFromDB("GeneralSettings/OnlineAccess_DispensingForm") = "Yes" Then
      sOutstr = RemoteRequest("Pharmacy")
      If sOutstr Then
        txtencid.Text = Split(sOutstr, "@")[0]
        $PatientComp = Split(sOutstr, "@")[1]
      Endif
    Endif
  Endif

End

'''XXXXXXXXXXXXXX'GET GENERAL INFO
Private Sub FillParentEncounter()

  If $ParentForm = "fmProcedure" Then
    fmProcedure.BasicInfoPatient()
  Else If $ParentForm = "fmPatPharmacy" Then
    fmPatPharmacy.BasicInfoPatient()
  Else If $ParentForm = "fmSamplingFir" Then
    fmSamplingFir.BasicInfoPatient()
  Else If $ParentForm = "fmExtProcedure" Then
    fmExtProcedure.BasicInfoPatient()
  Endif

End

Private Sub LockParentForms()

  If $ParentForm = "fmProcedure" Then
    fmProcedure.Enabled = False
  Else If $ParentForm = "fmPatPharmacy" Then
    fmPatPharmacy.Enabled = False
  Else If $ParentForm = "fmSamplingFir" Then
    fmSamplingFir.Enabled = False
  Else If $ParentForm = "fmExtProcedure" Then
    fmExtProcedure.Enabled = False
  Endif

End

Public Sub txtencid_LostFocus()

  GetEncounterValue()

End

Private Sub GetEncounterValue()

  Dim res As Result

  If Not txtpatientname.Text Then
    If Not txtencid.Text Then Return
    If modBasic.$EncIdPrefix And If txtencid.Text = modBasic.$EncIdPrefix Then
      txtencid.SetFocus
      Return
    Endif

    ' If modPatientSub.LockEncounterDesk(Trim(txtencid.Text)) = False Then
    '   txtencid.Text = ""
    '   Balloon.Warning(("Encounter Locked"), txtencid)
    '   Balloon.Delay = modBasic.$BalloonDelay
    '   txtencid.SetFocus
    ' Endif

    dtdob.Value = ""
    If txtencid.Text Then
      If txtencid.Text = modBasic.$EncIdPrefix Then
      Else
        modSettings.SaveValuesToLog("LastValue/Encounter", Trim(txtencid.Text))
      Endif
      res = modDatabase.$myConn.Exec("select fldpatientval from tblencounter where fldencounterval=&1", Trim(txtencid.Text))
      If res.Available = False Then
        modLinkDB.CheckEncDataRemote(Trim(txtencid.Text))
        txtencid.SetFocus
      Else
        $PatientEncounter = txtencid.Text
        txtpatientname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
        If txtpatientname.Text Then
          txtgender.Text = modPatient.GetPatientSex(Trim(txtencid.Text))
          txtpatientaddress.Text = modPatient.GetPatientAddressByEnc(Trim(txtencid.Text))
          dtdob.Value = modPatient.GetPatientBirthDay(Trim(txtencid.Text))
          modDateSub.GetAgeControlFromDate(dtdob.Value, txtpatage, cmbpatageunit)

          txtnow.Text = modPatient.CurrentAdmissionStatus(Trim(txtencid.Text))
          txtbedno.Text = modPatient.GetLocationSetting(Trim(txtencid.Text), txtnow.Text)
          txtpatdoa.Text = modReportVar.GetDateTimeReport(modPatient.GetRecordDate(Trim(txtencid.Text)), gb.GeneralDate)
          txtpatweight.Value = modClinic.GetBodyWeight(Trim(txtencid.Text))
          btnconsult.Tag = modPatient.GetAttendingConsultant(Trim(txtencid.Text))
          btnconsult.Text = modGeneral.GetUserFullName(btnconsult.Tag)
          modClinSub.GetPatientHeight(Trim(txtencid.Text), txtpatheight, cmbpatheightunit)
          UpdateTriage()
          Wait
          If modPatientSub.AllowEncIDHistory(Trim(txtencid.Text), modDatabase.$myConn) = True Then
            FillParentEncounter()
            BlockDCButton()
            txtencid.ReadOnly = True
            modImage.ShowPhotoSplash("Patient", modPatient.GetPatientNoByEnc(Trim(txtencid.Text)))
          Else
            Me.Enabled = False
            LockParentForms()
          Endif
        Else
          Balloon.Warning(("Encounter ID not found"), txtencid)
          Balloon.Delay = modBasic.$BalloonDelay
          txtencid.SetFocus
        Endif
      Endif
    Endif
  Endif

End

Public Sub UpdateTriage()

  txtcolor.Background = modPatient.GetPatientColor(Trim(txtencid.Text))

End

Private Sub BlockDCButton()

  Dim xx As Boolean

  xx = modPatient.EnableClinicForm(txtnow.Text)
  tlbtnadd1.Enabled = xx
  tlbtnaddbed.Enabled = xx
  tlbtnadwt.Enabled = xx
  dtdob.Enabled = xx
  btnpic.Enabled = xx
  btnconsult.Enabled = xx

  If $UserRestrict Then
    RestrictUserAccess()
  Endif

End

Private Sub RestrictUserAccess()

  If $UserRestrict.Exist("Bed Assignment") Then
    tlbtnaddbed.Enabled = False
  Endif
  If $UserRestrict.Exist("Body Weight") Then
    tlbtnadwt.Enabled = False
  Endif
  If $UserRestrict.Exist("Body Height") Then
    tlbtnadd1.Enabled = False
  Endif

End

Public Sub tlbtnadd1_Click()

  If txtencid.Text Then
    If txtpatheight.Value Then
      If cmbpatheightunit.Text Then
        modClinSub.UpdatePatientHeight(Trim(txtencid.Text), txtpatheight.Value, cmbpatheightunit.Text)
        tlbtnadd1.Enabled = False
        Balloon.Info(("Information updated"), tlbtnadd1)
        Balloon.Delay = modBasic.$BalloonDelay
      Endif
    Endif
  Endif

End

Public Sub dtdob_Click()

  If txtencid.Text Then
    If dtdob.Value Then
      modPatientSub.UpdatePatDOB(Trim(txtencid.Text), dtdob.Value)
      modDateSub.GetAgeControlFromDate(dtdob.Value, txtpatage, cmbpatageunit)
      Balloon.Info(("Information updated"), dtdob)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub cmbpatageunit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbpatageunit)
  modFillContainer.RestrictComboToContent(cmbpatageunit)

End

Public Sub cmbpatheightunit_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    tlbtnadd1_Click()
  Else
    modFillContainer.AutoFillComboBox(cmbpatheightunit)
    modFillContainer.RestrictComboToContent(cmbpatheightunit)
  Endif

End

Public Sub btnconsult_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Consultant"), modBasic.$IPConsultUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif
  If btnconsult.Tag And If txtencid.Text Then
    modPatientSub.UpdateAttendingConsultant(Trim(txtencid.Text), btnconsult.Tag)
    Balloon.Info(("Information saved"), btnconsult)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnwebcam_Click()

  If txtencid.Text Then
    GetEncounterValue()
  Else
    txtencid.Text = modDevice.ChooseBarCodeSource()
    txtencid.SetFocus
    txtencid.Pos = Len(txtencid.Text)
  Endif

End

Public Sub tlbtnaddbed_Click()

  Dim hForm As FmBedManage

  If txtencid.Text Then
    If MMain.$SISHAppMode = "HIS" Then
      If modHelpVariable.$LogInCategory = "Clinician" Then
        If txtnow.Text = "Admitted" Then
          hForm = New FmBedManage(Trim(txtencid.Text), txtbedno)
          hForm.ShowModal
        Endif
      Endif
    Else
      modPatientSub.UpdatePatLocation(Trim(txtencid.Text), txtbedno.Text)
      Balloon.Info(("Location Changed"), txtbedno)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub txtpatweight_GotFocus()

  If modBasic.$TabletModeEnable = "Yes" Then
    txtpatweight.Enabled = False
    txtpatweight.Value = InputValue(("Set Patient Body Weight"), txtencid.Text, txtpatweight.Value)
    txtpatweight.Enabled = True
  Endif

End

Public Sub txtpatheight_GotFocus()

  If modBasic.$TabletModeEnable = "Yes" Then
    txtpatheight.Enabled = False
    txtpatheight.Value = InputValue(("Set Patient Body Height"), txtencid.Text, txtpatheight.Value)
    txtpatheight.Enabled = True
  Endif

End

Public Sub tlbtnadwt_Click()

  If txtencid.Text Then
    If txtpatweight.Value Then
      modClinSub.AddGeneralParametersQuanti(Trim(txtencid.Text), "Body Weight", txtpatweight.Value)
      tlbtnadwt.Enabled = False
      Balloon.Info(("Information saved"), tlbtnadwt)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub txtpatweight_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    tlbtnadwt_Click()
  Endif

End

Public Sub tlbtnpayment_Click()

  If txtencid.Text Then
    modNonMedical.CurrentPayStatusMsg(Trim(txtencid.Text))
  Endif

End

Public Sub btnpic_Click()

  Dim hForm As FmPersonImage

  If txtencid.Text Then
    hForm = New FmPersonImage("Patient", modPatient.GetPatientNoByEnc(Trim(txtencid.Text)), False)
    hForm.ShowModal
  Endif

End

Public Sub btnsms_Click()

  Dim xval As String

  If btnconsult.Tag Then
    xval = modDevice.SendSMSToInpatDoc(Trim(txtencid.Text), modGeneral.GetUserContact(btnconsult.Tag))
    If xval Then
      Balloon.Info(xval, btnsms)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

Public Sub btnprofile_Click()

  Dim hForm As FmMinProfile

  hForm = New FmMinProfile(Trim(txtencid.Text))
  hForm.ShowModal()

End

Public Sub btnipnum_Click()

  Dim res As Result
  Dim xx As String

  xx = InputBox("Provide Inpatient (IP) Number of Patient", "Encounter", "")
  If xx Then
    res = modDatabase.$myConn.Exec("select fldencounterval from tblencounter where fldadmitid=&1 ORDER BY fldregdate", xx)
    If res.Available Then
      res.MoveLast
      txtencid.Text = res["fldencounterval"]
      txtencid.SetFocus
    Else
      Message.Info("IP Number Not Registered", "OK")
    Endif
  Endif

End

Public Sub txtencid_KeyPress()

  If Key.Code = Key.Down Then
    If Not txtencid.Text Then
      txtencid.Text = PatSearch("Encounter")
      txtencid.SetFocus
    Else
      If modBasic.$AutoEncSuffix = "Yes" Then
        txtencid.Text = txtencid.Text & modBasic.$HospCode
      Endif
    Endif
  Else
    modGeneralMain.InputUpCaseOnly()
  Endif

End
