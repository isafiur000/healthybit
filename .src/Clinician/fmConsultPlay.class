' Gambas class file

Private $rData2 As Result
Private $aMyFields2 As String[]
Private $encid As String
Private $sDeprt As String

Public Sub _new(encid As String, sDeprt As String)

  $encid = encid
  $sDeprt = sDeprt

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  CompleteGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Private Sub CompleteGrid()

  Dim sql As String
  Dim xFldList1 As String[]
  Dim xFldList2 As String[]

  xFldList1 = ["fldid", "fldconsulttime", "fldencounterval", "fldconsultname", "flduserid", "fldid"]
  xFldList1.Add(Quote("tblconsult"))
  xFldList2 = ["fldid", "fldconsulttime", "fldencounterval", "fldconsultname", "fldstatus", "fldid"]
  xFldList2.Add(Quote("tblopvisit"))
  If $sDeprt Then
    sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus=&2 and fldconsultname=&3" & " UNION ALL " & "select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus=&2 and fldconsultname=&3"                        ''
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Planned", $sDeprt)
  Else
    sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus=&2" & " UNION ALL " & "select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus=&2"
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Planned")
  Endif
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 250 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio
    .Columns[6].Width = 1

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Department"
    .Columns[4].Text = "Consultant"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData2.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  If Column = 1 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData2[$aMyFields2[Column]], gb.GeneralDate)
  Else If Column = 4 Then
    GridView2.Data.Text = modGeneral.GetUserFullName($rData2[$aMyFields2[Column]])
  Else If Column = 5 Then
    GridView2.Data.Picture = Picture["icon:/small/apply"]
    GridView2.Data.Text = ""
  Else
    GridView2.Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Public Sub GridView2_Click()

  Dim hForm As FmOPOutcome

  If GridView2.Column = 5 Then
    If GridView2[GridView2.Row, 6].Text = "tblconsult" Then
      hForm = New FmOPOutcome("Consultation", GridView2[GridView2.Row, 2].Text, GridView2[GridView2.Row, 0].Text)
    Else If GridView2[GridView2.Row, 6].Text = "tblopvisit" Then
      hForm = New FmOPOutcome("OP Visit", GridView2[GridView2.Row, 2].Text, GridView2[GridView2.Row, 0].Text)
    Endif
    hForm.ShowModal
    CompleteGrid()
  Endif

End
