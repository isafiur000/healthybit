' Gambas class file

Private $encList As String[]
Private $encBed As String[]
Private $sColor As String
Private $sDept As String

Public Sub _new(sDept As String, sOrder As String)

  $sDept = sDept
  $sColor = sOrder

End

Public Sub Form_Open()

  Me.FullScreen = True
  FillBedGrid()

End

Public Sub Form_Resize()

  ResizeGrid()
  GetColorSelectionGrid()

End

Private Sub FillBedGrid()

  Dim sql As String
  Dim res As Result
  Dim xbedlist As String[]
  Dim xenclist As String[]

  xenclist = New String[]
  xbedlist = New String[]
  If $sDept = "%" Then
    sql = "select fldencounterval,fldcurrlocat from tblencounter where fldadmission=&1 ORDER BY fldcurrlocat ASC"                       ''
    res = modDatabase.$myConn.Exec(sql, "Admitted")
    For Each res
      xbedlist.Add(res["fldencounterval"] & "@" & res["fldcurrlocat"])
      xenclist.Add(res["fldencounterval"])
    Next
  Else
    sql = "select fldencounterval,fldbed from tbldepartmentbed where fldencounterval like &1 and flddept=&2 ORDER BY fldbed ASC"                       ''
    res = modDatabase.$myConn.Exec(sql, "%", $sDept)
    For Each res
      xbedlist.Add(res["fldencounterval"] & "@" & res["fldbed"])
      xenclist.Add(res["fldencounterval"])
    Next
  Endif

  $encList = xenclist
  $encBed = xbedlist

End

Private Sub GetColorSelectionGrid()

  Dim i As Integer
  Dim xcoList As Integer[]
  Dim xcolor As Integer
  Dim xbedlist As String[]
  Dim xenclist As String[]

  If $sColor = "All" Then
    GridView1.Rows.Height = 50 * modBasic.$AppWidthRatio
  Else If $sColor = "Order" Then
    xbedlist = New String[]
    xenclist = New String[]
    xcoList = [Color.Red, Color.Yellow, Color.Green, Color.Blue, Color.Black, Color.Transparent]
    For Each xcolor In xcoList
      For i = 0 To GridView1.Rows.Count - 1
        If xcolor = GridView1[i, 0].Background Then
          xenclist.Add(GridView1[i, 2].Text)
          xbedlist.Add(GridView1[i, 2].Text & "@" & GridView1[i, 1].Text)
        Endif
      Next
    Next
    Wait
    $encList = xenclist
    $encBed = xbedlist
    ResizeGrid()
    GridView1.Rows.Height = 50 * modBasic.$AppWidthRatio
  Else
    For i = 0 To GridView1.Rows.Count - 1
      If modMisc.GetColorCode($sColor) = GridView1[i, 0].Background Then
        GridView1.Rows[i].Height = 50 * modBasic.$AppWidthRatio
      Else
        GridView1.Rows[i].Height = 1
      Endif
    Next
  Endif

End

Private Sub ResizeGrid()

  GridView1.Clear()
  GridView1.Columns.Count = 8
  GridView1.Rows.Count = $encBed.Count
  With GridView1
    .Rows.Height = 50 * modBasic.$AppWidthRatio
    .Columns[0].Width = 25
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio
    .Columns[6].Width = 225 * modBasic.$AppWidthRatio
    .Columns[7].Width = 125 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Bed"
    .Columns[2].Text = "EncID"
    .Columns[3].Text = "Name"
    .Columns[4].Text = "Age/Sex"
    .Columns[6].Text = "Diagnosis"
    .Columns[7].Text = "Consult"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  Dim asx As String[]

  asx = Split($encBed[Row], "@")
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Text = ""
    GridView1.Data.Background = modPatient.GetPatientColor(asx[0])
  Else If Column = 1 Then
    GridView1.Data.Text = asx[1]
  Else If Column = 3 Then
    GridView1.Data.Text = modPatient.GetPatientNameByEnc(asx[0], modDatabase.$syConn)
    GridView1.Data.WordWrap = True
  Else If Column = 4 Then
    GridView1.Data.RichText = modPatient.GetPatientAgeString(asx[0], Now()) & "/" & modPatient.GetPatientSex(asx[0], modDatabase.$syConn) & "<br>" & "<small>" & modReportVar.GetDateTimeReport(modPatient.GetAdmissionDate(asx[0]), gb.MediumDate) & "</small>"                           ''
  Else If Column = 5 Then
    GridView1.Data.Picture = Picture["icon:/small/access"]
    GridView1.Data.Text = ""
  Else If Column = 6 Then
    GridView1.Data.Font.Grade = -1
    GridView1.Data.Text = modPatient.PatientDiagnoCurrentList(asx[0]).Join(gb.NewLine)
    GridView1.Data.WordWrap = True
  Else If Column = 7 Then
    GridView1.Data.Text = modGeneral.GetUserFullName(modPatient.GetAttendingConsultant(asx[0]))
    GridView1.Data.WordWrap = True
  Else
    GridView1.Data.Text = asx[0]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub Timer1_Timer()

  Dim Row As Integer
  Dim Column As Integer

  If Second(Now()) = 10 Then
    Row = GridView1.Row
    Column = GridView1.Column
    FillBedGrid()
    GridView1.Row = Row
    GridView1.Column = Column
  Endif

End
