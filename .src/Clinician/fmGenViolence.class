' Gambas class file

Private $encid As String
Private $doa As Date
Private xYear As Integer

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  Dim xstatus As String
  Dim xgenvio As String

  modGeneralMain.ArrangeFormCentre(Me, "False")

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  $doa = modPatient.GetRecordDate($encid)
  txtpatientaddress.Text = modPatient.GetPatientAddressByEnc($encid)
  xYear = DateDiff(modPatient.GetPatientBirthDay($encid), Now(), gb.Year)

  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)

  modHMISRep.LoadHMISLists()
  cmbperpsex.List = ["Male", "Female", "Other"]
  cmbviotype.List = modHMISRep.$HMISViolTypeOCMC
  cmbviocause.List = modHMISRep.$HMISViolCauseOCMC
  cmbdisability.List = modHMISRep.$HMISDisabilityOCMC
  cmbservice.List = modHMISRep.$HMISServiceOCMC
  cmbperprelation.List = ["Within relatives", "Outside of relatives"]
  xgenvio = modPatient.GetDemographAccountValue($encid, "Gender Based Violence")
  If xgenvio = "Yes" Then
    chkgenviol.Value = True
    Frame1.Enabled = True
    Panel1.Enabled = True
  Else
    chkgenviol.Value = False
  Endif
  ShowPreviosData()

End

Public Sub btnpatcode_Click()

  If chkgenviol.Value = True Then
    modPatientSub.AddAccGeneralQualiData($encid, "Demographics", "Gender Based Violence", "Yes", "", Now(), True)
    Frame1.Enabled = True
    Panel1.Enabled = True
    Balloon.Info(("Information updated"), btnpatcode)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub ShowPreviosData()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldviotype,fldviocause,flddisability,fldservice,fldperpname,fldrelation,fldperpsex,fldperpage,fldcommtype,fldcommcause,fldcommability,fldcommservice,fldcomment from tblgenviolence where fldencounterval=&1", $encid)
  If res.Available Then
    cmbviotype.Text = res["fldviotype"]
    txtviotype.Text = res["fldcommtype"]
    cmbviocause.Text = res["fldviocause"]
    txtviocause.Text = res["fldcommcause"]
    cmbdisability.Text = res["flddisability"]
    txtdisability.Text = res["fldcommability"]
    cmbservice.Text = res["fldservice"]
    txtservice.Text = res["fldcommservice"]
    txtperpname.Text = res["fldperpname"]
    cmbperprelation.Text = res["fldrelation"]
    cmbperpsex.Text = res["fldperpsex"]
    If res["fldperpage"] Then
      txtperpage.Value = res["fldperpage"] / 365
    Endif
  Endif

End

Public Sub btnupdate_Click()

  Dim res1 As Result
  Dim res As Result

  If $encid Then
    res1 = modDatabase.$myConn.Exec("select fldid from tblgenviolence where fldencounterval=&1", $encid)
    If res1.Available Then
      res = modDatabase.$myConn.Edit("tblgenviolence", "fldid=&1", res1["fldid"])
      res["flduptime"] = Now()
    Else
      res = modDatabase.$myConn.Create("tblgenviolence")
      res["fldtime"] = Now()
    Endif
    res["fldencounterval"] = $encid
    res["fldviotype"] = cmbviotype.Text
    res["fldviocause"] = cmbviocause.Text
    res["flddisability"] = cmbdisability.Text
    res["fldservice"] = cmbservice.Text
    res["fldperpname"] = Trim(txtperpname.Text)
    res["fldrelation"] = cmbperprelation.Text
    res["fldperpsex"] = cmbperpsex.Text
    res["fldperpage"] = txtperpage.Value * 365

    res["fldcommtype"] = Trim(txtviotype.Text)
    res["fldcommcause"] = Trim(txtviocause.Text)
    res["fldcommability"] = Trim(txtdisability.Text)
    res["fldcommservice"] = Trim(txtservice.Text)
    res["fldcomment"] = ""

    res["flduserid"] = modBasic.$lbluser

    res["fldcomp"] = modBasic.$compID
    res["fldsave"] = True

    res["fldviolref"] = ""
    res["xyz"] = False
    res.Update

    Balloon.Info(("Information updated"), btnupdate)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub cmbviotype_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbviotype)
  modFillContainer.RestrictComboToContent(cmbviotype)

End

Public Sub cmbviocause_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbviocause)
  modFillContainer.RestrictComboToContent(cmbviocause)

End

Public Sub cmbdisability_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdisability)
  modFillContainer.RestrictComboToContent(cmbdisability)

End

Public Sub cmbservice_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbservice)
  modFillContainer.RestrictComboToContent(cmbservice)

End

Public Sub cmbperprelation_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbperprelation)
  modFillContainer.RestrictComboToContent(cmbperprelation)

End

Public Sub cmbperpsex_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbperpsex)
  modFillContainer.RestrictComboToContent(cmbperpsex)

End
