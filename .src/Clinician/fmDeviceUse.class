' Gambas class file

Private $encid As String
Private $sType As String
Private $tot As Integer
Private $totRecord As Integer
Private $rData As Result
Private $aMyFields As String[]
Private $AllLines As String[]
Private AppFactor As Float

Private aObj1 As New Object[50]
Private aObj1a As New Object[50]
Private aObj2 As New Object[50]
Private aObj3 As New Object[50]
Private aObj4 As New Object[50]

Public Sub _new(encid As String, sType As String)

  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  Dim xstatus As String

  modGeneralMain.ArrangeFormCentre(Me, "False")

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  txtpatientaddress.Text = modPatient.GetPatientAddressByEnc($encid)

  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)
  GetAllRoutes()
  AppFactor = modBasic.$AppScaleFactor

  ItemList()
  $tot = 0
  FillFrame()
  FillEventGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub Form_Resize()

  If Frame1.Height < ($totRecord * 30) * AppFactor Then
    Frame1.Height = ($totRecord * 30) * AppFactor
  Endif

End

Private Sub GetAllRoutes()

  $AllLines = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select fldlocat as col from tbldevices where flditem=&1", "%"))

End

Private Sub ItemList()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select flditem as col from tblevents where fldtype=&1", $sType)
  cmbitem.List = modControlSub.GetDirectFillresult(res)

End

Public Sub btnadditem_Click()

  Dim hForm As FmAddVarTwoCombo
  Dim xPass As String

  xPass = InputPassword("Provide Root Password", "Verification", "")
  If modPassword.GetSuperVerification(modBasic.$lbluser, xPass) = True Then
    hform = New FmAddVarTwoCombo("fldtype", "fldcategory", "flditem", "tblevents", $sType, modHIReport.$DeviceCategory)
    hform.ShowModal
    ItemList()
  Endif

End

Public Sub btnaddlocat_Click()

  Dim hForm As FmAddVarTwo
  Dim aList As String[]
  Dim bList As String[]
  Dim xPass As String

  xPass = InputPassword("Provide Root Password", "Verification", "")
  If modPassword.GetSuperVerification(modBasic.$lbluser, xPass) = True Then
    aList = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select flditem as col from tblevents where fldtype=&1", $sType))
    bList = New String[]
    hForm = New FmAddVarTwo("flditem", "fldlocat", "tbldevices", aList, bList)
    hform.ShowModal
    GetAllRoutes()
  Endif

End

Private Sub FillFrame()

  Dim i As Integer
  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldid,flditem,fldfirsttime,flddetail from tblpatevents where fldencounterval=&1 and fldtype=&2 and fldfirstsave=&3 and fldsecondsave=&4", $encid, $sType, True, False)                                                                   ''
  $totRecord = res.Count
  If res.Available = True Then
    i = 0
    For Each res
      FillObjectInFrame(i, res["flditem"], res["flddetail"], False, res["fldfirsttime"], "", res["fldid"])
      i = i + 1
    Next
    $tot = i - 1
  Endif

End

Public Sub btnadd_Click()

  If cmbitem.Text Then
    $tot = $tot + 1
    FillObjectInFrame($tot, cmbitem.Text, "", True, "", "", 0)
  Endif

End

Private Sub FillObjectInFrame(i As Integer, txtitem As String, txtlocat As String, optnew As Boolean, txt1 As Date, txt2 As Date, id As Long)

  Dim xList As String[]

  xList = New String[]
  aObj1[i] = New Label(Frame1) As "Labelgroup"
  aObj1a[i] = New ComboBox(Frame1) As "Location"
  aObj2[i] = New ButtonBox(Frame1) As "StartButton"
  aObj3[i] = New ButtonBox(Frame1) As "StopButton"
  aObj4[i] = New ValueBox(Frame1) As "ValueGroup"

  ' Frame1.Width = 800 * AppFactor
  With aObj1[i]
    .X = 5 * AppFactor
    .Y = (i * 30) * AppFactor
    .Width = 275 * AppFactor
    .Height = 25 * AppFactor
    .Text = txtitem
    .Tag = i
    ' .Font.Bold = True
    .Border = 2
  End With

  With aObj1a[i]
    .X = 290 * AppFactor
    .Y = (i * 30) * AppFactor
    .Width = 125 * AppFactor
    .Height = 25 * AppFactor
    .ReadOnly = False
    xList = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select fldlocat as col from tbldevices where flditem=&1", txtitem))
    If xList.Count Then
      .List = xList
    Else
      .List = $AllLines
    Endif
    .Text = txtlocat
    .Tag = i
    .Border = 2
  End With

  With aObj2[i]
    .X = 425 * AppFactor
    .Y = (i * 30) * AppFactor
    .Width = 150 * AppFactor
    .Height = 25 * AppFactor
    .Text = modReportVar.GetDateTimeReport(txt1, gb.GeneralDate)
    .Picture = Picture["icon:/small/play"]
    .Tag = i
    If optnew = True Then
      .Enabled = True
    Else If optnew = False Then
      .Enabled = False
    Endif
  End With

  With aObj3[i]
    .X = 585 * AppFactor
    .Y = (i * 30) * AppFactor
    .Width = 150 * AppFactor
    .Height = 25 * AppFactor
    .Text = modReportVar.GetDateTimeReport(txt2, gb.GeneralDate)
    .Picture = Picture["icon:/small/stop"]
    .Tag = i
    If optnew = True Then
      .Enabled = False
    Else If optnew = False Then
      .Enabled = True
    Endif
  End With

  With aObj4[i]
    .X = 745 * AppFactor
    .Y = (i * 30) * AppFactor
    .Width = 5 * AppFactor
    .Height = 25 * AppFactor
    .value = id
    .Visible = False
    .Tag = i
  End With

End

Public Sub StartButton_Click()

  Dim j As Integer
  Dim xindex As Long

  j = Last.Tag
  If aObj1a[j].Text Then
    aObj2[j].Text = modReportVar.GetDateTimeReport(Now(), gb.GeneralDate)
    xindex = modPatientGeneral.AddPatientEventsFirstTime($encid, $sType, aObj1[j].Text, aObj1a[j].Text, "")
    aObj4[j].Value = xindex
    aObj2[j].Enabled = False
    aObj3[j].Enabled = True
  Endif

End

Public Sub StopButton_Click()

  Dim j As Integer

  j = Last.Tag
  If aObj4[j].Value > 0 Then
    aObj3[j].Text = modReportVar.GetDateTimeReport(Now(), gb.GeneralDate)
    modPatientGeneral.UpdatePatientEventSecondTime(aObj4[j].Value)
    aObj3[j].Enabled = False
    aObj2[j].Enabled = True

    FillEventGrid()
  Endif

End

''------------------ Grid------------------
Private Sub FillEventGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,flditem,flddetail,fldfirsttime,fldsecondtime,fldid,fldfirsttime,fldsecondtime from tblpatevents where fldencounterval=&1 and fldtype=&2 and fldfirstsave=&3 and fldsecondsave=&4", $encid, $sType, True, True)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 175 * modBasic.$AppWidthRatio
    .Columns[2].Width = 175 * modBasic.$AppWidthRatio
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 175 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 1
    .Columns[7].Width = 1

    .Columns[1].Text = "Particulars"
    .Columns[2].Text = "Location"
    .Columns[3].Text = "Start"
    .Columns[4].Text = "Stop"
    .Columns[5].Text = "Hours"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 4 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 5 Then
    GridView1.Data.Text = modPatPatho.GetEventDuration($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Menu()

  If modBasic.$SuperUser = True Then
    mnuhide.Popup
  Endif

End

Public Sub mnueditfir_Click()

  Dim res As Result
  Dim xdate As Date

  If GridView1.Rows.Selection.Count Then
    xdate = GetDateValue(GridView1[GridView1.Row, 1].Text, "Starting DateTime", GridView1[GridView1.Row, 6].Text)
    If xdate Then
      If xdate < CDate(GridView1[GridView1.Row, 7].Text) Then
        res = modDatabase.$myConn.Edit("tblpatevents", "fldid=&1", GridView1[GridView1.Row, 0].Text)
        res["fldfirsttime"] = xdate
        res.Update
        FillEventGrid()
      Else
        Message.Warning("Incorrect DateTime", ("OK"))
      Endif
    Endif
  Endif

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count Then
    If Message.Question("Are you sure?", ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblpatevents", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      FillEventGrid()
    Endif
  Endif

End

Public Sub mnueditlocat_Click()

  Dim res As Result
  Dim xval As String

  If GridView1.Rows.Selection.Count Then
    xval = InputBox(GridView1[GridView1.Row, 1].Text, "Device Location", GridView1[GridView1.Row, 2].Text)
    If xval Then
      res = modDatabase.$myConn.Edit("tblpatevents", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      res["flddetail"] = xval
      res.Update
      FillEventGrid()
    Endif
  Endif

End
