' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $sValue As Long
Private $sType As String
Private $ItemList As String[]

Public Sub Run(sType As String) As Long

  $sType = sType
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  $ItemList = New String[]
  If $sType = "Personal" Then
    Me.Title = "Personal Regimen"
    lstletter.Visible = True
  Else
    Me.Title = "Medicine List"
  Endif
  SHowGrid()
  lstletter.List = modString.GetFirstLetterArray($ItemList)

End

Public Sub txtname_Change()

  SHowGrid()

End

Private Sub SHowGrid()

  Dim sql As String

  If $sType = "Personal" Then
    sql = "select fldid,fldid,fldroute,fldbrandid,flddose,fldfreq,fldday,fldqty,fldusage from tblmedregimen where (flduserid=&1 or flduserid=&2) and lower(fldbrandid) like &3 ORDER BY fldbrandid"
    If chkleftmain.Value = True Then
      $rData = modDatabase.$myConn.Exec(sql, modBasic.$lbluser, "%", "%" & LCase(Trim(txtname.Text)) & "%")
    Else
      $rData = modDatabase.$myConn.Exec(sql, modBasic.$lbluser, "%", LCase(Trim(txtname.Text)) & "%")
    Endif
  Else
    sql = "select fldid,fldid,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flddirection from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and flditemtype=&3 and fldcurval=&4 and lower(flditem) like &5 and fldid IN (SELECT MAX(fldid) FROM tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and flditemtype=&3 and fldcurval=&4 GROUP BY flditem)"
    If chkleftmain.Value = True Then
      $rData = modDatabase.$myConn.Exec(sql, $sType, True, "Medicines", "Continue", "%" & LCase(Trim(txtname.Text)) & "%")
    Else
      $rData = modDatabase.$myConn.Exec(sql, $sType, True, "Medicines", "Continue", LCase(Trim(txtname.Text)) & "%")
    Endif
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  For Each $rData
    If $sType = "Personal" Then
      $ItemList.Add($rData["fldbrandid"])
    Else
      $ItemList.Add($rData["flditem"])
    Endif
  Next

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 1
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 300 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 50 * modBasic.$AppWidthRatio
    .Columns[8].Width = 150 * modBasic.$AppWidthRatio

    .Columns[2].Text = "Route"
    .Columns[3].Text = "Medicine"
    .Columns[4].Text = "Dose"
    .Columns[5].Text = "Freq"
    .Columns[6].Text = "Days"
    .Columns[7].Text = "QTY"
    .Columns[8].Text = "Direction"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 3 Then
    GridView1.Data.Text = $rData[$aMyFields[Column]]
    GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  GridView1.Data.WordWrap = True

End

Public Sub GridView1_KeyRelease()

  If GridView1.Rows.Count > 0 Then
    If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
      $sValue = GridView1[GridView1.Row, 0].Text
      Me.Close(True)
    Endif
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub lstletter_Click()

  txtname.Text = lstletter.Text

End

Public Sub txtname_Click()

  txtname.Text = ""

End

Public Sub btnok_Click()

  If GridView1.Rows.Count > 0 Then
    $sValue = GridView1[GridView1.Row, 0].Text
    Me.Close(True)
  Else
    Me.Close()
  Endif

End
