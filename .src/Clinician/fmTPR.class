' Gambas class file

Private $encid As String
Private $ExamList As String[]

Public Sub _new(encid As String, sList As String[])

  $encid = encid
  $ExamList = sList

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")

End

Public Sub btnrefresh_Click()

  drwchart.Clear()
  CreateTPRChart()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Private Sub CreateTPRChart()

  Dim pointsArr_x As Date[]
  Dim pointsArr_y As Float[]
  Dim res1 As Result
  Dim xx As String

  Dim firdate As String
  Dim colval As Integer

  Dim diffhr As Float
  Dim xadmis As Date
  Dim dcons As Float
  Dim xdiff As Float
  Dim k As Integer
  Dim dt As String

  Dim wid As Float
  Dim mul As Float
  Dim nul As Float
  Dim i As Integer

  Dim maxobs As Float
  Dim minobs As Float

  Dim sVal As Integer
  Dim xVal As Integer
  Dim xdf As Float
  Dim xdate As Date
  Dim xhr As String

  ''find out parameters
  firdate = DateAdd(Now(), gb.Hour, -36)
  xhr = modSettings.ShowSettingFromFIle("ClinicalChart/HourDifference")
  If xhr Then
    xdf = CInt(xhr) / 60
  Else
    xdf = 0.25
  Endif

  xadmis = modPatient.GetAdmissionDate($encid)
  If xadmis Then
    xdate = xadmis
  Else
    xdate = modPatient.GetRecordDate($encid)
  Endif

  maxobs = 1
  minobs = 400

  mul = (drwchart.Height - 30) / (maxobs - minobs)
  nul = 15

  dcons = Floor((DateDiff(modDate.StartSqlDate(xdate), modDate.StartSqlHour(firdate), gb.Minute)) / 1440) * 1440
  diffhr = DateDiff(modDate.StartSqlDate(xdate), modDate.EndSqlDate(Now()), gb.Minute) - dcons
  xdiff = Fix(diffhr / 60)

  If diffhr < 1440 Then
    diffhr = 1440
  Endif
  wid = (xdf * (diffhr + 6)) + 50 'multiply x-coordinate by 20 for clear view
  drwchart.Width = wid
  drwchart.Background = Color.White

  Paint.Begin(drwchart)

  colval = 0
  For Each xx In $ExamList
    res1 = modDatabase.$myConn.Exec("select fldrepquanti,fldtime from tblpatientexam where fldencounterval=&1 and fldhead=&2 and fldtype=&3 and fldsave=&4 and fldtime>&5", $encid, xx, "Quantitative", True, modDate.StartSqlDate(firdate))                                                          ''
    If res1.Available Then

      ''get coordinates
      pointsArr_x = New Date[]
      pointsArr_y = New Float[]
      For Each res1
        pointsArr_x.Add(res1!fldtime)
        pointsArr_y.Add(res1!fldrepquanti)
      Next

      ''draw individual chart
      Paint.Brush = Paint.Color(ColorList(colval))
      Paint.LineWidth = 1
      Paint.Font.Size = 10
      Paint.MoveTo(20, (colval * 20) + 20)
      Paint.LineTo(40, (colval * 20) + 20)
      Paint.Text(xx, 50, (colval * 20) + 20)
      Paint.Stroke

      ''main chart
      Paint.Brush = Paint.Color(ColorList(colval))
      Paint.LineWidth = 1
      Paint.Font.Size = 10
      For i = 0 To pointsArr_x.Max
        xVal = DateDiff(modDate.StartSqlDate(xdate), pointsArr_x[i], gb.Minute) - dcons
        sVal = mul * (pointsArr_y[i] - minobs) + nul
        If i = 0 Then
          Paint.MoveTo(xdf * xVal, sVal)
        Else
          Paint.LineTo(xdf * xVal, sVal)
        Endif
      Next
      Paint.Stroke

      'numbers
      If modBasic.$ClinChartShowLabel = "No" Then
      Else
        If modBasic.$ClinColorLabel Then
          Paint.Brush = Paint.Color(modBasic.$ClinColorLabel)
        Else
          Paint.Brush = Paint.Color(Color.Black)
        Endif
        For i = 0 To pointsArr_x.Max
          xVal = DateDiff(modDate.StartSqlDate(xdate), pointsArr_x[i], gb.Minute) - dcons
          sVal = mul * (pointsArr_y[i] - minobs) + nul
          Paint.Text(pointsArr_y[i], xdf * xVal, sVal - 3)
        Next
        Paint.Fill
      Endif

      'points
      If modBasic.$ClinChartShowPoint = "No" Then
      Else
        If modBasic.$ClinColorPoints Then
          Paint.Brush = Paint.Color(modBasic.$ClinColorPoints)
        Else
          Paint.Brush = Paint.Color(Color.Black)
        Endif
        For i = 0 To pointsArr_x.Max
          xVal = DateDiff(modDate.StartSqlDate(xdate), pointsArr_x[i], gb.Minute) - dcons
          sVal = mul * (pointsArr_y[i] - minobs) + nul
          Paint.Arc(xdf * xVal, sVal, 3)
        Next
        Paint.Fill
      Endif

    Endif
    colval = colval + 1

    If pointsArr_x Then
      pointsArr_x.Clear()
      pointsArr_y.Clear()
    Endif
  Next

  ''draw grids
  If modBasic.$ClinChartShowHourGrid = "No" Then
  Else
    Paint.Font.Size = 10
    For i = 0 To xdiff + 6
      If Frac(i / 24) = 0 Then
        Paint.LineWidth = 1
        If modBasic.$ClinColorDayGrid Then
          Paint.Brush = Paint.Color(modBasic.$ClinColorDayGrid)
        Else
          Paint.Brush = Paint.Color(Color.Green)
        Endif
      Else
        Paint.LineWidth = 0.25
        If modBasic.$ClinColorHourGrid Then
          Paint.Brush = Paint.Color(modBasic.$ClinColorHourGrid)
        Else
          Paint.Brush = Paint.Color(Color.Blue)
        Endif
      Endif
      Paint.MoveTo(i * 60 * xdf, 0)
      Paint.LineTo(i * 60 * xdf, drwchart.Height)
      Paint.Stroke
    Next
  Endif

  If modBasic.$ClinChartShowGridLabel = "No" Then
  Else
    If modBasic.$ClinColorGridLabel Then
      Paint.Brush = Paint.Color(modBasic.$ClinColorGridLabel)
    Else
      Paint.Brush = Paint.Color(Color.Blue)
    Endif
    Paint.LineWidth = 0.25
    Paint.Font.Size = 10
    For k = 0 To xdiff + 6
      If k = 0 Then
        dt = "D:" & CStr(k + (dcons / 1440) + 1)
      Else If Frac(k / 24) = 0 Then
        dt = "D:" & CStr(k / 24 + (dcons / 1440) + 1)
      Else
        dt = "" ' "H:" & k
      Endif
      Paint.Text(dt, k * 60 * xdf, drwchart.Height - 2)
    Next
    Paint.Fill
  Endif

  Paint.End()

End

Private Function ColorList(i As Integer) As Integer

  Dim xx As Integer

  If i = 0 Then
    xx = Color.Black
  Else If i = 1 Then
    xx = Color.Blue
  Else If i = 2 Then
    xx = Color.Green
  Else If i = 3 Then
    xx = Color.Orange
  Else If i = 4 Then
    xx = Color.Cyan
  Else If i = 5 Then
    xx = Color.Gray
  Else If i = 6 Then
    xx = Color.Magenta
  Else If i = 7 Then
    xx = Color.Orange
  Else If i = 8 Then
    xx = Color.Pink
  Else If i = 9 Then
    xx = Color.Red
  Else If i = 10 Then
    xx = Color.Violet
  Else If i = 11 Then
    xx = Color.Yellow
  Else If i = 12 Then
    xx = Color.Black
  Else If i = 13 Then
    xx = Color.Blue
  Else If i = 14 Then
    xx = Color.Green
  Else If i = 15 Then
    xx = Color.Orange
  Else If i = 16 Then
    xx = Color.Cyan
  Else If i = 17 Then
    xx = Color.Gray
  Else If i = 18 Then
    xx = Color.Magenta
  Else If i = 19 Then
    xx = Color.Orange
  Else If i = 20 Then
    xx = Color.Pink
  Else If i = 21 Then
    xx = Color.Red
  Else If i = 22 Then
    xx = Color.Violet
  Else If i = 23 Then
    xx = Color.Yellow
  Endif

  Return xx

End
