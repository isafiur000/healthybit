' Gambas class file

Private $encid As String
Private $sType As String
Private $ItemType As String

Public Sub _new(encid As String, sType As String)

  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  cmbunit.List = ["Unit", "Minute", "Hour", "Day"]
  rbexam.Value = True
  rbexam_Click()
  FillGridView()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub rbexam_Click()

  $ItemType = "Exam"
  cmbexam.List = modMedicine.FillExamQualiOrQuantiCombo("%")

End

Public Sub rbtest_Click()

  $ItemType = "Test"
  cmbexam.List = modMedicine.FillLabTestCombo("%")

End

Public Sub rbradio_Click()

  $ItemType = "Radio"
  cmbexam.List = modMedicine.FillRadioTestCombo("%")

End

Public Sub cmbunit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbunit)
  modFillContainer.RestrictComboToContent(cmbunit)

End

Public Sub btnadd_Click()

  If $ItemType And If cmbexam.Text And If txtfreq.Value And If cmbunit.Text Then
    modPatientGeneral.AddMonitoringData($encid, $ItemType, cmbexam.Text, txtfreq.Value, cmbunit.Text, $sType)
    txtfreq.Value = 0
    txtfreq.Tag = ""
    cmbunit.Text = ""
    FillGridView()
  Endif

End

Private Sub FillGridView()

  Dim sql As String
  Dim res As Result
  Dim Column As Integer
  Dim fld As ResultField

  sql = "select fldid,fldcategory,flditem,fldevery,fldunit,fldid from tblmonitor where fldencounterval=&1 and fldstatus=&2"
  res = modDatabase.$myConn.Exec(sql, $encid, "Continue")
  GridView1.Clear
  GridView1.Columns.Count = res.Fields.Count
  GridView1.Rows.Count = res.Count

  For Each res
    Column = 0
    For Each fld In res.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, res.Index, Column)
      If Column = 5 Then
        GridView1[res.Index, Column].Picture = Picture["icon:/tiny/cancel"]
        GridView1[res.Index, Column].Text = ""
      Else
        GridView1[res.Index, Column].Text = res[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 1
    .Columns[2].Width = 300 * modBasic.$AppWidthRatio
    .Columns[3].Width = 50 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio

    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "Freq"
    .Columns[4].Text = "Unit"
  End With

End

Public Sub GridView1_Click()

  Dim res As Result

  If GridView1.Column = 5 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Edit("tblmonitor", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      res["fldstatus"] = "Discontinue"
      res["xyz"] = False
      res.Update
      FillGridView()
    Endif
  Endif

End

Public Sub cmbunit_GotFocus()

  If Not txtfreq.Value Then
    txtfreq.SetFocus
  Else
    If modBasic.$TabletModeEnable = "Yes" Then
      If Not cmbunit.Text Then
        cmbunit.Popup
      Endif
    Endif
  Endif

End
