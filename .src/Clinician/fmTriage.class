' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $encid As String
Private $sType As String
Private $ExamsVar As Variant[]
Private $ItemList As String[]

Private $optionType As String
Private $sysConst As String
Private $dataType As String
Private $optionList As String[]

Public Sub _new(sType As String, encid As String)

  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $sType

  modBasic.LoadClinicalChartSetting()
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  ' txtcolor.Background = modPatient.GetPatientColorStatic($encid)
  $ExamsVar = GetSelectExamVariant($sType)
  $ItemList = modString.GetListFromVariant($ExamsVar, "fldexamid", True)
  ListBox1.List = $ItemList
  ShowExamGrid()
  ListBox1.SetFocus

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key.Up Then
    ListBox1.SetFocus
  Else If Key.Code = Key.Down Then
    ListBox1.SetFocus
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub txtquali_KeyRelease()

  modFillContainer.AutoFillComboBox(txtquali)

End

Private Function GetSelectExamVariant(sCategory As String) As Variant[]

  Dim sColl As Collection
  Dim aVar As Variant[]

  aVar = New Variant[]
  For Each sColl In modTableCache.$ExaminationCompVar
    If sColl["fldcategory"] = sCategory Then
      aVar.Add(sColl)
    Endif
  Next

  Return aVar

End

Public Sub ListBox1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    ListBox1_Click()
  Endif

End

Public Sub ListBox1_Click()

  Dim xlimit As Float[]
  Dim sColl As Collection
  Dim res As Result

  txtexamval.Value = 0
  txtexamval.Tag = ""
  txtquali.Clear
  txtquali.Text = ""

  $optionType = ""
  $sysConst = ""
  $dataType = ""
  $optionList = New String[]

  lblexamname.Text = ListBox1.Text
  For Each sColl In $ExamsVar
    If sColl["fldexamid"] = lblexamname.Text Then
      $dataType = sColl["fldtype"]
      $sysConst = sColl["fldsysconst"]
      $optionType = sColl["fldtanswertype"]
      If sColl["fldoptionlist"] Then
        $optionList = JSON.Decode(sColl["fldoptionlist"])
      Else
        If sColl["fldtanswertype"] = "Dichotomous" Then
          $optionList = ["No", "Yes"]
        Endif
      Endif

      If $sysConst Then
        res = modDatabase.$myConn.Exec("select fldexamid,fldtype,fldoption,fldsysconst,fldcritical from tblexam where fldsysconst=&1", $sysConst)
        If res.Available Then
          res.MoveFirst
          $dataType = res["fldtype"]
          $optionType = res["fldoption"]
          Select res["fldoption"]
            Case "Clinical Scale"
              $optionList = modAllExam.GetCLinicalScaleOptions("Exam", res["fldexamid"])
            Case Else
              $optionList = modAllExam.SelectExamOptionList("Exam", res["fldexamid"])
          End Select
        Endif
      Endif

      Break
    Endif
  Next

  If $dataType = "Quantitative" Then
    txtexamval.Enabled = True
    txtexamval.Visible = True
    lblunit.Visible = True
    txtquali.Enabled = False
    txtquali.Visible = False
    xlimit = modClinic.GetRangeQuantiExamSys($sysConst, $encid)
    txtmin.Value = xlimit[0]
    txtmax.Value = xlimit[1]
    lblunit.Text = modClinic.GetExamUnit(ListBox1.Text, $encid)
  Else If $dataType = "Qualitative" Then
    txtexamval.Enabled = False
    txtexamval.Visible = False
    lblunit.Visible = False
    txtquali.Enabled = True
    txtquali.Visible = True
  Endif

End

Public Sub txtquali_GotFocus()

  Dim sVal As String[]

  If Not txtquali.Text Then
    If lblexamname.Text Then
      Select $optionType
        Case "Single Selection"
          txtquali.List = $optionList

        Case "Dichotomous"
          txtquali.List = $optionList
          If Not $optionList.Count Then
            txtquali.List = ["Yes", "No"]
          Endif

        Case "Clinical Scale"
          sVal = SubChoose($optionList, "Clinical Scale", lblexamname.Text)
          If sVal Then
            txtexamval.Value = CFloat(sVal[0])
            txtquali.Text = sVal[1]
            txtexamval.Visible = True
            txtquali.Visible = False
          Endif

        Case Else
          If modBasic.$DisplayQualiArea = "Yes" Then
            txtquali.Text = GetTextArea(lblexamname.Text, "")
          Endif

      End Select
    Endif
    txtquali.SetFocus
  Endif

End

Public Sub btnsave_Click()

  If lblexamname.Text Then
    If $dataType = "Quantitative" Then
      If txtmin.Value = 0 And If txtmax.Value = 0 Then
        chkabnormal.Value = False
      Else
        If txtexamval.Value < txtmin.Value Or If txtexamval.Value > txtmax.Value Then
          chkabnormal.Value = True
        Else
          chkabnormal.Value = False
        Endif
      Endif
      modClinSub.AddQuantiData($encid, "", lblexamname.Text, $optionType, txtexamval.Value, chkabnormal.Value, $sType, $sysConst)

    Else If $dataType = "Qualitative" Then
      If txtquali.Text Then
        modClinSub.AddClinicExam($encid, "", lblexamname.Text, $optionType, txtquali.Text, txtexamval.Value, chkabnormal.Value, $sType, $sysConst, "Regular")
      Endif
    Endif
    ShowExamGrid()
    txtquali.Clear()
    txtquali.Text = ""
    txtexamval.Value = 0
    txtexamval.Tag = ""

  Endif

End

Public Sub btnsave_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnsave_Click()
    ListBox1.SetFocus
  Endif

End

Public Sub btnsave_GotFocus()

  btnsave.Font.Bold = True

End

Public Sub btnsave_LostFocus()

  btnsave.Font.Bold = False

End

Private Sub ShowExamGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,fldid,fldtime,fldtype,fldhead,fldabnormal,fldid,fldsave,fldsysconst,fldoption from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldinput=&3", $encid, True, $sType)                                                                                 ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 25
    .Columns[2].Width = 150 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1 ''type
    .Columns[4].Width = 200 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio
    .Columns[6].Width = 175 * modBasic.$AppWidthRatio
    .Columns[7].Width = 1
    .Columns[8].Width = 1  ''sys constant
    .Columns[9].Width = 1  ''option type

    .Columns[2].Text = "DateTime"
    .Columns[4].Text = "Examination"
    .Columns[6].Text = "Observation"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 5 Then
    GridView1.Data.Picture = Picture[modMisc.GetGridIcon($rData[$aMyFields[Column]])]
    GridView1.Data.Text = ""
  Else If Column = 6 Then
    GridView1.Data.RichText = modClinic.GetExamValueString($encid, $rData[$aMyFields[Column]], False)
  Else If Column = 2 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 1 Then
    GridView1.Data.Background = modPathoSub.GetPatientColorByTriage($rData[$aMyFields[Column]])
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Click()

  Dim xquantival As Variant[]
  Dim yqualival As Variant[]
  Dim xlimit As Float[]
  Dim Row As Integer
  Dim xx As String

  Row = GridView1.Row
  If GridView1.Column = 6 Then
    If GridView1[GridView1.Row, 3].Text = "Quantitative" Then
      xlimit = modClinic.GetRangeQuantiExamSys(GridView1[GridView1.Row, 8].Text, $encid)
      xquantival = GetQuantiValues("Exam", $encid, GridView1[GridView1.Row, 4].Text, xlimit[0], xlimit[1], modClinic.GetExamValueByID(GridView1[GridView1.Row, 0].Text))                                       '
      If xquantival Then
        modClinSub.UpdateQuantiData(GridView1[GridView1.Row, 0].Text, xquantival[0], xquantival[1])
      Endif
    Else If GridView1[GridView1.Row, 3].Text = "Qualitative" Then
      If GridView1[GridView1.Row, 9].Text = "Clinical Scale" Then
        yqualival = modExamOption.GetClinScalePopUp("Exam", modFixClinic.GetExamIDFromSysConst(GridView1[GridView1.Row, 8].Text))
        If yqualival Then
          modClinSub.UpdateQualiQuantiData(GridView1[GridView1.Row, 0].Text, yqualival[1], CFloat(yqualival[0]), False)
        Endif
      Else
        yqualival = GetQualiValues(GridView1[GridView1.Row, 4].Text, modClinic.GetExamValueString($encid, GridView1[GridView1.Row, 0].Text, False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData(GridView1[GridView1.Row, 0].Text, yqualival[0], yqualival[1], yqualival[2])
        Endif
      Endif
    Endif
    ShowExamGrid()

  Else If GridView1.Column = 5 Then
    xx = InputCombo("Select Flag for the observation", "Change Flag", ["Normal", "Abnormal"], modMisc.GetIconValue(GridView1[GridView1.Row, 5].Picture), True)
    If xx Then
      modClinSub.UpdateExamFlag(GridView1[GridView1.Row, 0].Text, xx)
      ShowExamGrid()
    Endif

  Endif
  GridView1.Row = Row

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Edit("tblpatientexam", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then
        res["fldsave"] = False
        res["flduserid"] = modBasic.$lbluser
        res.Update
        ShowExamGrid()
      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif
    Endif
  Endif

End
