' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $encid As String
Private $sType As String
Private $ItemList As String[]

Public Sub _new(sType As String, encid As String)

  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  Dim hForm As FmQuantiEntry
  Dim xcolor As Integer

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $sType

  hForm = New FmQuantiEntry(txtexamval, pnlquanti)
  modBasic.LoadClinicalChartSetting()
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  xcolor = modPatient.GetPatientColorStatic($encid)
  txtcolor.Background = xcolor
  txtcolorfin.Background = xcolor
  If $sType = "Triage Examinations" Then
    $ItemList = modString.GetListFromCompareVariant(modFixPatho.$ExaminationCompVar, "fldexamid", "fldcategory", "Triage Examinations", True)
  Else If $sType = "Discharge Examinations" Then
    $ItemList = modString.GetListFromCompareVariant(modFixPatho.$ExaminationCompVar, "fldexamid", "fldcategory", "Discharge Examinations", True)
    Panel1.Visible = False
  Endif
  ShowExamGrid()
  ListBox1.SetFocus

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key.Up Then
    ListBox1.SetFocus
  Else If Key.Code = Key.Down Then
    ListBox1.SetFocus
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub ListBox1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    ListBox1_Click()
  Endif

End

Public Sub ListBox1_Click()

  Dim sType As String
  Dim xlimit As Float[]

  txtexamval.Value = 0
  txtexamval.Tag = ""
  txtquali.Clear
  txtquali.Text = ""
  lblexamname.Text = ListBox1.Text
  sType = modFixClinic.GetExaminationType(lblexamname.Text)
  If sType = "Quantitative" Then
    txtexamval.Enabled = True
    txtexamval.Visible = True
    lblunit.Visible = True
    txtquali.Enabled = False
    txtquali.Visible = False
    If modBasic.$TabletModeEnable = "Yes" Then
      pnlquanti.Visible = True
    Else
      pnlquanti.Visible = False
    Endif
    xlimit = modClinic.GetBothQuantiExamVal(ListBox1.Text, $encid)
    txtmin.Value = xlimit[0]
    txtmax.Value = xlimit[1]
    lblunit.Text = modClinic.GetExamUnit(ListBox1.Text, $encid)
  Else If sType = "Qualitative" Then
    txtexamval.Enabled = False
    txtexamval.Visible = False
    lblunit.Visible = False
    txtquali.Enabled = True
    txtquali.Visible = True
    pnlquanti.Visible = False
  Endif

End

Public Sub btnsave_Click()

  Dim sType As String
  Dim xsys As String
  Dim OptionType As String
  Dim hForm As CGetExamParam

  If lblexamname.Text Then
    hForm = New CGetExamParam(lblexamname.Text, "Exam")
    sType = hForm.GetExamDataType()
    xsys = hForm.GetExamConstant()
    OptionType = hForm.GetExamOption()

    If sType = "Quantitative" Then
      If txtmin.Value = 0 And If txtmax.Value = 0 Then
        chkabnormal.Value = False
      Else
        If txtexamval.Value < txtmin.Value Or If txtexamval.Value > txtmax.Value Then
          chkabnormal.Value = True
        Else
          chkabnormal.Value = False
        Endif
      Endif
      modClinSub.AddQuantiData($encid, "", lblexamname.Text, OptionType, txtexamval.Value, chkabnormal.Value, $sType, xsys)

    Else If sType = "Qualitative" Then
      If txtquali.Text Then
        modClinSub.AddClinicExam($encid, "", lblexamname.Text, OptionType, txtquali.Text, txtexamval.Value, chkabnormal.Value, $sType, xsys, "Regular")
      Endif
    Endif
    ShowExamGrid()
    txtquali.Clear()
    txtquali.Text = ""
    txtexamval.Value = 0
    txtexamval.Tag = ""
    pnlquanti.Visible = False

  Endif

End

Public Sub btnsave_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnsave_Click()
    ListBox1.SetFocus
  Endif

End

Public Sub btnsave_GotFocus()

  btnsave.Font.Bold = True

End

Public Sub btnsave_LostFocus()

  btnsave.Font.Bold = False

End

Private Sub ShowExamGrid()

  Dim prList As String[]

  $rData = modDatabase.$myConn.Exec("select fldid,fldid,fldtime,fldtype,fldhead,fldabnormal,fldid,fldsave from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldinput=&3", $encid, True, $sType)                                                                                 ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 25
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 1
    .Columns[4].Width = 200 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 1

    .Columns[2].Text = "DateTime"
    .Columns[4].Text = "Examination"
    .Columns[6].Text = "Observation"
  End With

  If $rData.Available Then
    prList = New String[]
    For Each $rData
      prList.Add($rData["fldhead"])
    Next
    ListBox1.List = modString.GetRemainingArray($ItemList, prList)
  Else
    ListBox1.List = $ItemList
  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 5 Then
    GridView1.Data.Picture = Picture[modMisc.GetGridIcon($rData[$aMyFields[Column]])]
    GridView1.Data.Text = ""
  Else If Column = 6 Then
    GridView1.Data.RichText = modClinic.GetExamValueString($encid, $rData[$aMyFields[Column]], False)
  Else If Column = 2 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 1 Then
    GridView1.Data.Background = modPathoSub.GetPatientColorByTriage($rData[$aMyFields[Column]])
    GridView1.Data.Text = ""
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Click()

  Dim xquantival As Variant[]
  Dim yqualival As Variant[]
  Dim sType As String
  Dim xlimit As Float[]
  Dim Row As Integer
  Dim xx As String
  Dim xType As String

  Row = GridView1.Row
  If GridView1.Column = 6 Then
    sType = modFixClinic.GetExaminationType(GridView1[GridView1.Row, 4].Text)
    If sType = "Quantitative" Then
      xlimit = modClinic.GetBothQuantiExamVal(GridView1[GridView1.Row, 4].Text, $encid)
      xquantival = GetQuantiValues("Exam", $encid, GridView1[GridView1.Row, 4].Text, xlimit[0], xlimit[1], modClinic.GetExamValueByID(GridView1[GridView1.Row, 0].Text))                                       '
      If xquantival Then
        modClinSub.UpdateQuantiData(GridView1[GridView1.Row, 0].Text, xquantival[0], xquantival[1])
      Endif
    Else If sType = "Qualitative" Then
      xType = modFixClinic.GetExamtOptionType(GridView1[GridView1.Row, 4].Text)
      If xType = "Clinical Scale" Then
        yqualival = modExamOption.GetClinScalePopUp("Exam", GridView1[GridView1.Row, 4].Text)
        If yqualival Then
          modClinSub.UpdateQualiQuantiData(GridView1[GridView1.Row, 0].Text, yqualival[1], CFloat(yqualival[0]), False)
        Endif
      Else If xType = "RichText Area" Then
        yqualival = GetQualiRich(GridView1[GridView1.Row, 4].Text, modClinic.GetExamValueString($encid, GridView1[GridView1.Row, 0].Text, False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData(GridView1[GridView1.Row, 0].Text, yqualival[0], yqualival[1], yqualival[2])
        Endif
      Else
        yqualival = GetQualiValues(GridView1[GridView1.Row, 4].Text, modClinic.GetExamValueString($encid, GridView1[GridView1.Row, 0].Text, False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData(GridView1[GridView1.Row, 0].Text, yqualival[0], yqualival[1], yqualival[2])
        Endif
      Endif
    Endif
    ShowExamGrid()

  Else If GridView1.Column = 5 Then
    xx = InputCombo("Select Flag for the observation", "Change Flag", ["Normal", "Abnormal"], modMisc.GetIconValue(GridView1[GridView1.Row, 5].Picture), True)
    If xx Then
      modClinSub.UpdateExamFlag(GridView1[GridView1.Row, 0].Text, xx)
      ShowExamGrid()
    Endif

  Endif
  GridView1.Row = Row

End

Public Sub txtquali_GotFocus()

  Dim xType As String
  Dim xscle As String[]

  If Not txtquali.Text Then
    If lblexamname.Text Then
      xType = modFixClinic.GetExamtOptionType(lblexamname.Text)
      If xType = "Single Selection" Or If xType = "Dichotomous" Then
        txtquali.List = modAllExam.SelectExamOptionList("Exam", lblexamname.Text)
        If modBasic.$TabletModeEnable = "Yes" Then
          txtquali.Popup()
        Endif
      Else If xType = "Clinical Scale" Then
        xscle = modExamOption.GetClinScalePopUp("Exam", lblexamname.Text)
        If xscle Then
          txtexamval.Value = CFloat(xscle[0])
          txtquali.Text = xscle[1]
          txtexamval.Visible = True
          txtquali.Visible = False
        Endif
      Else If xType = "Visual Input" Then
        txtquali.Text = modExamOption.GetVisualDataExamVal(lblexamname.Text)
      Else
        If modBasic.$DisplayQualiArea = "Yes" Then
          txtquali.Text = GetTextArea(lblexamname.Text, "")
        Endif
      Endif
    Endif
    txtquali.SetFocus
  Endif

End

''============== Color ====================
Public Sub btnred_Click()

  txtcolor.Background = Color.Red
  txtcolorfin.Background = Color.Transparent

End

Public Sub btnyellow_Click()

  txtcolor.Background = Color.Yellow
  txtcolorfin.Background = Color.Transparent

End

Public Sub btngreen_Click()

  txtcolor.Background = Color.Green
  txtcolorfin.Background = Color.Transparent

End

Public Sub btnblue_Click()

  txtcolor.Background = Color.Blue
  txtcolorfin.Background = Color.Transparent

End

Public Sub btnblack_Click()

  txtcolor.Background = Color.Black
  txtcolorfin.Background = Color.Transparent

End

Public Sub btncolor_Click()

  Dim xval As String

  If txtcolor.Background = Color.White Then
    xval = CStr(Color.Transparent)
  Else
    xval = CStr(txtcolor.Background)
  Endif

  If xval Then
    modPatient.SetPatientColor($encid, xval)
    txtcolorfin.Background = xval
    Balloon.Info(("Information saved"), btncolor)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Edit("tblpatientexam", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then
        res["fldsave"] = False
        res["flduserid"] = modBasic.$lbluser
        res.Update
        ShowExamGrid()
      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif
    Endif
  Endif

End
