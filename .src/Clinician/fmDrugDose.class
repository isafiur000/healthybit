' Gambas class file

Private $rData As Result

Private $encid As String
Private $disease As String

Public Sub _new(encid As String, disease As String)

  $encid = encid
  $disease = disease

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  ShowStockGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Private Sub ShowStockGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  sql = "select fldid,fldcodename,flddosetype,flddose,flddoseunit,fldfreq,fldday,fldid from tblregimen where flddisease=&1 and (fldgender=&2 or fldgender=&3) and (fldagegroup=&4 or fldagegroup=&5)"
  $rData = modDatabase.$myConn.Exec(sql, $disease, modPatient.GetPatientSex($encid), "Both Sex", modPatient.GetAgeGroupFIxed($encid, Now()), "All Age")

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      If Column = 7 Then
        GridView1[$rData.Index, Column].Picture = Picture["icon:/small/attach"]
        GridView1[$rData.Index, Column].Text = ""
      Else
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 225 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 50 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 25 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Drug"
    .Columns[2].Text = "Type"
    .Columns[3].Text = "Dose"
    .Columns[4].Text = "Unit"
    .Columns[5].Text = "Freq"
    .Columns[6].Text = "Day"
  End With

End
