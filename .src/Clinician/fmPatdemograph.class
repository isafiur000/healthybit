' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $encid As String
Private $sType As String
Private $mainList As String[]
Private $ItemList As String[]

Private aXpanel As New Object[50]
Private aIndexLabel As New Object[50]
Private aNameLabel As New Object[50]
Private aDateBox As New Object[50]
Private aBSDate As New Object[50]
Private aComboBox As New Object[50]
Private aDichoValue As New Object[50]
Private aValueBox As New Object[50]
Private aLineText As New Object[50]
Private aButtonBox As New Object[50]
Private $xType As String[]
Private $xOption As String[]
Private AppFactor As Float

Public Sub _new(encid As String, sType As String)

  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtpataddress.Text = modPatient.GetPatientAddressByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)

  ShowExamGrid()
  LoadListData()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
    ShowExamGrid()
  Else If TabPanel1.Index = 1 Then
    If Frame1.Children.Count Then
      Frame1.Children.Clear()
    Endif
    GetRemainingList()
    LoadControls()
  Endif

End

Private Sub LoadListData()

  If $sType = "Clinical" Then
    $mainList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flddemoid as col from tbldemographic where (fldcomp=&1 or fldcomp=&2) ORDER BY fldindex", modBasic.$compID, "%"))
  Else If $sType = "Acount" Then
    $mainList = modBasic.$DemogAccList
  Endif

End

Private Sub GetRemainingList()

  Dim prList As String[]

  If $rData.Available Then
    prList = New String[]
    For Each $rData
      prList.Add($rData["flditem"])
    Next
    $ItemList = modString.GetRemainingArray($mainList, prList)
  Else
    $ItemList = $mainList
  Endif

End

'''========================= Grouped ======================
Private Sub LoadControls()

  Dim i As Integer
  Dim sType As String
  Dim opt As String[]

  $xType = New String[]
  $xOption = New String[]
  For i = 0 To $ItemList.Count - 1
    If i < 49 Then
      If $sType = "Clinical" Then
        sType = modFixPatho.GetDemographicType($ItemList[i])
      Else If $sType = "Acount" Then
        sType = modPatient.GetAccDemographicType($ItemList[i])
      Endif
      $xType.Add(sType)

      If sType = "Single Selection" Or If sType = "Dichotomous" Or If sType = "Multiple Selection" Then
        If $sType = "Clinical" Then
          opt = modFixPatho.GetDemographicOptionList($ItemList[i], sType)
        Else If $sType = "Acount" Then
          opt = modPatient.GetAccDemographicOptionList($ItemList[i], sType)
        Endif
        $xOption.Add(opt.Join(";"))
      Else
        $xOption.Add("")
      Endif

      aXpanel[i] = New HBox(Frame1)
      aIndexLabel[i] = New Label(aXpanel[i]) As "IndexLabel"
      aNameLabel[i] = New TextLabel(aXpanel[i]) As "NameLabel"
      Select sType
        Case "Multiple Selection"
          aButtonBox[i] = New ButtonBox(aXpanel[i]) As "ButtonBoxgroup"
        Case "Date Time", "BS Date"
          aDateBox[i] = New DateBox(aXpanel[i]) As "DateGroup"
          aBSDate[i] = New ToolButton(aXpanel[i]) As "BSButton"
        Case "Single Selection"
          aComboBox[i] = New ComboBox(aXpanel[i]) As "ComboGroup"
        Case "Dichotomous"
          aDichoValue[i] = New DichotomousValue(aXpanel[i]) As "DichoText"
        Case "Quantitative"
          aValueBox[i] = New ValueBox(aXpanel[i]) As "ValueGroup"
        Case Else
          aLineText[i] = New TextBox(aXpanel[i]) As "TextLineGroup"
      End Select
    Endif
  Next
  DIsplayTextForm()
  If aComboBox[0] Then
    aComboBox[0].SetFocus
  Else If aValueBox[0] Then
    aValueBox[0].SetFocus
  Else If aLineText[0] Then
    aLineText[0].SetFocus
  Endif

End

Private Sub DIsplayTextForm()

  Dim i As Integer

  AppFactor = modBasic.$AppScaleFactor
  Frame1.Height = ($ItemList.Count * 30 + 50) * AppFactor
  Frame1.Width = 875 * AppFactor

  For i = 0 To $ItemList.Count - 1
    If i < 49 Then

      With aXpanel[i]
        .Height = 25 * AppFactor
        .Width = Frame1.Width
      End With

      ''create index label
      With aIndexLabel[i]
        .Width = 25 * AppFactor
        .Height = 25 * AppFactor
        .Text = i + 1
        .Tag = i
      End With

      ''create Name Label
      With aNameLabel[i]
        .Width = 300 * AppFactor
        .Height = 25 * AppFactor
        .Text = $ItemList[i]
        .Border = Border.Plain
        .Tag = i
        .Wrap = True
        .Tooltip = $ItemList[i]
      End With

      If $xType[i] = "Multiple Selection" Then
        With aButtonBox[i]
          .Width = 300 * AppFactor
          .Height = 25 * AppFactor
          .Picture = Picture["icon:/small/attach"]
          .Tag = i
        End With

      Else If $xType[i] = "Single Selection" Then
        With aComboBox[i]
          .Width = 300 * AppFactor
          .Height = 25 * AppFactor
          .List = Split($xOption[i], ";")
          .Tag = i
        End With

      Else If $xType[i] = "Dichotomous" Then
        With aDichoValue[i]
          .Width = 300 * AppFactor
          .Height = 25 * AppFactor
          .List = Split($xOption[i], ";")
          .Tag = i
        End With

      Else If $xType[i] = "Date Time" Then
        With aDateBox[i]
          .Width = 200 * AppFactor
          .Height = 25 * AppFactor
          .Mode = DateChooser.DateTime
          .Tag = i
        End With
        With aBSDate[i]
          .Width = 30 * AppFactor
          .Height = 25 * AppFactor
          .Picture = Picture["icon:/small/calendar"]
          .Tag = i
        End With

      Else If $xType[i] = "BS Date" Then
        With aDateBox[i]
          .Width = 200 * AppFactor
          .Height = 25 * AppFactor
          .Mode = DateChooser.DateTime
          .Tag = i
        End With
        With aBSDate[i]
          .Width = 30 * AppFactor
          .Height = 25 * AppFactor
          .Picture = Picture["icon:/small/calendar"]
          .Tag = i
        End With

      Else If $xType[i] = "Quantitative" Then
        With aValueBox[i]
          .Width = 200 * AppFactor
          .Height = 25 * AppFactor
          .Tag = i
        End With

      Else
        With aLineText[i]
          .Width = 300 * AppFactor
          .Height = 25 * AppFactor
          .Tag = i
        End With

      Endif

    Endif
  Next

End

Public Sub ComboGroup_MouseWheel()

  Stop Event

End

Public Sub ComboGroup_KeyRelease()

  Dim j As Integer

  j = Last.Tag
  ''enable on chnaging ComboBoxNew to ComboBox
  modFillContainer.AutoFillComboBox(aComboBox[j])
  modFillContainer.RestrictComboToContent(aComboBox[j])

End

Public Sub BSButton_Click()

  Dim j As Integer
  Dim xx As String

  j = Last.Tag
  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(aDateBox[j].Value))
  If xx Then
    aDateBox[j].Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub ButtonBoxgroup_Click()

  Dim j As Integer
  Dim xList As String[]
  Dim strLst As String[]

  j = Last.Tag
  xList = Split($xOption[j], ";")
  If xList Then
    strLst = SubChoose(xList, "Multiple Selection", $ItemList[j])
    If strLst Then
      aButtonBox[j].Text = strLst.Join(";")
    Endif
  Endif

End

Public Sub btnBrOK_Click()

  Dim i As Integer
  Dim xboolean As Boolean

  If modBasic.$ClinHistoryInput = "Single" Then
    xboolean = True
  Else
    xboolean = False
  Endif

  For i = 0 To $ItemList.Count - 1
    If i < 49 Then

      If $xType[i] = "Multiple Selection" Then
        If aButtonBox[i].Text Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData($encid, "Demographics", $ItemList[i], aButtonBox[i].Text, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData($encid, "Demographics", $ItemList[i], aButtonBox[i].Text, "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Single Selection" Then
        If aComboBox[i].Text Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData($encid, "Demographics", $ItemList[i], aComboBox[i].Text, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData($encid, "Demographics", $ItemList[i], aComboBox[i].Text, "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Dichotomous" Then
        If aDichoValue[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData($encid, "Demographics", $ItemList[i], aDichoValue[i].Value, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData($encid, "Demographics", $ItemList[i], aDichoValue[i].Value, "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Date Time" Then
        If aDateBox[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData($encid, "Demographics", $ItemList[i], modDate.DateStringForExam(aDateBox[i].Value), "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData($encid, "Demographics", $ItemList[i], modDate.DateStringForExam(aDateBox[i].Value), "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "BS Date" Then
        If aDateBox[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData($encid, "Demographics", $ItemList[i], modDate.ConvertToLocaldate(aDateBox[i].Value), "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData($encid, "Demographics", $ItemList[i], modDate.ConvertToLocaldate(aDateBox[i].Value), "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Quantitative" Then
        If aValueBox[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQuantiData($encid, "Demographics", $ItemList[i], aValueBox[i].Value, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQuantiData($encid, "Demographics", $ItemList[i], aValueBox[i].Value, "", Now(), True)
          Endif
        Endif

      Else
        If aLineText[i].Text Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData($encid, "Demographics", $ItemList[i], aLineText[i].Text, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData($encid, "Demographics", $ItemList[i], aLineText[i].Text, "", Now(), True)
          Endif
        Endif

      Endif

    Endif
  Next
  ShowExamGrid()
  btnBrOK.Enabled = False
  Balloon.Info(("Informaton saved"), btnBrOK)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnBrChange_Click()

  Me.Close()

End

''============================ Manual =====================
Private Sub ShowExamGrid()

  If $sType = "Clinical" Then
    $rData = modDatabase.$myConn.Exec("select fldid,flditem,fldreportquali,flduserid from tblexamgeneral where fldencounterval=&1 and fldinput=&2 ORDER by fldid DESC", $encid, "Demographics")                                                                                 ''
  Else If $sType = "Acount" Then
    $rData = modDatabase.$myConn.Exec("select fldid,flditem,fldreportquali,flduserid from tblpataccgeneral where fldencounterval=&1 and fldinput=&2 ORDER by fldid DESC", $encid, "Demographics")                                                                                 ''
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 225 * modBasic.$AppWidthRatio
    .Columns[2].Width = 325 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Variable"
    .Columns[2].Text = "Value"
    .Columns[3].Text = "User"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Click()

  Dim sType As String
  Dim opt As String[]
  Dim strLst As String[]
  Dim xx As String
  Dim yy As Variant[]
  Dim Row As Integer
  Dim xdate As Date

  If GridView1.Rows.Selection.Count > 0 Then
    If GridView1.Column = 2 Then
      Row = GridView1.Row

      If $sType = "Clinical" Then
        sType = modFixPatho.GetDemographicType(GridView1[GridView1.Row, 1].Text)
        If sType = "Single Selection" Or If sType = "Dichotomous" Then
          opt = modFixPatho.GetDemographicOptionList(GridView1[GridView1.Row, 1].Text, sType)
          strLst = SubChoose(opt, sType, GridView1[GridView1.Row, 1].Text)
          If strLst Then
            For Each xx In strLst
              modPatientGeneral.UpdateExamGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, xx, "")
            Next
          Endif

        Else If sType = "Multiple Selection" Then
          opt = modFixPatho.GetDemographicOptionList(GridView1[GridView1.Row, 1].Text, sType)
          strLst = SubChoose(opt, "Multiple Selection", GridView1[GridView1.Row, 1].Text)
          If strLst Then
            modPatientGeneral.UpdateExamGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, strLst.Join(";"), "")
          Endif

        Else If sType = "Quantitative" Then
          yy = GetQuantiValues("Demographics", $encid, GridView1[GridView1.Row, 1].Text, 0, 0, CFloat(GridView1[GridView1.Row, 2].Text))
          If yy Then
            modPatientGeneral.UpdateExamGeneralQuantiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, yy[0], "")
          Endif

        Else If sType = "Date Time" Then
          xdate = GetDateValue(GridView1[GridView1.Row, 1].Text, "Date Value", Val(GridView1[GridView1.Row, 2].Text))
          If xdate Then
            modPatientGeneral.UpdateExamGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, modDate.DateStringForExam(xdate), "")
          Endif

        Else If sType = "BS Date" Then
          xdate = GetDateValue(GridView1[GridView1.Row, 1].Text, "Date Value", modDate.ConvertToEnglishdate(GridView1[GridView1.Row, 2].Text))
          If xdate Then
            modPatientGeneral.UpdateExamGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, modDate.ConvertToLocaldate(xdate), "")
          Endif

        Else
          xx = GetTextArea(GridView1[GridView1.Row, 1].Text, GridView1[GridView1.Row, 2].Text)
          If xx Then
            modPatientGeneral.UpdateExamGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, xx, "")
          Endif
        Endif

      Else If $sType = "Acount" Then
        sType = modPatient.GetAccDemographicType(GridView1[GridView1.Row, 1].Text)
        If sType = "Single Selection" Or If sType = "Dichotomous" Then
          opt = modPatient.GetAccDemographicOptionList(GridView1[GridView1.Row, 1].Text, sType)
          strLst = SubChoose(opt, sType, GridView1[GridView1.Row, 1].Text)
          If strLst Then
            modPatientSub.UpdateAccGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, strLst.Join(";"), "", Now(), True)
          Endif

        Else If sType = "Multiple Selection" Then
          opt = modPatient.GetAccDemographicOptionList(GridView1[GridView1.Row, 1].Text, sType)
          strLst = SubChoose(opt, "Multiple Selection", GridView1[GridView1.Row, 1].Text)
          If strLst Then
            For Each xx In strLst
              modPatientSub.UpdateAccGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, xx, "", Now(), True)
            Next
          Endif

        Else If sType = "Quantitative" Then
          yy = GetQuantiValues("Demographics", $encid, GridView1[GridView1.Row, 1].Text, 0, 0, CFloat(GridView1[GridView1.Row, 2].Text))
          If yy Then
            modPatientSub.UpdateAccGeneralQuantiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, yy[0], "", Now(), True)
          Endif

        Else
          xx = GetTextArea(GridView1[GridView1.Row, 1].Text, GridView1[GridView1.Row, 2].Text)
          If xx Then
            modPatientSub.UpdateAccGeneralQualiData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text, xx, "", Now(), True)
          Endif
        Endif
      Endif

      ShowExamGrid()
      GridView1.Row = Row
    Endif
  Endif

End

Public Sub GridView1_Menu()

  mnufile.Popup()

End

Public Sub mnudelete_Click()

  If Message.Question(("Are you sure to delete this variable ?"), ("No"), ("Yes")) = 2 Then
    If GridView1.Rows.Selection.Count > 0 Then
      If $sType = "Clinical" Then
        If modGeneral.AllowClinicalEdit(GridView1[GridView1.Row, 3].Text) = True Then
          modDatabase.$myConn.Delete("tblexamgeneral", "fldid=&1", GridView1[GridView1.Row, 0].Text)
        Else
          Message.Warning("Authorization with " & GridView1[GridView1.Row, 3].Text, ("OK"))
        Endif
      Else If $sType = "Acount" Then
        modDatabase.$myConn.Delete("tblpataccgeneral", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      Endif
      ShowExamGrid()
    Endif
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "DEMOGRAPHIC VALUES", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btndemoreport_Click()

  Dim hCls As CReportCustom

  If $encid Then
    If modSettings.ShowSettingFromFIle("Diagnostic Help/Name") Then
      hCls = New CReportCustom($encid, "Diagnostic Help", "ReportSize", MMain.$defUnit)
      hCls.Preview()
    Endif
  Endif

End

Public Sub btndefaiult_Click()

  Dim j As Integer
  Dim res As Result
  Dim sql As String

  For j = 0 To $ItemList.Count - 1
    If j < 49 Then

      If $sType = "Clinical" Then
        sql = "select flddefault from tbldemographic where flddemoid=&1 and (fldcomp=&2 or fldcomp=&3)"
      Else If $sType = "Acount" Then
        sql = "select flddefault from tblaccdemograp where flddemoid=&1 and (fldcomp=&2 or fldcomp=&3)"
      Endif
      res = modDatabase.$myConn.Exec(sql, $ItemList[j], modBasic.$compID, "%")
      If res.Available Then
        For Each res
          If aComboBox[j] Then
            If Not aComboBox[j].Text Then
              aComboBox[j].Text = res["flddefault"]
            Endif
          Else If aValueBox[j] Then
            If Not aValueBox[j].Value Then
              If res["flddefault"] Then
                aValueBox[j].Value = CFloat(res["flddefault"])
              Endif
            Endif
          Else If aLineText[j] Then
            If Not aLineText[j].Text Then
              aLineText[j].Text = res["flddefault"]
            Endif
          Else If aDichoValue[j] Then
            If Not aDichoValue[j].Value Then
              aDichoValue[j].Value = res["flddefault"]
            Endif
          Endif
        Next
      Endif
    Endif

  Next

End

Public Sub btnlastval_Click()

  Dim j As Integer
  Dim stVal As String
  Dim encid As String

  encid = modPatient.GetSecondLastEncidPatient($encid)
  For j = 0 To $ItemList.Count - 1
    If j < 49 Then

      If $sType = "Clinical" Then
        stVal = modPatPatho.GetDemographicValue(encid, $ItemList[j])
      Else If $sType = "Acount" Then
      Endif

      If aComboBox[j] Then
        If Not aComboBox[j].Text Then
          aComboBox[j].Text = stVal
        Endif
      Else If aValueBox[j] Then
        If Not aValueBox[j].Value Then
          If stVal Then
            aValueBox[j].Value = CFloat(stVal)
          Endif
        Endif
      Else If aLineText[j] Then
        If Not aLineText[j].Text Then
          aLineText[j].Text = stVal
        Endif
      Else If aDichoValue[j] Then
        If Not aDichoValue[j].Value Then
          aDichoValue[j].Value = stVal
        Endif
      Endif

    Endif
  Next

End
