' Gambas class file

Private $rData As Result
Private $sFormat As String
Private $encid As String
Private $route As String
Private $stockid As String
Private $txtdose As ValueBox
Private $cmbfreq As ComboBox
Private $txtday As ValueBox

Public Sub _new(sFormat As String, encid As String, route As String, stockid As String, txtdose As ValueBox, cmbfreq As ComboBox, txtday As ValueBox)

  $sFormat = sFormat
  $encid = encid
  $route = route
  $stockid = stockid
  $txtdose = txtdose
  $cmbfreq = cmbfreq
  $txtday = txtday

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  ShowStockGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Private Sub ShowStockGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  sql = "select fldid,flddisease,fldid,fldfreq,fldday from tblregimen where fldroute=&1 and fldcodename=&2  and (fldgender=&3 or fldgender=&4) and (fldagegroup=&5 or fldagegroup=&6) and flddisease in(select fldcode from tblpatfindings where fldencounterval=&7 and fldsave=&8)"                                    ''
  $rData = modDatabase.$myConn.Exec(sql, $route, modMedConstant.GetCodeFromStockID($stockid), modPatient.GetPatientSex($encid), "Both Sex", modPatient.GetAgeGroupFIxed($encid, Now()), "All Age", $encid, True)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      If Column = 2 Then
        GridView1[$rData.Index, Column].Text = modMedDosing.GetDoseInMgFoFromRegID($rData["fldid"], $encid)
      Else
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 300 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 65 * modBasic.$AppWidthRatio
    .Columns[4].Width = 55 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Disease"
    .Columns[2].Text = "Dose"
    .Columns[3].Text = "Freq"
    .Columns[4].Text = "Day"
  End With

End

Public Sub GridView1_KeyPress()

  If GridView1.Rows.Selection.Count > 0 Then
    If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
      $txtdose.Value = GridView1[GridView1.Row, 2].Text
      $cmbfreq.Text = GridView1[GridView1.Row, 3].Text
      $txtday.Value = GridView1[GridView1.Row, 4].Text
      Me.Close
    Endif
  Endif

End
