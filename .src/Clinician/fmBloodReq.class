' Gambas class file

Private $encid As String
Private $PatientNum As String

Private $rData2 As Result
Private $aMyFields2 As String[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  Dim hForm As FmQuantiEntry
  Dim xstatus As String
  Dim def As String
  Dim xList As String[]

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If modBasic.$TabletModeEnable = "Yes" Then
    hForm = New FmQuantiEntry(txtqty, pnlquanti)
  Endif

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  $PatientNum = modPatient.GetPatientNoByEnc($encid)
  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)

  cmbitem.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(flditem) as col from tblbloodstore"))
  cmbgroup.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblbloodstore"))
  def = modSettings.ShowSettingFromFIle("BloodBank/GroupingCode")
  If def Then
    xList = GetBloodTypeList($PatientNum, modFixLab.GetLabTestIDFromSysConst(def))
    If xList And If xList.Count Then
      txtbloodgroup.Text = xList.Join(";")
    Endif
  Endif

  rbpresent.Value = True
  CompleteGrid()
  FillRequestGrid()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub cmbgroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbgroup)
  modFillContainer.RestrictComboToContent(cmbgroup)

End

Private Function GetBloodTypeList(xPatNo As String, strTest As String) As String[]

  Dim res As Result
  Dim xList As String[]

  res = modDatabase.$myConn.Exec("select distinct(tblpatlabtest.fldreportquali) as col from tblpatlabtest inner join tblencounter on tblpatlabtest.fldencounterval=tblencounter.fldencounterval where tblencounter.fldpatientval=&1 and (tblpatlabtest.fldstatus=&2 or tblpatlabtest.fldstatus=&3) and tblpatlabtest.fldtestid=&4 and tblpatlabtest.fldreportquali IS NOT &5", xPatNo, "Reported", "Verified", strTest, Null)
  xList = modControlSub.GetDirectFillresult(res)

  Return xList

End

Public Sub btnshow_Click()

  Dim res As Result

  If cmbitem.Text And If cmbgroup.Text Then
    res = modDatabase.$myConn.Exec("select flditemcode from tblbloodstore where flditem=&1 and fldgroup=&2", cmbitem.Text, cmbgroup.Text)
    If res.Available Then
      lblcode.Text = res["flditemcode"]
      txtqty.ReadOnly = False
      btnsubOK.Enabled = True
    Else
      Message.Warning("Item not coded", "OK")
    Endif
  Endif

End

Public Sub btnsubOK_Click()

  Dim i As Integer

  If cmbitem.Text And If cmbgroup.Text And If txtqty.Value Then
    If lblcode.Text Then
      pnlquanti.Visible = False
      For i = 1 To txtqty.Value
        modPatientGeneral.PatMedicalItemRequest("Blood", $encid, lblcode.Text, TextArea1.Text)
      Next
      btnsubOK.Enabled = False
      FillRequestGrid()
      lblcode.Text = ""
      cmbitem.Text = ""
      cmbgroup.Text = ""
      txtqty.Value = 0

    Endif
  Endif

End

Public Sub FillRequestGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  Dim res As Result

  sql = "select fldid,fldtime_order,fldcategory,flditem,fldpatinfo,fldid from tblpatmeditem where fldencounterval=&1 and fldorder=&2"                                             ''
  res = modDatabase.$myConn.Exec(sql, $encid, "Requested")

  GridView1.Clear
  GridView1.Columns.Count = res.Fields.Count
  GridView1.Rows.Count = res.Count

  For Each res
    Column = 0
    For Each fld In res.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, res.Index, Column)
      If Column = 1 Then
        GridView1[res.Index, Column].Text = modReportVar.GetDateTimeReport(res["fldtime_order"], gb.GeneralDate)
      Else If Column = 5 Then
        GridView1[res.Index, Column].Picture = Picture["icon:/tiny/cancel"]
        GridView1[res.Index, Column].Text = ""
      Else
        GridView1[res.Index, Column].Text = res[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 300 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Particulars"
    .Columns[4].Text = "Details"
  End With

End

Public Sub GridView1_Click()

  If GridView1.Column = 5 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblpatmeditem", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      FillRequestGrid()
    Endif
  Endif

End

Public Sub btnsubOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnsubOK_Click()
  Endif

End

Public Sub cmbitem_GotFocus()

  If modBasic.$TabletModeEnable = "Yes" Then
    If Not cmbitem.Text Then
      cmbitem.Popup
    Endif
  Endif

End

Public Sub txtqty_GotFocus()

  pnlquanti.Visible = True
  txtqty.SelectAll

End

''------------------------ Completed ----------------------
Public Sub rbready_Click()

  CompleteGrid()

End

Public Sub rbpresent_Click()

  CompleteGrid()

End

Private Sub CompleteGrid()

  Dim sql As String

  If rbpresent.Value = True Then
    sql = "select fldid,fldcode,flditem,fldid,fldfirsttime,fldsecondtime,fldcomment,fldexpiry,fldstatus from tblmedinventory where fldreceiver=&1 and fldcategory=&2 and (fldstatus=&3 or fldstatus=&4)"            '
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Blood", "Transferred", "Administered")
  Else If rbready.Value = True Then
    sql = "select fldid,fldcode,flditem,fldid,fldfirsttime,fldsecondtime,fldcomment,fldexpiry,fldstatus from tblmedinventory where fldreceiver=&1 and fldcategory=&2 and (fldstatus=&3 or fldstatus=&4)"            '
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Blood", "Allocated", "Requested")
  Endif
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 275 * modBasic.$AppWidthRatio
    .Columns[3].Width = 35 * modBasic.$AppWidthRatio
    .Columns[4].Width = 1
    .Columns[5].Width = 1
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 150 * modBasic.$AppWidthRatio
    .Columns[8].Width = 100 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Bag No"
    .Columns[2].Text = "Particulars"
    .Columns[6].Text = "Comment"
    .Columns[7].Text = "Expiry"
    .Columns[8].Text = "Status"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData2.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  If Column = 3 Then
    If rbpresent.Value = True Then
      GridView2.Data.Picture = Picture[GetStateIcon($rData2[$aMyFields2[Column]])]
      GridView2.Data.Alignment = Align.Center
    Endif
    GridView2.Data.Text = ""
  Else If Column = 7 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData2[$aMyFields2[Column]], gb.GeneralDate)
  Else
    GridView2.Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Private Function GetStateIcon(sIdx As Long) As String

  Dim res As Result
  Dim xicon As String

  res = modDatabase.$myConn.Exec("select fldfirsttime,fldsecondtime from tblmedinventory where fldid=&1", sIdx)
  If res.Available Then
    If res["fldfirsttime"] And If res["fldsecondtime"] Then
      xicon = "icon:/tiny/lock"
    Else If Not res["fldfirsttime"] And If Not res["fldsecondtime"] Then
      xicon = "icon:/tiny/play"
    Else
      xicon = "icon:/tiny/stop"
    Endif
  Else
    xicon = ""
  Endif

  Return xicon

End

Public Sub GridView2_Click()

  Dim res As Result

  If GridView2.Column = 3 Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1 and (fldstatus=&2 or fldstatus=&3)", GridView2[GridView2.Row, 0].Text, "Transferred", "Administered")
    If res.Available Then
      If GridView2[GridView2.Row, 3].Picture = Picture["icon:/tiny/play"] Then
        res["fldfirsttime"] = Now()
        res["fldstatus"] = "Administered"
        res["flduserid_start"] = modBasic.$lbluser
        res["fldcomp_start"] = modBasic.$compID
        res["xyz"] = False
        res.Update
        GridView2[GridView2.Row, 3].Picture = Picture["icon:/tiny/stop"]
      Else If GridView2[GridView2.Row, 3].Picture = Picture["icon:/tiny/stop"] Then
        res["fldsecondtime"] = Now()
        res["xyz"] = False
        GridView2[GridView2.Row, 3].Picture = Picture["icon:/tiny/lock"]
      Endif
    Endif
  Endif

End

Public Sub btnrequest_Click()

  Dim res As Result

  If GridView2.Rows.Selection.Count Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1 and fldstatus=&2 and flduserid_start IS NULL", GridView2[GridView2.Row, 0].Text, "Allocated")
    If res.Available Then
      res["fldstatus"] = "Requested"
      res["xyz"] = False
      res.Update
      CompleteGrid()
    Endif
  Endif

End

Public Sub btnreturn_Click()

  Dim res As Result

  If GridView2.Rows.Selection.Count Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1 and fldstatus=&2 and flduserid_start IS NULL", GridView2[GridView2.Row, 0].Text, "Transferred")
    If res.Available Then
      res["fldstatus"] = "Returned"
      res["xyz"] = False
      res.Update
      CompleteGrid()
    Endif
  Endif

End

Public Sub btnsetgroup_Click()

  Dim xx As String

  xx = InputBox("Provide Laboratory Codes for Blood Grouping", "")
  If xx Then
    modSettings.SaveSettingsToFile("BloodBank/GroupingCode", xx)
  Endif

End
