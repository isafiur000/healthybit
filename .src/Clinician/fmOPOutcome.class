' Gambas class file

Private $encid As String
Private $sType As String
Private $xSerial As Long

Public Sub _new(sType As String, encid As String, xSerial As Long)

  $sType = sType
  $encid = encid
  $xSerial = xSerial

End

Public Sub Form_Open()

  Dim res As Result
  Dim xList As String[]

  xList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldlocation from tblreferlist"))
  cmbrefer.List = xList

  res = modDatabase.$myConn.Exec("select fldfollowdate,fldreferto from tblencounter where fldencounterval=&1", $encid)
  If res.Available Then
    cmbrefer.Text = res["fldreferto"]
    dtfollowup.Value = res["fldfollowdate"]
  Endif

End

Public Sub rbfollowup_Click()

  cmbrefer.Visible = False
  dtfollowup.Visible = True
  dtfollowup.Value = Now()

End

Public Sub rbreferred_Click()

  cmbrefer.Visible = True
  dtfollowup.Visible = False

End

Public Sub rbadmission_Click()

  cmbrefer.Visible = False
  dtfollowup.Visible = False

End

Public Sub rbdeath_Click()

  cmbrefer.Visible = False
  dtfollowup.Visible = False

End

Public Sub rbimproved_Click()

  cmbrefer.Visible = False
  dtfollowup.Visible = False

End

Public Sub btnsave_Click()

  Dim xmode As String

  Inc Application.Busy
  modDatabase.$myConn.Begin
  If rbadmission.Value = True Then
    xmode = "Admission"
  Else If rbfollowup.Value = True Then
    xmode = "Follow Up"
    modPatientSub.UpdateCurrentStatus($encid, "Follow Up", dtfollowup.Value)
  Else If rbreferred.Value = True Then
    xmode = "Referred"
    modPatientSub.UpdateCurrentStatus($encid, "Refer(OPD)", cmbrefer.Text)
  Else If rbdeath.Value = True Then
    xmode = "Death"
    modPatientSub.UpdateCurrentStatus($encid, "Death(OPD)", "")
  Else If rbimproved.Value = True Then
    xmode = "Improved"
  Endif

  If xmode Then
    If $sType = "Consultation" Then
      modPatientSub.UpdateConsultData($xSerial, "Done", txtcomment.Text, xmode)                       ''
    Else If $sType = "OP Visit" Then
      modPatientSub.UpdateOPVisitData($xSerial, "Done", txtcomment.Text, xmode)
    Endif
  Endif
  modDatabase.$myConn.Commit
  lblmessage.Text = "Information updated"
  Dec Application.Busy

Catch
  modDatabase.$myConn.Rollback
  Dec Application.Busy
  modHelpVariable.CreateErrorReport()

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btnreport_Click()

  Dim hCls As CReportCustom
  Dim xPath As String

  If $encid Then
    If modPatientSub.GetClearanceForExit($encid) = True Then

      If modSettings.ShowSettingFromFIle("OPD Sheet/Name") Then
        If $sType = "Consultation" Then
          hCls = New CReportCustom($encid, "OPD Sheet", "ReportSize", MMain.$defUnit, "Consultation|" & CStr($xSerial))
        Else If $sType = "OP Visit" Then
          hCls = New CReportCustom($encid, "OPD Sheet", "ReportSize", MMain.$defUnit, "OPVisit|" & CStr($xSerial))
        Endif
        hCls.Preview()
      Else
        Inc Application.Busy
        xPath = modPatReports.ShowOPatSummary($encid)
        Dec Application.Busy
        modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
      Endif

    Endif
  Endif

End

Public Sub btnward_Click()

  Dim xList As String[]

  If rbadmission.Value = True Then
    xList = modGeneral.GetDepartmentAllList("Patient Ward")
    txtcomment.Text = InputCombo("Select Admission Department", $encid, xList, "", True)
  Endif

End
