' Gambas class file

Private $status As String
Private $Dept As String = "IPD"

Private $LedgerAC As String
Private $xBillType As String
Private $sBillMode As String
Private $LabelType As String

Private $encid As String
Private $DiscPackage As String
Private xYear As Integer
Private $ClinicianLst As String[]

Private $NarcoDoctor As String
Private $NarcoRegd As String

Private $patHeight As Float
Private $actualWt As Float

Public Sub _new(encid As String, sStatus As String, DiscPackage As String)

  $encid = encid
  $DiscPackage = DiscPackage
  $status = sStatus

End

Public Sub Form_Open()

  Dim xbirthDate As Date

  Me.Center
  xbirthDate = modPatient.GetPatientBirthDay($encid)
  xYear = DateDiff(xbirthDate, Now(), gb.Year)
  cmbdosingwt.List = ["Actual Body Weight", "Ideal Body Weight"]
  cmbdosingwt.Text = "Actual Body Weight"
  cmbtarget.List = modMedicine.$MedicineUnitList
  modStockSub.DisplayDefaultDispensingMode(rbrand, rbgeneric)
  If modSettings.ShowSettingFromFIle("ClinicForms/ShowInjectionSubRoutes") = "Disable" Then
    cmbroute.List = modMedicine.RouteMedicine()
  Else
    cmbroute.List = modMedicine.ClinicalRoute()
  Endif
  cmbfreq.List = modMedicine.FrequencyCombo()
  $ClinicianLst = New String[]
  SetNarcoticsPrescriber()

  ShowBillingParam($DiscPackage)
  GetCalculations()
  If $status = "Admitted" Then
    $LabelType = "Inpatient"
  Else
    $LabelType = "Outpatient"
  Endif
  GetFuluidList()
  FillDosingGrid()
  ClearAllControl()
  cmbroute.SetFocus

End

Public Sub Form_KeyRelease()

  ' modGeneralmain.GoToNextControlTab()

End

Private Sub GetFuluidList()

  Dim res As Result

  If chkout.Value = True Then
    res = modStock.ItemListForPurchaseBillMode("fluid", GetBrandGenerricType(), $sBillMode)
  Else
    res = modStock.FillMedicinesPrescribingComboResult("fluid", GetBrandGenerricType(), $sBillMode)
  Endif
  cmbdilution.List = modControlSub.GetDirectFillresult(res)

End

Private Sub GetCalculations()

  Dim cForm As CDosingParam
  Dim xidealwt As Float
  Dim xbodysurf As Float
  Dim xcreat As Float

  cForm = New CDosingParam($encid)
  $actualWt = cForm.GetActualWeight()
  $patHeight = cForm.GetPatientHeight()

  xidealwt = cForm.GetIdealBodyWeight()
  If xidealwt Then
    txtidealwt.Value = xidealwt
  Else
    txtidealwt.ReadOnly = False
  Endif

  xbodysurf = cForm.GetBodySurfaceArea()
  If xbodysurf Then
    txtbodysurfarea.Value = xbodysurf
  Else
    txtbodysurfarea.ReadOnly = False
  Endif

  xcreat = cForm.GetCreatinineClearance()
  If xcreat Then
    txtcreatclear.Value = xcreat
  Else
    txtcreatclear.ReadOnly = False
  Endif

End

Private Sub ShowBillingParam(sDisctype As String)

  Dim resx As Result

  resx = modDatabase.$myConn.Exec("select fldmode,fldbillingmode,fldacledger,fldbilltype,fldreference,fldlimit,fldlockstate from tbldiscount where fldtype=&1", sDisctype)
  If resx.Available Then
    ''billingmode
    If resx["fldbillingmode"] Then
      $sBillMode = resx["fldbillingmode"]
    Else
      $sBillMode = modPatient.GetPatBillingMode($encid)
    Endif
    ''ledger A/C
    $LedgerAC = resx["fldacledger"]
    ''BillType
    $xBillType = resx["fldbilltype"]
    If Not $xBillType Then
      $xBillType = "Cash"
    Endif
  Endif

End

Public Sub AlterScheme(sScheme As String)

  ShowBillingParam(sScheme)
  SetNarcoticsPrescriber()

End

''--------------------------------------
Private Sub ClearAllControl()

  cmbroute.Text = ""
  cmbmedicine.Text = ""
  cmbadminsite.Text = ""
  txtdosageform.Text = ""
  txttarget.Value = 0
  cmbtarget.Text = ""
  txtcalculate.Value = 0
  lblcalculate.Text = ""
  txtdose.Value = 0
  lbldose.Text = ""
  cmbfreq.Text = ""
  txtdailydose.Value = 0
  lbldailydose.Text = ""
  txtday.Value = 0
  txtdosecount.Value = 0
  txtqty.Value = 0
  txtstarthour.Value = 0
  dtstarting.Value = Now()
  dtlastclosing.Value = Now()
  dtending.Value = Now()
  cmbdilution.Text = ""
  txtdirection.Text = ""

End

Private Sub SetNarcoticsPrescriber()

  Dim res As Result

  If modBasic.$MedNarcoPrescriber = "User" Then
    res = modDatabase.$myConn.Exec("select fldusername,fldusercode from tbluser where flduserid=&1 and (fldopconsult=&2 or fldipconsult=&2) and fldstatus=&3 and fldusercode like &4", modBasic.$lbluser, True, "Active", "%")
    If res.Available Then
      $NarcoDoctor = res["fldusername"]
      $NarcoRegd = res["fldusercode"]
    Endif
  Endif

End

Public Sub chknextplan_Click()

  GetFuluidList()

End

Public Sub btnclear_Click()

  ClearAllControl()

End

Private Function GetBrandGenerricType() As String

  Dim brandOrGeneric As String

  If rbrand.Value = True Then
    brandOrGeneric = "Brand"
  Else
    brandOrGeneric = "Generic"
  Endif
  Return brandOrGeneric

End

Public Sub cmbroute_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbroute)
  modFillContainer.RestrictComboToContent(cmbroute)

End

Public Sub cmbfreq_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbfreq)
  modFillContainer.RestrictComboToContent(cmbfreq)

End

Public Sub cmbtarget_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtarget)
  modFillContainer.RestrictComboToContent(cmbtarget)

End

Public Sub cmbdilution_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdilution)
  modFillContainer.RestrictComboToContent(cmbdilution)

End

Public Sub cmbmedicine_GotFocus()

  Dim res As Result
  Dim xlist1 As String[]
  Dim xlist As String[]
  Dim aList As String[]

  If Not cmbroute.Text Then
    cmbroute.SetFocus
  Else If cmbroute.Text Then
    If Not cmbmedicine.Text Then

      If cmbroute.Text = "suture" Or If cmbroute.Text = "msurg" Or If cmbroute.Text = "ortho" Or If cmbroute.Text = "extra" Then
        If chkout.Value = True Then
          res = modStock.ItemListForPurchaseBillMode(cmbroute.Text, GetBrandGenerricType(), $sBillMode)
        Else
          res = modStock.FillMedicinesPrescribingComboResult(cmbroute.Text, GetBrandGenerricType(), $sBillMode)
        Endif
        cmbmedicine.Text = GridViewNew("Select Particulars", modControlSub.GetDirectFillresult(res), False, cmbmedicine, Panel1.Height - (4 * cmbmedicine.Height))
      Else
        If chkout.Value = True Then
          res = modStock.ItemListForPurchaseBillMode(cmbroute.Text, GetBrandGenerricType(), $sBillMode)
        Else
          res = modStock.FillMedicinesPrescribingComboResult(cmbroute.Text, GetBrandGenerricType(), $sBillMode)
        Endif
        If modBasic.$ClinCategoryGrid = "Yes" Then
          xlist1 = modControlSub.GetDirectFillresult(res)
          If xlist1 Then
            xlist = modMedConstant.GetStockIDWithCategory(xlist1)
            If xlist Then
              If modBasic.$ClinicDisableMedicines And If modBasic.$ClinicDisableMedicines.Count Then
                aList = modString.GetRemainingArray(xlist, modBasic.$ClinicDisableMedicines)
                cmbmedicine.Text = GridViewGroup("Select Medicine", aList, False, cmbmedicine, Panel1.Height - (4 * cmbmedicine.Height))
              Else
                cmbmedicine.Text = GridViewGroup("Select Medicine", xlist, False, cmbmedicine, Panel1.Height - (4 * cmbmedicine.Height))
              Endif
            Endif
          Endif
        Else
          xlist = modControlSub.GetDirectFillresult(res)
          If xlist Then
            If modBasic.$ClinicDisableMedicines And If modBasic.$ClinicDisableMedicines.Count Then
              aList = modString.GetRemainingArray(xlist, modBasic.$ClinicDisableMedicines)
              cmbmedicine.Text = GridViewNew("Select Particulars", aList, False, cmbmedicine, Panel1.Height - (4 * cmbmedicine.Height))
            Else
              cmbmedicine.Text = GridViewNew("Select Particulars", xlist, False, cmbmedicine, Panel1.Height - (4 * cmbmedicine.Height))
            Endif
          Endif
        Endif
      Endif

      If cmbmedicine.Text Then
        If rbrand.Value = True Then
          cmbmedicine.Text = modMedConstant.ConvertBrandToGeneric(cmbroute.Text, cmbmedicine.Text)
        Endif
        txtdosageform.Text = modMedConstant.GetDosageFormFromStockID(cmbmedicine.Text)
      Endif

    Endif
  Endif

End

Public Sub cmbmedicine_LostFocus()

  Dim cForm As CRecommendDosing

  If cmbmedicine.Text Then
    If modStockSub.GetComboStockControl(cmbroute.Text, cmbmedicine.Text, rbgeneric, rbrand, modDatabase.$medConn) = False Then
      cmbmedicine.Text = ""
      Balloon.Warning(("Item not listed"), cmbmedicine)
      Balloon.Delay = modBasic.$BalloonDelay
      cmbroute.SetFocus
    Else

      If rbrand.Value = True Then
        cmbmedicine.Text = modMedConstant.ConvertBrandToGeneric(cmbroute.Text, cmbmedicine.Text)
      Endif
      cForm = New CRecommendDosing($encid, xYear, cmbmedicine.Text)
      txttarget.Value = cForm.RecommendDose()
      cmbtarget.Text = cForm.DoseUnit()
      ' txtdose.Value = cForm.UnitDose()
      ' lbldose.Text = cForm.UnitDoseUnit()
      cmbfreq.Text = cForm.Frequency()

      CheckLastDispensing(cmbmedicine.Text)
      Select cmbroute.Text
        Case "oral", "liquid", "resp", "topical", "eye/ear", "anal/vaginal"
          cmbdilution.Enabled = False
        Case Else
          cmbdilution.Enabled = True
      End Select
      cmbadminsite.SetFocus

    Endif
  Endif

End

Private Sub CheckLastDispensing(xmedicine As String)
  '
  '   Dim cForm As CLastDispensing
  '   Dim xdate As Date
  '   Dim xdays As Integer
  '
  '   cForm = New CLastDispensing(xmedicine, $PatientNum, $xNHISCode, cmbdisctype.Text)
  '   txttotalqty.Value = cForm.GetLastQty()
  '   xdate = cForm.GetLastDate()
  '   xdays = cForm.GetLastDays()
  '
  '   If xdate Then
  '     txtlastdispdate.Text = modReportVar.GetDateTimeReport(xdate, gb.MediumDate)
  '     If DateDiff(xdate, Now(), gb.Month) <= 3 Then
  '       txtlastdispdate.Foreground = Color.Red
  '     Endif
  '
  '     If xdays Then
  '       If DateAdd(xdate, gb.Day, xdays) > Now() Then
  '         Message.Warning("Dispensed for " & xdays & " Days On " & modReportVar.GetDateTimeReport(xdate, gb.GeneralDate), ("OK"))
  '       Endif
  '     Endif
  '   Endif
  '

End

Public Sub btnadd_GotFocus()

  btnadd_Click()

End

Public Sub btnadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnadd_Click()
  Endif

End

Public Sub btnadd_Click()

  If cmbroute.Text And If cmbmedicine.Text And If txtqty.Value Then
    If modMisc.AllowDiagnoBilling($encid) = True Then
      EnterDosingRecord()
      FillDosingGrid()
      cmbroute.SetFocus
    Else
      Message.Warning("Diagnosis not provided", ("OK"))
      cmbroute.SetFocus
    Endif
  Endif '

End

Private Sub EnterDosingRecord()

  Dim txprescriber As String
  Dim txpresno As String

  Dim xtax As Float
  Dim xdisc As Float
  Dim itemtype As String
  Dim xfixrate As Float
  Dim xfixname As String

  Dim itemmode As String
  Dim xcurVal As String
  Dim xnarcotic As String[]
  Dim CPharmFix As CFixRatePharmacy

  If chknextplan.Value = True Then
    xcurVal = "Planned"
  Else
    If modBasic.$DispRequestStatus = "Reserve" Then
      xcurVal = "Reserve"
    Else
      xcurVal = "Continue"
    Endif
  Endif
  If cmbroute.Text And If cmbmedicine.Text Then

    itemtype = modNonMedical.GetBillItemCategoryFromCombo(cmbroute.Text)
    CPharmFix = New CFixRatePharmacy(itemtype, cmbmedicine.Text, $sBillMode)
    xfixrate = CPharmFix.GetFixRate()
    xfixname = CPharmFix.GetFixItem()

    itemmode = $sBillMode
    xtax = modNonMedical.ShowTaxValues(itemtype, cmbmedicine.Text)
    xdisc = modNonMedical.DiscPercentForCategoryValue($encid, $DiscPackage, itemtype, cmbmedicine.Text, itemmode)
    If modMedConstant.GetNarcoticType(cmbmedicine.Text) = "Yes" Then
      txprescriber = $NarcoDoctor
      txpresno = $NarcoRegd
      If Not txpresno Then
        xnarcotic = DualGridValue(("Select Narcotic Prescriber"), $ClinicianLst, False)
        If xnarcotic Then
          txprescriber = xnarcotic[1]
          txpresno = xnarcotic[0]
        Endif
      Endif
      If txprescriber And If txpresno Then
        modPharmSub.InsertDosingMedicine($encid, $xBillType, $sBillMode, $DiscPackage, $LedgerAC, cmbroute.Text, cmbmedicine.Text, txtdose.Value, cmbfreq.Text, txtday.Value, txtqty.Value, $status, txprescriber, txpresno, 0, xtax, xdisc, xfixname, xfixrate, "Request", $Dept, Trim(txtdirection.Text), cmbadminsite.Text, txtdosecount.Value, Trim(cmbdilution.Text), 0, xcurVal)                     '
      Endif
    Else
      modPharmSub.InsertDosingMedicine($encid, $xBillType, $sBillMode, $DiscPackage, $LedgerAC, cmbroute.Text, cmbmedicine.Text, txtdose.Value, cmbfreq.Text, txtday.Value, txtqty.Value, $status, "", "", 0, xtax, xdisc, xfixname, xfixrate, "Request", $Dept, Trim(txtdirection.Text), cmbadminsite.Text, txtdosecount.Value, Trim(cmbdilution.Text), 0, xcurVal)                     '
    Endif
    ClearAllControl()
  Endif

End

Private Sub FillDosingGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  Dim res As Result

  sql = "select fldid,fldstarttime,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flduserid_order,fldcurval,flddosecount,fldadminsite,flddirection,fldmixing from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstatus=&3 and fldorder=&4 and (fldcurval=&5 or fldcurval=&6) and flditemtype=&7 ORDER BY fldid DESC"                                                   ''
  res = modDatabase.$myConn.Exec(sql, $encid, False, $status, "Request", "Continue", "Reserve", "Medicines")

  GridView1.Clear
  GridView1.Columns.Count = res.Fields.Count
  GridView1.Rows.Count = res.Count

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 275 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 50 * modBasic.$AppWidthRatio
    .Columns[8].Width = 1
    .Columns[9].Width = 75 * modBasic.$AppWidthRatio
    .Columns[10].Width = 50 * modBasic.$AppWidthRatio
    .Columns[11].Width = 100 * modBasic.$AppWidthRatio
    .Columns[12].Width = 200 * modBasic.$AppWidthRatio
    .Columns[13].Width = 200 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Start"
    .Columns[2].Text = "Route"
    .Columns[3].Text = "Particulars"
    .Columns[4].Text = "Dose"
    .Columns[5].Text = "Freq"
    .Columns[6].Text = "Day"
    .Columns[7].Text = "QTY"
    .Columns[9].Text = "Status"
    .Columns[10].Text = "Count"
    .Columns[11].Text = "Site"
    .Columns[12].Text = "Direction"
    .Columns[13].Text = "Compounding"
  End With

  For Each res
    Column = 0
    For Each fld In res.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, res.Index, Column)
      If Column = 1 Then
        GridView1[res.Index, Column].Text = modReportVar.GetDateTimeReport(res["fldstarttime"], gb.GeneralDate)
      Else
        GridView1[res.Index, Column].Text = res[fld.Name]
      Endif
      GridView1.Rows[res.Index].Height = Max(GridView1.Rows[res.Index].Height, GridView1[res.Index, Column].Font.RichTextHeight(GridView1[res.Index, Column].Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
      GridView1[res.Index, Column].WordWrap = True

      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

End

''=============================Procedures =================
Private Function GetEndingTime(xStart As Date) As Date

  Dim xdate As Date

  xdate = DateAdd(xStart, gb.Day, txtday.Value)
  Return xdate

End

Public Sub txtstarthour_Change()

  dtstarting.Value = DateAdd(Now(), gb.Hour, txtstarthour.Value)
  dtending.Value = GetEndingTime(dtstarting.Value)

End

Private Function GetCalcDose() As Float

  Dim xweight As Float
  Dim xval As Float
  Dim xelimfrac As Float

  If cmbdosingwt.Text = "Actual Body Weight" Then
    xweight = $actualWt
  Else If cmbdosingwt.Text = "Ideal Body Weight" Then
    xweight = txtidealwt.Value
  Endif

  Select cmbtarget.Text
    Case "mg", "mcg", "gram", "IU", "Unit", "mL"
      xval = txttarget.Value
    Case "mg/Kg", "mcg/Kg", "gram/Kg", "IU/Kg", "Unit/Kg", "mL/Kg"
      xval = xweight * txttarget.Value
    Case "mg/sqm", "mcg/sqm", "gram/sqm", "IU/sqm", "Unit/sqm", "mL/sqm"
      xval = txtbodysurfarea.Value * txttarget.Value
  End Select

  If chkrenal.Value = True Then
    If txtcreatclear.Value Then
      xelimfrac = modMedConstant.GetDrugRenalEliminFraction(cmbmedicine.Text)
      If (xelimfrac * 100) > modBasic.$MedMinRenalElimin And If txtcreatclear.Value < modBasic.$MedMaxCreatClear Then                                             ''
        xval = xval * modMedDosing.GetDoseAdjustmentFactor(xelimfrac, txtcreatclear.Value)
      Endif
    Endif
  Endif

  Select cmbtarget.Text
    Case "mg", "mg/Kg", "mg/sqm"
      lblcalculate.Text = "mg"
    Case "mcg", "mcg/Kg", "mcg/sqm"
      lblcalculate.Text = "mcg"
    Case "gram", "gram/Kg", "gram/sqm"
      lblcalculate.Text = "gram"
    Case "IU", "IU/Kg", "IU/sqm"
      lblcalculate.Text = "IU"
    Case "Unit", "Unit/Kg", "Unit/sqm"
      lblcalculate.Text = "Unit"
    Case "mL", "mL/Kg", "mL/sqm"
      lblcalculate.Text = "mL"
  End Select

  Return xval

End

Public Sub txttarget_Change()

  txtcalculate.Value = GetCalcDose()
  txtdose.Value = Round(txtcalculate.Value, -2)
  lbldose.Text = lblcalculate.Text

End

Public Sub cmbtarget_Change()

  txtcalculate.Value = GetCalcDose()
  txtdose.Value = Round(txtcalculate.Value, -2)
  lbldose.Text = lblcalculate.Text

End

Private Function ConvertDailyFreToNumber(txtfreq As String) As Float

  Dim xx As Float

  If txtfreq Then
    Select Case txtfreq
      Case "PRN"
        xx = 3
      Case "SOS", "stat", "AM", "HS", "Pre", "Post"
        xx = 1
      Case "Two-hourly"
        xx = 12
      Case "Hourly"
        xx = 24
      Case Else
        xx = CFloat(txtfreq)
    End Select

  Else
    xx = 0
  Endif

  Return xx

End

Public Sub cmbfreq_Click()

  If cmbfreq.Text Then
    txtdailydose.Value = Round(txtdose.Value * ConvertDailyFreToNumber(cmbfreq.Text), -2)
    lbldailydose.Text = lbldose.Text
  Endif

End

Public Sub txtday_Change()

  If cmbfreq.Text Then
    txtdosecount.Value = Ceil(modPharmLabel.ConvertFrequencyToNumber(cmbfreq.Text) * txtday.Value)
    txtqty.Value = Ceil(modMedConstant.GetQuantityDosingForCOunt(cmbmedicine.Text, txtdose.Value, txtdosecount.Value, modBasic.$SalesQTYPack))
  Endif
  dtending.Value = GetEndingTime(dtstarting.Value)

End

Public Sub txtdosecount_Change()

  txtqty.Value = Ceil(modMedConstant.GetQuantityDosingForCOunt(cmbmedicine.Text, txtdose.Value, txtdosecount.Value, modBasic.$SalesQTYPack))

End

Public Sub txtdose_GotFocus()

  txtdose.SelectAll()

End

Public Sub txtday_GotFocus()

  txtday.SelectAll()

End

Public Sub txtdosecount_GotFocus()

  txtdosecount.SelectAll()

End

Public Sub txtqty_GotFocus()

  txtqty.SelectAll()

End

Public Sub txtstarthour_GotFocus()

  txtstarthour.SelectAll()

End
