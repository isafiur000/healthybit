' Gambas class file

Private $colvar1 As String
Private $colvar2 As String
Private $tbl As String
Private $fixField As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(colvar1 As String, colvar2 As String, sTable As String, fixField As String, fixValue As String)

  $colvar1 = colvar1
  $colvar2 = colvar2
  $tbl = sTable
  $fixField = fixField
  If fixValue Then
    txttype.Text = fixValue
    txttype.Enabled = False
  Endif

  If $tbl = "tblassetsname" Then
    Me.Title = "Fixed Assets Coding"
    Label1.Text = ("Name")
    Label2.Text = ("Ledger")
    lblgroup.Text = ("Category")
  Else If $tbl = "tblmunicipals" Then
    Me.Title = "Municipal Coding"
    Label1.Text = ("Municipal")
    Label2.Text = ("Province")
    lblgroup.Text = ("District")
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  txttype.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec(Subst("select distinct(&1) as col from &2", $fixField, $tbl)))

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub btnrefresh_Click()

  ShowGridView()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub ShowGridView()

  Dim sql As String

  If txttype.Text Then
    sql = Subst("select fldid,&1,&2 from &3 where &4", $colvar1, $colvar2, $tbl, $fixField) & "=&1"
    $rData = modDatabase.$myConn.Exec(sql, txttype.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

    With GridView1
      .Columns[0].Width = 1
      .Columns[1].Width = 250 * modBasic.$AppWidthRatio
      .Columns[2].Width = 200 * modBasic.$AppWidthRatio
      .Columns[1].Text = Label1.Text
      .Columns[2].Text = Label2.Text
    End With

  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  Select Column
    Case 1, 2
      GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
      GridView1.Data.WordWrap = True
  End Select

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtsecvar.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$fixField] = txttype.Text
    res[$colvar1] = Trim(txtfirvar.Text)
    res[$colvar2] = Trim(txtsecvar.Text)
    res.Update()
    ShowGridView()
    txtfirvar.Text = ""
    txtsecvar.Text = ""
    Balloon.Info(("Variable added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    txtfirvar.SetFocus
  Endif

End

Public Sub btnCancel_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete($tbl, "fldid=&1", GridView1[GridView1.Row, 0].Text)
    ShowGridView()
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Click()

  txtfirvar.Text = GridView1[GridView1.Row, 1].Text
  txtsecvar.Text = GridView1[GridView1.Row, 2].Text

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, $tbl, txtcsv.Text, xdelim)
    ShowGridView()
  Endif

End
