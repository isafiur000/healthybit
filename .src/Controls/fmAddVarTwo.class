' Gambas class file

Private $colvar1 As String
Private $colvar2 As String
Private $tbl As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(colvar1 As String, colvar2 As String, tbl As String, grpList As String[], itmList As String[])

  $colvar1 = colvar1
  $colvar2 = colvar2
  $tbl = tbl
  txtcategory.List = grpList
  txtvariable.List = itmList

  If $tbl = "tblsuturetype" Then
    Me.Title = "Suture Types"
    Label1.Text = ("Type")
    Label2.Text = ("Abbrev")
  Else If $tbl = "tblextrapayers" Then
    Me.Title = "Customer Profile"
    Label1.Text = ("Name")
    Label2.Text = ("Address")
  Else If $tbl = "tbldeptgroup" Then
    Me.Title = "Department Groups"
    Label1.Text = ("Group Name")
    Label2.Text = ("Department")
  Else If $tbl = "tblreferlist" Then
    Me.Title = "Referral Hospitals"
    Label1.Text = ("Hospital")
    Label2.Text = ("Hosp Code")
  Else If $tbl = "tblgroupproc" Then
    Me.Title = "Extra Procedures"
    Label1.Text = ("Group Name")
    Label2.Text = ("Procedures")
  Else If $tbl = "tblgroupexam" Then
    Me.Title = "Examination"
    Label1.Text = ("Group Name")
    Label2.Text = ("Examination")
  Else If $tbl = "tblacledger" Then
    Me.Title = "Ledger Accounts"
    Label1.Text = ("Ledger Type")
    Label2.Text = ("Ledger A/C")
  Else If $tbl = "tblmasterdept" Then
    Me.Title = "Master Departments"
    Label1.Text = ("Master")
    Label2.Text = ("Department")
  Else If $tbl = "tblfoodmix" Then
    Me.Title = "Food Mixtures"
    Label1.Text = ("Mixture Code")
    Label2.Text = ("Mixture Name")
  Else If $tbl = "tblschemecategory" Then
    Me.Title = "Scheme Category"
    Label1.Text = ("Category")
    Label2.Text = ("Package")
  Else If $tbl = "tblipdservice" Then
    Me.Title = "Service Categories"
    Label1.Text = ("Service")
    Label2.Text = ("Diagnosis")
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  FilVarList()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colvar1] = Trim(txtcategory.Text)
    res[$colvar2] = Trim(txtvariable.Text)
    res.Update()
    SHowSelectGrid()
    txtvariable.Text = ""
    Balloon.Info(("Variable added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    txtcategory.SetFocus
  Endif

End

Public Sub btnrefresh_Click()

  If txtcategory.Text Then
    SHowSelectGrid()
  Else
    FilVarList()
  Endif

End

Private Sub SHowSelectGrid()

  Dim sql As String

  If txtcategory.Text Then
    sql = Subst("select &1,&2 from &3 where &1", $colvar1, $colvar2, $tbl) & "=&1"
    $rData = modDatabase.$myConn.Exec(sql, txtcategory.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    With GridView1
      .Columns[0].Width = 225 * modBasic.$AppWidthRatio
      .Columns[1].Width = 225 * modBasic.$AppWidthRatio
      .Columns[0].Text = Label1.Text
      .Columns[1].Text = Label2.Text
    End With

  Endif

End

Private Sub FilVarList()

  Dim sql As String

  sql = Subst("select &1,&2 from &3 ORDER BY &1", $colvar1, $colvar2, $tbl)
  $rData = modDatabase.$myConn.Exec(sql)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 225 * modBasic.$AppWidthRatio
    .Columns[1].Width = 225 * modBasic.$AppWidthRatio
    .Columns[0].Text = Label1.Text
    .Columns[1].Text = Label2.Text
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub btnCancel_Click()

  If GridView1.Rows.Selection.Count Then
    If GridView1[GridView1.Row, 1].Text Then
      modDatabase.$myConn.Delete($tbl, $colvar1 & "=&1 and " & $colvar2 & "=&2", GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text)
    Else
      modDatabase.$myConn.Delete($tbl, $colvar1 & "=&1 and " & $colvar2 & " IS &2", GridView1[GridView1.Row, 0].Text, Null)
    Endif
    FilVarList()
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Click()

  txtcategory.Text = GridView1[GridView1.Row, 0].Text
  txtvariable.Text = GridView1[GridView1.Row, 1].Text

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, $tbl, txtcsv.Text, xdelim)
    FilVarList()
  Endif

End
