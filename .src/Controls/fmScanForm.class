' Gambas class file

Private $encid As String
Private $sType As String
Private $sExtra As String
Private $ImgList As String[]

Public Sub _new(encid As String, sType As String, sExtra As String)

  $encid = encid
  $sType = sType
  If sExtra Then
    $sExtra = sExtra
    btnupload.Visible = True
  Else
    $sExtra = ""
    btnupload.Visible = False
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  modSettings.ShowCheckBox(chkmultiple, "ScannerSetting/PDFSave")
  $ImgList = New String[]

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub chkmultiple_Click()

  modSettings.EnterCheckSetting(chkmultiple, "ScannerSetting/PDFSave")

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["S"] And If Key.Control Then
    btnsaveimg_Click()
  Else If Key.Code = Key["U"] And If Key.Control Then
    btnupload_Click()
  Else If Key.Code = Key.F1 Then
    txtpath_Click()
  Else If Key.Code = Key.F2 Then
    btnimage_Click()
  Else If Key.Code = Key.F3 Then
    btnfolder_Click()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub Slider1_Change()

  PictureBox1.Zoom = Slider1.Value / 100

End

Public Sub txtpath_Click()

  Dialog.Filter = ["*.jpg;*.jpeg;*.png;*.bmp;*.pdf", "Picture files"]
  If Dialog.OpenFile() Then Return
  txtpath.Text = Dialog.Path
  If Exist(txtpath.Text) Then
    Try PictureBox1.Image = Image.Load(txtpath.Text)
    txtdetail.Text = $sType & ":" & modString.GetDateString(Now())
    If chkmultiple.Value = True Then
      $ImgList.Add(txtpath.Text)
      txtpages.Value = $ImgList.Count
    Endif

    Balloon.Info("1 Images Added", txtpath)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  txtdetail.SetFocus

End

Public Sub btnimage_Click()

  Dim sList As String[]
  Dim Row As Integer

  sList = modImage.GetWebCamValueList(modDevice.WebCamDevice())
  Wait
  If sList And If sList.Count Then
    txtpath.Text = sList[0]
    Try PictureBox1.Image = Image.Load(txtpath.Text)
    txtdetail.Text = $sType & ":" & modString.GetDateString(Now())
    If chkmultiple.Value = True Then
      For Row = 0 To sList.Count - 1
        $ImgList.Add(sList[Row])
      Next
      txtpages.Value = $ImgList.Count
    Endif

    Balloon.Info(sList.Count & " Images Added", btnimage)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  txtdetail.SetFocus

End

Public Sub btnscanner_Click()

  txtpath.Text = ScannedFile()
  Wait
  If Exist(txtpath.Text) Then
    Try PictureBox1.Image = Image.Load(txtpath.Text)
    txtdetail.Text = $sType & ":" & modString.GetDateString(Now())
    If chkmultiple.Value = True Then
      $ImgList.Add(txtpath.Text)
      txtpages.Value = $ImgList.Count
    Endif

    Balloon.Info("1 Images Added", btnscanner)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  txtdetail.SetFocus

End

Public Sub btnfolder_Click()

  Dim FileList As String[]
  Dim sFile As String

  If Dialog.SelectDirectory() Then Return
  btnfolder.Tag = Dialog.Path
  If btnfolder.Tag Then
    FileList = Dir(btnfolder.Tag, Null, gb.File)
    If FileList And If FileList.Count
      txtpath.Text = btnfolder.Tag &/ FileList[0]
      Try PictureBox1.Image = Image.Load(txtpath.Text)
      txtdetail.Text = $sType & ":" & modString.GetDateString(Now())
      If chkmultiple.Value = True Then
        For Each sFile In FileList
          $ImgList.Add(btnfolder.Tag &/ sFile)
        Next
        txtpages.Value = $ImgList.Count
      Endif

      Balloon.Info(FileList.Count & " Images Added", btnfolder)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif
  txtdetail.SetFocus

End

Public Sub btnsaveimg_Click()

  Dim sPath As String
  Dim sLongID As Long

  If txtdetail.Text Then
    If chkmultiple.Value = True Then
      Select Case Lower(File.Ext(txtpath.Text))
        Case "bmp", "jpg", "jpeg", "png", "gif", "svg", "tiff"
          sPath = modDevAll.ComvertMultiImagesToPDF($ImgList)
        Case Else
          sPath = txtpath.Text
      End Select
    Else
      sPath = txtpath.Text
    Endif
    If Exist(sPath) Then
      Inc Application.Busy
      sLongID = modImage.SavePatientFileGeneral($encid, $sType, txtdetail.Text, sPath)    ''
      Dec Application.Busy
      If sLongID Then
        txtdetail.Text = ""
        Balloon.Info(("File saved"), btnsaveimg)
        Balloon.Delay = modBasic.$BalloonDelay
      Endif
    Endif
  Endif

End

Public Sub btnupload_Click()

  Dim xPath As String
  Dim sLongID As Long

  Dim asx As String[]
  Dim aOutPath As String
  Dim res As Result

  If txtdetail.Text Then
    Inc Application.Busy
    If chkmultiple.Value = True Then
      Select Case Lower(File.Ext(txtpath.Text))
        Case "bmp", "jpg", "jpeg", "png", "gif", "svg", "tiff"
          xPath = modDevAll.ComvertMultiImagesToPDF($ImgList)
        Case Else
          xPath = txtpath.Text
      End Select
    Else
      xPath = txtpath.Text
    Endif
    If Exist(xPath) Then
      asx = Split($sExtra, "|")
      If asx[1] Then
        If asx[0] = modBasic.$HIPackage Or If asx[0] = modBasic.$HIPackageER Then
          aOutPath = modClaimAPI.UploadImagesFilesClaimServerHI(asx[1], xPath, modAppSupport.FindWorkProgressBar(modHelpVariable.$LogInCategory))
        Else If asx[0] = modBasic.$SSFAccident Or If asx[0] = modBasic.$SSFMedical Or If asx[0] = modBasic.$SSFCritical Then

        Endif
      Endif
      sLongID = modImage.SavePatientFileGeneral($encid, $sType, txtdetail.Text, xPath)    ''
      If aOutPath Then
        LCDLabel1.Text = "UPLOADED"
        If sLongID Then
          res = modDatabase.$myConn.Edit("tblpatreport", "fldid=&1", sLongID)
          res["fldflag"] = True
          res.Update
        Endif
      Endif
    Endif
    Dec Application.Busy

    If sLongID Then
      txtdetail.Text = ""
      Balloon.Info(("File saved"), btnupload)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
