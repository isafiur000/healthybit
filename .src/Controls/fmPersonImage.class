' Gambas class file

Private $categ As String
Private $imgName As String
Private $Lock As Boolean

Public Sub _new(categ As String, imgName As String, sLock As Boolean)

  $categ = categ
  $imgName = imgName
  $Lock = sLock
  DisplayBlobData()

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If $Lock = True Then
    btnedit.Enabled = False
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub btnOpen_Click()

  Dialog.Filter = ["*.jpg;*.jpeg;*.png;*.bmp", "Picture files"]
  If Dialog.OpenFile() Then Return
  PictureBox1.Tag = Dialog.Path
  PictureBox1.Image = Image.Load(Dialog.Path)

End

Public Sub btnwebcam_Click()

  PictureBox1.Tag = modImage.GetWebCamValue(modDevice.WebCamDevice())
  If PictureBox1.Tag Then
    PictureBox1.Image = Image.Load(PictureBox1.Tag)
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim sPath As String

  If PictureBox1.Tag Then
    sPath = PictureBox1.Tag
    Inc Application.Busy
    res = modDatabase.$myConn.Edit("tblpersonimage", "fldcateg=&1 and fldname=&2", $categ, $imgName)
    If res.Available = False Then
      modImage.InsertPatientImage($categ, $imgName, PictureBox1.Tag)
    Else
      res["fldpic"] = File.Load(sPath)
      res["fldlink"] = ""
      res["flduserid"] = modBasic.$lbluser
      res["fldcomp"] = modBasic.$compID
      res["flduptime"] = Now()
      res.Update()
    Endif
    Dec Application.Busy
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub DisplayBlobData()

  Dim tempFile As String

  tempFile = modImage.DisplayPersonImageData($categ, $imgName)
  If tempFile Then
    PictureBox1.Tag = tempFile
    PictureBox1.Image = Image.Load(tempFile)
    PictureBox1.Zoom = Slider1.Value / 100
  Endif

End

Public Sub btnimgedit_Click()

  If PictureBox1.Tag Then
    modDevice.DesktopOpenFile(PictureBox1.Tag)
  Endif

End

Public Sub btnclear_Click()

  If PictureBox1.Tag Then
    PictureBox1.Image.Clear()
    PictureBox1.Refresh()
    Wait
  Endif

End

Public Sub Slider1_Change()

  PictureBox1.Zoom = Slider1.Value / 100

End

Public Sub btnCancel_Click()

  Me.Close

End
