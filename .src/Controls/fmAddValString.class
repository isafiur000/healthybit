' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $sTable As String
Private $sFldKey As String
Private $sFldVal As String
Private $sType As String

Public Sub _new(sType As String, sfldKey As String, sfldVal As String, sTable As String)

  $sType = sType
  $sTable = sTable
  $sFldKey = sfldKey
  $sFldVal = sfldVal

  If $sType = "Currency" Then
    Label1.Text = "Currency"
    Label2.Text = "Conversion"
  Else If $sType = "Tax Coding" Then
    Label1.Text = "Tax Code"
    Label2.Text = "Tax Percent"
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  ShowGriddata()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Private Sub ShowGriddata()

  $rData = modDatabase.$myConn.Exec(Subst("select &1,&2 from &3", $sFldKey, $sFldVal, $sTable))
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 200 * modBasic.$AppWidthRatio
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[0].Text = Label1.Text
    .Columns[1].Text = Label2.Text
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub GridView1_Click()

  Dim res As Result
  Dim xval As Float
  Dim Row As Integer
  Dim xrate As Float

  If GridView1.Column = 1 Then
    If GridView1[GridView1.Row, 1].Text Then
      xrate = GridView1[GridView1.Row, 1].Text
    Else
      xrate = 0
    Endif
    xval = InputValue("Put value for " & GridView1[GridView1.Row, 0].Text, $sType, xrate)                                   ''
    If xval Then
      Row = GridView1.Row
      res = modDatabase.$myConn.Edit($sTable, $sFldKey & "=&1", GridView1[GridView1.Row, 0].Text)
      res[$sFldVal] = xval
      res.Update
      ShowGriddata()
      GridView1.Row = Row
    Endif
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  res = modDatabase.$myConn.Create($sTable)
  res[$sFldKey] = txtkey.Text
  res[$sFldVal] = txtval.Value
  res.Update
  ShowGriddata()

End

Public Sub btndel_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete($sTable, $sFldKey & "=&1", GridView1[GridView1.Row, 0].Text)
    ShowGriddata()
  Endif

End
