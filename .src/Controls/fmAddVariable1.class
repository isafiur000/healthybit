' Gambas class file

Private $colvar As String
Private $colfix As String
Private $tbl As String

Public Sub _new(colvar As String, colfix As String, col As String, tbl As String)

  $colvar = colvar
  $colfix = colfix
  $tbl = tbl
  txtcategory.Text = col

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  FilVarList()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub txtvariable_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colvar] = Trim(txtvariable.Text)
    res[$colfix] = txtcategory.Text
    res.Update()
    FilVarList()
    txtvariable.Text = ""
    Balloon.Info(("Variable added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    txtvariable.SetFocus
  Endif

End

Private Sub FilVarList()

  Dim sql As String
  Dim res As Result

  sql = Subst("select &1 from &2 where &3", $colvar, $tbl, $colfix)
  res = modDatabase.$myConn.Exec(sql & "=&1", txtcategory.Text)
  modFillContainer.FillListBox(lstvariable, res, False)

End

Public Sub btnCancel_Click()

  If lstvariable.Text Then
    modDatabase.$myConn.Delete($tbl, $colvar & "=&1 and " & $colfix & "=&2", lstvariable.Text, txtcategory.Text)
    FilVarList()
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xxx As String[]
  Dim xitem As String
  Dim res As Result

  If Exist(txtcsv.Text) Then
    Inc Application.Busy
    xxx = modString.GetStringArrayFromFile(txtcsv.Text)
    For Each xitem In xxx
      If Trim(xitem) Then
        res = modDatabase.$myConn.Create($tbl)
        res[$colvar] = Trim(xitem)
        res[$colfix] = txtcategory.Text
        res.Update()
      Endif
    Next
    Dec Application.Busy
    FilVarList()
    txtcsv.Text = ""
    Balloon.Info(("Data Import completed"), btnupload)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
