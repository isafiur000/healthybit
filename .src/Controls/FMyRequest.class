' Gambas class file

Private $rData As MyResult
Private $aMyFields As String[]
Private $strType As String
Private $strTitle As String

Public Sub _new(res As MyResult, strTitle As String, strType As String, AllowExport As Boolean)

  $strType = strType
  $rData = res
  $strTitle = strTitle

  Me.Title = $strTitle
  mnutransfer.Visible = AllowExport

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If $strType = "ArchivedReports" Then
    mnudoreport.Visible = True
  Else If $strType = "ArchivedRepoReports" Then
    mnudoreport.Visible = True
  Endif
  ShowGridView()

End

Private Sub ShowGridView()

  $aMyFields = New String[]
  modGridView.ReadSmallMyData(GridView1, $rData, $aMyFields)
  ResizeGridView()

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)

  If $strType = "ArchivedReports" Then
    If Column = 1 Then
      GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
    Else If Column = 8 Then
      GridView1.Data.Picture = Picture["icon:/small/attach"]
      GridView1.Data.Text = ""
    Else
      GridView1.Data.Text = $rData[$aMyFields[Column]]
    Endif

  Else If $strType = "ArchivedRepoReports" Then
    If Column = 1 Then
      GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
    Else If Column = 9 Then
      GridView1.Data.Picture = Picture["icon:/small/attach"]
      GridView1.Data.Text = ""
    Else
      GridView1.Data.Text = $rData[$aMyFields[Column]]
    Endif

  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Private Sub ResizeGridView()

  With GridView1
    If $strType = "ArchivedReports" Then
      .Columns[0].Width = 1
      .Columns[1].Width = 150 * modBasic.$AppWidthRatio
      .Columns[2].Width = 125 * modBasic.$AppWidthRatio
      .Columns[3].Width = 150 * modBasic.$AppWidthRatio
      .Columns[4].Width = 250 * modBasic.$AppWidthRatio
      .Columns[5].Width = 1
      .Columns[6].Width = 1
      .Columns[7].Width = 1
      .Columns[8].Width = 25 * modBasic.$AppWidthRatio
      .Columns[9].Width = 1

      .Columns[1].Text = "DateTime"
      .Columns[2].Text = "Encounter"
      .Columns[3].Text = "Title"
      .Columns[4].Text = "Comment"

    Else If $strType = "ArchivedRepoReports" Then
      .Columns[0].Width = 1
      .Columns[1].Width = 150 * modBasic.$AppWidthRatio
      .Columns[2].Width = 125 * modBasic.$AppWidthRatio
      .Columns[3].Width = 150 * modBasic.$AppWidthRatio
      .Columns[4].Width = 250 * modBasic.$AppWidthRatio
      .Columns[5].Width = 1
      .Columns[6].Width = 1
      .Columns[7].Width = 1
      .Columns[8].Width = 75 * modBasic.$AppWidthRatio
      .Columns[9].Width = 25 * modBasic.$AppWidthRatio

      .Columns[1].Text = "DateTime"
      .Columns[2].Text = "Encounter"
      .Columns[3].Text = "Title"
      .Columns[4].Text = "Comment"
      .Columns[8].Text = "HF"

    Endif
  End With

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub mnuexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub mnutransfer_Click()

  modGridView.ExportGridToSqlite(GridView1)

End

Public Sub mnugenimage_Click()

  Dim hForm As FmPatImage

  If GridView1.Rows.Selection.Count Then
    hForm = New FmPatImage("IMAGE", GridView1[GridView1.Row, 0].Text, "")
    hForm.ShowModal
  Endif

End

Public Sub mnudicom_Click()

  Dim hForm As FmPatImage

  If GridView1.Rows.Selection.Count Then
    hForm = New FmPatImage("DICOM", GridView1[GridView1.Row, 0].Text, "")
    hForm.ShowModal
  Endif

End

Public Sub mnupacs_Click()

  Dim hForm As FmPacsFind

  If GridView1.Rows.Selection.Count Then
    hForm = New FmPacsFind(GridView1[GridView1.Row, 0].Text, "Visit")
    hForm.ShowModal
  Endif

End

Public Sub mnumovement_Click()

  Dim xcoord As String[]
  Dim xList As String[]
  Dim hForm As FmWebView

  xList = New String[]
  For Each $rData
    xcoord = modDataRepo.GetGeoCoordinates($rData["fldhospcode"])
    If xcoord Then
      If xcoord[0] And If xcoord[1] Then
        xList.Add(xcoord[0] & "," & xcoord[1])
      Endif
    Endif
  Next
  If xList.Count Then
    hForm = New FmWebView("https://www.google.com/maps/dir/" & xList.Join("/"))
    modAppSupport.FindWorSpace(modHelpVariable.$LogInCategory).Add(hForm)
  Endif

End

Public Sub mnuopenfile_Click()

  Dim xpath As String

  If GridView1.Rows.Selection.Count > 0 Then
    If $strType = "ArchivedRepoReports" Then
      xpath = modImage.GetBlobFileRemoteData(GridView1[GridView1.Row, 0].Text)
    Else
      xpath = modImage.GetBlobFileData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 8].Text)
    Endif
    If Exist(xpath) Then
      modDevice.DesktopOpenFile(xpath)
    Endif
  Endif

End

Public Sub mnuprint_Click()

  Dim xpath As String

  If GridView1.Rows.Selection.Count > 0 Then
    If $strType = "ArchivedRepoReports" Then
      xpath = modImage.GetBlobFileRemoteData(GridView1[GridView1.Row, 0].Text)
    Else
      xpath = modImage.GetBlobFileData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 8].Text)
    Endif
    If Exist(xpath) Then
      modPrint.DefaultPrinting(xpath, "ReportSize")
    Endif
  Endif

End

Public Sub mnuemail_Click()

  Dim xpath As String
  Dim hForm As FmRemoteMail
  Dim xx As String[]

  If GridView1.Rows.Selection.Count > 0 Then
    If $strType = "ArchivedRepoReports" Then
      xpath = modImage.GetBlobFileRemoteData(GridView1[GridView1.Row, 0].Text)
    Else
      xpath = modImage.GetBlobFileData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 8].Text)
    Endif
    If Exist(xpath) Then
      xx = New String[]
      xx.Add(modPatient.GetPatientEmail(GridView1[GridView1.Row, 7].Text))
      hForm = New FmRemoteMail(xx, xpath, "Report", GridView1[GridView1.Row, 3].Text)
      hForm.ShowModal()
    Endif
  Endif

End

Public Sub GridView1_Click()

  Dim xpath As String

  If GridView1.Column = 8 Then
    If $strType = "ArchivedReports" Then
      xpath = modImage.GetBlobFileData(GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 8].Text)
      If Exist(xpath) Then
        modDevice.DesktopOpenFile(xpath)
      Endif
    Endif
  Else If GridView1.Column = 9 Then
    If $strType = "ArchivedRepoReports" Then
      xpath = modImage.GetBlobFileRemoteData(GridView1[GridView1.Row, 0].Text)
      If Exist(xpath) Then
        modDevice.DesktopOpenFile(xpath)
      Endif
    Endif
  Endif

End
