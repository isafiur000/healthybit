' Gambas class file

Private $typ As String
Private $tbl As String

Public Sub _new(sTitle As String, xType As String, xItemName As String)

  $typ = xType
  Me.Title = sTitle
  txtitemname.Text = xItemName

  Select $typ
    Case "No Discount List"
      cmbcategory.List = ["Diagnostic Tests", "General Services", "Procedures", "Equipment", "Radio Diagnostics", "Other Items", "Medicines", "Surgicals", "Extra Items"]
    Case "Prohibited List"
      cmbcategory.List = ["Diagnostic Tests", "General Services", "Procedures", "Equipment", "Radio Diagnostics", "Other Items"]
    Case "Complaints List"
      cmbcategory.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldcategory) as col from tblsymptoms"))
    Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
      cmbcategory.List = ["Qualitative", "Quantitative"]
  End Select
  ShowFilledList()

End

Public Sub cmbcategory_Click()

  chkall.Value = False
  LoadSelectedListsView()

End

Private Sub LoadSelectedListsView()

  Select $typ
    Case "No Discount List"
      NonStockBillingItemListView(cmbcategory.Text)
    Case "Prohibited List"
      GetServiceBillingItemListView(cmbcategory.Text, modNonMedical.GetDiscBindBillMode(txtitemname.Text))
    Case "Complaints List"
      CompComplaintsListView(cmbcategory.Text)
    Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
      CompVitalExamListView(cmbcategory.Text)
  End Select

End

Public Sub chkall_Click()

  Dim i As Integer
  Dim xpic As Picture

  If chkall.Value = True Then
    xpic = Picture["icons/checked.png"]
  Else
    xpic = Picture["icons/unchecked.png"]
  Endif

  For i = 1 To lstfirst.Count
    lstfirst[CStr(i)].Picture = xpic
  Next

End

Private Sub ShowFilledList()

  Dim sql As String
  Dim res As Result
  Dim asx As String[]

  Select $typ
    Case "No Discount List"
      $tbl = "tblnodiscount"
      sql = "select flditemname as col from tblnodiscount"
      res = modDatabase.$myConn.Exec(sql)
    Case "Prohibited List"
      $tbl = "tbllockedservice"
      sql = "select flditemname as col from tbllockedservice where flddisctype=&1"
      res = modDatabase.$myConn.Exec(sql, txtitemname.Text)
    Case "Complaints List"
      $tbl = "tblcomplaints"
      asx = Split(txtitemname.Text, "|")
      If asx.Count = 2 Then
        res = modDatabase.$myConn.Exec("select fldsymptom as col from tblcomplaints where fldcomp=&1 and fldtype=&2", asx[0], asx[1])
      Else
        res = modDatabase.$myConn.Exec("select fldsymptom as col from tblcomplaints where fldcomp=&1", txtitemname.Text)
      Endif
    Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
      $tbl = "tblcompexam"
      sql = "select fldexamid as col from tblcompexam where fldcomp=&1 and fldcategory=&2"
      res = modDatabase.$myConn.Exec(sql, txtitemname.Text, $typ)
  End Select
  modFillContainer.FillListBox(lstsecond, res, False)

End

Public Sub btnsave_Click()

  Dim res As Result
  Dim i As Integer
  Dim asx As String[]

  Inc Application.Busy
  For i = 1 To lstfirst.Count
    If lstfirst[CStr(i)].Picture = Picture["icons/checked.png"] Then
      If lstsecond.List.Exist(lstfirst[CStr(i)].Text) = False Then
        res = modDatabase.$myConn.Create($tbl)
        Select $typ
          Case "No Discount List"
            res["flditemname"] = lstfirst[CStr(i)].Text
            res["flditemtype"] = cmbcategory.Text
            res["flduserid"] = modBasic.$lbluser
            res["fldtime"] = Now()
            res["fldcomp"] = modBasic.$compID
          Case "Prohibited List"
            res["flddisctype"] = txtitemname.Text
            res["flditemname"] = lstfirst[CStr(i)].Text
            res["flditemtype"] = cmbcategory.Text
            res["flduserid"] = modBasic.$lbluser
            res["fldtime"] = Now()
            res["fldcomp"] = modBasic.$compID
          Case "Complaints List"
            asx = Split(txtitemname.Text, "|")
            res = modDatabase.$myConn.Create("tblcomplaints")
            res["fldtype"] = asx[1]
            res["fldsymptom"] = lstfirst[CStr(i)].Text
            res["fldflag"] = False
            res["fldcomp"] = asx[0]
          Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
            res["fldtype"] = cmbcategory.Text
            res["fldcategory"] = $typ
            res["fldexamid"] = lstfirst[CStr(i)].Text
            res["fldsysconst"] = modFixClinic.GetExamSysName(lstfirst[CStr(i)].Text)
            res["fldflag"] = False
            res["fldcomp"] = txtitemname.Text
        End Select
        res.Update()
      Endif
    Endif
  Next
  Dec Application.Busy
  ShowFilledList()
  LoadSelectedListsView()
  cmbcategory.SetFocus

End

Public Sub lstfirst_Menu()

  mnufile.Popup

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbcategory.Text = ""

End

Public Sub lstfirst_Click()

  modFillContainer.ShowCheckedListView(lstfirst)

End

Public Sub lstfirst_KeyRelease()

  If Key.Code = Key.Space Then
    modFillContainer.ShowCheckedListView(lstfirst)
  Endif

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Escape Then
    Me.Close
  Endif

End

Private Sub FillGridViewSelected(res As Result)

  Dim pic1 As Picture
  Dim i As Integer

  pic1 = Picture["icons/unchecked.png"]
  lstfirst.Clear
  i = 1
  If res.Available Then
    For Each res
      If Len(res!col) Then
        If lstsecond.List.Exist(res!col) Then
        Else
          lstfirst.Add(CStr(i), res!col, pic1)
          i = i + 1
        Endif
      Endif
    Next
  Endif

End

Private Sub GetServiceBillingItemListView(sType As String, sBillMode As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select flditemname as col from tblservicecost where flditemtype=&1 and fldgroup=&2", sType, sBillMode)
  FillGridViewSelected(res)

End

Private Sub NonStockBillingItemListView(sType As String)

  Dim res As Result

  If sType = "Medicines" Then
    res = modDatabase.$myConn.Exec("select fldbrandid as col from tblmedbrand")
  Else If sType = "Surgicals" Then
    res = modDatabase.$myConn.Exec("select fldbrandid as col from tblsurgbrand")
  Else If sType = "Extra Items" Then
    res = modDatabase.$myConn.Exec("select fldbrandid as col from tblextrabrand")
  Else
    res = modDatabase.$myConn.Exec("select flditemname as col from tblservicecost where flditemtype=&1", sType) '("Diagnostic Tests","Procedures","General Services")
  Endif
  FillGridViewSelected(res)

End

Private Sub CompComplaintsListView(sType As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldsymptom as col from tblsymptoms where fldcategory like &1", sType)
  FillGridViewSelected(res)

End

Private Sub CompVitalExamListView(sType As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldexamid as col from tblexam where fldtype like &1 and fldstatus=&2", sType, "Active")
  FillGridViewSelected(res)

End
