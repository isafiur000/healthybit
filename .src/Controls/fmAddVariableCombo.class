' Gambas class file

Private $colval As String
Private $colfix As String
Private $tbl As String
Private $kpdistinct As Boolean
Private $combolist As String[]

Public Sub _new(colval As String, colfix As String, fixval As String, tbl As String, combolist As String[], keepdistinct As Boolean)

  $colval = colval 'col for variable to add
  $colfix = colfix 'col for category
  $tbl = tbl
  $combolist = combolist
  $kpdistinct = keepdistinct
  txtcategory.Text = fixval 'category

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  FilVarList()

  If $kpdistinct = True Then
    cmbvariable.List = modString.GetRemainingArray($combolist, lstvariable.List)
  Else
    cmbvariable.List = $combolist
  Endif

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub btnOK_Click()

  Dim res As Result

  If cmbvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colval] = Trim(cmbvariable.Text)
    res[$colfix] = txtcategory.Text
    res.Update()
    FilVarList()
    cmbvariable.Text = ""
    Balloon.Info(("Variable added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    cmbvariable.SetFocus
  Endif

End

Public Sub btnaddmany_Click()

  Dim xlist As String[]
  Dim res As Result
  Dim xx As String

  xlist = SelectListView(("Select Variables to add"), cmbvariable.List, False)
  If xlist Then
    For Each xx In xlist
      res = modDatabase.$myConn.Create($tbl)
      res[$colval] = xx
      res[$colfix] = txtcategory.Text
      res.Update()
    Next
    FilVarList()
    cmbvariable.Text = ""
    cmbvariable.SetFocus
  Endif

End

Private Sub FilVarList()

  Dim sql As String
  Dim res As Result

  sql = Subst("select &1 from &2 where &3", $colval, $tbl, $colfix)
  res = modDatabase.$myConn.Exec(sql & "=&1", txtcategory.Text)
  modFillContainer.FillListBox(lstvariable, res, False)

End

Public Sub btnCancel_Click()

  If lstvariable.Text Then
    modDatabase.$myConn.Delete($tbl, $colval & "=&1 and " & $colfix & "=&2", lstvariable.Text, txtcategory.Text)
    FilVarList()
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub cmbvariable_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbvariable)
  modFillContainer.RestrictComboToContent(cmbvariable)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xxx As String[]
  Dim xitem As String
  Dim res As Result

  If Exist(txtcsv.Text) Then
    Inc Application.Busy
    xxx = modString.GetStringArrayFromFile(txtcsv.Text)
    For Each xitem In xxx
      If Trim(xitem) Then
        res = modDatabase.$myConn.Create($tbl)
        res[$colval] = Trim(xitem)
        res[$colfix] = txtcategory.Text
        res.Update()
      Endif
    Next
    Dec Application.Busy
    FilVarList()
    txtcsv.Text = ""
    Balloon.Info(("Data Import completed"), btnupload)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
