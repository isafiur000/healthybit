' Gambas class file

Private $colCateg As String
Private $colCombo As String
Private $coltext As String
Private $tbl As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(colcateg As String, colCombo As String, coltext As String, sTable As String, fixCateg As String, OptionList As String[])

  $colCateg = colcateg
  $colCombo = colCombo
  $coltext = coltext
  $tbl = sTable
  txtgroup.Text = fixCateg
  txtgroup.Enabled = False

  If OptionList.Count Then
    cmbtype.List = OptionList
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  ShowGridView()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub ShowGridView()

  Dim sql As String

  If txtgroup.Text Then
    sql = Subst("select fldid,&1,&2 from &3 where &4", $colCombo, $coltext, $tbl, $colCateg) & "=&1"
    $rData = modDatabase.$myConn.Exec(sql, txtgroup.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

    With GridView1
      .Columns[0].Width = 1
      .Columns[1].Width = 250 * modBasic.$AppWidthRatio
      .Columns[2].Width = 200 * modBasic.$AppWidthRatio
      .Columns[1].Text = "Group"
      .Columns[2].Text = "Variable"
    End With

  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  Select Column
    Case 1, 2
      GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
      GridView1.Data.WordWrap = True
  End Select

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colCateg] = Trim(txtgroup.Text)
    res[$colCombo] = cmbtype.Text
    res[$coltext] = Trim(txtvariable.Text)
    res.Update()
    ShowGridView()
    cmbtype.Text = ""
    txtvariable.Text = ""
    Balloon.Info(("Variable added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    cmbtype.SetFocus
  Endif

End

Public Sub btnCancel_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete($tbl, "fldid=&1", GridView1[GridView1.Row, 0].Text)
    ShowGridView()
    cmbtype.Text = ""
    txtvariable.Text = ""
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Click()

  cmbtype.Text = GridView1[GridView1.Row, 1].Text
  txtvariable.Text = GridView1[GridView1.Row, 2].Text

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, $tbl, txtcsv.Text, xdelim)
    ShowGridView()
  Endif

End
