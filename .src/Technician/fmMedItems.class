' Gambas class file

Private $sType As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(sType As String)

  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Vertical")
  Me.Icon = Picture[modGeneralMain.$strLogo]
  Me.Title = $sType & " Inventory"
  lbldate.Value = Date()
  If $sType = "Blood" Then
    cmbitem.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemcode as col from tblbloodstore"))
  Endif
  cmbitem.Text = "%"
  cmbstatus.List = ["Active", "Used", "Rejected", "Expired"]
  cmbstatus.Text = "Active"

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["R"] And If Key.Control Then
    btnrefresh_Click()
  Endif

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(lbldate.Value))
  If xx Then
    lbldate.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnentry_Click()

  Dim hForm As FmBloodEntry

  hForm = New FmBloodEntry($sType)
  hForm.ShowModal
  FillPatDateGrid()

End

Private Sub FillPatDateGrid()

  Dim sql As String

  If chkexpiry.Value = True Then
    sql = "select fldid,fldcode,flditem,fldencounterval,fldencname,fldcollect,fldexpiry,fldstatus,fldcomment,fldid from tblmedinventory where fldcategory=&1 and flditem like &2 and fldstatus like &3 and fldexpiry<&4"                                                                              ''
  Else
    sql = "select fldid,fldcode,flditem,fldencounterval,fldencname,fldcollect,fldexpiry,fldstatus,fldcomment,fldid from tblmedinventory where fldcategory=&1 and flditem like &2 and fldstatus like &3"                                                                              ''
  Endif
  $rData = modDatabase.$myConn.Exec(sql, $sType, cmbitem.Text, cmbstatus.Text, lbldate.Value)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Private Sub ResizeGrid()

  With GridView1
    .Columns[0].Width = 25 * modBasic.$AppWidthRatio
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 300 * modBasic.$AppWidthRatio
    .Columns[3].Width = 125 * modBasic.$AppWidthRatio
    .Columns[4].Width = 175 * modBasic.$AppWidthRatio
    .Columns[5].Width = 150 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 100 * modBasic.$AppWidthRatio
    .Columns[8].Width = 225 * modBasic.$AppWidthRatio
    .Columns[9].Width = 1

    .Columns[1].Text = "Code"
    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "EncID"
    .Columns[4].Text = "Provider"
    .Columns[5].Text = "CollectDate"
    .Columns[6].Text = "ExpiryDate"
    .Columns[7].Text = "Status"
    .Columns[8].Text = "Comment"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 0 Then
    GridView1.Data.Picture = Picture["icons/unchecked.png"]
  Else If Column = 5 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 6 Then
    If $rData[$aMyFields[Column]] <= Now() Then
      GridView1.Data.Foreground = modBasic.$HighLightColor
    Endif
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub GridView1_Click()

  Dim row As Integer
  Dim col As Integer

  row = GridView1.Row
  col = GridView1.Column
  If GridView1.Column = 0 Then
    modGridView.CheckUncheckGridNoDB(GridView1, 0)
  Endif
  GridView1.Row = row
  GridView1.Column = col

End

''-----------------------------  button and menu ------------------------------------------------
Public Sub btnrefresh_Click()

  FillPatDateGrid()
  GridView1.SetFocus

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $sType & " Report", modReportVar.GetDateTimeReport(lbldate.Value, gb.MediumDate))

End

Public Sub mnuselall_Click()

  Dim i As Integer

  For i = 0 To GridView1.Rows.Count - 1
    GridView1[i, 0].Picture = Picture["icons/checked.png"]
  Next

End

Public Sub mnudesel_Click()

  Dim i As Integer

  For i = 0 To GridView1.Rows.Count - 1
    GridView1[i, 0].Picture = Picture["icons/unchecked.png"]
  Next

End

Public Sub mnuprocalter_Click()

  Dim res As Result
  Dim xx As String
  Dim row As Integer
  Dim col As Integer

  row = GridView1.Row
  col = GridView1.Column
  If GridView1.Rows.Selection.Count > 0 Then
    xx = InputCombo("Status of the selected item", GridView1[GridView1.Row, 1].Text, cmbstatus.List, GridView1[GridView1.Row, 7].Text, True)
    If xx Then
      res = modDatabase.$myConn.Edit("tblmedinventory", "fldcategory=&1 and fldid=&2", $sType, GridView1[GridView1.Row, 9].Text)
      res["fldstatus"] = xx
      res["xyz"] = False
      res.Update
      FillPatDateGrid()
      GridView1.Row = row
      GridView1.Column = col
    Endif
  Endif

End

Public Sub mnudispense_Click()

  Dim xidList As Long[]
  Dim i As Integer
  Dim hForm As FmMedItemUse

  xidList = New Long[]
  For i = 0 To GridView1.Rows.Count - 1
    If GridView1[i, 0].Picture = Picture["icons/checked.png"] Then
      If GridView1[i, 7].Text = "Active" Then
        xidList.Add(GridView1[i, 9].Text)
      Endif
    Endif
  Next
  If xidList.Count Then
    hForm = New FmMedItemUse(xidList)
    hForm.ShowModal
    FillPatDateGrid()
  Endif

End

Public Sub mnuconsume_Click()

  Dim hForm As FmPatConsume

  If GridView1.Rows.Selection.Count > 0 Then
    hForm = New FmPatConsume(GridView1[GridView1.Row, 9].Text)
    hForm.ShowModal
    FillPatDateGrid()
  Endif

End

Public Sub mnuedit_Click()

  Dim hForm As FmBloodEdit
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    hForm = New FmBloodEdit($sType, GridView1[GridView1.Row, 1].Text)
    hForm.ShowModal
    FillPatDateGrid()
    GridView1.Row = Row
  Endif

End

Public Sub btnsearch_Click()

  Dim sql As String
  Dim enc As String

  If modBasic.$AutoEncPrefix Then
    enc = InputBox(("Encounter ID of Patient"), ("Search Patient"), modBasic.$EncIdPrefix)
  Else
    enc = InputBox(("Encounter ID of Patient"), ("Search Patient"), "")
  Endif
  If enc Then
    sql = "select fldid,fldcode,flditem,fldencounterval,fldencname,fldcollect,fldexpiry,fldstatus,fldcomment,fldid from tblmedinventory where fldcategory=&1 and fldencounterval=&2"
    $rData = modDatabase.$myConn.Exec(sql, $sType, enc)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ResizeGrid()
  Endif

End

Public Sub mnurequest_Click()

End
