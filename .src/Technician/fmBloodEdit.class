' Gambas class file

Private $sType As String

Public Sub _new(sType As String, sCode As String)

  $sType = sType
  txtcode.Text = sCode

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  Me.Title = $sType & " Product Entry"
  If $sType = "Blood" Then
    cmbitem.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemcode as col from tblbloodstore"))
  Endif
  RefreshValue(txtcode.Text)
  txtcode.SetFocus

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["F"] And If Key.Control Then
    txtencid.Text = GetEncid()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub dtnepcollect_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtcollection.Value))
  If xx Then
    dtcollection.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtnepexp_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtexpiry.Value))
  If xx Then
    dtexpiry.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Private Sub RefreshValue(sCode As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select flditem,fldencounterval,fldencname,fldcollect,fldexpiry,fldcomment from tblmedinventory where fldcategory=&1 and fldcode=&2", $sType, sCode)                                                              ''
  If res.Available Then
    cmbitem.Text = res["flditem"]
    txtencid.Text = res["fldencounterval"]
    txtencname.Text = res["fldencname"]
    dtcollection.Value = res["fldcollect"]
    dtexpiry.Value = res["fldexpiry"]
    txtcomment.Text = res["fldcomment"]
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If cmbitem.Text And If txtcode.Text Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldcategory=&1 and fldcode=&2", $sType, Trim(txtcode.Text))
    res["flditem"] = cmbitem.Text
    res["fldencounterval"] = Trim(txtencid.Text)
    res["fldencname"] = Trim(txtencname.Text)
    res["fldcollect"] = dtcollection.Value
    res["fldexpiry"] = dtexpiry.Value
    res["flduserid"] = modBasic.$lbluser
    res["fldtime"] = Now()
    res["fldcomp"] = modBasic.$compID
    res["fldcomment"] = txtcomment.Text
    res["xyz"] = False
    res.Update
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub txtencid_GotFocus()

  txtencname.ReadOnly = False

End

Public Sub txtencid_LostFocus()

  btnrefresh_Click()

End

Public Sub btnrefresh_Click()

  txtencname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
  If txtencname.Text Then
    txtencname.ReadOnly = True
  Endif

End

Public Sub txtcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtencid_KeyPress()

  modGeneralMain.InputUpCaseOnly()

End
