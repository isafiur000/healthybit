' Gambas class file

Private $SIndex As Long

Public Sub _new(ItemIndex As Long)

  $SIndex = ItemIndex

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If $SIndex Then
    DisplayItemDetail()
  Endif
  ShowConsumeGrid()
  txtencid.SetFocus

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key["F"] And If Key.Control Then
    txtencid.Text = GetEncid()
  Endif

End

Private Sub DisplayItemDetail()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldcode,flditem,fldcategory from tblmedinventory where fldid=&1", $SIndex)
  If res.Available Then
    If res!flditem Then
      txtitemname.Text = res!flditem
    Endif
    If res!fldcode Then
      txtcode.Text = res!fldcode
    Endif
    If res!fldcategory Then
      txtcategory.Text = res!fldcategory
    Endif
  Endif

End

Public Sub btnrefresh_Click()

  Dim xstatus As String

  If txtencid.Text Then
    txtpatientname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    txtgender.Text = modPatient.GetPatientSex(Trim(txtencid.Text))
    txtpatientaddress.Text = modPatient.GetPatientAddressByEnc(Trim(txtencid.Text))

    xstatus = modPatient.CurrentAdmissionStatus(Trim(txtencid.Text))
    txtlocation.Text = modPatient.GetLocationSetting(Trim(txtencid.Text), xstatus)
    ShowConsumeGrid()
  Endif

End

Public Sub btnmarkdisp_Click()

  Dim res2 As Result

  If txtencid.Text Then
    modDatabase.$myConn.Begin
    modPatientGeneral.PatMedicalItemDirectUse(txtcategory.Text, Trim(txtencid.Text), txtitemname.Text, "", txtcode.Text, txtcomment.Text)

    res2 = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1", $SIndex)
    res2["fldstatus"] = "Used"
    res2["fldsave"] = False
    res2["xyz"] = False
    res2.Update
    modDatabase.$myConn.Commit
    Balloon.Info(("Information saved"), btnmarkdisp)
    Balloon.Delay = modBasic.$BalloonDelay
    ShowConsumeGrid()
  Endif

Catch
  modDatabase.$myConn.Rollback
  modHelpVariable.CreateErrorReport()

End

Private Sub ShowConsumeGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  Dim $rData As Result

  sql = "select fldid,fldtime,fldcategory,flditem,fldpatinfo from tblpatmeditem where fldencounterval=&1 and fldorder=&2"                                             ''
  $rData = modDatabase.$myConn.Exec(sql, Trim(txtencid.Text), "Done")

  GridView2.Clear
  GridView2.Columns.Count = $rData.Fields.Count
  GridView2.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView2, $rData.Index, Column)
      If Column = 1 Then
        GridView2[$rData.Index, Column].Text = modReportVar.GetDateTimeReport($rData["fldtime"], gb.GeneralDate)
      Else
        GridView2[$rData.Index, Column].Text = $rData[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView2.Row = 0

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 300 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Particulars"
    .Columns[4].Text = "Detail"
  End With

End

Public Sub txtencid_KeyPress()

  If Key.Code = Key.Down Then
    If modBasic.$AutoEncSuffix = "Yes" Then
      txtencid.Text = txtencid.Text & modBasic.$HospCode
    Endif
  Else
    modGeneralMain.InputUpCaseOnly()
  Endif

End
