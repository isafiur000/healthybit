' Gambas class file

Private $encid As String
Private $sMode As String
Private $PatientNum As String

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(encid As String, sMode As String)

  $encid = encid
  $sMode = sMode

End

Public Sub Form_Open()

  $PatientNum = modPatient.GetPatientNoByEnc($encid)
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  txtpatientaddress.Text = modPatient.GetPatientAddressByEnc($encid)
  txtage.Text = modPatient.GetPatientAgeString($encid, Now())
  ShowTablesGrid()

End

Private Sub ShowTablesGrid()

  Dim sql As String

  sql = "select fldid,fldencounterval,fldencounterval,fldtestid,fldsampletype,fldabnormal,fldreportquali,fldreportquanti,fldtime_report,fldstatus,fldtime_report,fldmethod,fldcondition,fldcomment,fldtestno,fldpacstudy from tbltelradiotest where fldencounterval=&1"                      ''
  $rData = modDatabase.$myConn.Exec(sql, $encid)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 225 * modBasic.$AppWidthRatio
    .Columns[4].Width = 1
    .Columns[5].Width = 25 * modBasic.$AppWidthRatio
    .Columns[6].Width = 300 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 150 * modBasic.$AppWidthRatio
    .Columns[9].Width = 1
    .Columns[10].Width = 1
    .Columns[11].Width = 100 * modBasic.$AppWidthRatio
    .Columns[12].Width = 1
    .Columns[13].Width = 1
    .Columns[14].Width = 1   ''fldtestno
    .Columns[15].Width = 1   ''fldpacstudy

    .Columns[1].Text = "EncID"
    .Columns[3].Text = "TestName"
    .Columns[6].Text = "Observation"
    .Columns[7].Text = "Value"
    .Columns[8].Text = "ReportingTime"
    .Columns[11].Text = "Method"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 3 Then
    GridView1.Data.Text = $rData[$aMyFields[Column]]
    GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  Else If Column = 5 Then
    GridView1.Data.Picture = Picture[modMisc.GetGridIcon($rData[$aMyFields[Column]])]
    GridView1.Data.Text = ""
  Else If Column = 6 Then
    GridView1.Data.RichText = $rData[$aMyFields[Column]]
  Else If Column = 8 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  GridView1.Data.WordWrap = True

End

Public Sub GridView1_Select()

  BlankSubGrid()
  SHowQualiSUbTest(GridView1[GridView1.Row, 14].Text)

End

Private Sub SHowQualiSUbTest(serial As Long)

  Dim sql As String
  Dim res As Result
  Dim Column As Integer
  Dim fld As ResultField

  GridView2.Clear
  sql = "select fldid,fldchk,fldsubtest,fldabnormal,fldreport,fldorder from tbltelradiosubtest where fldencounterval=&1 and fldtestid=&2"
  res = modDatabase.$myConn.Exec(sql, $encid, serial)

  GridView2.Columns.Count = res.Fields.Count
  GridView2.Rows.Count = res.Count

  For Each res
    Column = 0
    For Each fld In res.Fields
      modGeneralMain.GridExplicitDecoration(GridView2, res.Index, Column)
      If Column = 3 Then
        GridView2[res.Index, Column].Picture = Picture[modMisc.GetGridIcon(res["fldabnormal"])]
        GridView2[res.Index, Column].Text = ""
      Else If Column = 4 Then
        GridView2[res.Index, Column].RichText = res[fld.Name]
      Else
        GridView2[res.Index, Column].Text = res[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next

  ResizeGrid()

End

Private Sub ResizeGrid()

  With GridView2
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 1
    .Columns[2].Width = 250 * modBasic.$AppWidthRatio
    .Columns[3].Width = 25 * modBasic.$AppWidthRatio
    .Columns[4].Width = 500 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[2].Text = "SubTest"
    .Columns[4].Text = "Observation"
  End With

End

Private Sub BlankSubGrid()

  GridView2.Clear
  GridView2.Columns.Count = 6
  GridView2.Rows.Count = 0
  ResizeGrid()

End

''================================== View =================
Public Sub btnplay_Click()

  Dim res As Result
  Dim xLink As String
  Dim xserver As String

  If GridView1.Rows.Selection.Count Then
    If GridView1[GridView1.Row, 15].Text Then

      res = modDatabase.$myConn.Exec("select fldpacstudy,fldpacseries,fldpacsform from tbltelradiotest where fldid=&1", GridView1[GridView1.Row, 0].Text)
      If res.Available Then
        If res["fldpacseries"] Then
          xserver = InputCombo("Select PACS Node to Use", "PACS", modMisc.GetPacsServerList(), "", True)
          If xserver Then
            modpacs.GetPacsSetting(xserver)
            xLink = modPACS.DicomSeriesWebView(res["fldpacsform"], $encid, res["fldpacstudy"], res["fldpacseries"])
            modControlSub.OpenBrowser(xLink)
          Endif
        Endif
      Endif

    Endif
  Endif

End

Public Sub btnimage_Click()

  Dim hForm As FmTestImage

  If GridView1.Rows.Selection.Count > 0 Then
    hForm = New FmTestImage("IMAGE", $encid, GridView1[GridView1.Row, 14].Text, GridView1[GridView1.Row, 3].Text, "Radiology", "")
    hForm.ShowModal
  Endif

End

Public Sub btndicom_Click()

  Dim hForm As FmTestImage

  If GridView1.Rows.Selection.Count > 0 Then
    hForm = New FmTestImage("DICOM", $encid, GridView1[GridView1.Row, 14].Text, GridView1[GridView1.Row, 3].Text, "Radiology", "")
    hForm.ShowModal
  Endif

End

''====================== Repository ================
Public Sub cmbexpert_Click()

  Dim xMedUser As String[]
  Dim res As Result
  Dim xPath As String

  res = modDatabase.$myConn.Exec("select fldcategory,fldhospital,fldteleuser,fldusername from tbltelemeduser where fldstatus=&1", "Active")
  xPath = modTextDB.ConvertResultToCSV(res, ";", False)
  If Exist(xPath) Then
    xMedUser = TreeViewCode(xPath, ";", 3, True)
    If xMedUser And If xMedUser.Count Then
      cmbexpert.Tag = xMedUser[1]
      cmbexpert.Text = modGeneral.GetTeleMedUserName(cmbexpert.Tag)
    Endif
  Endif

End

Public Sub btnshowexpert_Click()

  Inc Application.Busy
  modDatabase.$myConn.Delete("tbltelemeduser")
  Wait
  modRepoSync.ReadSyncConfig()
  modRepoSync.GetAPISyncTwoSource(modDatabase.$myConn, "tbltelemeduser", True)
  Dec Application.Busy

End

Public Sub btnupload_Click()

  Dim res As Result
  Dim xstUID As String
  Dim xserver As String
  Dim res1 As Result
  Dim res2 As Result

  Dim arcmd1 As String[]
  Dim arcmd2 As String[]
  Dim arcmd3 As String[]
  Dim arcmd4 As String[]
  Dim arcmd5 As String[]

  If GridView1.Rows.Selection.Count Then
    If GridView1[GridView1.Row, 15].Text Then
      res = modDatabase.$myConn.Exec("select fldpacstudy,fldpacseries,fldpacsform from tbltelradiotest where fldid=&1", GridView1[GridView1.Row, 0].Text)
      If res.Available Then

        xserver = InputCombo("Select PACS Node to Use", "PACS", modMisc.GetPacsServerList(), "", True)
        Inc Application.Busy
        If xserver Then
          modpacs.GetPacsSetting(xserver)
          If res["fldpacsform"] = "Orthanc" Then
            xstUID = res["fldpacstudy"]
          Else
            xstUID = modPACS.GetOrthancString($encid & "|" & res["fldpacstudy"])
          Endif
          If xstUID Then
            modRepository.UploadOrthancStudy(xstUID)
          Endif
        Endif

        Select modBasic.$ClinicTelemed
          Case "Enable", "Partial"
            arcmd1 = [Application.Name, "--upload-repo-one", "tblencounter", $encid]
            arcmd2 = [Application.Name, "--upload-repo-one", "tblpatientinfo", $PatientNum]
            arcmd3 = [Application.Name, "--upload-repo-one", "tbltelradiotest", GridView1[GridView1.Row, 0].Text]
            modBasic.DebugAPIString("Execute " & arcmd1.Join(Space(1)))
            Exec arcmd1 Wait
            modBasic.DebugAPIString("Execute " & arcmd2.Join(Space(1)))
            Exec arcmd2 Wait
            modBasic.DebugAPIString("Execute " & arcmd3.Join(Space(1)))
            Exec arcmd3 Wait
            res1 = modDatabase.$myConn.Exec("select fldid from tbltelradiosubtest where fldencounterval=&1 and fldtestid=&2", $encid, GridView1[GridView1.Row, 14].Text)
            If res1.Available Then
              For Each res1
                arcmd4 = [Application.Name, "--upload-repo-one", "tbltelradiosubtest", res1["fldid"]]
                modBasic.DebugAPIString("Execute " & arcmd4.Join(Space(1)))
                Exec arcmd4 Wait
              Next
            Endif
            res2 = modDatabase.$myConn.Exec("select fldid from tblpatserialimage where fldencounterval=&1 and fldtestid=&2", $encid, GridView1[GridView1.Row, 14].Text)
            If res2.Available Then
              For Each res2
                arcmd5 = [Application.Name, "--uploadimg-repo-one", "tblpatserialimage", res2["fldid"]]
                modBasic.DebugAPIString("Execute " & arcmd5.Join(Space(1)))
                Exec arcmd5 Wait
              Next
            Endif

          Case "Complete"
            modDataRepo.GetRepoUploadEncounterData($encid)
        End Select
        Dec Application.Busy

      Endif
    Endif
  Endif

End
