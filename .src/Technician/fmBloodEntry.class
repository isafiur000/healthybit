' Gambas class file

Private $sType As String

Public Sub _new(sType As String)

  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  Me.Title = $sType & " Product Entry"
  If $sType = "Blood" Then
    cmbitem.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(flditem) as col from tblblood"))
  Endif
  dtcollection.Value = Now()
  dtexpiry.Value = Now()
  cmbitem.SetFocus

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key["F"] And If Key.Control Then
    txtencid.Text = GetEncid()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub dtnepcollect_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtcollection.Value))
  If xx Then
    dtcollection.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtnepexp_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtexpiry.Value))
  If xx Then
    dtexpiry.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtcollection_Click()

  Dim xshelf As Integer

  If cmbitem.Text Then
    xshelf = modMedConstant.GetBloodShelfLife(cmbitem.Text)
    If xshelf Then
      dtexpiry.Value = DateAdd(dtcollection.Value, gb.Day, xshelf)
    Endif
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result
  Dim i As Integer

  If cmbitem.Text And If txtqty.Value And If txtcode.Text Then
    For i = 1 To txtqty.Value
      res = modDatabase.$myConn.Create("tblmedinventory")
      res["fldcategory"] = $sType
      res["fldcode"] = txtcode.Text & "-" & CStr(i)
      res["flditem"] = cmbitem.Text
      res["fldencounterval"] = txtencid.Text
      res["fldencname"] = txtencname.Text
      res["fldcollect"] = dtcollection.Value
      res["fldexpiry"] = dtexpiry.Value
      res["fldstatus"] = "Active"
      res["fldsave"] = True
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldcomment"] = txtcomment.Text
      res["xyz"] = False
      res.Update
    Next
    Balloon.Info(("Information saved"), btnadd)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub btnrefresh_Click()

  txtencname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
  If txtencname.Text Then
    txtencname.ReadOnly = True
  Endif

End

Public Sub txtcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtencid_KeyPress()

  If Key.Code = Key.Down Then
    If modBasic.$AutoEncSuffix = "Yes" Then
      txtencid.Text = txtencid.Text & modBasic.$HospCode
    Endif
  Else
    modGeneralMain.InputUpCaseOnly()
  Endif

End
