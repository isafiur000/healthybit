' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]
Private $rData2 As Result
Private $aMyFields2 As String[]
Private $TestList As String[]

Public Sub _new(encid As String)

  $encid = encid

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  modLabSub.DisplayDefaultTestUnit(rbsi, rbmetric)
  $TestList = New String[]

  SHowSavedGrid()
  ShowSampled()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key.F1 Then
    TabPanel1.Index = 0
  Else If Key.Code = Key.F2 Then
    TabPanel1.Index = 1
  Else If Key.Code = Key.F3 Then
    TabPanel1.Index = 2
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
    SHowSavedGrid()
  Else If TabPanel1.Index = 1 Then
    ShowSampled()
  Else If TabPanel1.Index = 2 Then
    lsttestname.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldtestid) as col from tblpatlabtest where fldencounterval=&1", $encid))
  Endif

End

''----------------------------------- Billed but not sampled ------------------------------------
Private Sub SHowSavedGrid()

  Dim sql As String

  sql = "select fldid,fldtime,flditemname,fldrefer,fldtarget from tblpatbilling where fldencounterval=&1 and flditemtype=&2 and fldsave=&3 and (fldstatus=&4 or fldstatus=&5)"
  $rData1 = modDatabase.$myConn.Exec(sql, $encid, "Diagnostic Tests", True, "Done", "Cleared")
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)

  With GridView2
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 300 * modBasic.$AppWidthRatio
    .Columns[3].Width = 150 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Test Name"
    .Columns[3].Text = "ReferBy"
    .Columns[4].Text = "Target"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData1.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  If Column = 1 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else If Column = 3 Then
    GridView2.Data.Text = modGeneral.GetUserFullName($rData1[$aMyFields1[Column]])
  Else
    GridView2.Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

''------------------------------------------- Reported -----------------------------------
Public Sub FillLabtable()

  Dim sql As String

  sql = "select fldid,fldsampletype,fldmethod,fldabnormal,fldid,fldstatus,fldtime_sample,fldtime_report,fldtest_type from tblpatlabtest where fldencounterval=&1 and fldtestid=&2 and (fldstatus=&3 or fldstatus=&4)"
  $rData = modDatabase.$myConn.Exec(sql, $encid, lsttestname.Text, "Reported", "Verified")
  $aMyFields = New String[]
  modGridView.ReadSmallData(TableView1, $rData, $aMyFields)
  With TableView1
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 25 * modBasic.$AppWidthRatio
    .Columns[4].Width = 225 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 150 * modBasic.$AppWidthRatio
    .Columns[8].Width = 1

    .Columns[1].Text = "Specimen"
    .Columns[2].Text = "Method"
    .Columns[4].Text = "Observation"
    .Columns[5].Text = "Status"
    .Columns[6].Text = "SampleTime"
    .Columns[7].Text = "ReportTime"
  End With

End

Public Sub TableView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(TableView1, Row)
  If Column = 3 Then
    TableView1.Data.Picture = Picture[modMisc.GetGridIcon($rData[$aMyFields[Column]])]
    TableView1.Data.Text = ""
  Else If Column = 4 Then
    TableView1.Data.RichText = modLabTest.GetLabTestValueSubString($rData["fldid"], modLabSub.GetTestUnitFromButton(rbsi, rbmetric), True)
    If modBasic.$RichtextResizeRow = "Yes" Then
      TableView1.Rows[Row].Height = Max(TableView1.Rows[Row].Height, TableView1.Data.Font.RichTextHeight(TableView1.Data.RichText, TableView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (TableView1.Rows.Height - TableView1.Font.Height))
    Endif
  Else If Column = 6 Then
    TableView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldtime_sample"], gb.GeneralDate)
  Else If Column = 7 Then
    TableView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldtime_report"], gb.GeneralDate)
  Else
    TableView1.Data.Text = $rData[$aMyFields[Column]]
  Endif
  TableView1.Data.WordWrap = True

End

Public Sub lsttestname_Click()

  If lsttestname.Text Then
    FillLabtable()
  Endif

End

Public Sub lsttestname_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    lsttestname_Click()
  Endif

End

''----------------------------- Sampled but not Reported -----------------------------
Private Sub ShowSampled()

  Dim sql As String

  sql = "select fldid,fldsampletype,fldtestid,fldabnormal,fldsampleid,fldmethod,fldtime_sample from tblpatlabtest where fldencounterval=&1 and fldstatus=&2"
  $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Sampled")
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView3, $rData2, $aMyFields2)
  With GridView3
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 250 * modBasic.$AppWidthRatio
    .Columns[3].Width = 25 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Specimen"
    .Columns[2].Text = "Test Name"
    .Columns[4].Text = "Sample"
    .Columns[5].Text = "Method"
    .Columns[6].Text = "SampleDate"
  End With

End

Public Sub GridView3_Data(Row As Integer, Column As Integer)

  $rData2.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView3, Row)
  If Column = 3 Then
    GridView3.Data.Picture = Picture["icons/null.svg"]
    GridView3.Data.Text = ""
  Else If Column = 6 Then
    GridView3.Data.Text = modReportVar.GetDateTimeReport($rData2["fldtime_sample"], gb.GeneralDate)
  Else
    GridView3.Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

' Public Sub txtsearch_KeyPress()
'
'   modGeneralMain.InputTextSearchKeyOnly()
'
' End
