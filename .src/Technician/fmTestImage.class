' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $encid As String
Private $strType As String

Public Sub _new(strType As String, encid As String, sIndex As Long, testname As String, sMode As String, sPath As String)

  $strType = strType
  $encid = encid
  txtid.Value = sIndex
  lbltest.Text = testname
  lblobs.Text = sMode
  If sPath Then
    PictureBox1.Tag = sPath
  Endif

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $strType & " Files"
  ShowGridImage()
  DisplaySelectImg()

  Select modHelpVariable.$LogInCategory
    Case "Clinician", "Technician"
      btnOK.Enabled = True
      btnChange.Enabled = True
    Case Else
      btnOK.Enabled = False
      btnChange.Enabled = False
  End Select

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub Slider1_Change()

  PictureBox1.Zoom = Slider1.Value / 100

End

Public Sub txttitle_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Private Sub ShowGridImage()

  Dim sql As String

  If MMain.$WebEntry = True Then
    sql = "select fldrepoid,fldtitle,fldlink from tblpatserialimage where fldencounterval=&1 and fldtestid=&2 and fldcateg=&3 and fldmode=&4"
  Else
    sql = "select fldid,fldtitle,fldlink from tblpatserialimage where fldencounterval=&1 and fldtestid=&2 and fldcateg=&3 and fldmode=&4"
  Endif
  $rData = modDatabase.$myConn.Exec(sql, $encid, txtid.Value, $strType, lblobs.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(dgimage, $rData, $aMyFields)
  dgimage.Columns[0].Width = 1
  dgimage.Columns[1].Width = 275 * modBasic.$AppWidthRatio
  dgimage.Columns[2].Width = 1

End

Public Sub dgimage_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(dgimage, Row)
  dgimage.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub dgimage_Click()

  DisplayBlobData(dgimage[dgimage.Row, 0].Text)
  DisplaySelectImg()

End

Private Sub DisplaySelectImg()

  Dim xpath As String

  If PictureBox1.Tag Then
    If $strType = "IMAGE" Then
      Try PictureBox1.Image = Image.Load(PictureBox1.Tag)
    Else If $strType = "DICOM" Then
      xpath = modDevAll.ConverDicomToPNG(PictureBox1.Tag)
      Try PictureBox1.Image = Image.Load(xpath)
    Endif
  Endif

End

Private Sub DisplayBlobData(serial As Variant)

  Dim res As Result
  Dim sql As String
  Dim tempFile As String
  Dim hFile As Blob
  Dim aFile As String

  If MMain.$WebEntry = True Then
    sql = "select fldpic,fldtitle,fldkeyword,flddetail,fldlink,fldcateg from tblpatserialimage where fldrepoid=&1"
  Else
    sql = "select fldpic,fldtitle,fldkeyword,flddetail,fldlink,fldcateg from tblpatserialimage where fldid=&1"
  Endif
  res = modDatabase.$myConn.Exec(sql, serial)
  If res.Available Then
    If res["fldcateg"] = "IMAGE" Then
      tempFile = Temp() & ".png"
    Else If res["fldcateg"] = "DICOM" Then
      tempFile = Temp() & ".dcm"
    Endif
    If res["fldlink"] Then
      Inc Application.Busy
      aFile = modFTPSub.GetFileFromLocalFTP(res["fldlink"])
      If Exist(aFile) Then
        Copy aFile To tempFile
      Endif
      Dec Application.Busy
    Else
      hFile = res["fldpic"]
      If hFile.Length Then
        File.Save(tempFile, hFile.Data)
      Endif
    Endif
    PictureBox1.Tag = tempFile

    txttitle.Text = res["fldtitle"]
    txtkeyword.Text = res["fldkeyword"]
    txtdetail.Text = res["flddetail"]
  Endif

End

Public Sub dgimage_KeyPress()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    dgimage_Click()
  Endif

End

''''
Public Sub btnOpen_Click()

  If $strType = "IMAGE" Then
    Dialog.Filter = ["*.jpg;*.jpeg;*.png;*.bmp", "Picture files"]
  Else If $strType = "DICOM" Then
    Dialog.Filter = ["*.dcm", "DICOM files"]
  Endif

  If Dialog.OpenFile() Then Return
  PictureBox1.Tag = Dialog.Path
  DisplaySelectImg()

End

Public Sub btnwebcam_Click()

  If $strType = "IMAGE" Then
    PictureBox1.Tag = modImage.GetWebCamValue(modDevice.WebCamDevice())
    If PictureBox1.Tag Then
      PictureBox1.Image = Image.Load(PictureBox1.Tag)
    Endif
  Else If $strType = "DICOM" Then
  Endif

End

Public Sub btnpencil_Click()

  Dim xxx As String[]

  If $strType = "IMAGE" Then
    xxx = CustomDraw()
    If xxx Then
      PictureBox1.Tag = xxx[0]
      If PictureBox1.Tag Then
        PictureBox1.Image = Image.Load(PictureBox1.Tag)
      Endif
    Endif
  Else If $strType = "DICOM" Then
  Endif

End

Public Sub btnplay_Click()

  If PictureBox1.Tag Then
    If $strType = "IMAGE" Then
      modDevice.DesktopOpenFile(PictureBox1.Tag)
    Else If $strType = "DICOM" Then
      modDevice.ViewDicomFile(PictureBox1.Tag)
    Endif
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result
  Dim sPath As String
  Dim xblobfile As String
  Dim xIntVal As String

  If txttitle.Text And If PictureBox1.Tag Then
    Inc Application.Busy
    If $strType = "IMAGE" Then
      sPath = PictureBox1.Tag
    Else If $strType = "DICOM" Then
      sPath = PictureBox1.Tag
    Endif

    If modMisc.BlobTarget("Patient") = "FTP" Then
      xblobfile = modFTPSub.SendBlobToFTP(sPath, CStr(txtid.Value), "Patient")
    Else
      xblobfile = File.Load(sPath)
    Endif
    If xblobfile Then
      res = modDatabase.$myConn.Create("tblpatserialimage")
      res["fldencounterval"] = $encid
      res["fldcateg"] = $strType
      res["fldmode"] = lblobs.Text
      res["fldtestid"] = txtid.Value
      If modMisc.BlobTarget("Patient") = "FTP" Then
        res["fldpic"] = ""
        res["fldlink"] = xblobfile
      Else
        res["fldpic"] = xblobfile
        res["fldlink"] = ""
      Endif
      res["fldtitle"] = Trim(txttitle.Text)
      res["fldkeyword"] = Trim(txtkeyword.Text)
      res["flddetail"] = txtdetail.Text
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldsave"] = True
      res["flduptime"] = ""
      If MMain.$WebEntry = True Then
        xIntVal = modString.GetDateString(Now())
        res["fldid"] = CLong(xIntVal)
        res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
        res["fldrepodate"] = Now()
        res["fldrepomac"] = modHelpVariable.$MACAddress
        res["fldhospcode"] = modBasic.$HospCode
      Endif
      res.Update()
      Dec Application.Busy

      ShowGridImage()
      Balloon.Info(("Information saved"), btnOK)
      Balloon.Delay = modBasic.$BalloonDelay
      txttitle.Text = ""
      txtkeyword.Text = ""
      txtdetail.Text = ""
    Endif
  Endif

End

Public Sub btnChange_Click()

  Dim res As Result
  Dim sPath As String
  Dim xblobfile As String

  If dgimage.Rows.Selection.Count > 0 Then
    If txttitle.Text Then

      If MMain.$WebEntry = True Then
        res = modDatabase.$myConn.Edit("tblpatserialimage", "fldrepoid=&1", dgimage[dgimage.Row, 0].Text)
      Else
        res = modDatabase.$myConn.Edit("tblpatserialimage", "fldid=&1", dgimage[dgimage.Row, 0].Text)              ''
      Endif
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then

        If PictureBox1.Tag Then
          If $strType = "IMAGE" Then
            sPath = PictureBox1.Tag
          Else If $strType = "DICOM" Then
            sPath = PictureBox1.Tag
          Endif

          If modMisc.BlobTarget("Patient") = "FTP" Then
            xblobfile = modFTPSub.SendBlobToFTP(sPath, CStr(txtid.Value), "Patient")
          Else
            xblobfile = File.Load(sPath)
          Endif
          If xblobfile Then
            If modMisc.BlobTarget("Patient") = "FTP" Then
              res["fldpic"] = ""
              res["fldlink"] = xblobfile
            Else
              res["fldpic"] = xblobfile
              res["fldlink"] = ""
            Endif

            res["fldtitle"] = Trim(txttitle.Text)
            res["fldkeyword"] = Trim(txtkeyword.Text)
            res["flddetail"] = txtdetail.Text
            res["flduptime"] = Now()
            res["flduserid"] = modBasic.$lbluser
            res.Update()
            ShowGridImage()
            Balloon.Delete(("Information updated"), btnChange)
            Balloon.Delay = modBasic.$BalloonDelay
          Endif
        Endif

      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif

    Endif
  Endif

End

Public Sub btnreport_Click()

  ImageReport()

End

Public Sub ImageReport()

  Dim $BillingReport As CReportHTML
  Dim res1 As Result
  Dim asx As New String[0]
  Dim tempFile As String
  Dim hFile As Blob
  Dim aFile As String

  $BillingReport = New CReportHTML([("Date"), ("Title"), ("Description"), ("Image")], "PatientReport", $encid)
  $BillingReport.UserData.Add("REPORT", "Report")
  $BillingReport.UserData.Add($strType, "PARAM1")

  res1 = modDatabase.$myConn.Exec("select fldtime,fldpic,fldtitle,flddetail,fldlink from tblpatserialimage where fldencounterval=&1 and fldtestid=&2 and fldcateg=&3 and fldmode=&4", $encid, txtid.Value, $strType, lblobs.Text)                      ''
  For Each res1
    tempFile = Temp() & ".png"
    If res1["fldlink"] Then
      Inc Application.Busy
      aFile = modFTPSub.GetFileFromLocalFTP(res1["fldlink"])
      If Exist(aFile) Then
        Copy aFile To tempFile
      Endif
      Dec Application.Busy
    Else
      hFile = res1["fldpic"]
      If hFile.Length Then
        File.Save(tempFile, hFile.Data)
      Endif
    Endif

    With asx
      .Add(modReportVar.GetDateTimeReport(res1["fldtime"], gb.GeneralDate))
      .Add(res1["fldtitle"])
      .Add(res1["flddetail"])
      .Add(modString.GetImageForHTML(tempFile))
    End With
    $BillingReport.Add(asx)
    asx.Clear()
  Next
  modControlSub.OpenHTMLPreview("", $BillingReport.NewHTMLPath(), "ReportSize")

End

Public Sub btnscan_Click()

  If $strType = "IMAGE" Then
    PictureBox1.Tag = ScannedFile()
    If PictureBox1.Tag Then
      PictureBox1.Image = Image.Load(PictureBox1.Tag)
    Endif
  Else If $strType = "DICOM" Then
  Endif

End
