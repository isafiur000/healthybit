' Gambas class file

Private $rData As Result

Private $sTest As String
Private $sType As String

Public Sub _new(sTest As String, sType As String)

  $sTest = sTest
  $sType = sType

  lbltestname.Text = $sTest
  If $sType = "Exam" Then
    Panel1.Visible = False
  Else If $sType = "Radio" Then
    Panel1.Visible = False
  Endif
  ShowExamGrid()

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbagegroup.List = modMedicine.AgeGroupListShortAll()
  cmbgender.List = ["Male", "Female", "Both Sex"]

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbagegroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbagegroup)

End

Public Sub cmbgender_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbgender)
  modFillContainer.RestrictComboToContent(cmbgender)

End

Private Sub ShowExamGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  If $sType = "Exam" Then
    sql = "select fldid,fldptsex,fldagegroup,fldmax,fldmin,fldcomment from tblexamcomment where fldexamid=&1"
  Else If $sType = "Test" Then
    sql = "select fldid,fldptsex,fldagegroup,fldmax,fldmin,fldunit,fldcomment from tbltestcomment where fldtestid=&1"
  Else If $sType = "Radio" Then
    sql = "select fldid,fldptsex,fldagegroup,fldmax,fldmin,fldcomment from tblradiocomment where fldexamid=&1"
  Endif
  $rData = modDatabase.$medConn.Exec(sql, $sTest)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 75 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    If $sType = "Test"
      .Columns[5].Width = 75 * modBasic.$AppWidthRatio
      .Columns[6].Width = 200 * modBasic.$AppWidthRatio
    Else
      .Columns[5].Width = 275 * modBasic.$AppWidthRatio
    Endif

    .Columns[1].Text = "Gender"
    .Columns[2].Text = "AgeGroup"
    .Columns[3].Text = "Max"
    .Columns[4].Text = "Min"
    If $sType = "Test"
      .Columns[5].Text = "Unit"
      .Columns[6].Text = "Interpretation"
    Else
      .Columns[5].Text = "Interpretation"
    Endif
  End With

End

Private Function LabUnitForm() As String

  Dim xx As String

  If rbsi.Value = True Then
    xx = "SI"
  Else
    xx = "Metric"
  Endif
  Return xx

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If cmbagegroup.Text And If cmbgender.Text Then
    If $sType = "Exam" Then
      res = modDatabase.$medConn.Create("tblexamcomment")
      res["fldexamid"] = lbltestname.Text
      res["fldptsex"] = cmbgender.Text
      res["fldagegroup"] = cmbagegroup.Text
      res["fldmax"] = txtmax.Value
      res["fldmin"] = txtmin.Value
      res["fldcomment"] = txtcomm.Text
      res.Update
    Else If $sType = "Test" Then
      res = modDatabase.$medConn.Create("tbltestcomment")
      res["fldtestid"] = lbltestname.Text
      res["fldptsex"] = cmbgender.Text
      res["fldagegroup"] = cmbagegroup.Text
      res["fldmax"] = txtmax.Value
      res["fldmin"] = txtmin.Value
      res["fldcomment"] = txtcomm.Text
      res["fldunit"] = LabUnitForm()
      res.Update
    Else If $sType = "Radio" Then
      res = modDatabase.$medConn.Create("tblradiocomment")
      res["fldexamid"] = lbltestname.Text
      res["fldptsex"] = cmbgender.Text
      res["fldagegroup"] = cmbagegroup.Text
      res["fldmax"] = txtmax.Value
      res["fldmin"] = txtmin.Value
      res["fldcomment"] = txtcomm.Text
      res.Update
    Endif
    ShowExamGrid()
    BlankAll()
  Endif
  cmbagegroup.SetFocus

End

Private Sub BlankAll()

  cmbagegroup.Text = ""
  cmbgender.Text = ""
  txtcomm.Text = ""
  txtmax.Value = 0
  txtmin.Value = 0

End

Public Sub btnBrChange_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count > 0 Then
    If cmbagegroup.Text And If cmbgender.Text Then
      If $sType = "Exam" Then
        res = modDatabase.$medConn.Edit("tblexamcomment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
        res["fldptsex"] = cmbgender.Text
        res["fldagegroup"] = cmbagegroup.Text
        res["fldmax"] = txtmax.Value
        res["fldmin"] = txtmin.Value
        res["fldcomment"] = txtcomm.Text
        res.Update
      Else If $sType = "Test" Then
        res = modDatabase.$medConn.Edit("tbltestcomment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
        res["fldptsex"] = cmbgender.Text
        res["fldagegroup"] = cmbagegroup.Text
        res["fldmax"] = txtmax.Value
        res["fldmin"] = txtmin.Value
        res["fldcomment"] = txtcomm.Text
        res["fldunit"] = LabUnitForm()
        res.Update
      Else If $sType = "Radio" Then
        res = modDatabase.$medConn.Edit("tblradiocomment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
        res["fldptsex"] = cmbgender.Text
        res["fldagegroup"] = cmbagegroup.Text
        res["fldmax"] = txtmax.Value
        res["fldmin"] = txtmin.Value
        res["fldcomment"] = txtcomm.Text
        res.Update
      Endif
      ShowExamGrid()
    Endif
    cmbagegroup.SetFocus
  Endif

End

Public Sub btnBrCancel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If $sType = "Exam" Then
      modDatabase.$medConn.Delete("tblexamcomment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    Else If $sType = "Test" Then
      modDatabase.$medConn.Delete("tbltestcomment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    Else If $sType = "Radio" Then
      modDatabase.$medConn.Delete("tblradiocomment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    Endif
    ShowExamGrid()
    BlankAll()
  Endif

End

Public Sub GridView1_Click()

  If $sType = "Exam" Then
    cmbgender.Text = GridView1[GridView1.Row, 1].Text
    cmbagegroup.Text = GridView1[GridView1.Row, 2].Text
    txtmax.Value = GridView1[GridView1.Row, 3].Text
    txtmin.Value = GridView1[GridView1.Row, 4].Text
    txtcomm.Text = GridView1[GridView1.Row, 5].Text

  Else If $sType = "Test" Then
    cmbgender.Text = GridView1[GridView1.Row, 1].Text
    cmbagegroup.Text = GridView1[GridView1.Row, 2].Text
    txtmax.Value = GridView1[GridView1.Row, 3].Text
    txtmin.Value = GridView1[GridView1.Row, 4].Text
    txtcomm.Text = GridView1[GridView1.Row, 6].Text
    If GridView1[GridView1.Row, 5].Text = "SI" Then
      rbsi.Value = True
    Else If GridView1[GridView1.Row, 5].Text = "Metric" Then
      rbmetric.Value = True
    Endif

  Else If $sType = "Radio" Then
    cmbgender.Text = GridView1[GridView1.Row, 1].Text
    cmbagegroup.Text = GridView1[GridView1.Row, 2].Text
    txtmax.Value = GridView1[GridView1.Row, 3].Text
    txtmin.Value = GridView1[GridView1.Row, 4].Text
    txtcomm.Text = GridView1[GridView1.Row, 5].Text

  Endif

End
