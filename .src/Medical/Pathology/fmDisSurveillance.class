' Gambas class file

Private $sType As String
Private $tbl As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(sType As String)

  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  If $sType = "Surveillance" Then
    $tbl = "tblsurveillance"
    cmbgroup.List = modHMISRep.GetEWARSGroup()
  Else If $sType = "Credit Billing" Then
    $tbl = "tblbilldisease"
    cmbgroup.List = ["HI Chronic Disease", "SSF Critical Disease"]
  Endif

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnrefresh_Click()

  If cmbgroup.Text Then
    ShowGridData()
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If Not txtcode.Text Then
    txtcode.Text = "Other"
  Endif
  If txtsearch.Text And If txtcode.Text Then
    res = modDatabase.$medConn.Create($tbl)
    res["fldgroup"] = Trim(cmbgroup.Text)
    res["flddisease"] = Trim(txtsearch.Text)
    res["fldcodeid"] = Trim(txtcode.Text)
    res["fldcodenew"] = Trim(txtnewcode.Text)
    res.Update
    ShowGridData()
    txtcode.Text = ""
    txtsearch.Text = ""
    txtnewcode.Text = ""
    Balloon.Info(("Information saved"), btnadd)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count Then
    res = modDatabase.$medConn.Edit($tbl, "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldgroup"] = Trim(cmbgroup.Text)
    res["flddisease"] = Trim(txtsearch.Text)
    res["fldcodeid"] = Trim(txtcode.Text)
    res["fldcodenew"] = Trim(txtnewcode.Text)
    res.Update
    ShowGridData()
  Endif

End

Public Sub GridView1_Menu()

  mnufile.Popup

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$medConn.Delete($tbl, "fldid=&1", GridView1[GridView1.Row, 0].Text)
      ShowGridData()
      txtcode.Text = ""
      txtsearch.Text = ""
      Balloon.Info(("Information deleted"), btnedit)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Private Sub ShowGridData()

  $rData = modDatabase.$medConn.Exec(Subst("select fldid,flddisease,fldcodeid,fldcodenew from &1", $tbl) & " where fldgroup=&1", Trim(cmbgroup.Text))
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 50 * modBasic.$AppWidthRatio
    .Columns[1].Width = 350 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[0].Text = "SNo"
    .Columns[1].Text = "Disease"
    .Columns[2].Text = "Code"
    .Columns[3].Text = "Code"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnaddicd_Click()

  Dim xx As String[]

  xx = ICDGridView("Select Disease")
  If xx Then
    txtsearch.Text = xx[0]
    txtnewcode.Text = xx[1]
    txtcode.Text = xx[2]
  Endif

End
