' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Column")
  Me.Icon = Picture[modGeneralMain.$strLogo]
  cmbcategory.List = modMedicine.GetPathoCategoryList("Symptom")
  FillGridData()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub tlbtnadd_Click()

  Dim hForm As FmAddVariable1

  hform = New FmAddVariable1("flclass", "fldcategory", "Symptom", "tblpathocategory")
  hform.ShowModal
  cmbcategory.List = modMedicine.GetPathoCategoryList("Symptom")

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub txtsymptom_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Private Sub BlankAllControl()

  txtsymptom.Text = ""
  cmbcategory.Text = ""
  txtcode.Text = ""
  txtdetail.Text = ""

End

Public Sub txtsearch_Change()

  FillGridData()

End

Private Sub FillGridData()

  $rData = modDatabase.$medConn.Exec("select fldsymptom,fldcategory,fldsymdetail,fldcode from tblsymptoms where lower(fldsymptom) like &1", LCase(txtsearch.Text) & "%")   ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 400 * modBasic.$AppWidthRatio
    .Columns[1].Width = 1
    .Columns[2].Width = 1
    .Columns[3].Width = 1
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub GridView1_Click()

  txtsymptom.Text = GridView1[GridView1.Row, 0].Text
  cmbcategory.Text = GridView1[GridView1.Row, 1].Text
  txtdetail.Text = GridView1[GridView1.Row, 2].Text
  txtcode.Text = GridView1[GridView1.Row, 3].Text
  If txtsymptom.Text Then
    ShowSubSymptom()
  Endif

End

Public Sub GridView1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    GridView1_Click()
  Endif

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If txtsymptom.Text And If cmbcategory.Text Then                                                        ''
    res = modDatabase.$medConn.Create("tblsymptoms")
    res["fldcategory"] = cmbcategory.Text
    res["fldsymptom"] = Trim(txtsymptom.Text)
    res["fldcode"] = Trim(txtcode.Text)
    res["fldsymdetail"] = txtdetail.Text
    res.Update()
    FillGridData()
    Balloon.Info(("Information saved"), btnBrOK)
    Balloon.Delay = modBasic.$BalloonDelay
    BlankAllControl()
  Endif

End

Public Sub btnBrChange_Click()

  Dim res As Result
  Dim Row As Integer

  If txtsymptom.Text And If cmbcategory.Text Then
    Row = GridView1.Row
    res = modDatabase.$medConn.Edit("tblsymptoms", "fldsymptom=&1", Trim(txtsymptom.Text))
    res["fldsymptom"] = Trim(txtsymptom.Text)
    res["fldcode"] = Trim(txtcode.Text)
    res["fldsymdetail"] = txtdetail.Text
    res.Update()
    FillGridData()
    Balloon.Info(("Information updated"), btnBrChange)
    Balloon.Delay = modBasic.$BalloonDelay
    GridView1.Row = Row
  Endif

End

Public Sub btnBrCancel_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    modDatabase.$medConn.Delete("tblsymptoms", "fldsymptom=&1", Trim(txtsymptom.Text))
    modDatabase.$medConn.Delete("tblsubsymptoms", "fldsymptom=&1", Trim(txtsymptom.Text))
    FillGridData()
    Balloon.Delete(("Information deleted"), btnBrCancel)
    Balloon.Delay = modBasic.$BalloonDelay
    BlankAllControl()
    lstsubsympt.Clear
  Endif

End

''------------------------ Sub Symptoms -----------------------------
Private Sub ShowSubSymptom()

  Dim sql As String
  Dim res As Result

  sql = "select fldsubsymptom as col from tblsubsymptoms where fldsymptom=&1"
  res = modDatabase.$medConn.Exec(sql, Trim(txtsymptom.Text))
  modFillContainer.FillListBox(lstsubsympt, res, False)

End

Public Sub btnsubtype_Click()

  Dim res As Result

  If txtsubtype.Text Then
    res = modDatabase.$medConn.Create("tblsubsymptoms")
    res["fldsymptom"] = Trim(txtsymptom.Text)
    res["fldsubsymptom"] = Trim(txtsubtype.Text)
    res.Update()
    ShowSubSymptom()
    Balloon.Info(("Information saved"), btnsubtype)
    Balloon.Delay = modBasic.$BalloonDelay
    txtsubtype.Text = ""
  Endif

End

Public Sub btndelsubsymp_Click()

  modDatabase.$medConn.Delete("tblsubsymptoms", "fldsubsymptom=&1", lstsubsympt.Text)
  ShowSubSymptom()
  Balloon.Delete(("Information deleted"), btndelsubsymp)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub txtimport_Click()

  If Dialog.OpenFile() Then Return
  txtimport.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If Trim(txtdelim.Text) Then
    xdelim = Trim(txtdelim.Text)
  Else
    xdelim = ";"
  Endif

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$medConn, "tblsymptoms", txtimport.Text, xdelim)
    FillGridData()
    Balloon.Info(("Data Import completed"), btnupload)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
