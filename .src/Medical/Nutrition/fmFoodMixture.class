' Gambas class file

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbgroup.List = modControlSub.GetDirectFillresult(modDatabase.$medConn.Exec("select fldgroup as col from tblfoodmix"))
  cmbtype.List = modControlSub.GetDirectFillresult(modDatabase.$medConn.Exec("select distinct(fldfoodtype) as col from tblfoodtype"))
  cmbstatus.List = ["Active", "Inactive"]

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbstatus_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbstatus)
  modFillContainer.RestrictComboToContent(cmbstatus)

End

Public Sub btnaddmixture_Click()

  Dim hForm As FmAddVarTwo
  Dim yy As String[]

  yy = New String[]
  hForm = New FmAddVarTwo("fldgroup", "fldtitle", "tblfoodmix", cmbgroup.List, yy)
  hForm.ShowModal

End

Public Sub btnlist_Click()

  Dim xPath As String

  Inc Application.Busy
  xPath = modMedicalReport.ShowFoodGroupListHTML()
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End

Private Sub ShowGridData()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  Dim res As Result

  If cmbgroup.Text Then
    sql = "select fldid,flditemname,flditemamt,fldstatus from tblfoodgroup where fldgroup=&1 ORDER BY flditemname ASC"
    res = modDatabase.$medConn.Exec(sql, cmbgroup.Text)

    GridView1.Clear
    GridView1.Columns.Count = res.Fields.Count
    GridView1.Rows.Count = res.Count

    With GridView1
      .Rows.Height = modBasic.$AppGridRowHeight
      .Columns[0].Width = 1
      .Columns[1].Width = 350 * modBasic.$AppWidthRatio
      .Columns[2].Width = 125 * modBasic.$AppWidthRatio
      .Columns[3].Width = 75 * modBasic.$AppWidthRatio

      .Columns[1].Text = "Particulars"
      .Columns[2].Text = "Content(gm)"
      .Columns[3].Text = "Status"
    End With

    For Each res
      Column = 0
      For Each fld In res.Fields
        modGeneralMain.GridExplicitDecoration(GridView1, res.Index, Column)
        GridView1[res.Index, Column].Text = res[fld.Name]
        GridView1.Rows[res.Index].Height = Max(GridView1.Rows[res.Index].Height, GridView1[res.Index, Column].Font.RichTextHeight(GridView1[res.Index, Column].Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
        GridView1[res.Index, Column].WordWrap = True

        Column = Column + 1
      Next
    Next
    GridView1.Row = 0

  Endif

End

Public Sub btnrefresh_Click()

  ShowGridData()
  ' cmbtype.SetFocus

End

Public Sub btnrefresh_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnrefresh_Click()
  Endif

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$medConn.Delete("tblfoodgroup", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      ShowGridData()
    Endif
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub cmbtype_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtype)
  modFillContainer.RestrictComboToContent(cmbtype)

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub cmbtype_Click()

  If cmbtype.Text Then
    cmbitem.List = modMedicine.GetFoodSelectedType(cmbtype.Text)
  Endif

End

Public Sub cmbitem_GotFocus()

  Dim xLst As String[]

  If cmbtype.Text Then
    If Not cmbitem.Text Then
      If modBasic.$ItemListFormat = "GridView" Then
        xLst = modMedicine.GetFoodSelectedType(cmbtype.Text)
        If xLst.Count Then
          cmbitem.Text = GridViewNew("Select Particulars", xLst, False, cmbitem)
        Endif
      Endif
    Endif
  Else
    cmbtype.SetFocus
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If cmbtype.Text And If cmbitem.Text And If txtdose.Value Then
    res = modDatabase.$medConn.Create("tblfoodgroup")
    res["fldgroup"] = cmbgroup.Text
    res["flditemname"] = cmbitem.Text
    res["flditemamt"] = txtdose.Value
    res["fldprep"] = txtmethod.Text
    res["fldstatus"] = cmbstatus.Text
    res.Update
    ShowGridData()
    GetAllBlank()
    cmbtype.SetFocus
  Endif

End

Private Sub GetAllBlank()

  cmbtype.Text = ""
  cmbitem.Text = ""
  txtmethod.Text = ""
  txtdose.Value = 0
  cmbstatus.Text = ""

End

Public Sub btnadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnadd_Click()
  Endif

End
