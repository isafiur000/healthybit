' Gambas class file

Private $rReq As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

  cmbagegroup.List = modMedicine.AgeGroupListShortAll()
  cmbsex.List = ["Male", "Female", "Both Sex"]
  OpenDietRecord()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

'''------------------------------------------------------- REQUIREMENTS --------------------------------------------------------------
Public Sub btnreqOK_Click()

  Dim res As Result

  If cmbsex.Text And If cmbagegroup.Text Then
    res = modDatabase.$medConn.Create("tblnutrition")
    res["fldptsex"] = cmbsex.Text
    res["fldagegroup"] = cmbagegroup.Text
    res["fldfluid"] = txtreqfluid.Value
    res["fldprotein"] = txtreqprotein.Value
    res["fldlipid"] = txtreqlipid.Value
    res["flddextrose"] = txtreqdextrose.Value
    res["fldnne"] = txtreqnne.Value
    res["fldsodium"] = txtreqsodium.Value
    res["fldpotassium"] = txtreqpotas.Value
    res["fldvitamin"] = txtreqvitamin.Value
    res["fldreference"] = txtreference.Text
    res.Update
    Balloon.Info(("Information saved"), btnreqOK)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  cmbagegroup.SetFocus

End

Private Sub OpenDietRecord()

  Dim sql As String

  sql = "select fldptsex,fldagegroup,fldfluid,fldprotein,fldlipid,flddextrose,fldnne,fldsodium,fldpotassium,fldvitamin,fldreference from tblnutrition"
  $rReq = modDatabase.$medConn.Exec(sql)
  If $rReq.Available Then
    $rReq.MoveFirst
    ShowRequirements()
  Endif

End

Private Sub ShowRequirements()

  cmbsex.Text = $rReq["fldptsex"]
  cmbagegroup.Text = $rReq["fldagegroup"]
  txtreqfluid.Value = $rReq["fldfluid"]
  txtreqprotein.Value = $rReq["fldprotein"]
  txtreqlipid.Value = $rReq["fldlipid"]
  txtreqdextrose.Value = $rReq["flddextrose"]
  txtreqnne.Value = $rReq["fldnne"]
  txtreqsodium.Value = $rReq["fldsodium"]
  txtreqpotas.Value = $rReq["fldpotassium"]
  txtreqvitamin.Value = $rReq["fldvitamin"]
  txtreference.Text = $rReq["fldreference"]

End

Public Sub btnreqprev_Click()

  If $rReq.Index = 0 Then
    $rReq.MoveFirst
  Else
    $rReq.MovePrevious
  Endif
  ShowRequirements()

End

Public Sub btnreqnext_Click()

  If $rReq.Index = $rReq.Count - 1 Then
    $rReq.MoveLast
  Else
    $rReq.MoveNext
  Endif
  ShowRequirements()

End
