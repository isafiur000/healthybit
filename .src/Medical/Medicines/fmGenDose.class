' Gambas class file

Private $strCode As String
Private $rData As Result

Public Sub _new(strCode As String)

  $strCode = strCode
  txtcode.Text = $strCode

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbgender.List = ["Male", "Female", "Both Sex"]
  cmbagegroup.List = modMedicine.AgeGroupListShortAll()
  cmbMaxUnDoseUnit.List = modMedicine.$MedicineUnitList
  cmbMaxDalAdDoseUnit.List = modMedicine.$MedicineUnitList
  cmbMinDalAdDoseUnit.List = modMedicine.$MedicineUnitList

  SHowDoseGrid()

End

Public Sub cmbagegroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbagegroup)

End

Public Sub cmbgender_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbgender)
  modFillContainer.RestrictComboToContent(cmbgender)

End

Public Sub cmbMaxDalAdDoseUnit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbMaxDalAdDoseUnit)
  modFillContainer.RestrictComboToContent(cmbMaxDalAdDoseUnit)

End

Public Sub cmbMinDalAdDoseUnit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbMinDalAdDoseUnit)
  modFillContainer.RestrictComboToContent(cmbMinDalAdDoseUnit)

End

Public Sub cmbMaxUnDoseUnit_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbMaxUnDoseUnit)
  modFillContainer.RestrictComboToContent(cmbMaxUnDoseUnit)

End

Private Sub SHowDoseGrid()

  Dim Column As Integer
  Dim fld As ResultField

  $rData = modDatabase.$medConn.Exec("select fldid,fldptsex,fldagegroup,fldmaxfreq,fldmaxundose,fldmaxundoseunit,fldmaxdaladdose,fldmaxdaladdoseunit,fldmindaladdose,fldmindaladdoseunit from tblcodelimit where fldcodename=&1", txtcode.Text)                                                ''
  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 80 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 75 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 75 * modBasic.$AppWidthRatio
    .Columns[9].Width = 75 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Gender"
    .Columns[2].Text = "AgeGroup"
    .Columns[3].Text = "MaxFreq"
    .Columns[4].Text = "MaxUnitDose"
    .Columns[5].Text = "Unit"
    .Columns[6].Text = "MaxDailyDose"
    .Columns[7].Text = "Unit"
    .Columns[8].Text = "MinDailyDose"
    .Columns[9].Text = "Unit"
  End With

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If txtcode.Text And If cmbgender.Text And If cmbagegroup.Text Then
    res = modDatabase.$medConn.Create("tblcodelimit")
    res["fldcodename"] = txtcode.Text
    res["fldptsex"] = cmbgender.Text
    res["fldagegroup"] = cmbagegroup.Text
    res["fldmaxfreq"] = txtMaxFreq.Value
    res["fldmaxundose"] = txtMaxUnDose.Value
    res["fldmaxundoseunit"] = cmbMaxUnDoseUnit.Text
    res["fldmaxdaladdose"] = txtMaxDalAdDose.Value
    res["fldmaxdaladdoseunit"] = cmbMaxDalAdDoseUnit.Text
    res["fldmindaladdose"] = txtMinDalAdDose.Value
    res["fldmindaladdoseunit"] = cmbMinDalAdDoseUnit.Text
    If MMain.$IsDrugInfo = True Then
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldverifyid"] = ""
      res["fldverifytime"] = ""
      res["fldnote"] = ""
    Endif
    res.Update
    SHowDoseGrid()
    Balloon.Info(("Information updated"), btnBrOK)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnBrChange_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count > 0 Then
    If txtcode.Text And If cmbgender.Text And If cmbagegroup.Text Then
      res = modDatabase.$medConn.Edit("tblcodelimit", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      If res.Available Then
        res["fldptsex"] = cmbgender.Text
        res["fldagegroup"] = cmbagegroup.Text
        res["fldmaxfreq"] = txtMaxFreq.Value
        res["fldmaxundose"] = txtMaxUnDose.Value
        res["fldmaxundoseunit"] = cmbMaxUnDoseUnit.Text
        res["fldmaxdaladdose"] = txtMaxDalAdDose.Value
        res["fldmaxdaladdoseunit"] = cmbMaxDalAdDoseUnit.Text
        res["fldmindaladdose"] = txtMinDalAdDose.Value
        res["fldmindaladdoseunit"] = cmbMinDalAdDoseUnit.Text
        If MMain.$IsDrugInfo = True Then
          res["flduserid"] = modBasic.$lbluser
          res["fldtime"] = Now()
          res["fldcomp"] = modBasic.$compID
          res["fldverifyid"] = ""
          res["fldverifytime"] = ""
          res["fldnote"] = ""
        Endif
        res.Update
        SHowDoseGrid()
        Balloon.Info(("Information updated"), btnBrChange)
        Balloon.Delay = modBasic.$BalloonDelay
      Endif
    Endif
  Endif

End

Public Sub GridView1_Click()

  Dim res As Result

  res = modDatabase.$medConn.Exec("select fldcodename,fldptsex,fldagegroup,fldmaxfreq,fldmaxundose,fldmaxundoseunit,fldmaxdaladdose,fldmaxdaladdoseunit,fldmindaladdose,fldmindaladdoseunit from tblcodelimit where fldid=&1", GridView1[GridView1.Row, 0].Text)
  If res.Available Then
    cmbagegroup.Text = res["fldagegroup"]
    cmbgender.Text = res["fldptsex"]
    txtMaxFreq.Value = res["fldmaxfreq"]
    txtMaxUnDose.Value = res["fldmaxundose"]
    cmbMaxUnDoseUnit.Text = res["fldmaxundoseunit"]
    txtMaxDalAdDose.Value = res["fldmaxdaladdose"]
    cmbMaxDalAdDoseUnit.Text = res["fldmaxdaladdoseunit"]
    txtMinDalAdDose.Value = res["fldmindaladdose"]
    cmbMinDalAdDoseUnit.Text = res["fldmindaladdoseunit"]
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$medConn.Delete("tblcodelimit", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      SHowDoseGrid()
    Endif
  Endif

End
