' Gambas class file

Private $strCode As String
Private $sTitle As String
Private $mRes As Result

Public Sub _new(sTitle As String, strCode As String)

  $strCode = strCode
  $sTitle = sTitle

  Me.Title = $sTitle
  lblcode.Text = $strCode

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  If $sTitle = "Renal Impairment Precautions" Then
    lblcondition.Text = "Condition"
    cmbcondition.List = ["mildly impaired", "moderately impaired", "severely impaired"]
  Endif
  ShowOutput()
  ShowSubOutput()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbcondition_Click()

  txtgfrA.Value = modMedicine.KidneyCondiToGFRFirst(cmbcondition.Text)
  txtgfrB.Value = modMedicine.KidneyCondiToGFRLast(cmbcondition.Text)

End

'''
Private Sub ShowOutput()

  Dim sql As String

  sql = "Select medid,fldgfrfrom,fldgfrto,fldoutput,fldreference from tblmedrenal where fldcodename=&1"
  $mRes = modDatabase.$medConn.Exec(sql, lblcode.Text)

End

Private Sub ShowSubOutput()

  If $mRes.Available Then
    txtmedid.Value = $mRes!medid
    txtgfrA.Value = $mRes!fldgfrfrom
    txtgfrB.Value = $mRes!fldgfrto
    txtwarn.Text = $mRes!fldoutput
    txtreference.Text = $mRes!fldreference
    btnsubOK.Text = "&Add"
  Endif

End

Public Sub btnprev_Click()

  If $mRes.Index = 0 Then
    $mRes.MoveFirst
  Else
    $mRes.MovePrevious
  Endif
  ShowSubOutput()

End

Public Sub btnnext_Click()

  If $mRes.Index = $mRes.Count - 1 Then
    $mRes.MoveLast
  Else
    $mRes.MoveNext
  Endif
  ShowSubOutput()

End

Public Sub btnsubOK_Click()

  If btnsubOK.Text = "&Add" Then
    btnsubOK.Text = "&Save"
    txtwarn.Text = ""
    txtreference.Text = ""

  Else If btnsubOK.Text = "&Save" Then
    btnsubOK.Text = "&Add"
    $mRes = modDatabase.$medConn.Create("tblmedrenal")
    $mRes["fldcodename"] = lblcode.Text
    $mRes["fldgfrfrom"] = txtgfrA.Value
    $mRes["fldgfrto"] = txtgfrB.Value
    $mRes["fldoutput"] = Trim(txtwarn.Text)
    $mRes["fldreference"] = Trim(txtreference.Text)
    If MMain.$IsDrugInfo = True Then
      $mRes["flduserid"] = modBasic.$lbluser
      $mRes["fldtime"] = Now()
      $mRes["fldcomp"] = modBasic.$compID
      $mRes["fldverifyid"] = ""
      $mRes["fldverifytime"] = ""
      $mRes["fldnote"] = ""
    Endif
    $mRes.Update()
    ShowOutput()
    ShowSubOutput()
    Balloon.Info(("Information saved"), btnsubOK)
    Balloon.Delay = modBasic.$BalloonDelay

  Endif

End

Public Sub cmbcondition_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcondition)
  modFillContainer.RestrictComboToContent(cmbcondition)

End

Public Sub btnsubChange_Click()

  Dim res As Result

  If btnsubChange.Text = "&Edit" Then
    btnsubChange.Text = "&Update"

  Else If btnsubChange.Text = "&Update" Then
    btnsubChange.Text = "&Edit"
    res = modDatabase.$medConn.Edit("tblmedrenal", "medid=&1", txtmedid.Value)
    If res.Available Then
      res["fldgfrfrom"] = txtgfrA.Value
      res["fldgfrto"] = txtgfrB.Value
      res["fldoutput"] = Trim(txtwarn.Text)
      res["fldreference"] = Trim(txtreference.Text)
      If MMain.$IsDrugInfo = True Then
        res["flduserid"] = modBasic.$lbluser
        res["fldtime"] = Now()
        res["fldcomp"] = modBasic.$compID
        res["fldverifyid"] = ""
        res["fldverifytime"] = ""
        res["fldnote"] = ""
      Endif
      res.Update()
      Balloon.Info(("Information updated"), btnsubChange)
      Balloon.Delay = modBasic.$BalloonDelay
      ShowOutput()
      ShowSubOutput()
    Endif

  Endif

End

Public Sub btnsubCancel_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    modDatabase.$medConn.Delete("tblmedrenal", "medid=&1", txtmedid.Value)
    txtreference.Text = ""
    txtwarn.Text = ""
    ShowOutput()
    ShowSubOutput()
    Balloon.Delete(("Information deleted"), btnsubCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
