' Gambas class file

Private $sCategory As String
Private $sType As String

Private $tblpatbilling As String
Private $tblpatbilldetail As String
Private $tbltempbilldetail As String

Public Sub _new(sCategory As String, sType As String, sLocaType As String, sLocation As String)

  $sCategory = sCategory
  $sType = sType
  cmbsearch.Text = sLocaType
  txtselect.Text = sLocation

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If $sCategory = "Credit Sales" Then
    Panel6.Visible = True
    cmbpatgroup.List = ["Social Service", "Employee", "All Patients"]
    cmbpatgroup.Text = "All Patients"
  Endif
  lblitem.Text = $sCategory & " : " & $sType
  cmbfiscal.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldlabel) as col from tblfiscalid"))
  cmbledger.List = modControlSub.GetDirectFillresult(modDatabase.$syConn.Exec("select fldacledger as col from tblacledger"))
  cmbledger.Add("%")
  cmbledger.Text = "%"

  LoadTableNames()
  dtfir.Value = Now()
  dtlast.Value = Now()
  dtfir_Change()
  dtlast_Change()

End

Private Sub LoadTableNames()

  Dim res As Result

  If cmbfiscal.Text = "Current" Then
    $tblpatbilling = "tblpatbilling"
    $tblpatbilldetail = "tblpatbilldetail"
    $tbltempbilldetail = "tbltempbilldetail"
  Else
    res = modDatabase.$syConn.Exec("select fldpatbilling,fldpatbilldetail,fldtempbilldetail from tblfisclosing where fldindex=&1 and (fldstate=&2 or fldstate IS NULL)", cmbfiscal.Text, "Active")
    If res.Available Then
      If res["fldpatbilling"] Then
        $tblpatbilling = res["fldpatbilling"]
      Else
        $tblpatbilling = "tblpatbilling"
      Endif
      If res["fldpatbilldetail"] Then
        $tblpatbilldetail = res["fldpatbilldetail"]
      Else
        $tblpatbilldetail = "tblpatbilldetail"
      Endif
      If res["fldtempbilldetail"] Then
        $tbltempbilldetail = res["fldtempbilldetail"]
      Else
        $tbltempbilldetail = "tbltempbilldetail"
      Endif
    Else
      $tblpatbilling = "tblpatbilling"
      $tblpatbilldetail = "tblpatbilldetail"
      $tbltempbilldetail = "tbltempbilldetail"
    Endif
  Endif

End

Public Sub cmbfiscal_Click()

  Dim res As Result

  LoadTableNames()
  If cmbfiscal.Text Then
    res = modDatabase.$myConn.Exec("select fldfrom,fldend from tblfiscalid where fldlabel=&1", cmbfiscal.Text)
    If res.Available Then
      res.MoveFirst
      dtfir.Value = res["fldfrom"]
      dtlast.Value = res["fldend"]
      If dtfir.Value Then
        dtfir_Change()
      Endif
      If dtlast.Value Then
        dtlast_Change()
      Endif
    Endif
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub dtfir_Change()

  txtnepfir.Text = modDate.ConvertToLocaldate(dtfir.Value)

End

Public Sub dtlast_Change()

  txtneplast.Text = modDate.ConvertToLocaldate(dtlast.Value)

End

Public Sub txtnepfir_LostFocus()

  dtfir.Value = modDate.ConvertToEnglishdate(txtnepfir.Text)

End

Public Sub txtneplast_LostFocus()

  dtlast.Value = modDate.ConvertToEnglishdate(txtneplast.Text)

End

Public Sub btnclose_Click()

  Me.Close

End

Public Sub btnsave_Click()

  modMisc.GetDateChooserRangeSetting(dtfir, dtlast)

  If cmbledger.Text Then
    GenerateReport()
  Endif

End

Private Sub GenerateReport()

  Dim xPath As String

  Inc Application.Busy
  If $sCategory = "Credit Sales" Then
    If $sType = "Invoice" Then
      xPath = modBillingExtra.CreditSalesWithDetailDue("Invoice", "Date", modDatabase.$syConn, "Credit", dtfir.Value, dtlast.Value, $tblpatbilldetail, cmbledger.Text, cmbpatgroup.Text, cmbsearch.Text, txtselect.Text)
    Else If $sType = "Receipt" Then
      xPath = modBillingExtra.CreditSalesWithDetailDue("Receipt", "Date", modDatabase.$syConn, "Credit", dtfir.Value, dtlast.Value, $tbltempbilldetail, cmbledger.Text, cmbpatgroup.Text, cmbsearch.Text, txtselect.Text)
    Endif

  Else If $sCategory = "Due Clearance" Then
    If $sType = "Invoice" Then
      xPath = modBillingExtra.CreditSalesDueCelared("Invoice", "Date", modDatabase.$syConn, dtfir.Value, dtlast.Value, $tblpatbilldetail, cmbledger.Text, cmbsearch.Text, txtselect.Text)
    Else If $sType = "Receipt" Then
      xPath = modBillingExtra.CreditSalesDueCelared("Receipt", "Date", modDatabase.$syConn, dtfir.Value, dtlast.Value, $tbltempbilldetail, cmbledger.Text, cmbsearch.Text, txtselect.Text)
    Endif

  Else If $sCategory = "Discount" Then
    If $sType = "Invoice" Then
      xPath = modBillingExtra.TaxDiscReportFinance("Invoice", "Date", modDatabase.$syConn, "Discount", dtfir.Value, dtlast.Value, $tblpatbilldetail, cmbledger.Text, cmbsearch.Text, txtselect.Text)
    Else If $sType = "Receipt" Then
      xPath = modBillingExtra.TaxDiscReportFinance("Receipt", "Date", modDatabase.$syConn, "Discount", dtfir.Value, dtlast.Value, $tbltempbilldetail, cmbledger.Text, cmbsearch.Text, txtselect.Text)
    Endif
  Endif
  Dec Application.Busy

  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End
