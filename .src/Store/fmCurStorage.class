' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $strType As String

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  lblcomp.Text = modBasic.$compID
  rbmed.Value = True
  $strType = "Medicines"
  ShowLocation()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub rbmed_Click()

  $strType = "Medicines"

End

Public Sub rbsurg_Click()

  $strType = "Surgicals"

End

Public Sub rbextra_Click()

  $strType = "Extra Items"

End

Public Sub btnrefresh_Click()

  ShowLocation()

End

Private Sub ShowLocation()

  $rData = modDatabase.$myConn.Exec("select fldstockno,fldstockid,fldbatch,fldcode from tblentry where fldcomp=&1 and fldcategory=&2 ORDER BY fldstockid", modBasic.$compID, $strType)                                                       ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 300 * modBasic.$AppWidthRatio
    .Columns[2].Width = 150 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio

    .Columns[0].Text = "StockID"
    .Columns[1].Text = "Particulars"
    .Columns[2].Text = "Batch"
    .Columns[3].Text = "Code"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Click()

  txtid.Value = GridView1[GridView1.Row, 0].Text
  txtitem.Text = GridView1[GridView1.Row, 1].Text
  txtcode.Text = GridView1[GridView1.Row, 3].Text

End

Public Sub btnedit_Click()

  Dim res As Result

  If txtcode.Text And If txtid.Value Then
    res = modDatabase.$myConn.Edit("tblentry", "fldstockno=&1", txtid.Value)
    res["fldcode"] = txtcode.Text
    res["xyz"] = False
    res.Update
    Balloon.Info(("Information saved"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
    ShowLocation()
  Endif

End
