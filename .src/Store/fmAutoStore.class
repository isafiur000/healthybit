' Gambas class file

Private $sCompId As String
Private $colmName As String
Private $sWaitDay As Integer
Private $dt1 As Date
Private $dt2 As Date

Private $xVar As Variant[]
Private sTime As Date

Public Sub _new(sCompID As String, sColumnName As String, sDays As Integer)

  $sCompId = sCompID
  $colmName = sColumnName
  $sWaitDay = sDays

End

Public Sub Form_Open()

  ' modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Vertical")
  ' Me.Icon = Picture[modGeneralMain.$strLogo]
  cmbcomtaget.List = modBasic.$AllCompPerList
  $dt2 = modDate.StartSqlDate(Now())
  $dt1 = DateAdd($dt2, gb.Day, 0 - $sWaitDay)
  lblfirst.Text = $sCompId & " : Based on Sales from " & modReportVar.GetDateTimeReport($dt1, gb.MediumDate) & " TO " & modReportVar.GetDateTimeReport($dt2, gb.MediumDate)

  $xVar = New Variant[]
  sTime = Now()
  Timer1.Enabled = True

End

Public Sub Timer1_Timer()

  If DateDiff(sTime, Now(), gb.Second) > 1 Then
    Timer1.Enabled = False
    If $sCompId And If $sWaitDay Then
      ShowStoreRequestMedSurg($sCompId, $dt1, $dt2)
    Endif
  Endif

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Private Sub ShowStoreRequestMedSurg(CompId As String, dt1 As Date, dt2 As Date)

  Dim sql As String
  Dim res As Result
  Dim aColl As Collection

  Dim xsales As Float
  Dim xcurr As Float
  Dim xnew As Float

  sql = "select flditem,SUM(fldqtydisp-fldqtyret) as qty from tblpatdosing where fldtime>=&1 and fldtime<=&2 and fldsave_order=&3 and " & $colmName & " like &4 GROUP BY flditem"
  res = modDatabase.$syConn.Exec(sql, dt1, dt2, True, compid)

  modAppSupport.FindWorkProgressBar(modHelpVariable.$LogInCategory).Value = 0
  For Each res
    xsales = 0
    xcurr = 0
    xnew = 0
    xsales = res["qty"]
    xcurr = modStock.TotalQTYbyBrand(res["flditem"], compid)
    xnew = xsales - xcurr
    If xnew > 0 Then
      aColl = New Collection
      aColl.Add(res["flditem"], CStr(0))
      aColl.Add(xsales, CStr(1))
      aColl.Add(xcurr, CStr(2))
      aColl.Add(xnew, CStr(3))
      $xVar.Add(aColl)
    Endif
    modAppSupport.FindWorkProgressBar(modHelpVariable.$LogInCategory).Value = (res.Index + 1) / res.Count
    Wait
  Next

  GridView1.Clear
  GridView1.Columns.Count = 5
  GridView1.Rows.Count = $xVar.Count
  GridView1.Rows.Height = modBasic.$AppGridRowHeight

  With GridView1
    .Columns[0].Width = 450 * modBasic.$AppWidthRatio
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 25 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Particulars"
    .Columns[1].Text = "SalesQTY"
    .Columns[2].Text = "CurrQTY"
    .Columns[3].Text = "Request"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 4 Then
    GridView1.Data.Picture = Picture["icons/unchecked.png"]
  Else
    GridView1.Data.Text = $xVar[Row][CStr(Column)]
  Endif

End

Public Sub GridView1_Click()

  If GridView1.Column = 4 Then
    modGridView.CheckUncheckGridNoDB(GridView1, 4)
  Endif

End

Public Sub chkseltop_Click()

  Dim Row As Integer

  For Row = 0 To GridView1.Rows.Count - 1
    If chkseltop.Value = True Then
      GridView1[Row, 4].Picture = Picture["icons/checked.png"]
    Else If chkseltop.Value = False Then
      GridView1[Row, 4].Picture = Picture["icons/unchecked.png"]
    Endif
  Next

End

Public Sub btnadd_Click()

  Dim Row As Integer

  If cmbcomtaget.Text Then
    Inc Application.Busy
    For Row = 0 To GridView1.Rows.Count - 1
      If GridView1[Row, 4].Picture = Picture["icons/checked.png"] Then
        AddRequest(Row, $sCompId, cmbcomtaget.Text)
        GridView1[Row, 4].Picture = Picture["icons/unchecked.png"]
      Endif
    Next
    Dec Application.Busy
  Endif

End

Private Sub AddRequest(Row As Integer, sRequest As String, sTarget As String)

  Dim res As Result

  res = modDatabase.$myConn.Create("tblrequest")
  res["fldstockid"] = GridView1[Row, 0].Text
  res["fldcategory"] = modStock.GetPharmCategoryFromStockName(GridView1[Row, 0].Text, sRequest)
  res["fldqty"] = GridView1[Row, 3].Text
  res["fldstatus"] = "Requested"
  res["fldfinalqty"] = GridView1[Row, 3].Text
  res["fldurgent"] = "Normal"
  res["fldexpdate"] = DateAdd(Now(), gb.Day, 1)
  res["fldreference"] = ""
  res["fldremarks"] = ""

  res["fldorduserid"] = modBasic.$lbluser
  res["fldordtime"] = Now()
  res["fldordcomp"] = sRequest

  res["flduserid"] = ""
  res["fldtime"] = ""
  res["fldcomp"] = sTarget
  res["fldsave"] = False
  res.Update

End
