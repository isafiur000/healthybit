' Gambas class file

Private $dosno As Long

Public Sub _new(dosno As Long)

  $dosno = dosno

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbfreq.List = modMedicine.FrequencyCombo()
  ShowDoseInfo($dosno)
  TaperingGrid()
  txtdose.SetFocus

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Private Sub ShowDoseInfo(dosno As Long)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldroute,flditem,fldencounterval from tblpatdosing where fldid=&1", dosno)
  txtencid.Text = res!fldencounterval
  txtroute.Text = res!fldroute
  txtmedicine.Text = res!flditem

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tbltaperdose", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    TaperingGrid()
  Endif

End

Private Sub TaperingGrid()

  Dim $rData As Result
  Dim Column As Integer
  Dim fld As ResultField

  $rData = modDatabase.$myConn.Exec("select fldid,flddose,fldfreq,flddays from tbltaperdose where flddoseno=&1", $dosno)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 175 * modBasic.$AppWidthRatio
    .Columns[3].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Unit Dose"
    .Columns[2].Text = "Frequency"
    .Columns[3].Text = "Duration"
  End With

End

Private Sub ShowBlank()

  txtdose.Value = 0
  cmbfreq.Text = ""
  txtday.Value = 0

End

Public Sub btnadd_Click()

  Dim res As Result

  If $dosno And If txtdose.Value And If cmbfreq.Text And If txtday.Value Then
    res = modDatabase.$myConn.Create("tbltaperdose")
    res["flddoseno"] = $dosno
    res["flddose"] = txtdose.Value
    res["fldfreq"] = cmbfreq.Text
    res["flddays"] = txtday.Value
    res.Update
    TaperingGrid()
    ShowBlank()
    txtdose.SetFocus
  Endif

End

Public Sub btnadd_GotFocus()

  btnadd_Click()

End

Public Sub cmbfreq_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbfreq)
  modFillContainer.RestrictComboToContent(cmbfreq)

End

Public Sub txtdose_GotFocus()

  txtdose.SelectAll

End

Public Sub txtday_GotFocus()

  txtday.SelectAll

End
