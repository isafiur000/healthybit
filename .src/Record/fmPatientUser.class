' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbusertype.List = ["Encounter", "PatientID"]
  cmbstatus.List = ["Active", "Inactive"]
  MakeAllTextBlank()
  GetUserList()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Private Sub MakeAllTextBlank()

  txtuserid.Text = ""
  txtfullname.Text = ""
  txtgender.Text = ""
  txtcontact.Text = ""
  txtpassword.Text = ""
  cmbstatus.Text = ""
  cmbusertype.Text = ""
  dtfrom.Value = Date()
  dtto.Value = Date()
  chkpass.Enabled = True

End

Public Sub GetUserList()

  $rData = modDatabase.$myConn.Exec("select fldpatientval,fldstatus,fldpatientval from tblpatientpass where lower(fldpatientval) like &1 ORDER BY fldpatientval ASC", LCase(txtsearch.Text) & "%")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 100 * modBasic.$AppWidthRatio
    .Columns[1].Width = 25 * modBasic.$AppWidthRatio
    .Columns[2].Width = 200 * modBasic.$AppWidthRatio
    .Columns[0].Text = "UserID"
    .Columns[2].Text = "User Name"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 1 Then
    GridView1.Data.Picture = Picture[modMisc.GetGridIcon(GetStatusBool($rData[$aMyFields[Column]]))]
    GridView1.Data.Text = ""
  Else If Column = 2 Then
    GridView1.Data.Text = modPatient.PatientFullNameByPatID($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Function GetStatusBool(sVal As String) As Boolean

  Dim xx As Boolean

  If sVal = "Active" Then
    xx = False
  Else
    xx = True
  Endif
  Return xx

End

Public Sub txtsearch_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
  Else
    GetUserList()
  Endif

End

Public Sub GridView1_Click()

  Dim res As Result
  Dim sql As String

  If GridView1.Rows.Count > 0 Then
    chkpass.Enabled = False
    txtpassword.Password = True
    sql = "select fldpatientval,fldpass,fldfromdate,fldtodate,fldstatus,fldusercode from tblpatientpass where fldpatientval=&1"                        ''
    res = modDatabase.$myConn.Exec(sql, GridView1[GridView1.Row, 0].Text)
    txtuserid.Text = res["fldpatientval"]
    txtpassword.Text = modPassword.DecryptSimple(res["fldpass"])
    dtfrom.Value = res["fldfromdate"]
    dtto.Value = res["fldtodate"]
    cmbusertype.Text = res["fldusercode"]
    cmbstatus.Text = res["fldstatus"]
    If cmbusertype.Text = "Encounter" Then
      txtfullname.Text = modPatient.GetPatientNameByEnc(Trim(txtuserid.Text))
      txtgender.Text = modPatient.GetPatientSex(Trim(txtuserid.Text))
      txtcontact.Text = modPatient.GetPatientContactByEnc(Trim(txtuserid.Text))
    Else
      txtfullname.Text = modPatient.PatientFullNameByPatID(Trim(txtuserid.Text))
      txtgender.Text = modPatient.PatientGenderByPatNo(Trim(txtuserid.Text))
      txtcontact.Text = modPatient.GetPatientContactByPatNo(Trim(txtuserid.Text))
    Endif

    btnOK.Text = "&Add"
    btnChange.Text = "&Edit"
  Endif

End

Public Sub chkpass_Click()

  If chkpass.Value = True Then
    txtpassword.Password = False
  Else If chkpass.Value = False
    txtpassword.Password = True
  Endif

End

Public Sub btnshow_Click()

  If txtuserid.Text Then
    If cmbusertype.Text = "Encounter" Then
      txtfullname.Text = modPatient.GetPatientNameByEnc(Trim(txtuserid.Text))
      txtgender.Text = modPatient.GetPatientSex(Trim(txtuserid.Text))
      txtcontact.Text = modPatient.GetPatientContactByEnc(Trim(txtuserid.Text))
    Else
      txtfullname.Text = modPatient.PatientFullNameByPatID(Trim(txtuserid.Text))
      txtgender.Text = modPatient.PatientGenderByPatNo(Trim(txtuserid.Text))
      txtcontact.Text = modPatient.GetPatientContactByPatNo(Trim(txtuserid.Text))
    Endif
  Endif

End

Public Sub btnOK_Click()

  If btnOK.Text = "&Add" Then
    btnOK.Text = "&Save"
    MakeAllTextBlank()
    txtuserid.SetFocus

  Else If btnOK.Text = "&Save" Then
    chkpass.Enabled = False
    txtpassword.Password = True
    If txtuserid.Text And If txtfullname.Text Then
      modGeneral.AddPatientUserAccount(Trim(txtuserid.Text), Trim(txtpassword.Text), dtfrom.Value, dtto.Value, Trim(cmbusertype.Text), cmbstatus.Text)
      btnOK.Text = "&Add"
      GetUserList()
      Balloon.Info(("Information added"), btnOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnChange_Click()

  Dim res As Result
  Dim Row As Integer

  Row = GridView1.Row
  If btnChange.Text = "&Edit" Then
    btnChange.Text = "&Update"
    txtuserid.Enabled = False

  Else If btnChange.Text = "&Update" Then
    If txtuserid.Text And If txtfullname.Text Then
      res = modDatabase.$myConn.Edit("tblpatientpass", "fldpatientval=&1", txtuserid.Text)
      res["fldpass"] = modPassword.EncryptSimple(Trim(txtpassword.Text))
      res["fldfromdate"] = dtfrom.Value
      res["fldtodate"] = dtto.Value
      res["fldusercode"] = Trim(cmbusertype.Text)
      res["fldstatus"] = cmbstatus.Text
      res["xyz"] = False
      res.Update()
      btnChange.Text = "&Edit"
      txtuserid.Enabled = True
      GetUserList()
      Balloon.Info(("Information updated"), btnChange)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif
  GridView1.Row = Row

End

Public Sub btnCancel_Click()

  If Message.Question(("Do you want to delete this user?"), ("No"), ("Yes")) = 2 Then
    If txtuserid.Text Then
      modDatabase.$myConn.Delete("tblpatientpass", "fldpatientval=&1", txtuserid.Text)
      MakeAllTextBlank()
      GetUserList()
      Balloon.Info(("Information deleted"), btnCancel)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub txtsearch_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtuserid_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnrandpass_Click()

  txtpassword.Text = modPassword.GetRandomPassword()

End

Public Sub btnpasssms_Click()

  If txtpassword.Text And If txtcontact.Text Then
    If modBasic.$SMSQueDisable = "No" Then
      modAppSupport.SaveSMSEntry(Trim(txtcontact.Text), txtpassword.Text, "Waiting", "")
      Message.Info("SMS Qued", ("OK"))
    Else
      modAppSupport.SendSMSSingle(Trim(txtcontact.Text), txtpassword.Text)
    Endif
  Endif

End
