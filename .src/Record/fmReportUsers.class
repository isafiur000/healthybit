' Gambas class file

Private $txtuser As String

Public Sub _new(txtuser As String)

  $txtuser = txtuser
  ShowProfile()

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub txtnameimage_Click()

  Dialog.Filter = ["*.jpg;*.jpeg;*.png;*.bmp", "Picture files"]
  If Dialog.OpenFile() Then Return
  txtnameimage.Text = Dialog.Path
  modImage.StretchtPictureToBox(pictimage, Dialog.Path)

End

Private Sub ShowProfile()

  Dim res As Result
  Dim tempFile As String
  Dim hFile As Blob

  tempFile = Temp()
  res = modDatabase.$myConn.Exec("select fldname,fldnamefont,fldsigimage,fldtitle,flddetail,flddefault from tblreportuser where flduserid=&1", $txtuser)
  If res.Available = True Then

    txtname.Text = res["fldname"]
    txtnamefont.Value = res["fldnamefont"]
    hFile = res["fldsigimage"]
    If hFile.Length Then
      File.Save(tempFile, hFile.Data)
      modImage.StretchtPictureToBox(pictimage, tempFile)
    Endif

    txttitle.Text = res["fldtitle"]
    txtprofile.Text = res["flddetail"]
    If res["flddefault"] = "Text" Then
      rbtext.Value = True
    Else If res["flddefault"] = "Image" Then
      rbimage.Value = True
    Endif

  Endif

End

Public Sub btnfoot1_Click()

  Dim rs As Result
  Dim res As Result
  Dim sType As String

  If rbtext.Value = True Then
    sType = "Text"
  Else If rbimage.Value = True Then
    sType = "Image"
  Endif

  rs = modDatabase.$myConn.Exec("select fldname from tblreportuser where flduserid=&1", $txtuser)
  If rs.Available = True Then
    res = modDatabase.$myConn.Edit("tblreportuser", "flduserid=&1", $txtuser)
  Else If rs.Available = False Then
    res = modDatabase.$myConn.Create("tblreportuser")
    res["flduserid"] = $txtuser
  Endif
  res["fldname"] = txtname.Text
  res["fldnamefont"] = txtnamefont.Value
  If txtnameimage.Text Then
    res["fldsigimage"] = File.Load(txtnameimage.Text)
  Endif
  res["fldtitle"] = txttitle.Text
  res["flddetail"] = txtprofile.Text
  res["flddefault"] = sType
  res.Update
  Balloon.Info(("Information saved"), btnfoot1)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnresize_Click()

  fmResizeImage.Close
  fmResizeImage.ShowModal

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End
