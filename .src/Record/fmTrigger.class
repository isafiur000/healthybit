' Gambas class file

Private $rData As Result
Private $TableList As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  $TableList = modDBApp.FillDBTables(modDatabase.$myConn, False)
  ListBox1.List = $TableList

  cmbtime.List = ["BEFORE", "AFTER"]
  cmbevent.List = ["DELETE", "UPDATE", "INSERT"]
  cmbeffect.List = ["Log Record", "Raise Error"]
  ' If modHelpVariable.$IRDLevel = "Yes" Then
  '   mnudel.Visible = False
  ' Else
  '   mnudel.Visible = True
  ' Endif
  FillTriggerGrid()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub txtsearch_Change()

  ListBox1.List = modString.SelectStringArrayToText(txtsearch.Text, $TableList)

End

Private Sub CreateTriggerSel(sName As String, sTable As String, sTime As String, sEvent As String)

  If modDatabase.$myConn.Type = "mysql" Then
    If cmbeffect.Text = "Log Record" Then
      modDBApp.CreateTrigger(modDatabase.$myConn, sName, sTable, sTime, sEvent)
    Else
      modDatabase.$myConn.MySQL.Trigger.Add(sName, sTable, sTime, sEvent, "SIGNAL SQLSTATE '45000' SET MESSAGE_TEXT = 'Custom Error'")
    Endif
  Endif

End

Public Sub btncreate_Click()

  If txtname.Text And If ListBox1.Text And If cmbtime.Text And If cmbevent.Text Then
    CreateTriggerSel(txtname.Text, ListBox1.Text, cmbtime.Text, cmbevent.Text)
    FillTriggerGrid()
    Balloon.Info(("Trigger Added"), btncreate)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub FillTriggerGrid()

  If modHelpVariable.$SysMainService = "mysql" Then
    MysqlTriggerList()
  Endif

End

Private Sub MysqlTriggerList()

  Dim Column As Integer
  Dim fld As ResultField

  $rData = modDatabase.$myConn.Exec("select TRIGGER_NAME, ACTION_TIMING,EVENT_MANIPULATION,ACTION_STATEMENT from information_schema.triggers where trigger_schema=&1", modDatabase.$myConn.Name)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      GridView1[$rData.Index, 3].WordWrap = True
      GridView1[$rData.Index, 3].Alignment = Align.TopLeft
      Column = Column + 1
    Next
  Next

  With GridView1
    .Rows.Height = 4 * modBasic.$AppGridRowHeight
    .Columns[0].Width = 100 * modBasic.$AppWidthRatio
    .Columns[1].Width = 75 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 500 * modBasic.$AppWidthRatio
    .Columns[0].Text = "Name"
    .Columns[1].Text = "Timing"
    .Columns[2].Text = "Event"
    .Columns[3].Text = "Trigger"
  End With

End

Public Sub GridView1_Menu()

  mnugrid.Popup

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If InStr(GridView1[GridView1.Row, 0].Text, "tblpatbilldetail") > 0 Then
      Message.Warning(("Deletion Not Permitted"), ("OK"))
    Else
      modDatabase.$myConn.MySQL.Trigger.Delete(GridView1[GridView1.Row, 0].Text, False, modDatabase.$myConn.Name)
      MysqlTriggerList()
    Endif
  Endif

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End
