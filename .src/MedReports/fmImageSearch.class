' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $SSQLFields As String[]
Private $ProgressBar1 As ProgressBar
Private $RepoStr As String

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Vertical")
  Me.Icon = Picture[modGeneralMain.$strLogo]
  btnmnuqty.Menu = "mnuqty"
  If MMain.$WebReport = "Multiple" Then
    If modBasic.$HospCode Then
      ' cmblocation.Text = "Hospital"
      cmblocation.Text = modDataRepo.$RepositoryMode
      cmbvalue.Text = modBasic.$HospCode
      pnlocat.Enabled = False
    Else
      cmblocation.List = ["Hospital", "Municipality", "Category", "District", "Province"]
    Endif

  Else
    pnlocat.Visible = False
  Endif

  dtfir.Value = Now()
  dtlast.Value = Now()
  txtsearch.Text = "%"
  If MMain.$WebReport = "Multiple" Then
    $SSQLFields = ["fldrepoid", "fldencounterval", "fldencounterval", "fldencounterval", "fldtitle", "flddetail", "fldtime", "fldcateg"]
  Else
    $SSQLFields = ["fldid", "fldencounterval", "fldencounterval", "fldencounterval", "fldtitle", "flddetail", "fldtime", "fldcateg"]
  Endif

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtfir.Value))
  If xx Then
    dtfir.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtneplast_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlast.Value))
  If xx Then
    dtlast.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Private Function ExecuteQuery(sType As String) As Result

  Dim xFldList1 As String[]
  Dim xFldList2 As String[]
  Dim xType As String
  Dim sql As String
  Dim res As Result
  Dim xhospfld As String

  xhospfld = modDataRepo.HospitalField()
  xFldList1 = $SSQLFields.Copy()
  xFldList1.Add(Quote("tblpatimagedata"))
  If xhospfld Then
    xFldList1.Add(xhospfld)
  Endif
  xFldList2 = $SSQLFields.Copy()
  xFldList2.Add(Quote("tblpatserialimage"))
  If xhospfld Then
    xFldList2.Add(xhospfld)
  Endif

  If sType = "IMAGE+DICOM" Then
    xType = "%"
  Else
    xType = sType
  Endif

  $RepoStr = modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
  sql = "select " & xFldList1.Join(",") & " from tblpatimagedata where fldtime>=&1 and fldtime<=&2 and fldkeyword like &3 and fldcateg like &4" & $RepoStr & " UNION ALL select " & xFldList2.Join(",") & " from tblpatserialimage where fldtime>=&1 and fldtime<=&2 and fldkeyword like &3 and fldcateg like &4" & $RepoStr                                                                        ''
  res = modDatabase.$syConn.Exec(sql, modDate.StartSqlDate(dtfir.Value), modDate.EndSqlDate(dtlast.Value), txtsearch.Text, xType)

  Return res

End

Private Function GetGridViewValue(Column As Integer, xVariable As Variant) As Variant

  Dim xxx As Variant

  If Column = 2 Then
    xxx = modPatient.GetPatientNameByEnc(xVariable, modDatabase.$syConn)
  Else If Column = 3 Then
    xxx = modPatient.GetPatientSex(xVariable, modDatabase.$syConn)
  Else If Column = 6 Then
    xxx = modReportVar.GetDateTimeReport(xVariable, gb.GeneralDate)
  Else If Column = 9 Then
    xxx = modDataRepo.GetHospitalTextLabel(xVariable)
  Else
    xxx = xVariable
  Endif

  Return xxx

End

Public Sub btnrefresh_Click()

  TabStrip1.Index = 0
  modMisc.GetDateRangeSetting(dtfir, dtlast)

  Inc Application.Busy
  If chkimage.Value Or If chkdicom.Value Then
    If chkimage.Value And If chkdicom.Value Then
      $rData = ExecuteQuery("IMAGE+DICOM")
    Else If chkimage.Value Then
      $rData = ExecuteQuery("IMAGE")
    Else If chkdicom.Value Then
      $rData = ExecuteQuery("DICOM")
    Endif

    If chkgrid.Value = True Then
      FillPatGrid()
    Endif

    If chkchart.Value = True Then
      FillItemChartQTY()
    Endif

  Endif
  Dec Application.Busy

End

Public Sub FillPatGrid()

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 175 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio
    .Columns[5].Width = 250 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio
    .Columns[7].Width = 1
    .Columns[8].Width = 1

    .Columns[1].Text = "EncID"
    .Columns[2].Text = "Name"
    .Columns[3].Text = "Gender"
    .Columns[4].Text = "Title"
    .Columns[5].Text = "Detail"
    .Columns[6].Text = "Date"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = GetGridViewValue(Column, $rData[$aMyFields[Column]])
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

''---------------------------------- Chart -------------------------------------
Private Sub FillItemChartQTY()

  Dim xLst As String[]
  Dim sFile As String

  xLst = New String[]
  For Each $rData
    xLst.Add(DateDiff(dtfir.Value, $rData["fldtime"], modChart.GetDateIntegerFromSetting()))
  Next

  sFile = Temp()
  File.Save(sFile, xLst.Join(gb.NewLine))
  mnustat.Tag = sFile

  If modBasic.$ChartExecStat = "PSPP" Then
    modImage.StretchtPictureToBox(PictureBox1, modChart.FrequencyChart(sFile, "FILE Count"))
  Else
    modImage.StretchtPictureToBox(PictureBox1, modChart.FrequencyChart(sFile, "FILE Count"))
  Endif

End

Public Sub PictureBox1_Menu()

  mnuqty.Popup

End

Public Sub mnuexpoqty_Click()

  If Dialog.SelectDirectory() Then Return
  PictureBox1.Image.Save(Dialog.Path &/ "ImageQTY" & ".png")

End

Public Sub mnustat_Click()

  If modBasic.$ChartExecStat = "PSPP" Then
    modControlSub.OpenBrowser(modPSPP.CreatePSPPStatFIle("freqtable", mnustat.Tag))
  Else
    modControlSub.OpenBrowser(modPSPP.CreatePSPPStatFIle("freqtable", mnustat.Tag))
  Endif

End

''--------------------------- Grid View report -----------------------------
Public Sub GridView1_Menu()

  mnugridview.Popup

End

Public Sub GridView1_DblClick()

  Dim xx As Integer
  Dim yy As Integer

  xx = Mouse.StartX - GridView1.Left
  yy = Mouse.StartY - GridView1.Top

  Balloon.Info(("Current Column is ") & CStr(GridView1.Column + 1), GridView1, xx, yy)
  Balloon.Delay = 1000

End

Public Sub mnuimage_Click()

  Dim tempFile As String
  Dim hFile As Blob
  Dim res As Result
  Dim xcateg As String
  Dim aFile As String

  If MMain.$WebEntry = True Then
    If GridView1[GridView1.Row, 8].Text = "tblpatimagedata" Then
      res = modDatabase.$syConn.Exec("select fldcateg,fldpic,fldlink,fldcateg from tblpatimagedata where fldrepoid=&1", GridView1[GridView1.Row, 0].Text)
    Else If GridView1[GridView1.Row, 8].Text = "tblpatserialimage" Then
      res = modDatabase.$syConn.Exec("select fldpic,fldlink,fldcateg from tblpatserialimage where fldrepoid=&1", GridView1[GridView1.Row, 0].Text)
    Endif
  Else
    If GridView1[GridView1.Row, 8].Text = "tblpatimagedata" Then
      res = modDatabase.$syConn.Exec("select fldcateg,fldpic,fldlink,fldcateg from tblpatimagedata where fldid=&1", GridView1[GridView1.Row, 0].Text)
    Else If GridView1[GridView1.Row, 8].Text = "tblpatserialimage" Then
      res = modDatabase.$syConn.Exec("select fldpic,fldlink,fldcateg from tblpatserialimage where fldid=&1", GridView1[GridView1.Row, 0].Text)
    Endif
  Endif

  If res.Available Then
    If res["fldcateg"] Then
      xcateg = res["fldcateg"]
    Else
      xcateg = "IMAGE"
    Endif

    If xcateg = "IMAGE" Then
      tempFile = Temp() & ".png"
    Else If xcateg = "DICOM" Then
      tempFile = Temp() & ".dcm"
    Endif
    If res["fldlink"] Then
      Inc Application.Busy
      aFile = modFTPSub.GetFileFromLocalFTP(res["fldlink"])
      If Exist(aFile) Then
        Copy aFile To tempFile
      Endif
      Dec Application.Busy
    Else
      hFile = res["fldpic"]
      If hFile.Length Then
        File.Save(tempFile, hFile.Data)
      Endif
    Endif
  Endif

  If xcateg = "IMAGE" Then
    modDevice.DesktopOpenFile(tempFile)
  Else If xcateg = "DICOM" Then
    modDevice.ViewDicomFile(tempFile)
  Endif

End

Public Sub btnexpo_Click()

  Dim xHeader As String[]
  Dim xhide As Integer[]
  Dim Column As Integer
  Dim xCollRow As Collection
  Dim xColum As Integer

  Dim $hGridExportTable As CExportResult
  Dim xparam1 As String
  Dim xparam2 As String
  Dim encColumn As Integer

  If MMain.$IsGUIApp = True Then
    $ProgressBar1 = modAppSupport.FindWorkProgressBar(modHelpVariable.$LogInCategory)
    $ProgressBar1.Visible = True
    $ProgressBar1.Value = 0
  Endif

  xHeader = New String[]
  xhide = New Integer[]
  For Column = 0 To GridView1.Columns.Count - 1
    xHeader.Add(GridView1.Columns[Column].Text)
    If GridView1.Columns[Column].Width < 5 Then
      xhide.Add(Column)
    Endif
  Next
  xparam1 = "IMAGE SEARCH: " & txtsearch.Text
  xparam2 = modReportVar.GetDateTimeReport(dtfir.Value, gb.MediumDate) & " TO " & modReportVar.GetDateTimeReport(dtlast.Value, gb.MediumDate)
  encColumn = 1
  $hGridExportTable = New CExportResult(Me.Tag, xHeader, xhide, xparam1, xparam2, encColumn)

  Inc Application.Busy
  For Each $rData
    xCollRow = New Collection
    For xColum = 0 To $SSQLFields.Count - 1
      xCollRow.Add(GetGridViewValue(xColum, $rData[$SSQLFields[xColum]]), CStr(xColum))
    Next
    $hGridExportTable.Add($rData.Index, xCollRow)

    If MMain.$IsGUIApp = True Then
      $ProgressBar1.Value = ($rData.Index + 1) / $rData.Count
      Wait
    Endif
  Next
  Dec Application.Busy
  If MMain.$IsGUIApp = True Then
    $ProgressBar1.Visible = False
  Endif
  modControlSub.DisplayReportExport($hGridExportTable.HTMLPath())

End

Public Sub mnuReport_Click()

  If GridView1.Rows.Count > 0 Then
    If modPatientSub.AllowEncIDHistory(GridView1[GridView1.Row, 1].Text, modDatabase.$syConn) = True Then
      modPatReports.GetSelectedPatientValues(GridView1[GridView1.Row, 1].Text, MMain.$defUnit)
    Endif                                     ''
  Endif

End

Public Sub chkchart_Click()

  TabStrip1.Index = 1

End

Public Sub mnuexpocolumn_Click()

  Dim hForm As FmCustColumnSet

  hForm = New FmCustColumnSet(Me.Tag)
  hForm.ShowModal

End
