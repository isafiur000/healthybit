' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]

Private $dtList As Date[]
Private $ival As Integer

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel2, "Vertical")
  Me.Icon = Picture[modGeneralMain.$strLogo]
  Frame1.Text = "Total"
  $dtList = GetDateList()
  $ival = 0

  GetSummaryVariables($dtList[$ival])
  LoadSurveillanceData()
  Timer1.Enabled = True

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Private Function GetDateList() As Date[]

  Dim xdt1 As Date
  Dim xdt2 As Date
  Dim dtlst As Date[]

  xdt1 = DateAdd(Date(), gb.Day, -31)
  xdt2 = DateAdd(Date(), gb.Day, -1)
  dtlst = modDate.GetDateArrayBetween(xdt1, xdt2)
  Return dtlst

End

Public Sub Timer1_Timer()

  If $ival > $dtList.Count - 1 Then
    $ival = 0
  Endif
  GetSummaryVariables($dtList[$ival])
  LoadSurveillanceData()
  $ival = $ival + 1

End

''------------------------ Surveillance -------------------------
Private Sub LoadSurveillanceData()

  Dim xhospfld As String
  Dim xFldList As String[]
  Dim sql As String
  Dim xlastdt As Date

  xhospfld = modDataRepo.HospitalField()
  xlastdt = DateAdd(Date(), gb.Day, -90)
  xFldList = ["fldid", "fldtime", "fldencounterval", "fldencounterval", modDataRepo.SerialField(), "fldencounterval", "fldencounterval", "fldcodenew", "fldcode", xhospfld]

  sql = "select " & xFldList.Join(",") & " from tblpatfindings where fldtime>=&1 and fldtime<=&2 and (fldtype=&3 or fldtype=&4) and fldsave=&5 and fldcodenew in(select fldcodenew from tblsurveillance)"
  $rData1 = modDatabase.$syConn.Exec(sql, modDate.StartSqlDate(xlastdt), modDate.EndSqlDate(Now()), "Provisional Diagnosis", "Final Diagnosis", True)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)
  With GridView2
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 125 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 200 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Date"
    .Columns[2].Text = "EncID"
    .Columns[3].Text = "Name"
    .Columns[4].Text = "Age"
    .Columns[5].Text = "Gender"
    .Columns[6].Text = "PatientNo"
    .Columns[7].Text = "Code"
    .Columns[8].Text = "Observation"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData1.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  If Column = 1 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else If Column = 3 Then
    GridView2.Data.Text = modPatient.GetPatientNameByEnc($rData1[$aMyFields1[Column]], modDatabase.$syConn)
  Else If Column = 4 Then
    GridView2.Data.Text = modMedReports.GetPatientPastAgeString("tblpatfindings", $rData1[$aMyFields1[Column]])
  Else If Column = 5 Then
    GridView2.Data.Text = modPatient.GetPatientSex($rData1[$aMyFields1[Column]], modDatabase.$syConn)
  Else If Column = 6 Then
    GridView2.Data.Text = modPatient.GetPatientNoByEnc($rData1[$aMyFields1[Column]])
  Else If Column = 9 Then
    GridView2.Data.Text = modDataRepo.GetHospitalTextLabel($rData1[$aMyFields1[Column]])
  Else
    GridView2.Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

''----------------------- statistics ----------------------
Private Function GetEncCount(tblname As String, flddate As String, xdat As Date) As String

  Dim res1 As Result
  Dim xx As String

  If txtdistrict.Text Then
    res1 = modDatabase.$syConn.Exec("select count(fldencounterval) as cnt from " & tblname & " where " & flddate & ">=&1 and " & flddate & "<=&2 and fldhospcode in(select fldhospcode from tblhospitals where flddistrict like &3)", modDate.StartSqlDate(xdat), modDate.EndSqlDate(xdat), txtdistrict.Text)
  Else
    res1 = modDatabase.$syConn.Exec("select count(fldencounterval) as cnt from " & tblname & " where " & flddate & ">=&1 and " & flddate & "<=&2", modDate.StartSqlDate(xdat), modDate.EndSqlDate(xdat))                                   ''
  Endif
  ''
  If res1.Available Then
    If res1["cnt"] Then
      xx = CStr(res1["cnt"])
    Else
      xx = "__"
    Endif
  Else
    xx = "__"
  Endif
  Return xx

End

Private Sub GetSummaryVariables(xdat As Date)

  Frame1.Text = modReportVar.GetDateTimeReport(xdat, gb.MediumDate) & Space(2)
  txtconsultno.Text = GetEncCount("tblconsult", "fldconsulttime", xdat)
  txtdeliveryno.Text = GetEncCount("tblconfinement", "flddeltime", xdat)
  txtadmisno.Text = GetEncCount("tblencounter", "flddoa", xdat)
  txtdiscno.Text = GetEncCount("tblencounter", "flddod", xdat)
  txtprocedno.Text = GetEncCount("tblpatgeneral", "fldtime", xdat)
  txtlabno.Text = GetEncCount("tblpatlabtest", "fldtime_report", xdat)
  txtradiono.Text = GetEncCount("tblpatradiotest", "fldtime_report", xdat)

End

''----------------------- Descriptions ------------------------
Public Sub btndistrict_Click()

  txtdistrict.Text = Last.Tag
  If txtdistrict.Text Then
    SHowDistrictGrid()

    GetSummaryVariables($dtList[$ival])
  Endif

End

Private Sub SHowDistrictGrid()

  $rData = modDatabase.$syConn.Exec("select fldhospcode,fldhospname,fldlatitude,fldlongitude from tblhospitals where flddistrict=&1", txtdistrict.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 200 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 1
    .Columns[1].Text = "Hospitals"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnstats_Click()

  Dim hForm As FmDistReport

  If GridView1.Rows.Selection.Count Then
    hForm = New FmDistReport(GridView1[GridView1.Row, 0].Text, "Hospital")
    hForm.ShowModal
  Endif

End

Public Sub btnview_Click()

  Dim sURL As String = "http://maps.google.com/maps?z=18&q=[LAT],[LONG]"
  Dim hWebForm As FmWebView

  If GridView1.Rows.Selection.Count Then
    sURL = Replace(sURL, "[LAT]", CStr(GridView1[GridView1.Row, 2].Text))
    sURL = Replace(sURL, "[LONG]", CStr(GridView1[GridView1.Row, 3].Text))
    hWebForm = New FmWebView(sURL)
    modAppSupport.FindWorSpace(modHelpVariable.$LogInCategory).Add(hWebForm)
  Endif

End

Public Sub txtdistrict_Click()

  Dim hForm As FmDistReport

  If txtdistrict.Text Then
    hForm = New FmDistReport(txtdistrict.Text, "District")
    hForm.ShowModal
  Endif

End

Private Sub GetDailyReport(tblname As String, flddate As String, xdat As Date)

  Dim $BillingReport As CReportHTML
  Dim asx As New String[0]
  Dim res As Result

  Inc Application.Busy
  $BillingReport = New CReportHTML([("Hospital"), ("Count")], "", "")
  $BillingReport.UserData.Add("Summary", "PARAM1")
  $BillingReport.UserData.Add(modReportVar.GetDateTimeReport(xdat, gb.MediumDate), "PARAM2")

  If txtdistrict.Text Then
    res = modDatabase.$syConn.Exec("select fldhospcode,count(fldencounterval) as cnt from " & tblname & " where " & flddate & ">=&1 and " & flddate & "<=&2 and fldhospcode in(select fldhospcode from tblhospitals where flddistrict like &3) GROUP BY fldhospcode", modDate.StartSqlDate(xdat), modDate.EndSqlDate(xdat), txtdistrict.Text)
  Else
    res = modDatabase.$syConn.Exec("select fldhospcode,count(fldencounterval) as cnt from " & tblname & " where " & flddate & ">=&1 and " & flddate & "<=&2 GROUP BY fldhospcode", modDate.StartSqlDate(xdat), modDate.EndSqlDate(xdat))                                   ''
  Endif
  If res.Available Then
    For Each res
      With asx
        .Add(modDataRepo.GetHospitaltName(res["fldhospcode"]))
        .Add(modReportVar.GetLocaleNumberFormat(res["cnt"], 0))
      End With
      $BillingReport.Add(asx)
      asx.Clear()
    Next

  Endif
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", $BillingReport.NewHTMLPath(), "ReportSize")

End

Public Sub btnconsult_Click()

  GetDailyReport("tblconsult", "fldconsulttime", $dtList[$ival])

End

Public Sub btndelivery_Click()

  GetDailyReport("tblconfinement", "flddeltime", $dtList[$ival])

End

Public Sub btnadmission_Click()

  GetDailyReport("tblencounter", "flddoa", $dtList[$ival])

End

Public Sub btnexit_Click()

  GetDailyReport("tblencounter", "flddod", $dtList[$ival])

End

Public Sub btnmajor_Click()

  GetDailyReport("tblpatgeneral", "fldtime", $dtList[$ival])

End

Public Sub btnlab_Click()

  GetDailyReport("tblpatlabtest", "fldtime_report", $dtList[$ival])

End

Public Sub btnradio_Click()

  GetDailyReport("tblpatradiotest", "fldtime_report", $dtList[$ival])

End
