' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbvendor.List = modSupplier.FillVendorNameCombo("%")
  cmbcategory.List = ["Payment", "Credit Note"]
  cmbmode.List = ["Cash", "Cheque"]

  ShowPaymentGrid()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub cmbvendor_Click()

  txtvendor.Text = modSupplier.GetAddFromSuppName(cmbvendor.Text)

End

Public Sub cmbmode_Click()

  If cmbmode.Text = "Cheque" Then
    lblcheq.Visible = True
    txtchequeno.Visible = True
    lblbank.Visible = True
    txtbank.Visible = True
  Else If cmbmode.Text = "Cash" Then
    lblcheq.Visible = False
    txtchequeno.Visible = False
    txtchequeno.Text = ""
    lblbank.Visible = False
    txtbank.Visible = False
    txtbank.Text = ""
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If cmbcategory.Text And If txtreference.Text And If txtamount.Value And If cmbvendor.Text And If txtpaidby.Text And If txtrecby.Text Then                 ''
    res = modDatabase.$myConn.Create("tblpayment")
    res["fldcategory"] = cmbcategory.Text
    res["fldreference"] = txtreference.Text
    res["fldsuppname"] = cmbvendor.Text
    res["fldpayamount"] = txtamount.Value
    res["fldpaytype"] = cmbmode.Text
    res["fldchequeno"] = txtchequeno.Text
    res["fldbankname"] = txtbank.Text
    res["fldpaidby"] = txtpaidby.Text
    res["fldpaidbypost"] = txtpaidpost.Text
    res["fldrecvby"] = txtrecby.Text
    res["fldrecvbypost"] = txtrecpost.Text
    res["fldrecvbycontact"] = txtrecvcontact.Text
    res["flduserid"] = modBasic.$lbluser
    res["fldtime"] = Now()
    res["fldcomp"] = modBasic.$compID
    res["fldsave"] = False
    res["xyz"] = False
    res.Update
    ShowPaymentGrid()
  Endif

End

Private Sub ShowPaymentGrid()

  Dim sql As String

  sql = "select fldid,fldtime,fldcategory,fldreference,fldsuppname,fldpayamount,fldpaidby from tblpayment where fldsave=&1"
  $rData = modDatabase.$myConn.Exec(sql, False)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  ResizeGrid()

End

Private Sub ResizeGrid()

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 120 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 120 * modBasic.$AppWidthRatio
    .Columns[4].Width = 250 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Date"
    .Columns[2].Text = "Category"
    .Columns[3].Text = "Reference"
    .Columns[4].Text = "Supplier"
    .Columns[5].Text = "AMT"
    .Columns[6].Text = "Paid By"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 1 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldtime"], gb.MediumDate)
  Else If Column = 5 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Menu()

  mnuhidden.Popup

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblpayment", "fldid=&1 and fldsave=&2", GridView1[GridView1.Row, 0].Text, False)
    ShowPaymentGrid()
  Endif

End

Public Sub btnexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "Payment", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub cmbvendor_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbvendor)
  modFillContainer.RestrictComboToContent(cmbvendor)

End

Public Sub cmbmode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmode)
  modFillContainer.RestrictComboToContent(cmbmode)

End

Public Sub btnsave_Click()

  Dim res As Result

  Inc Application.Busy
  modDatabase.$myConn.Begin
  res = modDatabase.$myConn.Edit("tblpayment", "fldsave=&1", False)
  For Each res
    modStockSub.AddAmountToVendor(res["fldsuppname"], res["fldpayamount"], res["fldcategory"])
    res["flduserid"] = modBasic.$lbluser
    res["fldtime"] = Now()
    res["fldcomp"] = modBasic.$compID
    res["fldsave"] = True
    res["xyz"] = False
    res.Update
  Next
  modDatabase.$myConn.Commit
  Dec Application.Busy
  ShowPaymentGrid()

Catch
  modDatabase.$myConn.Rollback
  Dec Application.Busy
  modHelpVariable.CreateErrorReport()

End

Public Sub btnshow_Click()

  Dim sql As String

  If btnshow.Text = "Show" Then
    sql = "select fldid,fldtime,fldcategory,fldreference,fldsuppname,fldpayamount,fldpaidby from tblpayment where fldsave=&1"
    $rData = modDatabase.$myConn.Exec(sql, True)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ResizeGrid()
    btnshow.Text = "Close"
  Else If btnshow.Text = "Close" Then
    ShowPaymentGrid()
    btnshow.Text = "Show"
  Endif

End

Public Sub btnreference_Click()

  Dim xList As String[]

  If cmbvendor.Text And If cmbcategory.Text Then
    If cmbcategory.Text = "Payment" Then
      xList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldreference) as col from tblpurchasebill where fldsuppname=&1 and fldpurtype=&2", cmbvendor.Text, "Credit Payment"))                   ''
    Else If cmbcategory.Text = "Credit Note" Then
      xList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldreference) as col from tblstockreturn where fldsuppname=&1 and fldsave=&2", cmbvendor.Text, True))
    Endif
    If xList And If xList.Count Then
      txtreference.Text = GridViewNew("Select Reference Number", xList, False, Panel1, GridView1.Height)
    Endif
  Endif

End
