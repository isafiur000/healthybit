' Gambas class file

Private $Depart As String
Private $rData As Result
Private $aMyFields As String[]

Private $CategLst As String[]

Public Sub _new(sDepart As String)

  $Depart = sDepart

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  lbldepart.Text = $Depart
  cmboxygen.List = ["Present", "Absent"]
  cmbventilator.List = ["Present", "Absent"]
  cmbother.List = ["Present", "Absent"]
  cmbstatus.List = ["Active", "Inactive"]  ''"Maintenance","Day Use"
  cmbservice.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditem as col from tblipdservice"))
  $CategLst = modControlSub.GetDirectFillresult(modDatabase.$icdConn.Exec("select distinct(fldicdchpter) as col from tblicddisease"))
  SHowDepartBed()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnaddservice_Click()

  Dim hForm As FmAddVarTwo
  Dim xx As String[]

  xx = New String[]
  hForm = New FmAddVarTwo("flditem", "flddiagnosis", "tblipdservice", xx, $CategLst)
  hForm.ShowModal
  cmbservice.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditem as col from tblipdservice"))

End

Public Sub cmbservice_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbservice)
  modFillContainer.RestrictComboToContent(cmbservice)

End

Public Sub cmboxygen_KeyRelease()

  modFillContainer.AutoFillComboBox(cmboxygen)
  modFillContainer.RestrictComboToContent(cmboxygen)

End

Public Sub cmbventilator_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbventilator)
  modFillContainer.RestrictComboToContent(cmbventilator)

End

Public Sub cmbstatus_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbstatus)
  modFillContainer.RestrictComboToContent(cmbstatus)

End

Private Sub SHowDepartBed()

  Dim sql As String

  sql = "select fldbed,fldstatus,fldservice,fldoxyport,fldventilator,fldother from tbldepartmentbed where flddept=&1"           ''
  $rData = modDatabase.$myConn.Exec(sql, $Depart)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 125 * modBasic.$AppWidthRatio
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Bed No"
    .Columns[1].Text = "Status"
    .Columns[2].Text = "Service"
    .Columns[3].Text = "Oxygen"
    .Columns[4].Text = "Ventilator"
    .Columns[5].Text = "Other"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Private Sub BlankAllControl()

  txtbedno.Text = ""
  cmbservice.Text = ""
  cmboxygen.Text = ""
  cmbventilator.Text = ""
  cmbother.Text = ""
  cmbstatus.Text = ""

End

Public Sub GridView1_Click()

  txtbedno.Text = GridView1[GridView1.Row, 0].Text
  cmbstatus.Text = GridView1[GridView1.Row, 1].Text
  cmbservice.Text = GridView1[GridView1.Row, 2].Text
  cmboxygen.Text = GridView1[GridView1.Row, 3].Text
  cmbventilator.Text = GridView1[GridView1.Row, 4].Text
  cmbother.Text = GridView1[GridView1.Row, 5].Text

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudelete_Click()

  If txtbedno.Text Then
    modDatabase.$myConn.Delete("tbldepartmentbed", "fldbed=&1", Trim(txtbedno.Text))
    SHowDepartBed()
    BlankAllControl()
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If txtbedno.Text Then
    res = modDatabase.$myConn.Create("tbldepartmentbed")
    res["fldbed"] = Trim(txtbedno.Text)
    res["flddept"] = $Depart
    res["fldservice"] = cmbservice.Text
    res["fldoxyport"] = Trim(cmboxygen.Text)
    res["fldventilator"] = Trim(cmbventilator.Text)
    res["fldother"] = Trim(cmbother.Text)
    res["fldstatus"] = cmbstatus.Text
    res["fldencounterval"] = ""
    res["fldaddservice"] = ""
    res["fldflag"] = False
    res["fldfreedate"] = ""
    res["xyz"] = False
    res.Update
    SHowDepartBed()
    BlankAllControl()
    Balloon.Info(("Information saved"), btnadd)
    Balloon.Delay = modBasic.$BalloonDelay
    txtbedno.SetFocus
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If txtbedno.Text Then
    res = modDatabase.$myConn.Edit("tbldepartmentbed", "fldbed=&1", Trim(txtbedno.Text))
    res["fldservice"] = cmbservice.Text
    res["fldoxyport"] = Trim(cmboxygen.Text)
    res["fldventilator"] = Trim(cmbventilator.Text)
    res["fldother"] = Trim(cmbother.Text)
    res["fldstatus"] = cmbstatus.Text
    res["xyz"] = False
    res.Update
    SHowDepartBed()
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $Depart, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End
