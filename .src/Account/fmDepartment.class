' Gambas class file

Private $rData As Result

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbcategory.List = ["Consultation", "OPD Visit", "Patient Ward"]
  cmbpackage.List = modNonMedical.FillDiscountCombo()
  cmbactive.List = modGeneral.GetDepartCaptionList()
  cmbcompid.List = modBasic.$AllCompList
  ShowCostGrid()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub cmbpackage_KeyRelease()

  modFillContainer.RestrictComboToContent(cmbpackage)

End

Public Sub btnrefresh_Click()

  ShowCostGrid()

End

Public Sub btnBrOK_Click()

  Dim res As Result
  Dim xIntVal As String

  If txtdept.Text Then
    res = modDatabase.$myConn.Create("tbldepartment")
    res["flddept"] = Trim(txtdept.Text)
    res["flddeptcode"] = Trim(txtdeptcode.Text)
    res["fldroom"] = txtroom.Text
    res["flddisctype"] = cmbpackage.Text
    res["fldcateg"] = cmbcategory.Text
    res["fldactive"] = cmbactive.Text
    res["fldcomp"] = cmbcompid.Text
    res["fldinterval"] = txtinterval.Value
    res["fldstart"] = dtstart.Value
    If MMain.$WebEntry = True Then
      xIntVal = modString.GetDateString(Now())
      res["fldid"] = CLong(xIntVal)
      res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
      res["fldrepodate"] = Now()
      res["fldrepomac"] = modHelpVariable.$MACAddress
      res["fldhospcode"] = modBasic.$HospCode
    Endif
    res.Update()
    ShowCostGrid()
    Balloon.Info(("Information saved"), btnBrOK)
    Balloon.Delay = modBasic.$BalloonDelay
    cmbactive.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldactive) as col from tbldepartment"))
    txtdept.SetFocus
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit("tbldepartment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["flddept"] = Trim(txtdept.Text)
    res["flddeptcode"] = Trim(txtdeptcode.Text)
    res["fldroom"] = txtroom.Text
    res["flddisctype"] = cmbpackage.Text
    res["fldcateg"] = cmbcategory.Text
    res["fldactive"] = cmbactive.Text
    res["fldcomp"] = cmbcompid.Text
    res["fldinterval"] = txtinterval.Value
    res["fldstart"] = dtstart.Value
    res["xyz"] = False
    res.Update()
    ShowCostGrid()
    GridView1.Row = Row
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub ShowCostGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  Dim xcateg As String

  If cmbcategory.Text Then
    xcateg = cmbcategory.Text
  Else
    xcateg = "%"
  Endif
  sql = "select fldid,flddept,fldcateg,fldroom,flddisctype,fldactive,flddeptcode,fldcomp,fldinterval,fldstart from tbldepartment where fldcateg like &1 ORDER BY flddept ASC"
  $rData = modDatabase.$myConn.Exec(sql, xcateg)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 150 * modBasic.$AppWidthRatio
    .Columns[2].Width = 1
    .Columns[3].Width = 50 * modBasic.$AppWidthRatio
    .Columns[4].Width = 125 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio
    .Columns[6].Width = 100 * modBasic.$AppWidthRatio
    .Columns[7].Width = 100 * modBasic.$AppWidthRatio
    .Columns[8].Width = 1
    .Columns[9].Width = 1

    .Columns[1].Text = "Department"
    .Columns[3].Text = "RoomNo"
    .Columns[4].Text = "Package"
    .Columns[5].Text = "Caption"
    .Columns[6].Text = "Code"
    .Columns[7].Text = "CompID"
  End With

End

Public Sub mnudel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tbldepartment", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      ShowCostGrid()
    Endif
  Endif

End

Public Sub GridView1_Click()

  txtdept.Text = GridView1[GridView1.Row, 1].Text
  cmbcategory.Text = GridView1[GridView1.Row, 2].Text
  txtroom.Text = GridView1[GridView1.Row, 3].Text
  cmbpackage.Text = GridView1[GridView1.Row, 4].Text
  cmbactive.Text = GridView1[GridView1.Row, 5].Text
  txtdeptcode.Text = GridView1[GridView1.Row, 6].Text
  cmbcompid.Text = GridView1[GridView1.Row, 7].Text
  If GridView1[GridView1.Row, 8].Text Then
    txtinterval.Value = GridView1[GridView1.Row, 8].Text
  Else
    txtinterval.Value = 0
  Endif
  dtstart.Value = GridView1[GridView1.Row, 9].Text

End

Public Sub btnbed_Click()

  Dim hForm As FmDepartBed

  If cmbcategory.Text = "Patient Ward" Then
    hForm = New FmDepartBed(txtdept.Text)
    hForm.ShowModal
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "DEPARTMENTS", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub txtdept_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub btnbedrepo_Click()

  Dim xPath As String

  Inc Application.Busy
  xPath = modGENReport.ShowDepartmentsReport()
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End

Public Sub btnbedfee_Click()

  fmAddDeptBed.Close
  fmAddDeptBed.ShowModal

End
