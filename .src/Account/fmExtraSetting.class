' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmblocation.List = modBasic.$AllCompList
  cmbcategory.List = modNonMedical.BillItemCategory()
  cmbmode.List = modNonMedical.FillDiscountCombo()

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If cmblocation.Text And If cmbcategory.Text Then
    res = modDatabase.$myConn.Create("tblextrasetting")
    res["fldcomp"] = cmblocation.Text
    res["fldcateg"] = cmbcategory.Text
    res["flddiscpercent"] = txtdisc.Value
    res["fldcashmode"] = cmbmode.Text
    res.Update()
    ShowGrid()
    Balloon.Info(("Information added"), btnadd)
    Balloon.Delay = modBasic.$BalloonDelay
    cmblocation.SetFocus
  Endif

End

Private Sub ShowGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,fldcateg,flddiscpercent,fldcashmode from tblextrasetting where fldcomp=&1", cmblocation.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  GridView1.Columns[0].Width = 1
  GridView1.Columns[1].Width = 225 * modBasic.$AppWidthRatio
  GridView1.Columns[2].Width = 75 * modBasic.$AppWidthRatio
  GridView1.Columns[3].Width = 100 * modBasic.$AppWidthRatio
  GridView1.Columns[1].Text = "Category"
  GridView1.Columns[2].Text = "Disc%"
  GridView1.Columns[3].Text = "PatType"

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub cmblocation_Click()

  ShowGrid()
  lbllocation.Text = modGeneral.GetCompNameFromCompID(cmblocation.Text)

End

Public Sub GridView1_Click()

  cmbcategory.Text = GridView1[GridView1.Row, 1].Text
  cmbmode.Text = GridView1[GridView1.Row, 3].Text
  txtdisc.Value = GridView1[GridView1.Row, 2].Text

End

Public Sub btndel_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblextrasetting", "fldid=&1", GridView1[GridView1.Row, 0].Text)
      ShowGrid()
      Balloon.Delete(("Information deleted"), btndel)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub cmbmode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmode)
  modFillContainer.RestrictComboToContent(cmbmode)

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim Row As Integer

  If GridView1.Rows.Selection.Count > 0 Then
    Row = GridView1.Row
    res = modDatabase.$myConn.Edit("tblextrasetting", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldcateg"] = cmbcategory.Text
    res["flddiscpercent"] = txtdisc.Value
    res["fldcashmode"] = cmbmode.Text
    res.Update()
    ShowGrid()
    GridView1.Row = Row
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnlist_Click()

  Dim xPath As String

  Inc Application.Busy
  xPath = modRepoMaster.ShowComBillingListReport()
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End
