' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  txtdate.Text = "0000/00"
  rbentry.Value = True

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub btnconsultname_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Sharing User"), modGeneral.CategoricalUserList("fldpayable"))
  If xMedUser And If xMedUser.Count Then
    btnconsultname.Tag = xMedUser[0]
    btnconsultname.Text = xMedUser[1]
  Else
    btnconsultname.Tag = ""
    btnconsultname.Text = ""
  Endif

End

Private Sub ShowUserSharing()

End

Public Sub btnshow_Click()

  Dim xdaterange As Date[]

  GetAllBlank()
  If txtdate.Text And If txtdate.Text <> "0000/00" Then
    xdaterange = modDate.GetBSDateRange(txtdate.Text)
    If xdaterange Then
      dtfir.Value = xdaterange[0]
      dtlast.Value = xdaterange[1]
    Endif
  Endif

  If btnconsultname.Tag And If txtdate.Text And If chkalldate.Value = False Then
    ShowUserSharing()
    txttaxper.Value = modGeneral.GetStaffTaxPer(btnconsultname.Tag)
  Endif
  SHowGridFill()
  btnreport.Enabled = True

End

Private Sub SHowGridFill()

  If txtdate.Text <> "0000/00" And If chkalldate.Value = False Then
    If btnconsultname.Tag Then
      $rData = modDatabase.$myConn.Exec("select fldid,fldpayto,fldfrom,fldto,fldshareamt,fldextra,fldtdsper,fldsharenet,fldcomment from tblsharepayment where fldfrom>=&1 and fldto<=&2 and fldpayto like &3", modDate.StartSqlDate(dtfir.Value), modDate.EndSqlDate(dtlast.Value), btnconsultname.Tag)                                                       ''
    Else
      $rData = modDatabase.$myConn.Exec("select fldid,fldpayto,fldfrom,fldto,fldshareamt,fldextra,fldtdsper,fldsharenet,fldcomment from tblsharepayment where fldfrom>=&1 and fldto<=&2", modDate.StartSqlDate(dtfir.Value), modDate.EndSqlDate(dtlast.Value))
    Endif
  Else
    If btnconsultname.Tag Then
      $rData = modDatabase.$myConn.Exec("select fldid,fldpayto,fldfrom,fldto,fldshareamt,fldextra,fldtdsper,fldsharenet,fldcomment from tblsharepayment where fldpayto like &1", btnconsultname.Tag)                                                       ''
    Else
      $rData = modDatabase.$myConn.Exec("select fldid,fldpayto,fldfrom,fldto,fldshareamt,fldextra,fldtdsper,fldsharenet,fldcomment from tblsharepayment")
    Endif
  Endif

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 75 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
    .Columns[8].Width = 150 * modBasic.$AppWidthRatio

    .Columns[1].Text = "UserID"
    .Columns[2].Text = "From"
    .Columns[3].Text = "To"
    .Columns[4].Text = "Earned"
    .Columns[5].Text = "Extra"
    .Columns[6].Text = "TDS"
    .Columns[7].Text = "NetAMT"
    .Columns[8].Text = "Comment"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 2 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnexport_Click()

  Dim xuser As String
  Dim xdate As String

  If chkalldate.Value = True Then
    xdate = "All Dates"
  Else
    xdate = txtdate.Text
  Endif
  If btnconsultname.Tag Then
    xuser = btnconsultname.Text
  Else
    xuser = "All Users"
  Endif
  modCHTMLReport.ExportGridToHTML(GridView1, xuser, xdate)

End

Private Sub GetAllBlank()

  dtfir.Value = ""
  dtlast.Value = ""
  txtshare.Value = 0
  txttds.Value = 0
  txtnetshare.Value = 0
  txtextra.Value = 0
  txtcomment.Text = ""

End

Public Sub btnaddnew_Click()

  Dim res As Result

  If btnconsultname.Tag And If dtfir.Value And If dtlast.Value Then
    If Message.Question("Are you sure ?", ("No"), ("OK")) = 2 Then
      res = modDatabase.$myConn.Create("tblsharepayment")
      res["fldpayto"] = btnconsultname.Tag
      res["fldfrom"] = modDate.StartSqlDate(dtfir.Value)
      res["fldto"] = modDate.EndSqlDate(dtlast.Value)
      res["fldshareamt"] = txtshare.Value
      res["fldextra"] = txtextra.Value
      res["fldtdsper"] = txttds.Value
      res["fldsharenet"] = txtnetshare.Value
      res["fldcomment"] = txtcomment.Text

      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldsave"] = True
      res["flduptime"] = ""
      res["xyz"] = False
      res.Update
      SHowGridFill()
    Endif
  Else
    Message.Warning("Either Date is incorrect or User ID", ("OK"))
  Endif

End

Private Sub DoCalculate()

  txttds.Value = (txttaxper.Value / 100) * (txtshare.Value + txtextra.Value)
  txtnetshare.Value = txtshare.Value + txtextra.Value - txttds.Value

End

Public Sub txtshare_Change()

  DoCalculate()

End

Public Sub txtextra_Change()

  DoCalculate()

End

Public Sub txttaxper_Change()

  DoCalculate()

End

Public Sub txttds_Change()

  txtnetshare.Value = txtshare.Value + txtextra.Value - txttds.Value

End

Private Function GetInvoiced() As Boolean

  Dim xx As Boolean

  If rbinv.Value = True Then
    xx = True
  Else
    xx = False
  Endif
  Return xx

End

Private Function GetSelUserList() As String[]

  Dim bList As String[]
  Dim xAllList As String[]
  Dim userList As String[]
  Dim xgroupbool As Boolean

  xgroupbool = False
  xAllList = New String[]
  If rbinv.Value = True Then
    bList = modControlSub.GetDirectFillresult(modDatabase.$syConn.Exec("select distinct(fldvalue) as col from tblpatgenshare where flditemid in(select fldid from tblpatbilling where (flditemqty-fldretqty)>&1 and fldtime>=&2 and fldtime<=&3) and fldsave=&4", 0, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), xgroupbool))
  Else If rbentry.Value = True Then
    bList = modControlSub.GetDirectFillresult(modDatabase.$syConn.Exec("select distinct(fldvalue) as col from tblpatgenshare where flditemid in(select fldid from tblpatbilling where (flditemqty-fldretqty)>&1) and fldtime>=&2 and fldtime<=&3 and fldsave=&4", 0, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), xgroupbool))
  Endif
  If bList Then
    userList = modString.BinaryDistinctStringArray(bList, gb.Ascent)
  Endif

  Return userList

End

Public Sub btnreport_Click()

  Dim xPath As String
  Dim xList As String[]

  xList = GetSelUserList()
  If xList Then
    Inc Application.Busy
    xPath = modRHTMLShareGroup.SharingPaymentFixAMT(modDatabase.$syConn, dtfir.Value, dtlast.Value, GetInvoiced(), xList)
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End
