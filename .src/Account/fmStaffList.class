' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $RepoStr As String

Public Sub Form_Open()

  Dim xrank As String
  Dim xunit As String
  Dim xlocat As String
  Dim xcont As Control
  Dim OBSwatchall As Observer

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Column")
  If modBasic.$FocusBackColor Then
    For Each xcont In Me.Controls
      OBSwatchall = New Observer(xcont) As "WatchAll"
    Next
  Endif

  If MMain.$WebReport = "Multiple" Then
    If modBasic.$HospCode Then
      cmblocation.Text = modDataRepo.$RepositoryMode
      cmbvalue.Text = modBasic.$HospCode
      pnlrep.Enabled = False
    Else
      cmblocation.List = ["Hospital", "Municipality", "Category", "District", "Province"]
    Endif
    pnlbuttons.Visible = False

  Else
    pnlrep.Visible = False
  Endif
  cmbsex.List = ["Male", "Female", "Other"]
  cmblood.List = ["A+ve", "A-ve", "B+ve", "B-ve", "AB+ve", "AB-ve", "O+ve", "O-ve"]
  cmbstatus.List = ["Active", "Inactive"]
  cmbdistrict.List = modBasic.$DistrictList
  cmbdiscount.List = modNonMedical.FillDiscountCombo()
  cmbrelation.List = modBasic.$RelationList
  cmbpost.List = GetUserPostList()
  cmbcategory.List = GetUserTitleList()
  cmbservsearch.List = GetUserTitleList()
  cmbservsearch.Add("%")
  cmbservsearch.Text = "%"

  xrank = modSettings.ShowSettingFromFIle("RegistrationForm/RankList")
  If Exist(xrank) Then
    cmbrank.List = modString.GetStringArrayFromFile(xrank)
  Endif
  xunit = modSettings.ShowSettingFromFIle("RegistrationForm/UnitList")
  If Exist(xunit) Then
    cmbunit.List = modString.GetStringArrayFromFile(xunit)
  Endif
  xlocat = modSettings.ShowSettingFromFIle("RegistrationForm/PostingList")
  If Exist(xlocat) Then
    cmbdepart.List = modString.GetStringArrayFromFile(xlocat)
  Endif
  rbcode.Value = True
  dtjoin.Value = Now()

  FillStaffGrid()
  txtsearch.SetFocus

End

Public Sub WatchAll_GotFocus()

  Last.Background = modBasic.$FocusBackColor

End

Public Sub WatchAll_LostFocus()

  Last.Background = Color.Default

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key["B"] And If Key.Control Then
    MakeAllBlank()
  Else If Key.Code = Key["F"] And If Key.Control Then
    btnfileno_Click()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmblocation_Click()

  cmbvalue.Clear()
  If cmblocation.Text Then
    cmbvalue.List = modDataRepo.GetRepoValueListType(cmblocation.Text)
  Endif

End

Private Sub MakeAllBlank()

  txtpatcode.Text = ""
  cmbcategory.Text = ""
  txtservid.Text = ""
  txtnamefir.Text = ""
  txtnamelast.Text = ""
  cmbsex.Text = ""
  dtdob.Value = ""
  txtward.Text = ""
  txtaddfir.Text = ""
  cmbdistrict.Text = ""
  txtcontact.Text = ""
  txtemail.Text = ""
  txtguardian.Text = ""
  cmbrelation.Text = ""
  dtjoin.Value = ""
  dtend.Value = ""
  txtuaserid.Text = ""
  txtpatno.Text = ""
  cmbdiscount.Text = ""
  cmbstatus.Text = ""

  txtcitizen.Text = ""
  cmblood.Text = ""
  cmbreligion.Text = ""
  txtidentify.Text = ""

  txtgovtaccount.Text = ""
  txtbankname.Text = ""
  txtbankaccount.Text = ""
  txttaxpercent.Value = 0

  cmbdepart.Text = ""
  cmbpost.Text = ""
  cmbrank.Text = ""
  cmbunit.Text = ""
  txtremarks.Text = ""

End

Public Sub btndependent_Click()

  $RepoStr = modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
  $rData = modDatabase.$myConn.Exec("select fldserviceid,fldptnamefir,fldptnamelast,fldcategory,fldptcode from tblstafflist where lower(fldptcode) like &1 and fldcategory like &2" & $RepoStr & " ORDER BY fldptnamefir", LCase(txtsearch.Text) & "%", cmbservsearch.Text)                                                                 ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  SetLeftGrid()

End

Public Sub btnservsearch_Click()

  FillStaffGrid()

End

Private Sub FillStaffGrid()

  $RepoStr = modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
  If rbcode.Value Then
    $rData = modDatabase.$myConn.Exec("select fldserviceid,fldptnamefir,fldptnamelast,fldcategory,fldptcode from tblstafflist where lower(fldptcode) like &1 and fldcategory like &2 and fldidentify IS NULL" & $RepoStr & " ORDER BY fldptnamefir", LCase(txtsearch.Text) & "%", cmbservsearch.Text)                                                                 ''
  Else If rbname.Value Then
    $rData = modDatabase.$myConn.Exec("select fldserviceid,fldptnamefir,fldptnamelast,fldcategory,fldptcode from tblstafflist where lower(fldptnamefir) like &1 and fldcategory like &2 and fldidentify IS NULL" & $RepoStr & " ORDER BY fldptnamefir", LCase(txtsearch.Text) & "%", cmbservsearch.Text)                                                                 ''
  Else If rbsurname.Value Then
    $rData = modDatabase.$myConn.Exec("select fldserviceid,fldptnamefir,fldptnamelast,fldcategory,fldptcode from tblstafflist where lower(fldptnamelast) like &1 and fldcategory like &2 and fldidentify IS NULL" & $RepoStr & " ORDER BY fldptnamefir", LCase(txtsearch.Text) & "%", cmbservsearch.Text)                                                                 ''
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  SetLeftGrid()

End

Private Sub SetLeftGrid()

  With GridView1
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 100 * modBasic.$AppWidthRatio
    .Columns[0].Text = "ServiceID"
    .Columns[2].Text = "Name"
    .Columns[3].Text = "SurName"
    .Columns[3].Text = "Category"
    .Columns[4].Text = "Staff ID"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnOK_Click()

  Dim res As Result
  Dim res1 As Result

  If txtpatcode.Text And If cmbcategory.Text Then
    res1 = modDatabase.$myConn.Exec("select fldptcode from tblstafflist where fldptcode=&1", txtpatcode.Text)
    If res1.Available Then
      Message.Warning(("Duplicate Code"), ("OK"))
    Else
      res = modDatabase.$myConn.Create("tblstafflist")
      res["fldptcode"] = Trim(txtpatcode.Text)
      res["fldcategory"] = cmbcategory.Text
      res["fldserviceid"] = Trim(txtservid.Text)
      res["fldptnamefir"] = String.UCaseFirst(Trim(txtnamefir.Text))
      res["fldptnamelast"] = String.UCaseFirst(Trim(txtnamelast.Text))
      res["fldptsex"] = Trim(cmbsex.Text)
      res["fldptbirday"] = dtdob.Value
      res["fldptaddvill"] = String.UCaseFirst(Trim(txtaddfir.Text))
      res["fldptaddward"] = Trim(txtward.Text)
      res["fldptadddist"] = String.UCaseFirst(Trim(cmbdistrict.Text))
      res["fldptcontact"] = Trim(txtcontact.Text)
      res["fldemail"] = Trim(txtemail.Text)
      res["fldptguardian"] = Trim(txtguardian.Text)
      res["fldrelation"] = cmbrelation.Text
      res["fldjoindate"] = dtjoin.Value
      res["fldenddate"] = dtend.Value
      res["flduserid"] = Trim(txtuaserid.Text)
      res["fldpatientval"] = Trim(txtpatno.Text)
      res["flddisctype"] = cmbdiscount.Text
      res["fldstatus"] = cmbstatus.Text

      res["fldcitizen"] = Trim(txtcitizen.Text)
      res["fldcontype"] = cmblood.Text
      res["fldreligion"] = cmbreligion.Text
      res["fldidentify"] = txtidentify.Text

      res["fldgovtaccount"] = Trim(txtgovtaccount.Text)
      res["fldbankname"] = Trim(txtbankname.Text)
      res["fldbankaccount"] = Trim(txtbankaccount.Text)
      res["fldtaxpercent"] = txttaxpercent.Value

      res["flddept"] = cmbdepart.Text
      res["fldpost"] = cmbpost.Text
      res["fldrank"] = cmbrank.Text
      res["fldunit"] = cmbunit.Text

      res["fldremark"] = txtremarks.Text
      res["fldtime"] = Now()
      res["xyz"] = False

      res.Update
      FillStaffGrid()
      Balloon.Info(("Information saved"), btnOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnChange_Click()

  Dim res As Result
  Dim Row As Integer

  Row = GridView1.Row
  If txtpatcode.Text And If cmbcategory.Text Then
    res = modDatabase.$myConn.Edit("tblstafflist", "fldptcode=&1", txtpatcode.Text)
    res["fldcategory"] = cmbcategory.Text
    res["fldserviceid"] = Trim(txtservid.Text)
    res["fldptnamefir"] = String.UCaseFirst(Trim(txtnamefir.Text))
    res["fldptnamelast"] = String.UCaseFirst(Trim(txtnamelast.Text))
    res["fldptsex"] = Trim(cmbsex.Text)
    res["fldptbirday"] = dtdob.Value
    res["fldptaddvill"] = String.UCaseFirst(Trim(txtaddfir.Text))
    res["fldptaddward"] = Trim(txtward.Text)
    res["fldptadddist"] = String.UCaseFirst(Trim(cmbdistrict.Text))
    res["fldptcontact"] = Trim(txtcontact.Text)
    res["fldemail"] = Trim(txtemail.Text)
    res["fldptguardian"] = Trim(txtguardian.Text)
    res["fldrelation"] = cmbrelation.Text
    res["fldjoindate"] = dtjoin.Value
    res["fldenddate"] = dtend.Value
    res["flduserid"] = Trim(txtuaserid.Text)
    res["fldpatientval"] = Trim(txtpatno.Text)
    res["flddisctype"] = cmbdiscount.Text
    res["fldstatus"] = cmbstatus.Text

    res["fldcitizen"] = Trim(txtcitizen.Text)
    res["fldcontype"] = cmblood.Text
    res["fldreligion"] = cmbreligion.Text
    res["fldidentify"] = txtidentify.Text

    res["fldgovtaccount"] = Trim(txtgovtaccount.Text)
    res["fldbankname"] = Trim(txtbankname.Text)
    res["fldbankaccount"] = Trim(txtbankaccount.Text)
    res["fldtaxpercent"] = txttaxpercent.Value

    res["flddept"] = cmbdepart.Text
    res["fldpost"] = cmbpost.Text
    res["fldrank"] = cmbrank.Text
    res["fldunit"] = cmbunit.Text

    res["fldremark"] = txtremarks.Text
    res["fldtime"] = Now()
    res["xyz"] = False

    res.Update
    FillStaffGrid()
    Balloon.Info(("Information updated"), btnChange)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  GridView1.Row = Row

End

Public Sub btnCancel_Click()

  If txtpatcode.Text Then
    modDatabase.$myConn.Delete("tblstafflist", "fldptcode=&1", txtpatcode.Text)
    FillStaffGrid()
    MakeAllBlank()
    Balloon.Delete(("Information deleted"), btnChange)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub SHowGridData(patcode As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldcategory,fldserviceid,fldptnamefir,fldptnamelast,fldptsex,fldptaddvill,fldptaddward,fldptadddist,fldptcontact,fldptbirday,fldemail,fldptguardian,fldrelation,fldjoindate,fldenddate,flduserid,fldpatientval,flddisctype,fldstatus,fldcitizen,fldcontype,fldreligion,fldidentify,fldgovtaccount,fldbankname,fldbankaccount,fldtaxpercent,flddept,fldpost,fldrank,fldunit,fldremark from tblstafflist where fldptcode=&1", patcode)                                                   ''
  If res.Available Then
    cmbcategory.Text = res["fldcategory"]
    txtservid.Text = res["fldserviceid"]
    txtnamefir.Text = res["fldptnamefir"]
    txtnamelast.Text = res["fldptnamelast"]
    cmbsex.Text = res["fldptsex"]
    dtdob.Value = res["fldptbirday"]
    cmbdistrict.Text = res["fldptadddist"]  ''district always before address
    txtward.Text = res["fldptaddward"]
    txtaddfir.Text = res["fldptaddvill"]
    txtcontact.Text = res["fldptcontact"]
    txtemail.Text = res["fldemail"]
    txtguardian.Text = res["fldptguardian"]
    cmbrelation.Text = res["fldrelation"]
    dtjoin.Value = res["fldjoindate"]
    dtend.Value = res["fldenddate"]
    txtuaserid.Text = res["flduserid"]
    txtpatno.Text = res["fldpatientval"]
    cmbdiscount.Text = res["flddisctype"]
    cmbstatus.Text = res["fldstatus"]

    txtcitizen.Text = res["fldcitizen"]
    cmblood.Text = res["fldcontype"]
    cmbreligion.Text = res["fldreligion"]
    txtidentify.Text = res["fldidentify"]

    txtgovtaccount.Text = res["fldgovtaccount"]
    txtbankname.Text = res["fldbankname"]
    txtbankaccount.Text = res["fldbankaccount"]
    If res["fldtaxpercent"] Then
      txttaxpercent.Value = res["fldtaxpercent"]
    Else
      txttaxpercent.Value = 0
    Endif

    cmbdepart.Text = res["flddept"]
    cmbpost.Text = res["fldpost"]
    cmbrank.Text = res["fldrank"]
    cmbunit.Text = res["fldunit"]

    txtremarks.Text = res["fldremark"]
  Endif

End

Public Sub GridView1_Click()

  If GridView1.Rows.Selection.Count > 0 Then
    MakeAllBlank()
    txtpatcode.Text = GridView1[GridView1.Row, 4].Text
    SHowGridData(txtpatcode.Text)
  Endif

End

Public Sub btnpatno_Click()

  If txtpatno.Text Then
    GetPatientValue(Trim(txtpatno.Text))
  Endif

End

Public Sub btnencid_Click()

  Dim xval As String

  xval = InputBox(("Enter Patient's Encounter ID"), "Encounter ID", "")
  If xval Then
    txtpatno.Text = modPatient.GetPatientNoByEnc(xval)
    If txtpatno.Text Then
      GetPatientValue(txtpatno.Text)
    Endif
  Endif

End

Public Sub btnfileno_Click()

  Dim xx As String
  Dim res As Result

  xx = InputBox("Enter Patient File Number", "File Number", "")
  If xx Then
    res = modDatabase.$myConn.Exec("select fldpatientval from tblpatientinfo where fldadmitfile=&1", xx)
    If res.Available Then
      res.MoveFirst
      txtpatno.Text = res["fldpatientval"]
      If txtpatno.Text Then
        GetPatientValue(txtpatno.Text)
      Endif
    Endif
  Endif

End

Private Sub GetPatientValue(patno As String)

  Dim sql As String
  Dim res As Result

  If patno Then
    txtpatno.Text = patno
    sql = "select fldptnamefir,fldptnamelast,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldptadmindate,fldemail,fldrelation,fldptcode,flddiscount from tblpatientinfo where fldpatientval=&1"
    res = modDatabase.$myConn.Exec(sql, Trim(patno))
    If res.Available = False Then
      modLinkDB.CheckPatientDataRemote(Trim(patno))
    Else
      txtnamefir.Text = res["fldptnamefir"]
      txtnamelast.Text = res["fldptnamelast"]
      cmbsex.Text = res["fldptsex"]
      dtdob.Value = res["fldptbirday"]
      cmbdistrict.Text = res["fldptadddist"]  ''district always before address
      txtaddfir.Text = res["fldptaddvill"]
      txtward.Text = res["fldptaddward"]
      txtcontact.Text = res["fldptcontact"]
      txtemail.Text = res["fldemail"]
      txtpatcode.Text = res["fldptcode"]
      txtguardian.Text = res["fldptguardian"]
      cmbrelation.Text = res["fldrelation"]
      cmbdiscount.Text = res["flddiscount"]
    Endif
  Endif

End

Public Sub btnuserefresh_Click()

  Dim res As Result

  If txtuaserid.Text Then
    res = modDatabase.$myConn.Edit("tbluser", "flduserid=&1", Trim(txtuaserid.Text))
    If res.Available Then
      If dtend.Value Then
        res["fldtodate"] = dtend.Value
      Endif
      res["fldstatus"] = cmbstatus.Text
      res["xyz"] = False
      res.Update
      Balloon.Info(("Information updated"), btnuserefresh)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnblank_Click()

  MakeAllBlank()

End

Public Sub btncheck_Click()

  If txtpatcode.Text Then
    SHowGridData(Trim(txtpatcode.Text))
  Endif

End

Public Sub cmbdistrict_Click()

  txtaddfir.Clear()
  If cmbdistrict.Text Then
    txtaddfir.List = modDataRepo.GetMunicipalsByDistrict(cmbdistrict.Text)
  Endif

End

Public Sub btnuserid_Click()

  txtuaserid.Text = MedicalSelectedUser(("Select User"), modGeneral.CategoricalUserList("%"))

End

Public Sub dtnepdob_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtdob.Value))
  If xx Then
    dtdob.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtnepjoin_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtjoin.Value))
  If xx Then
    dtjoin.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtnepend_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtend.Value))
  If xx Then
    dtend.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub tlbtnaddpost_Click()

  Dim hForm As FmAddVariableGrid

  hForm = New FmAddVariableGrid("fldcategory", "tblusercategory")
  hForm.ShowModal
  cmbpost.List = GetUserPostList()

End

Private Function GetUserPostList() As String[]

  Dim res As Result
  Dim xxx As String[]

  res = modDatabase.$myConn.Exec("select fldcategory from tblusercategory")
  xxx = modControlSub.GetDirectFillresult(res)
  Return xxx

End

Public Sub btntype_Click()

  Dim hForm As FmAddVariable1

  hForm = New FmAddVariable1("fldcategory", "fldparam", "Employee", "tblmiscategory")
  hForm.ShowModal
  cmbcategory.List = GetUserTitleList()

End

Private Function GetUserTitleList() As String[]

  Dim res As Result
  Dim xxx As String[]

  res = modDatabase.$myConn.Exec("select fldcategory from tblmiscategory where fldparam=&1", "Employee")
  xxx = modControlSub.GetDirectFillresult(res)
  Return xxx

End

Public Sub btnrelation_Click()

  Dim hForm As FmAddVariableGrid

  hForm = New FmAddVariableGrid("flditem", "tblrelations")
  hForm.ShowModal
  modBasic.FillRelationList()
  cmbrelation.List = modBasic.$RelationList

End

Public Sub tlbtnadddepart_Click()

  Dim xpath As String

  If Dialog.OpenFile() Then Return
  xpath = Dialog.Path
  If Exist(xpath) Then
    modSettings.SaveSettingsToFile("RegistrationForm/PostingList", xpath)
    cmbdepart.List = modString.GetStringArrayFromFile(xpath)
  Endif

End

Public Sub btnrank_Click()

  Dim xpath As String

  If Dialog.OpenFile() Then Return
  xpath = Dialog.Path
  If Exist(xpath) Then
    modSettings.SaveSettingsToFile("RegistrationForm/RankList", xpath)
    cmbrank.List = modString.GetStringArrayFromFile(xpath)
  Endif

End

Public Sub btnunit_Click()

  Dim xpath As String

  If Dialog.OpenFile() Then Return
  xpath = Dialog.Path
  If Exist(xpath) Then
    modSettings.SaveSettingsToFile("RegistrationForm/UnitList", xpath)
    cmbunit.List = modString.GetStringArrayFromFile(xpath)
  Endif

End

Public Sub btngenerate_Click()

  Dim xval As String
  Dim res As Result

  xval = "EMP-" & UCase(Left(modPassword.GetRandomPassword(), 6))
  res = modDatabase.$myConn.Exec("select fldptcode from tblstafflist where fldptcode=&1", xval)
  If Not res.Available Then
    txtpatcode.Text = xval
  Endif

End

Public Sub btnpic_Click()

  Dim hForm As FmPersonImage

  If txtpatcode.Text Then
    If cmbstatus.Text = "Active" Then
      hForm = New FmPersonImage("Staff", Trim(txtpatcode.Text), True)
    Else '
      hForm = New FmPersonImage("Staff", Trim(txtpatcode.Text), False)
    Endif
    hForm.ShowModal
  Endif

End

Public Sub btndocs_Click()

  Dim hForm As FmOfficeDocs

  If txtpatcode.Text Then
    If cmbstatus.Text = "Active" Then
      hForm = New FmOfficeDocs("Employee", Trim(txtpatcode.Text), True, "")
    Else
      hForm = New FmOfficeDocs("Employee", Trim(txtpatcode.Text), False, "")
    Endif
    hForm.ShowModal
  Endif

End

Public Sub cmbsex_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbsex)
  modFillContainer.RestrictComboToContent(cmbsex)

End

Public Sub cmblood_KeyRelease()

  modFillContainer.AutoFillComboBox(cmblood)
  modFillContainer.RestrictComboToContent(cmblood)

End

Public Sub cmbpost_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbpost)
  modFillContainer.RestrictComboToContent(cmbpost)

End

Public Sub cmbstatus_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbstatus)
  modFillContainer.RestrictComboToContent(cmbstatus)

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub cmbdiscount_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdiscount)
  modFillContainer.RestrictComboToContent(cmbdiscount)

End

Public Sub cmbdistrict_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdistrict)
  modFillContainer.RestrictComboToContent(cmbdistrict)

End

Public Sub txtaddfir_KeyRelease()

  modFillContainer.AutoFillComboBox(txtaddfir)
  modFillContainer.RestrictComboToContent(txtaddfir)

End

Public Sub cmbrelation_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbrelation)
  modFillContainer.RestrictComboToContent(cmbrelation)

End

Public Sub txtsearch_Change()

  FillStaffGrid()

End

Public Sub txtstaffcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtstaffname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtstfsurnam_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtpatcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtpatno_KeyPress()

  If Key.Code = Key.Down Then
    If Not txtpatno.Text Then
      txtpatno.Text = PatSearch("PatientNo")
    Endif
  Endif

End

Public Sub btnfamily_Click()

  Dim hForm As FmStaffFamily

  If txtpatcode.Text Then
    If MMain.$WebReport = "Multiple" Then
      hForm = New FmStaffFamily(txtpatcode.Text, True)
    Else
      hForm = New FmStaffFamily(txtpatcode.Text, False)
    Endif
    hForm.ShowModal
  Endif

End

Public Sub btnaccount_Click()

  Dim encid As String
  Dim hForm As FmPatAccEdit

  If txtpatno.Text Then
    encid = modPatient.GetLastEncoutnerFromPatNo(Trim(txtpatno.Text))
    If encid Then
      hForm = New FmPatAccEdit(encid)
      hForm.ShowModal
    Endif
  Endif

End
