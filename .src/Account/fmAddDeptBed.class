' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmboxygen.List = ["Present"]
  cmbventilator.List = ["Present"]
  cmbother.List = ["Present"]
  cmbdepart.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flddept as col from tbldepartment where fldcateg=&1", "Patient Ward"))
  cmbpackage.List = modNonMedical.FillDiscountCombo()

End

Public Sub cmbpackage_Click()

  cmbbillmode.Text = modNonMedical.GetDiscBindBillMode(cmbpackage.Text)

End

Public Sub cmbpackage_LostFocus()

  Dim xList As String[]

  cmbitem.Clear()
  cmbconsult.Clear()
  If cmbbillmode.Text Then
    xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
    cmbitem.List = xList
    cmbconsult.List = xList
    cmbexitcharge.List = xList
  Endif

End

Public Sub btnrefresh_Click()

  ShowGridView()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmbdepart_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdepart)
  modFillContainer.RestrictComboToContent(cmbdepart)

End

Public Sub cmbpackage_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbpackage)
  modFillContainer.RestrictComboToContent(cmbpackage)

End

Public Sub cmbitem_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)

End

Public Sub cmbconsult_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbconsult)
  modFillContainer.RestrictComboToContent(cmbconsult)

End

Public Sub cmbexitcharge_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbexitcharge)
  modFillContainer.RestrictComboToContent(cmbexitcharge)

End

Private Sub BlankAllControl()

  cmbpackage.Text = ""
  cmbbillmode.Text = ""
  cmbitem.Text = ""
  cmbconsult.Text = ""
  cmbexitcharge.Text = ""
  cmboxygen.Text = ""
  cmbventilator.Text = ""
  cmbother.Text = ""

End

Public Sub ShowGridView()

  Dim sql As String

  If cmbdepart.Text Then
    sql = "select fldid,flddisctype,fldbedcharge,fldconsultcharge,fldoxyport,fldventilator,fldother,fldbillingmode,fldexititem from tbldepartbedcharge where flddept=&1"
    $rData = modDatabase.$myConn.Exec(sql, cmbdepart.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

    With GridView1
      .Columns[0].Width = 1
      .Columns[1].Width = 125 * modBasic.$AppWidthRatio
      .Columns[2].Width = 200 * modBasic.$AppWidthRatio
      .Columns[3].Width = 200 * modBasic.$AppWidthRatio
      .Columns[4].Width = 100 * modBasic.$AppWidthRatio
      .Columns[5].Width = 100 * modBasic.$AppWidthRatio
      .Columns[6].Width = 100 * modBasic.$AppWidthRatio
      .Columns[7].Width = 1
      .Columns[8].Width = 100 * modBasic.$AppWidthRatio

      .Columns[1].Text = "Package"
      .Columns[2].Text = "Bed Charges"
      .Columns[3].Text = "Consultation"
      .Columns[4].Text = "Oxygen"
      .Columns[5].Text = "Ventilator"
      .Columns[6].Text = "Other"
      .Columns[8].Text = "Exit Charge"
    End With

  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Menu()

  mnufile.Popup

End

Public Sub mnudelete_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete("tbldepartbedcharge", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    ShowGridView()
    Balloon.Delete(("Variable deleted"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result

  If cmbdepart.Text Then
    res = modDatabase.$myConn.Create("tbldepartbedcharge")
    res["flddept"] = cmbdepart.Text
    If chkdefault.Value = True Then
      res["fldoxyport"] = ""
      res["fldventilator"] = ""
      res["fldother"] = ""
    Else
      res["fldoxyport"] = cmboxygen.Text
      res["fldventilator"] = cmbventilator.Text
      res["fldother"] = cmbother.Text
    Endif
    res["flddisctype"] = cmbpackage.Text
    res["fldbillingmode"] = cmbbillmode.Text
    res["fldbedcharge"] = cmbitem.Text
    res["fldconsultcharge"] = cmbconsult.Text
    res["fldexititem"] = cmbexitcharge.Text
    res.Update()
    ShowGridView()
    BlankAllControl()
    Balloon.Info(("Information added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    cmbbillmode.SetFocus
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count Then
    res = modDatabase.$myConn.Edit("tbldepartbedcharge", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["flddept"] = cmbdepart.Text
    If chkdefault.Value = True Then
      res["fldoxyport"] = ""
      res["fldventilator"] = ""
      res["fldother"] = ""
    Else
      res["fldoxyport"] = cmboxygen.Text
      res["fldventilator"] = cmbventilator.Text
      res["fldother"] = cmbother.Text
    Endif
    res["flddisctype"] = cmbpackage.Text
    res["fldbillingmode"] = cmbbillmode.Text
    res["fldbedcharge"] = cmbitem.Text
    res["fldconsultcharge"] = cmbconsult.Text
    res["fldexititem"] = cmbexitcharge.Text
    res.Update()
    ShowGridView()
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "Bed Charges", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Click()

  If cmbdepart.Text Then
    cmbpackage.Text = GridView1[GridView1.Row, 1].Text
    cmbbillmode.Text = GridView1[GridView1.Row, 7].Text
    cmbitem.Text = GridView1[GridView1.Row, 2].Text
    cmbconsult.Text = GridView1[GridView1.Row, 3].Text
    cmboxygen.Text = GridView1[GridView1.Row, 4].Text
    cmbventilator.Text = GridView1[GridView1.Row, 5].Text
    cmbother.Text = GridView1[GridView1.Row, 6].Text
    cmbexitcharge.Text = GridView1[GridView1.Row, 8].Text
  Endif

End

Public Sub cmbitem_GotFocus()

  Dim xList As String[]

  If cmbbillmode.Text Then
    If Not cmbitem.Text Then
      If modBasic.$ItemListFormat = "GridView" Then
        xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
        If xList.Count Then
          cmbitem.Text = GridViewNew("Select Particulars", xList, False, cmbitem)
        Endif
      Endif
    Endif
  Endif

End

Public Sub cmbconsult_GotFocus()

  Dim xList As String[]

  If cmbbillmode.Text Then
    If Not cmbconsult.Text Then
      If modBasic.$ItemListFormat = "GridView" Then
        xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
        If xList.Count Then
          cmbconsult.Text = GridViewNew("Select Particulars", xList, False, cmbconsult)
        Endif
      Endif
    Endif
  Endif

End

Public Sub cmbexitcharge_GotFocus()

  Dim xList As String[]

  If cmbbillmode.Text Then
    If Not cmbexitcharge.Text Then
      If modBasic.$ItemListFormat = "GridView" Then
        xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
        If xList.Count Then
          cmbexitcharge.Text = GridViewNew("Select Particulars", xList, False, cmbexitcharge)
        Endif
      Endif
    Endif
  Endif

End

Public Sub chkdefault_Click()

  cmboxygen.Text = ""
  cmbventilator.Text = ""
  cmbother.Text = ""
  If chkdefault.Value = True Then
    cmboxygen.Enabled = False
    cmbventilator.Enabled = False
    cmbother.Enabled = False
  Else
    cmboxygen.Enabled = True
    cmbventilator.Enabled = True
    cmbother.Enabled = True
  Endif

End
