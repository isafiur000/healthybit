' Gambas class file

Private $billMode As String
Private $rDisc As Result
Private $ItemList As String[]

Public Sub _new(sName As String, sBillMode As String)
  
  txtdisclabel.Text = sName
  $billMode = sBillMode
  
End

Public Sub Form_Open()
  
  modGeneralMain.ArrangeFormCentre(Me)
  cmbtype.List = modNonMedical.BillItemCategory()
  ShowDeptValues()
  ShowDiscountException()
  
End

Public Sub Form_Resize()
  
  modGeneralMain.ShowSelectedFormMenu(Me)
  
End

Public Sub Form_Close()
  
  modGeneralMain.RecordFormExit(Me)
  
End

Public Sub Form_KeyRelease()
  
  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif
  
End

Private Sub ShowDeptValues()
  
  Dim res As Result
  
  res = modDatabase.$myConn.Exec("select fldlab,fldradio,fldproc,fldequip,fldservice,fldother,fldmedicine,fldsurgical,fldextra,fldregist from tbldiscount where fldtype=&1", txtdisclabel.Text)
  If res.Available Then
    txtlabper.Value = res["fldlab"]
    txtradioper.Value = res["fldradio"]
    txtprocper.Value = res["fldproc"]
    txtequipper.Value = res["fldequip"]
    txtserviceper.Value = res["fldservice"]
    txtotherper.Value = res["fldother"]
    txtmedper.Value = res["fldmedicine"]
    txtsurgper.Value = res["fldsurgical"]
    txtextraper.Value = res["fldextra"]
  Endif                                                           ''
  
End

Public Sub btneditall_Click()
  
  Dim res As Result
  
  res = modDatabase.$myConn.Edit("tbldiscount", "fldtype=&1", txtdisclabel.Text)
  If res.Available Then
    res["fldlab"] = txtlabper.Value
    res["fldradio"] = txtradioper.Value
    res["fldproc"] = txtprocper.Value
    res["fldequip"] = txtequipper.Value
    res["fldservice"] = txtserviceper.Value
    res["fldother"] = txtotherper.Value
    res["fldmedicine"] = txtmedper.Value
    res["fldsurgical"] = txtsurgper.Value
    res["fldextra"] = txtextraper.Value
    res.Update
    Balloon.Info(("Information saved"), btneditall)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  
End

Public Sub cmbtype_KeyRelease()
  
  modFillContainer.AutoFillComboBox(cmbtype)
  modFillContainer.RestrictComboToContent(cmbtype)
  
End

Private Function GetComboList() As String[]
  
  Dim xxx As String[]
  Dim xxx1 As String[]
  Dim xxx2 As String[]
  
  Select cmbtype.Text
    Case "Medicines", "Surgicals", "Extra Items"
      xxx = New String[]
      If cmbtype.Text = "Medicines" Then
        xxx1 = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldbrandid as col from tblmedbrand"))
        If $billMode Then 
          xxx2 = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname from tblstockrate where fldbillingmode=&1 and flddrug in(select flddrug from tbldrug)", $billMode))
        Endif 
      Else If cmbtype.Text = "Surgicals" Then
        xxx1 = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldbrandid as col from tblsurgbrand"))
        If $billMode Then 
          xxx2 = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname from tblstockrate where fldbillingmode=&1 and flddrug in(select fldsurgid from tblsurgicals)", $billMode))
        Endif 
      Else If cmbtype.Text = "Extra Items" Then
        xxx1 = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldbrandid as col from tblextrabrand"))
        If $billMode Then 
          xxx2 = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname from tblstockrate where fldbillingmode=&1 and flddrug in(select fldextraid from tblextrabrand)", $billMode))
        Endif 
      Endif 
      If xxx1.Count Then 
        xxx.Insert(xxx1)
      Endif 
      If $billMode And If xxx2.Count Then 
        xxx.Insert(xxx2)
      Endif 
      
    Case Else 
      If $billMode Then 
        xxx = modNonMedical.NonStockBillingItemArray(cmbtype.Text, $billMode)
      Else 
        xxx = modNonMedical.NonStockBillingItemArray(cmbtype.Text, "%")
      Endif 
  End Select
  
  Return xxx
  
End

Public Sub cmbtype_Click()
  
  $ItemList = New String[]
  cmbitem.List = GetComboList()
  
End

Public Sub cmbitem_KeyRelease()
  
  modFillContainer.AutoFillComboBox(cmbitem)
  modFillContainer.RestrictComboToContent(cmbitem)
  
End

Public Sub btnitemulti_Click()
  
  Dim xxx As String[]
  
  xxx = GridListView(("Select Particulars"), cmbitem.List)
  If xxx And If xxx.Count Then
    $ItemList.Insert(xxx)
  Endif
  
End

Public Sub txtexception_GotFocus()
  
  If cmbitem.Text Then
    $ItemList.Add(cmbitem.Text)
  Endif
  
End

Public Sub btnaddexcept_Click()
  
  Dim res As Result
  Dim xval As String
  
  If $ItemList.Count Then
    For Each xval In $ItemList
      res = modDatabase.$myConn.Create("tblcustdiscount")
      res["fldtype"] = txtdisclabel.Text
      res["flditemname"] = xval
      res["flditemtype"] = cmbtype.Text
      res["fldpercent"] = txtexception.Text
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res.Update
    Next
    ShowDiscountException()
    cmbtype.Text = ""
    cmbitem.Clear()
    cmbitem.Text = ""
    txtexception.Value = 0
    cmbtype.SetFocus
  Endif
  
End

Private Sub ShowDiscountException()
  
  Dim Column As Integer
  Dim fld As ResultField
  
  $rDisc = modDatabase.$myConn.Exec("select fldid,flditemtype,flditemname,fldpercent from tblcustdiscount where fldtype=&1", txtdisclabel.Text)
  
  GridView1.Clear
  GridView1.Columns.Count = $rDisc.Fields.Count
  GridView1.Rows.Count = $rDisc.Count
  
  For Each $rDisc
    Column = 0
    For Each fld In $rDisc.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rDisc.Index, Column)
      GridView1[$rDisc.Index, Column].Text = $rDisc[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0
  
  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 350 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    
    .Columns[1].Text = "Category"
    .Columns[2].Text = "ItemName"
    .Columns[3].Text = "Disc%"
  End With
  
End

Public Sub GridView1_Menu()
  
  mnufile.Popup
  
End

Public Sub mnudelete_Click()
  
  If GridView1.Rows.Selection.Count > 0 Then
    modDatabase.$myConn.Delete("tblcustdiscount", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    ShowDiscountException()
    Balloon.Delete(("Information deleted"), GridView1)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif
  
End

Public Sub GridView1_Click()
  
  Dim res As Result
  Dim Row As Integer
  Dim xval As Float
  
  If GridView1.Column = 3 Then
    Row = GridView1.Row
    xval = InputValue(("Enter Discount Precentage"), "Custom Discount", GridView1[GridView1.Row, 3].Text)
    res = modDatabase.$myConn.Edit("tblcustdiscount", "fldid=&1", GridView1[GridView1.Row, 0].Text)
    res["fldpercent"] = xval
    res.Update
    ShowDiscountException()
    GridView1.Row = Row
  Endif
  
End
