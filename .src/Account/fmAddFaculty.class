' Gambas class file

Private $varList As String[]
Private $rData As Result
Private $aMyFields As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  $varList = modNonMedical.GetGroupNameAccount()
  FilVarList()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create("tblmasterdept")
    res["fldmaster"] = Trim(txtcategory.Text)
    res["fldgroup"] = Trim(txtvariable.Text)
    res.Update()
    SHowSelectGrid()
    txtvariable.Text = ""
    Balloon.Info(("Variable added"), btnOK)
    Balloon.Delay = modBasic.$BalloonDelay
    txtcategory.SetFocus
  Endif

End

Public Sub btnrefresh_Click()

  SHowSelectGrid()

End

Private Sub SHowSelectGrid()

  Dim sql As String

  If txtcategory.Text Then
    sql = "select fldmaster,fldgroup from tblmasterdept where fldmaster=&1"
    $rData = modDatabase.$myConn.Exec(sql, txtcategory.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    GetColumnItemList()

    With GridView1
      .Columns[0].Width = 225 * modBasic.$AppWidthRatio
      .Columns[1].Width = 225 * modBasic.$AppWidthRatio
      .Columns[0].Text = Label1.Text
      .Columns[1].Text = Label2.Text
    End With
  Endif

End

Private Sub FilVarList()

  Dim sql As String

  sql = "select fldmaster,fldgroup from tblmasterdept ORDER BY fldmaster"
  $rData = modDatabase.$myConn.Exec(sql)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  GetColumnItemList()

  With GridView1
    .Columns[0].Width = 225 * modBasic.$AppWidthRatio
    .Columns[1].Width = 225 * modBasic.$AppWidthRatio
    .Columns[0].Text = Label1.Text
    .Columns[1].Text = Label2.Text
  End With

End

Private Sub GetColumnItemList()

  Dim res As Result
  Dim aColList As String[]

  aColList = New String[]
  res = modDatabase.$myConn.Exec("select fldmaster,fldgroup from tblmasterdept")
  For Each res
    aColList.Add(res["fldgroup"])
  Next
  txtvariable.List = modString.GetRemainingArray($varList, aColList)
  txtcategory.List = modControlSub.GetDirectFillresultNoNull(modDatabase.$myConn.Exec("select distinct(fldmaster) as col from tblmasterdept"))

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = $rData[$aMyFields[Column]]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Public Sub btnCancel_Click()

  If GridView1.Rows.Selection.Count Then
    modDatabase.$myConn.Delete("tblmasterdept", "fldmaster=&1 and fldgroup=&2", GridView1[GridView1.Row, 0].Text, GridView1[GridView1.Row, 1].Text)
    If txtcategory.Text Then
      SHowSelectGrid()
    Else
      FilVarList()
    Endif
    Balloon.Delete(("Variable deleted"), btnCancel)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Click()

  txtcategory.Text = GridView1[GridView1.Row, 0].Text
  txtvariable.Text = GridView1[GridView1.Row, 1].Text

End

Public Sub txtcsv_Click()

  If Dialog.OpenFile() Then Return
  txtcsv.Text = Dialog.Path

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tblmasterdept", txtcsv.Text, xdelim)
    FilVarList()
  Endif

End
