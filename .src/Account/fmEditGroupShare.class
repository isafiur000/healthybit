' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $SelMode As String

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  cmbcategory.List = modNonMedical.BillItemCategory()
  cmbusertype.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(flditem) as col from tblprocedureuser"))

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Private Sub ClearCOntrols()

  txtid.Value = 0
  cmbusertype.Text = ""
  cmbcategory.Text = ""
  txtmixper.Value = 0
  txtitem.Text = ""
  txtchange.Text = ""
  txtchanreason.Text = ""
  txtpatname.Text = ""
  txtagesex.Text = ""

End

Public Sub btnconsult_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Sharing User"), modGeneral.CategoricalUserList("fldpayable"))
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif

End

Public Sub btnshowencounter_Click()

  ClearCOntrols()
  txtinvoice.Text = ""
  btnconsult.Tag = ""
  btnconsult.Text = ""
  If txtencid.Text Then
    $SelMode = "Encounter"
    $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,flditemid,flditemid,fldcategory,fldusertype,fldvalue,fldvalue,fldmixper,fldchange,fldchreason from tblpatgenshare where fldencounterval=&1 and fldsave=&2", Trim(txtencid.Text), True)                       '
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ShowGridView()
  Endif

End

Public Sub btnshowinvoice_Click()

  ClearCOntrols()
  txtencid.Text = ""
  btnconsult.Tag = ""
  btnconsult.Text = ""
  If txtinvoice.Text Then
    $SelMode = "Invoice"
    $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,flditemid,flditemid,fldcategory,fldusertype,fldvalue,fldvalue,fldmixper,fldchange,fldchreason from tblpatgenshare where flditemid in(select fldid from tblpatbilling where fldbillno=&1) and fldsave=&2", Trim(txtinvoice.Text), True)                       '
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ShowGridView()
  Endif

End

Public Sub btnshowuser_Click()

  ClearCOntrols()
  txtencid.Text = ""
  txtinvoice.Text = ""
  If btnconsult.Tag Then
    $SelMode = "UserID"
    $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,flditemid,flditemid,fldcategory,fldusertype,fldvalue,fldvalue,fldmixper,fldchange,fldchreason from tblpatgenshare where fldvalue=&1 and fldsave=&2", btnconsult.Tag, True)                       '
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    ShowGridView()
  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 2 Then
    GridView1.Data.Text = modNonMedical.GetBillingNoFromID($rData[$aMyFields[Column]])
  Else If Column = 3 Then
    GridView1.Data.Text = modNonMedical.GetBillingItemFromID($rData[$aMyFields[Column]])
  Else If Column = 7 Then
    GridView1.Data.Text = modGeneral.GetUserFullName($rData[$aMyFields[Column]])
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Sub ShowGridView()

  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 100 * modBasic.$AppWidthRatio
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 175 * modBasic.$AppWidthRatio
    .Columns[4].Width = 1
    .Columns[5].Width = 125 * modBasic.$AppWidthRatio
    .Columns[6].Width = 1
    .Columns[7].Width = 200 * modBasic.$AppWidthRatio
    .Columns[8].Width = 50 * modBasic.$AppWidthRatio
    .Columns[9].Width = 75 * modBasic.$AppWidthRatio
    .Columns[10].Width = 100 * modBasic.$AppWidthRatio

    .Columns[1].Text = "Encounter"
    .Columns[2].Text = "Invoice"
    .Columns[3].Text = "Particulars"
    .Columns[5].Text = "User Type"
    .Columns[7].Text = "User Name"
    .Columns[8].Text = "Frac(%)"
    .Columns[9].Text = "Extra"
    .Columns[10].Text = "Reason"
  End With

End

Public Sub GridView1_Click()

  If GridView1.Rows.Selection.Count Then
    txtid.Value = GridView1[GridView1.Row, 0].Text
    txtencid.Text = GridView1[GridView1.Row, 1].Text
    txtinvoice.Text = GridView1[GridView1.Row, 2].Text
    txtitem.Text = GridView1[GridView1.Row, 3].Text
    cmbcategory.Text = GridView1[GridView1.Row, 4].Text
    cmbusertype.Text = GridView1[GridView1.Row, 5].Text
    btnconsult.Tag = GridView1[GridView1.Row, 6].Text
    btnconsult.Text = modGeneral.GetUserFullName(btnconsult.Tag)
    txtmixper.Value = GridView1[GridView1.Row, 8].Text
    txtchange.Value = GridView1[GridView1.Row, 9].Text
    txtchanreason.Text = GridView1[GridView1.Row, 10].Text

    If txtencid.Text Then
      txtpatname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
      txtagesex.Text = modPatient.GetPatientAgeString(Trim(txtencid.Text), Now()) & "/" & modPatient.GetPatientSex(Trim(txtencid.Text))
    Endif
  Endif

End

Public Sub btnupdate_Click()

  Dim res As Result
  Dim Row As Integer

  If txtencid.Text And If cmbusertype.Text And If cmbcategory.Text Then
    If btnconsult.Tag Then
      If txtid.Value Then
        Row = GridView1.Row
        If Message.Question("Are you sure?", ("NO"), ("OK")) = 2 Then
          res = modDatabase.$myConn.Edit("tblpatgenshare", "fldid=&1 and fldsave=&2", txtid.Value, True)
          res["fldcategory"] = cmbcategory.Text
          res["fldusertype"] = cmbusertype.Text
          res["fldvalue"] = btnconsult.Tag
          If txtmixper.Value <= 100 Then
            res["fldmixper"] = txtmixper.Value
          Endif
          res["fldchange"] = txtchange.Value
          res["fldchreason"] = Trim(txtchanreason.Text)
          res.Update
          If $SelMode = "Encounter" Then
            btnshowencounter_Click()
          Else If $SelMode = "Invoice" Then
            btnshowinvoice_Click()
          Else If $SelMode = "UserID" Then
            btnshowuser_Click()
          Endif
        Endif
        GridView1.Row = Row
      Endif
    Endif
  Endif

End
