' Gambas class file

Private $rData1 As Result
Private $aMyFields1 As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False", False)
  rbmanual.Value = True

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

'''---------------------------------- Reporting List ----------------------------
Public Sub btnrefresh_Click()

  Inc Application.Busy
  FillReportList()
  Dec Application.Busy
  GridView1.SetFocus

End

Private Sub FillReportList()

  If rbmanual.Value = True Then
    $rData1 = modDatabase.$myConn.Exec("select fldid,fldencounterval,fldencounterval,fldbillid,fldbillid,flditemname,fldsharename,fldshareuser,fldtotalper,flduserper,flduseramt,fldusertax from tblpatusershares where fldactive=&1", "Waiting")
  Else If rbsetting.Value = True Then
    $rData1 = modDatabase.$myConn.Exec("select fldid,fldencounterval,fldencounterval,flditemid,fldcategory,flditemid,fldusertype,fldvalue,fldreport from tblpatgenshare where fldactive=&1 and fldsave=&2", "Waiting", True)
  Endif

  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView1, $rData1, $aMyFields1)
  With GridView1
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 175 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 125 * modBasic.$AppWidthRatio
    .Columns[5].Width = 200 * modBasic.$AppWidthRatio
    .Columns[6].Width = 100 * modBasic.$AppWidthRatio
    .Columns[7].Width = 125 * modBasic.$AppWidthRatio
    If rbmanual.Value = True Then
      .Columns[8].Width = 75 * modBasic.$AppWidthRatio
      .Columns[9].Width = 75 * modBasic.$AppWidthRatio
      .Columns[10].Width = 100 * modBasic.$AppWidthRatio
      .Columns[11].Width = 75 * modBasic.$AppWidthRatio
    Else If rbsetting.Value = True Then
      .Columns[8].Width = 200 * modBasic.$AppWidthRatio
    Endif

    .Columns[1].Text = "EncID"
    .Columns[2].Text = "Name"
    .Columns[3].Text = "ItemID"
    .Columns[4].Text = "Category"
    .Columns[5].Text = "Particular"
    .Columns[6].Text = "UserType"
    .Columns[7].Text = "UserName"
    If rbmanual.Value = True Then
      .Columns[8].Text = "Total%"
      .Columns[9].Text = "User%"
      .Columns[10].Text = "Amount"
      .Columns[11].Text = "TDS"
    Else If rbsetting.Value = True Then
      .Columns[8].Text = "Comment"
    Endif

  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData1.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 2 Then
    GridView1.Data.Text = modPatient.GetPatientNameByEnc($rData1[$aMyFields1[Column]], modDatabase.$syConn)
  Else If Column = 4 Then
    If rbmanual.Value = True Then
      GridView1.Data.Text = modNonMedical.GetItemTypeFrombillID($rData1[$aMyFields1[Column]])
    Else If rbsetting.Value = True Then
      GridView1.Data.Text = $rData1[$aMyFields1[Column]]
    Endif
  Else If Column = 5 Then
    If rbmanual.Value = True Then
      GridView1.Data.Text = $rData1[$aMyFields1[Column]]
    Else If rbsetting.Value = True Then
      GridView1.Data.Text = modNonMedical.GetBillingItemFromID($rData1[$aMyFields1[Column]])
    Endif
  Else
    GridView1.Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

''------------------------------------ individual patient tests ------------------------
Public Sub mnusharesaved_Click()

  Dim hForm As FmDiffShare

  If modBasic.$BillEditPayable = "Yes" Then
    If GridView1.Rows.Selection.Count Then
      If Not GridView1[GridView1.Row, 10].Text Then
        hForm = New FmDiffShare(GridView1[GridView1.Row, 0].Text)
        hForm.ShowModal
      Endif
    Endif
  Endif

End

Public Sub GridView1_Menu()

  mnudo.Popup

End

Public Sub mnuactive_Click()

  Dim res As Result

  If rbmanual.Value = True Then
    res = modDatabase.$myConn.Edit("tblpatusershares", "fldid=&1", GridView1[GridView1.Row, 0].Text)
  Else If rbsetting.Value = True Then
    res = modDatabase.$myConn.Edit("tblpatgenshare", "fldid=&1 and fldsave=&2", GridView1[GridView1.Row, 0].Text, True)
  Endif
  res["fldactive"] = "Active"
  res.Update
  FillReportList()

End

Public Sub rbmanual_Click()

  FillReportList()

End

Public Sub rbsetting_Click()

  FillReportList()

End
