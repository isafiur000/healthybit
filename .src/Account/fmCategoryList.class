' Gambas class file

Private $packList As String[]
Private $rData As Result

Public Sub Form_Open()

  Me.Center
  $packList = modNonMedical.FillDiscountCombo()
  cmbpackage.List = $packList
  ShowCategoryGrid()

End

Public Sub btnlist_Click()

  Dim hForm As FmAddVarTwo
  Dim xx As String[]

  xx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldcategory as col from tblcategorylist"))
  hForm = New FmAddVarTwo("fldcategory", "fldpackage", "tblschemecategory", xx, $packList)
  hForm.Tag = "Scheme Category"
  hForm.ShowModal

End

Private Sub ClearAll()

  txtcategory.Text = ""
  cmbpackage.Text = ""
  chkrank.Value = False
  chkunit.Value = False

End

Public Sub btnadd_Click()

  Dim res As Result

  If txtcategory.Text Then
    res = modDatabase.$myConn.Create("tblcategorylist")
    res["fldcategory"] = Trim(txtcategory.Text)
    res["fldpackage"] = cmbpackage.Text
    res["fldhiderank"] = chkrank.Value
    res["fldhideunit"] = chkunit.Value
    res.Update
    Balloon.Info(("Information saved"), btnadd)
    Balloon.Delay = modBasic.$BalloonDelay
    ShowCategoryGrid()
    ClearAll()
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If txtcategory.Text Then
    res = modDatabase.$myConn.Edit("tblcategorylist", "fldcategory=&1", Trim(txtcategory.Text))
    res["fldpackage"] = cmbpackage.Text
    res["fldhiderank"] = chkrank.Value
    res["fldhideunit"] = chkunit.Value
    res.Update
    Balloon.Info(("Information updated"), btnedit)
    Balloon.Delay = modBasic.$BalloonDelay
    ShowCategoryGrid()
  Endif

End

Private Sub ShowCategoryGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  sql = "select fldcategory,fldpackage,fldhiderank,fldhideunit from tblcategorylist"
  $rData = modDatabase.$myConn.Exec(sql)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 200 * modBasic.$AppWidthRatio
    .Columns[1].Width = 200 * modBasic.$AppWidthRatio
    .Columns[2].Width = 50 * modBasic.$AppWidthRatio
    .Columns[3].Width = 50 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Category"
    .Columns[1].Text = "Package"
    .Columns[2].Text = "Rank"
    .Columns[3].Text = "Unit"
  End With

End

Public Sub GridView1_Click()

  txtcategory.Text = GridView1[GridView1.Row, 0].Text
  cmbpackage.Text = GridView1[GridView1.Row, 1].Text
  If GridView1[GridView1.Row, 2].Text Then
    chkrank.Value = True
  Else
    chkrank.Value = False
  Endif
  If GridView1[GridView1.Row, 3].Text Then
    chkunit.Value = True
  Else
    chkunit.Value = False
  Endif

End

Public Sub GridView1_Menu()

  mnuhide.Popup()

End

Public Sub mnudelete_Click()

  If txtcategory.Text Then
    modDatabase.$myConn.Delete("tblcategorylist", "fldcategory=&1", Trim(txtcategory.Text))
    ShowCategoryGrid()
    ClearAll()
  Endif

End
