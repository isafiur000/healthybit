' Gambas class file

Private $RepoStr As String

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Column")

  If MMain.$WebReport = "Multiple" Then
    If modBasic.$HospCode Then
      ' cmblocation.Text = "Hospital"
      cmblocation.Text = modDataRepo.$RepositoryMode
      cmbvalue.Text = modBasic.$HospCode
      pnlocat.Enabled = False
    Else
      cmblocation.List = ["Hospital", "Municipality", "Category", "District", "Province"]
    Endif
  Else
    pnlocat.Visible = False
  Endif

  cmbcategory.List = ["Diagnostic Tests", "General Services", "Procedures", "Equipment", "Radio Diagnostics", "Other Items"]
  cmbstatus.List = ["Active", "Inactive"]
  cmbmode.List = modNonMedical.FillCashModeCombo()
  cmbmode.Add("%")
  cmbmode.Text = ""

  cmbsection.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldsection as col from tblbillsection"))
  txtunit.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemunit from tblcurrency"))
  cmbtaxcode.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldgroup as col from tbltaxgroup"))
  ShowCostGrid()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub cmblocation_Click()

  cmbvalue.Clear()
  If cmblocation.Text Then
    cmbvalue.List = modDataRepo.GetRepoValueListType(cmblocation.Text)
  Endif

End

''--------------------------- Controls ---------------------------
Public Sub txtcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub cmbitemcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub txtunit_KeyRelease()

  modFillContainer.AutoFillComboBox(txtunit)
  modFillContainer.RestrictComboToContent(txtunit)

End

Public Sub cmbcomp_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcomp)
  modFillContainer.RestrictComboToContent(cmbcomp)

End

Public Sub cmbcomp_LostFocus()

  modFillContainer.RestrictCombo(cmbcomp)

End

Public Sub cmbstatus_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbstatus)
  modFillContainer.RestrictComboToContent(cmbstatus)

End

Public Sub cmbtaxcode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtaxcode)
  modFillContainer.RestrictComboToContent(cmbtaxcode)

End

Public Sub cmbstatus_LostFocus()

  modFillContainer.RestrictCombo(cmbstatus)

End

Public Sub cmbmode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmode)

End

Private Sub CLearBoxAll()

  cmbitemcode.Text = ""
  txtcost.Value = 0
  txtname.Text = ""
  cmbcomp.Text = ""
  cmbsection.Text = ""
  txtcode.Text = ""
  txtunit.Text = ""
  cmbtaxcode.Text = ""
  chkeditdisc.Value = False
  chkeditrate.Value = False
  chkpayble.Value = False
  chkentryself.Value = False
  txthospshare.Value = 0
  txtinstrushare.Value = 0
  txtdepartshare.Value = 0
  txtanesthshare.Value = 0
  txtothershare.Value = 0

End

''--------------------------------- Grid view --------------------------
Private Sub SHowDataSelect(sItem As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select flditemtype,flditemcost,fldtarget,fldgroup,fldcode,fldstatus,fldbillitem,fldreport,flditemunit,fldtaxcode,fldeditrate,fldeditdisc,fldpayable,fldpatentry,flditemcode,fldhospitalshare,fldinstrumshare,flddepartshare,fldanesthshare,fldothershare from tblservicecost where flditemname=&1", sItem)
  If res.Available Then
    txtname.Text = sItem
    cmbcategory.Text = res["flditemtype"]
    txtabbv.Text = res["flditemcode"]
    txtcost.Value = res["flditemcost"]
    txtunit.Text = res["flditemunit"]
    cmbcomp.Text = res["fldtarget"]
    cmbmode.Text = res["fldgroup"]
    txtcode.Text = res["fldcode"]
    cmbstatus.Text = res["fldstatus"]
    cmbitemcode.Text = res["fldbillitem"]
    cmbsection.Text = res["fldreport"]
    cmbtaxcode.Text = res["fldtaxcode"]
    If res["fldeditrate"] Then
      chkeditrate.Value = res["fldeditrate"]
    Else
      chkeditrate.Value = False
    Endif
    If res["fldeditdisc"] Then
      chkeditdisc.Value = res["fldeditdisc"]
    Else
      chkeditdisc.Value = False
    Endif
    If res["fldpayable"] Then
      chkpayble.Value = res["fldpayable"]
    Else
      chkpayble.Value = False
    Endif
    If res["fldpatentry"] Then
      chkentryself.Value = res["fldpatentry"]
    Else
      chkentryself.Value = False
    Endif

    If res["fldhospitalshare"] Then
      txthospshare.Value = res["fldhospitalshare"]
    Else
      txthospshare.Value = 0
    Endif
    If res["fldinstrumshare"] Then
      txtinstrushare.Value = res["fldinstrumshare"]
    Else
      txtinstrushare.Value = 0
    Endif
    If res["flddepartshare"] Then
      txtdepartshare.Value = res["flddepartshare"]
    Else
      txtdepartshare.Value = 0
    Endif
    If res["fldanesthshare"] Then
      txtanesthshare.Value = res["fldanesthshare"]
    Else
      txtanesthshare.Value = 0
    Endif
    If res["fldothershare"] Then
      txtothershare.Value = res["fldothershare"]
    Else
      txtothershare.Value = 0
    Endif

  Endif

End

Public Sub GridView1_Click()

  If GridView1.Current.Picture = Picture["icons/coll2.png"] Then
    ShowCostGridDetail(GridView1.Current.Text)
  Else If GridView1.Current.Picture = Picture["icons/coll3.png"] Then
    CLearBoxAll()
    SHowDataSelect(GridView1.Current.Text)
  Endif

End

Public Sub btnrefresh_Click()

  ShowCostGrid()

End

Private Sub ShowCostGrid()

  Dim res3 As Result
  Dim pic2 As Picture

  GridView1.Clear()
  GridView1.Columns.Count = 9
  DisplayGridFormat()

  pic2 = Picture["icons/coll2.png"]
  $RepoStr = modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
  res3 = modDatabase.$myConn.Exec("select distinct(fldbillitem) as col from tblservicecost where lower(fldbillitem) like &1" & $RepoStr & " ORDER BY fldbillitem", LCase(txtsearch.Text) & "%")
  If res3.Available Then
    For Each res3
      GridView1.Add(res3["col"], res3["col"], pic2)
    Next
  Endif

End

Private Sub ShowCostGridDetail(sKey As String)

  Dim pic3 As Picture

  Dim res4 As Result
  Dim l As Integer

  pic3 = Picture["icons/coll3.png"]
  res4 = modDatabase.$medConn.Exec("select flditemname,flditemunit,flditemcost,fldtarget,fldgroup,fldcode,fldstatus,fldbillitem,fldreport from tblservicecost where fldbillitem=&1" & $RepoStr & " ORDER BY flditemname ASC", sKey)
  If res4.Available Then
    res4.MoveFirst
    For l = 1 To res4.Count
      GridView1.Add(sKey & "@" & CStr(l) & "@" & res4["flditemname"], res4["flditemname"], pic3, sKey)
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][1] = GetCurrencyFormat(res4["flditemunit"])
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][2] = Format(res4["flditemcost"], gb.Fixed)
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][3] = res4["fldtarget"]
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][4] = res4["fldgroup"]
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][5] = res4["fldcode"]
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][6] = res4["fldstatus"]
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][7] = res4["fldbillitem"]
      GridView1[sKey & "@" & CStr(l) & "@" & res4["flditemname"]][8] = res4["fldreport"]
      res4.MoveNext
    Next
  Endif

End

Private Sub DisplayGridFormat()

  With GridView1
    .Columns[0].Width = 250 * modBasic.$AppWidthRatio
    .Columns[1].Width = 50 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio
    .Columns[4].Width = 125 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[6].Width = 1
    .Columns[7].Width = 1
    .Columns[8].Width = 100 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Particulars"
    .Columns[1].Text = "Unit"
    .Columns[2].Text = "Rate"
    .Columns[3].Text = "Comp/Charge"
    .Columns[4].Text = "RatePlan"
    .Columns[8].Text = "Section"
  End With

End

Private Function GetCurrencyFormat(sCurr As String) As String

  Dim xval As String

  If sCurr Then
    xval = sCurr
  Else
    xval = modBasic.$currency
  Endif
  Return xval

End

''-------------------------- Procedures ------------------------------------
Private Function ServListNo() As Long

  Dim res As Result
  Dim xx As Long

  res = modDatabase.$myConn.Exec("select MAX(fldid) as fldid from tblservicecost")
  If res!fldid Then
    xx = res!fldid + 1
  Else
    xx = 1
  Endif
  Return xx

End

Private Function GetTotalShare() As Float

  Dim xx As Float

  xx = txthospshare.Value + txtinstrushare.Value + txtdepartshare.Value + txtanesthshare.Value + txtothershare.Value
  Return xx

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If txtname.Text And If cmbcomp.Text And If cmbmode.Text Then
    If GetTotalShare() <= 100 Then
      txtname.Text = Trim(txtname.Text)
      res = modDatabase.$myConn.Create("tblservicecost")
      res["flditemname"] = String.UCaseFirst(txtname.Text)
      res["flditemcode"] = txtabbv.Text
      res["fldid"] = ServListNo()
      res["fldcode"] = txtcode.Text
      res["fldbillitem"] = cmbitemcode.Text
      res["flditemtype"] = cmbcategory.Text

      res["flditemcost"] = txtcost.Value
      res["flditemunit"] = Trim(txtunit.Text)
      res["fldgroup"] = cmbmode.Text
      res["fldreport"] = cmbsection.Text
      res["fldstatus"] = cmbstatus.Text
      res["fldtarget"] = cmbcomp.Text

      res["fldtaxcode"] = cmbtaxcode.Text
      res["fldeditrate"] = chkeditrate.Value
      res["fldeditdisc"] = chkeditdisc.Value
      res["fldpayable"] = chkpayble.Value
      res["fldpatentry"] = chkentryself.Value

      res["fldhospitalshare"] = txthospshare.Value
      res["fldinstrumshare"] = txtinstrushare.Value
      res["flddepartshare"] = txtdepartshare.Value
      res["fldanesthshare"] = txtanesthshare.Value
      res["fldothershare"] = txtothershare.Value

      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res.Update()
      ShowCostGrid()
      Balloon.Info(("Information saved"), btnBrOK)
      Balloon.Delay = modBasic.$BalloonDelay
      cmbitemcode.SetFocus
    Endif
  Endif

End

Public Sub mnudelete_Click()

  Dim res As Result

  If GridView1.Rows.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Exec("select COUNT(fldid) as cnt from tblpatbilling where flditemname=&1", GridView1.Current.Text)
      If res!cnt = 0 Then
        modDatabase.$myConn.Delete("tblservicecost", "flditemname=&1", GridView1.Current.Text)
        ShowCostGrid()
      Else
        Message.Warning(("Deletion not permitted"), ("OK"))
      Endif
    Endif
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim Row As Integer

  If txtname.Text And If cmbcomp.Text And If cmbmode.Text Then
    If GetTotalShare() <= 100 Then
      If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then

        res = modDatabase.$myConn.Edit("tblservicecost", "flditemname=&1", Trim(txtname.Text))
        If res.Available = True Then
          Row = GridView1.Row
          res["flditemcode"] = txtabbv.Text
          res["fldcode"] = txtcode.Text
          res["fldbillitem"] = cmbitemcode.Text
          res["flditemcost"] = txtcost.Value
          res["flditemunit"] = Trim(txtunit.Text)
          ' res["flditemtype"] = cmbcategory.Text

          res["fldgroup"] = cmbmode.Text
          res["fldreport"] = cmbsection.Text
          res["fldstatus"] = cmbstatus.Text
          res["fldtarget"] = cmbcomp.Text

          res["fldtaxcode"] = cmbtaxcode.Text
          res["fldeditrate"] = chkeditrate.Value
          res["fldeditdisc"] = chkeditdisc.Value
          res["fldpayable"] = chkpayble.Value
          res["fldpatentry"] = chkentryself.Value

          res["fldhospitalshare"] = txthospshare.Value
          res["fldinstrumshare"] = txtinstrushare.Value
          res["flddepartshare"] = txtdepartshare.Value
          res["fldanesthshare"] = txtanesthshare.Value
          res["fldothershare"] = txtothershare.Value

          res["flduserid"] = modBasic.$lbluser
          res["fldtime"] = Now()
          res["fldcomp"] = modBasic.$compID
          res["xyz"] = False
          res.Update()
          ShowCostGrid()
          GridView1.Row = Row
          Balloon.Info(("Information updated"), btnedit)
          Balloon.Delay = modBasic.$BalloonDelay
        Endif

      Endif
    Endif
  Endif

End

Public Sub btnBrOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnBrOK_Click()
  Endif

End

Public Sub btnBrOK_GotFocus()

  btnBrOK.Font.Bold = True

End

Public Sub btnBrOK_LostFocus()

  btnBrOK.Font.Bold = False

End

Public Sub btnaddunit_Click()

  Dim hForm As FmAddValString

  hForm = New FmAddValString("Currency", "flditemunit", "fldconversion", "tblcurrency")
  hForm.ShowModal
  txtunit.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemunit from tblcurrency"))

End

Public Sub btnaddtax_Click()

  Dim hForm As FmAddValString

  hForm = New FmAddValString("Tax Coding", "fldgroup", "fldtaxper", "tbltaxgroup")
  hForm.ShowModal
  cmbtaxcode.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldgroup as col from tbltaxgroup"))

End

Public Sub btndetail_Click()

  Dim hForm As FmServGroup

  If txtname.Text Then
    hForm = New FmServGroup("Cost Breakdown", txtname.Text)
    hForm.ShowModal
  Endif

End
