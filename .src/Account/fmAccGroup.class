' Gambas class file

Private $rData As Result

Public Sub Form_Open()
  
  modGeneralMain.ArrangeFormCentre(Me)
  cmbgroup.List = modNonMedical.GetGroupNameAccount()
  
End

Public Sub Form_Close()
  
  modGeneralMain.RecordFormExit(Me)
  
End

Public Sub Form_Resize()
  
  modGeneralMain.ShowSelectedFormMenu(Me)
  
End

Public Sub btnadd_Click()
  
  Dim hForm As FmSelectListAcc
  
  If cmbgroup.Text Then
    hForm = New FmSelectListAcc(("Select Items for the group"), "Service Grouping", cmbgroup.Text)
    hForm.ShowModal
    ShowGridData()
    cmbgroup.List = modNonMedical.GetGroupNameAccount()
  Endif
  
End

Private Sub ShowGridData()
  
  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField
  
  If cmbgroup.Text Then
    sql = "select flditemname,flditemtype,flditemname from tblreportgroup where fldgroup=&1 ORDER BY flditemname ASC"
    $rData = modDatabase.$myConn.Exec(sql, cmbgroup.Text)
    
    GridView1.Clear
    GridView1.Columns.Count = $rData.Fields.Count
    GridView1.Rows.Count = $rData.Count
    
    For Each $rData
      Column = 0
      For Each fld In $rData.Fields
        modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
        Column = Column + 1
      Next
    Next
    GridView1.Row = 0
    
    With GridView1
      .Rows.Height = modBasic.$AppGridRowHeight
      .Columns[0].Width = 1
      .Columns[1].Width = 120 * modBasic.$AppWidthRatio
      .Columns[2].Width = 450 * modBasic.$AppWidthRatio
      
      .Columns[1].Text = "Category"
      .Columns[2].Text = "Particulars"
    End With
    
  Endif
  
End

Public Sub btnrefresh_Click()
  
  ShowGridData()
  
End

Public Sub mnudel_Click()
  
  If GridView1.Rows.Selection.Count > 0 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblreportgroup", "flditemname=&1", GridView1[GridView1.Row, 0].Text)
      ShowGridData()
    Endif
  Endif
  
End

Public Sub GridView1_Menu()
  
  mnuhide.Popup
  
End

Public Sub Form_KeyRelease()
  
  If Key.Code = Key.Esc Then
    Me.Close
  Else
    modGeneralmain.GoToNextControlTab()
  Endif
  
End

Public Sub btnlist_Click()
  
  Dim xPath As String
  
  Inc Application.Busy
  xPath = modRepoMaster.ShowReportGroupListHTML()
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  
End

Public Sub btnduplicate_Click()
  
  Dim hForm As FRequest
  Dim res As Result
  
  res = modDatabase.$syConn.Exec("select flditemname,flditemtype,flditemname,count(flditemname) FROM tblreportgroup GROUP BY flditemname HAVING COUNT(flditemname)>&1", 1)                          ''
  hForm = New FRequest(res, ("Items Duplicate in Grouping List"), "AccGroup:Duplicates", False)
  hForm.ShowModal
  
End

Public Sub btnmissed_Click()
  
  Dim hForm As FRequest
  Dim res As Result
  Dim xx As String
  
  xx = InputCombo(("Select Item Category"), ("Missing Item List"), ["Service Related", "Inventory Related", "Mapped Items"], "", True)
  If xx Then
    If xx = "Service Related" Then
      res = modDatabase.$syConn.Exec("select flditemname,flditemtype,flditemcost FROM tblservicecost where flditemname NOT IN(select flditemname from tblreportgroup)")                          ''
      hForm = New FRequest(res, ("Items Missing from Grouping List"), "AccGroup:Missing", False)
      hForm.ShowModal
    Else If xx = "Inventory Related" Then
      res = modDatabase.$syConn.Exec("select DISTINCT(fldstockid),fldcategory,AVG(fldsellpr) FROM tblentry where fldstockid NOT IN(select flditemname from tblreportgroup)")                          ''
      hForm = New FRequest(res, ("Items Missing from Grouping List"), "ItemGroup:Missing", False)
      hForm.ShowModal
    Else If xx = "Mapped Items" Then
      res = modDatabase.$syConn.Exec("select DISTINCT(flditemname),fldcategory,AVG(fldrate) FROM tblstockrate where flditemname NOT IN(select flditemname from tblreportgroup)")                          ''
      hForm = New FRequest(res, ("Items Missing from Grouping List"), "AccGroup:Missing", False)
      hForm.ShowModal
    Endif
  Endif
  
End

Public Sub btndeleted_Click()
  
  Dim hForm As FRequest
  Dim res As Result
  
  res = modDatabase.$syConn.Exec("select DISTINCT(flditemname),flditemtype,flditemrate from tblpatbilling where flditemname NOT IN(select flditemname from tblservicecost) and flditemname NOT IN(select fldstockid from tblentry) and flditemname NOT IN(select flditemname from tblstockrate)")                          ''
  hForm = New FRequest(res, ("Items Deleted from Costing List"), "AccGroup:Deleted", False)
  hForm.ShowModal
  
End

Public Sub btnfaculty_Click()
  
  fmAddFaculty.Close
  fmAddFaculty.Show
  
End
