' Gambas class file

Private $sTitle As String
Private $sUser As Variant[]
Private $sValue As String

Public Sub Run(sTitle As String, sUser As Variant[]) As String

  $sTitle = sTitle
  $sUser = sUser

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  Me.Center
  Me.Title = $sTitle
  FillUserList()
  txtsearch.SetFocus

End

Private Sub FillUserList()

  Dim pic1 As Picture
  Dim xx As Variant
  Dim sLst As String[]

  ListView1.Clear()
  pic1 = Picture["icon:/small/identity"]
  If $sUser Then
    For Each xx In $sUser
      sLst = xx
      If Trim(sLst[0]) Then

        If modBasic.$DefaultUserSearch = "Name" Then
          If LCase(sLst[1]) Like LCase(txtsearch.Text) & "*" Then
            ListView1.Add(sLst[0], sLst[1], pic1)
          Endif
        Else
          If LCase(sLst[0]) Like LCase(txtsearch.Text) & "*" Then
            ListView1.Add(sLst[0], sLst[1], pic1)
          Endif
        Endif

      Endif
    Next
  Endif

End

Private Sub AddSelected()

  If ListView1.Selection.Count > 0 Then
    If ListView1.Current.Key Then
      $sValue = ListView1.Current.Key
      Me.Close(True)
    Endif
  Endif

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    AddSelected()
  Endif

End

Public Sub btnsel_Click()

  AddSelected()

End

Public Sub txtsearch_KeyRelease()

  If Key.Code = Key.Down Then
    ListView1.SetFocus
  Endif

End

Public Sub txtsearch_Change()

  FillUserList()

End

Public Sub txtsearch_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End
