' Gambas class file

Private $rData As Result

Private $route As String
Private $stockid As String
Private $sType As String
Private $sValue As String

Public Sub Run(route As String, stockid As String, sType As String) As String

  $route = route
  $stockid = stockid
  $sType = sType
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  cmbroute.List = modMedicine.ComboRoute()
  cmbroute.Text = $route
  cmbmedicine.Text = $stockid
  If $sType = "Brand" Then
    rbrand.Value = True
  Else If $sType = "Generic" Then
    rbgeneric.Value = True
  Endif
  If cmbroute.Text And If cmbmedicine.Text Then
    ShowStockGrid()
    Panel1.Enabled = False
  Endif

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Private Sub ShowStockGrid()

  Dim sql As String
  Dim Column As Integer
  Dim fld As ResultField

  sql = "select fldstockid,fldstockid,fldcomp,fldqty from tblentry where fldqty>&1 and fldstockid in(select fldbrandid From tblmedbrand where fldactive=&2 and flddrug=&3 and flddrug in(select flddrug From tbldrug where fldroute=&4))"                              ''
  $rData = modDatabase.$myConn.Exec(sql, 0, "Active", modMedConstant.GetDrugFromStockID(cmbmedicine.Text), cmbroute.Text)

  GridView1.Clear
  GridView1.Columns.Count = $rData.Fields.Count
  GridView1.Rows.Count = $rData.Count

  For Each $rData
    Column = 0
    For Each fld In $rData.Fields
      modGeneralMain.GridExplicitDecoration(GridView1, $rData.Index, Column)
      If Column = 1 Then
        GridView1[$rData.Index, Column].Text = modMedConstant.ConvertGenericToBrand($rData[fld.Name])
      Else
        GridView1[$rData.Index, Column].Text = $rData[fld.Name]
      Endif
      Column = Column + 1
    Next
  Next
  GridView1.Row = 0

  With GridView1
    .Rows.Height = modBasic.$AppGridRowHeight
    .Columns[0].Width = 350 * modBasic.$AppWidthRatio
    .Columns[1].Width = 175 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 75 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Medicine"
    .Columns[1].Text = "Brand"
    .Columns[2].Text = "Location"
    .Columns[3].Text = "QTY"
  End With
  btnshow.Enabled = False

End

Public Sub GridView1_KeyPress()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If GridView1.Rows.Selection.Count > 0 Then
      If $sType = "Brand" Then
        $sValue = GridView1[GridView1.Row, 1].Text
      Else
        $sValue = GridView1[GridView1.Row, 0].Text
      Endif
      Me.Close(True)
    Endif
  Else If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub cmbroute_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbroute)
  modFillContainer.RestrictComboToContent(cmbroute)

End

Public Sub cmbroute_Click()

  cmbmedicine.Clear
  btnshow.Enabled = True

End

Public Sub cmbmedicine_GotFocus()

  Dim brandOrGeneric As String
  Dim res As Result

  If rbrand.Value = True Then
    brandOrGeneric = "Brand"
  Else
    brandOrGeneric = "Generic"
  Endif
  If cmbroute.Text Then
    If Not cmbmedicine.Text Then
      res = modStock.ItemListForPurchaseResult(cmbroute.Text, brandOrGeneric)
      cmbmedicine.Text = GridViewNew("Select Particulars", modControlSub.GetDirectFillresult(res), False, cmbmedicine, GridView1.Height)
      cmbmedicine.SetFocus
    Endif
  Endif

End

Public Sub btnshow_Click()

  If cmbmedicine.Text Then
    If modStockSub.GetComboStockControl(cmbroute.Text, cmbmedicine.Text, rbgeneric, rbrand) = False Then
      cmbmedicine.Text = ""
      Balloon.Warning(("Item not listed"), cmbmedicine)
      Balloon.Delay = modBasic.$BalloonDelay
      cmbroute.SetFocus
    Else
      If rbrand.Value = True Then
        cmbmedicine.Text = modMedConstant.ConvertBrandToGeneric(cmbroute.Text, cmbmedicine.Text)
      Endif
      ShowStockGrid()
      GridView1.SetFocus
    Endif
  Endif

End

Public Sub btnshow_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    btnshow_Click()
  Endif

End
