' Gambas class file

Private $sValue As String[]

Public Sub Run() As String[]

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")

End

Public Sub txtpath_Click()

  Dialog.Filter = ["*.jpg;*.jpeg;*.png;*.bmp;*.pdf", "Picture files"]
  If Dialog.OpenFile() Then Return
  txtpath.Text = Dialog.Path
  If Exist(txtpath.Text) Then
    ListBox1.Add(txtpath.Text)
  Endif

End

Public Sub btnscanner_Click()

  txtpath.Text = ScannedFile()
  Wait
  If Exist(txtpath.Text) Then
    ListBox1.Add(txtpath.Text)
  Endif

End

Public Sub btnimage_Click()

  Dim sList As String[]
  Dim Row As Integer

  sList = modImage.GetWebCamValueList(modDevice.WebCamDevice())
  Wait
  If sList And If sList.Count Then
    txtpath.Text = sList[0]
    For Row = 0 To sList.Count - 1
      ListBox1.Add(sList[Row])
    Next
  Endif

End

Public Sub btnfolder_Click()

  Dim FileList As String[]
  Dim sFile As String

  If Dialog.SelectDirectory() Then Return
  btnfolder.Tag = Dialog.Path
  txtpath.Text = btnfolder.Tag
  If btnfolder.Tag Then
    FileList = Dir(btnfolder.Tag, Null, gb.File)
    For Each sFile In FileList
      ListBox1.Add(btnfolder.Tag &/ sFile)
    Next
    Balloon.Info(FileList.Count & " Images Added", btnfolder)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnclose_Click()

  If ListBox1.Count Then
    $sValue = ListBox1.List
    Me.Close(True)
  Else
    Me.Close
  Endif

End
