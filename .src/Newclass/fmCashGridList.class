' Gambas class file

Private $sValue As String
Private $xControl As Control
Private $sHeight As Integer

Private $GridDataList As String[]
Private $GridWorkList As String[]

Public Sub Run(dataList As String[], Optional xControl As Control, Optional sHeight As Integer) As String

  $GridDataList = dataList
  If xControl Then
    $xControl = xControl
  Endif
  If sHeight Then
    $sHeight = sHeight
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  If modBasic.$ItemListModalCentre = "Yes" Then
  Else
    If $xControl Then
      Me.Left = $xControl.ScreenX
      Me.Top = $xControl.ScreenY
      Me.Width = $xControl.Width
      Me.Border = False
    Endif
    If $sHeight Then
      Me.Height = $sHeight
    Endif
  Endif

  If modBasic.$BillItemMode = "Code" Then
    rbcode.Value = True
  Else
    rbtext.Value = True
  Endif
  chkleftmain.Value = modBasic.$SearchBothSide

  $GridWorkList = $GridDataList
  FillItemGrid()

End

Public Sub rbtext_Click()

  modSettings.SaveSettingsToFile("CashBilling/ItemMode", "Text")
  modBasic.$BillItemMode = "Text"
  txtname.SetFocus

End

Public Sub rbcode_Click()

  modSettings.SaveSettingsToFile("CashBilling/ItemMode", "Code")
  modBasic.$BillItemMode = "Code"
  txtcode.SetFocus

End

Private Sub FillItemGrid()

  If $GridWorkList Then
    GridView1.Clear()
    GridView1.Columns.Count = 3
    GridView1.Rows.Count = $GridWorkList.Count

    With GridView1
      .Rows.Height = modBasic.$AppGridRowHeight
      .Columns[0].Width = Me.Width - (175 * modBasic.$AppWidthRatio + lstletter.Width)
      .Columns[1].Width = 75 * modBasic.$AppWidthRatio
      .Columns[2].Width = 75 * modBasic.$AppWidthRatio

      .Columns[0].Text = "Particulars"
      .Columns[1].Text = "ItemCode"
      .Columns[2].Text = "Charge"
    End With
    GridView1.Row = 0
  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  modGeneralMain.GridViewDecoration(GridView1, Row)
  GridView1.Data.Text = Split($GridWorkList[Row], "|")[Column]
  GridView1.Rows[Row].Height = Max(GridView1.Rows[Row].Height, GridView1.Data.Font.RichTextHeight(GridView1.Data.Text, GridView1.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView1.Rows.Height - GridView1.Font.Height))
  GridView1.Data.WordWrap = True

End

Private Sub SortSelectedRows()

  Dim xx As String
  Dim asx As String[]
  Dim xfinal As String[]

  xfinal = New String[]
  If $GridDataList Then
    For Each xx In $GridDataList
      asx = Split(xx, "|")
      If rbtext.Value = True Then
        If chkleftmain.Value Then
          If LCase(asx[0]) Like "*" & LCase(txtname.Text) & "*" Then
            xfinal.Add(asx[0] & "|" & asx[1] & "|" & asx[2])
          Endif
        Else
          If LCase(asx[0]) Like LCase(txtname.Text) & "*" Then
            xfinal.Add(asx[0] & "|" & asx[1] & "|" & asx[2])
          Endif
        Endif
      Else If rbcode.Value = True Then
        If LCase(asx[1]) Like LCase(txtcode.Text) & "*" Then
          xfinal.Add(asx[0] & "|" & asx[1] & "|" & asx[2])
        Endif
      Endif
    Next
  Endif
  $GridWorkList = xfinal
  FillItemGrid()

End

Public Sub GridView1_Click()

  If GridView1.Rows.Count > 0 Then
    $sValue = GridView1[GridView1.Row, 0].Text
    Me.Close(True)
  Endif

End

Public Sub GridView1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If GridView1.Rows.Count > 0 Then
      $sValue = GridView1[GridView1.Row, 0].Text
      Me.Close(True)
    Endif
  Else If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub txtname_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtcode_KeyPress()

  modGeneralMain.InputTextSearchKeyOnly()

End

Public Sub txtname_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Else If Key.Code = Key.Up Then
    GridView1.Row = GridView1.Rows.Count - 1
    GridView1.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If GridView1.Rows.Count > 0 Then
      $sValue = GridView1[GridView1.Row, 0].Text
      Me.Close(True)
    Endif
  Endif

End

Public Sub txtname_Change()

  SortSelectedRows()

End

Public Sub txtcode_KeyRelease()

  If Key.Code = Key.Down Then
    GridView1.SetFocus
  Else If Key.Code = Key.Up Then
    GridView1.Row = GridView1.Rows.Count - 1
    GridView1.SetFocus
  Else If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If GridView1.Rows.Count > 0 Then
      $sValue = GridView1[GridView1.Row, 0].Text
      Me.Close(True)
    Endif
  Endif

End

Public Sub txtcode_Change()

  SortSelectedRows()

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Else If Key.Code = Key.Right And If rbtext.Value = True Then
    rbcode.Value = True
    rbcode_Click()
  Else If Key.Code = Key.Left And If rbcode.Value = True Then
    rbtext.Value = True
    rbtext_Click()
  Endif

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub Form_Deactivate()

  Me.Close()

End
