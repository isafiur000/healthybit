' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]
Private $suppName As String

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Vertical")
  Me.Icon = Picture[modGeneralMain.$strLogo]

  If MMain.$WebReport = "Multiple" Then
    pnlbuttons.Visible = False
  Endif

  Select modHelpVariable.$LogInCategory
    Case "Purchase"
      If modBasic.$ViewLockInventory = "Location" Then
        cmbcomp.Text = modBasic.$compID
        cmbcomp.Enabled = False
        mnudel.Enabled = False
      Else
        cmbcomp.List = modBasic.$AllCompPerList
        cmbcomp.Text = "%"
      Endif

    Case Else
      cmbcomp.List = modBasic.$AllCompPerList
      cmbcomp.Text = "%"
      pnlbuttons.Visible = False
      Panel2.Enabled = False
  End Select

  cmbledger.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldacledger) as col from tblacledger"))
  cmbstatus.List = ["Active", "Inactive"]
  cmbpaymode.List = ["Cash Payment", "Credit Payment"]
  dtstart.Value = Now()
  BlankAll()
  If MMain.$RepoReportMode = True Then
    pnlbuttons.Visible = False
    mnudel.Visible = False
  Endif
  FillSupplierInfo()
  txtsupname.SetFocus

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 1 Then
    FillGridSupplier()
  Endif

End

Private Sub BlankAll()

  txtconphone.Text = ""
  txtcontname.Text = ""
  txtcreday.Value = 0
  txtsupadd.Text = ""
  txtsuphone.Text = ""
  txtsupname.Text = ""
  txtemail.Text = ""
  dtstart.Value = Date()
  cmbpaymode.Text = ""
  cmbstatus.Text = ""

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub dtnepstart_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtstart.Value))
  If xx Then
    dtnepstart.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub txtsupname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub cmbpaymode_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbpaymode)
  modFillContainer.RestrictComboToContent(cmbpaymode)

End

Public Sub cmbstatus_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbstatus)
  modFillContainer.RestrictComboToContent(cmbstatus)

End

Public Sub txtsupname_LostFocus()

  txtsupname.Text = UCase(txtsupname.Text)

End

Public Sub txtsupadd_LostFocus()

  txtsupadd.text = String.UCaseFirst(txtsupadd.text)

End

Public Sub txtcontname_LostFocus()

  txtcontname.text = String.UCaseFirst(txtcontname.text)

End

Public Sub btnfullrep_Click()

  ' Dim sAmt As String[]
  '
  ' sAmt = New String[]
  ' sAmt.Add("All")
  ' sAmt.Add("")
  ' sAmt.Add("")
  ' sAmt.Add(modReportVar.GetLocaleNumberFormat(txttotdebit.Value, gb.Currency))
  ' sAmt.Add(modReportVar.GetLocaleNumberFormat(txttotcredit.Value, gb.Currency))
  ' sAmt.Add(modReportVar.GetLocaleNumberFormat(txttotcredit.Value - txttotdebit.Value, gb.Currency))
  modCHTMLReport.ExportGridToHTML(GridView1, "SUPPLIERS", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btnadd_Click()

  Dim res As Result

  If txtsupname.Text And If cmbstatus.Text Then
    res = modDatabase.$myConn.Create("tblsupplier")
    res["fldsuppname"] = Trim(txtsupname.Text)
    res["fldsuppaddress"] = Trim(txtsupadd.Text)
    res["fldsupppan"] = Trim(txtpanno.Text)
    res["fldsuppledger"] = cmbledger.Text
    res["fldsuppphone"] = txtsuphone.Text
    res["fldsuppemail"] = Trim(txtemail.Text)
    res["fldcontactname"] = txtcontname.Text
    res["fldcontactphone"] = txtconphone.Text
    res["fldstartdate"] = dtstart.Value
    res["fldpaymentmode"] = cmbpaymode.Text
    res["fldcreditday"] = txtcreday.Value
    res["fldactive"] = cmbstatus.Text
    res["fldpaiddebit"] = 0
    res["fldleftcredit"] = 0
    res.Update()
    BlankAll()
    FillSupplierInfo()
    Balloon.Info(("Information saved"), btnadd)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    If GridView1.Rows.Selection.Count > 0 Then
      res = modDatabase.$myConn.Edit("tblsupplier", "fldsuppname=&1", GridView1[GridView1.Row, 0].Text)
      res["fldsuppaddress"] = Trim(txtsupadd.Text)
      res["fldsupppan"] = Trim(txtpanno.Text)
      res["fldsuppledger"] = cmbledger.Text
      res["fldsuppphone"] = txtsuphone.Text
      res["fldsuppemail"] = Trim(txtemail.Text)
      res["fldcontactname"] = txtcontname.Text
      res["fldcontactphone"] = txtconphone.Text
      res["fldstartdate"] = dtstart.Value
      res["fldpaymentmode"] = cmbpaymode.Text
      res["fldcreditday"] = txtcreday.Value
      res["fldactive"] = cmbstatus.Text
      res["xyz"] = False
      res.Update()
      FillSupplierInfo()
      Balloon.Info(("Information updated"), btnedit)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub GridView2_Menu()

  mnuhide.Popup

End

Public Sub mnudel_Click()

  If GridView2.Rows.Selection.Count > 0 Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblsupplier", "fldsuppname=&1 and fldpaiddebit=&2 and fldleftcredit=&3", GridView2[GridView2.Row, 0].Text, 0, 0)
      FillSupplierInfo()
      BlankAll()
    Endif
  Endif

End

Public Sub GridView2_Click()

  Dim sql As String
  Dim res As Result

  sql = "select fldsuppname,fldsuppaddress,fldsupppan,fldsuppledger,fldsuppphone,fldsuppemail,fldcontactname,fldcontactphone,fldstartdate,fldpaymentmode,fldcreditday,fldactive from tblsupplier where fldsuppname=&1"
  res = modDatabase.$myConn.Exec(sql, GridView2[GridView2.Row, 0].Text)
  If res.Available Then
    txtsupadd.Text = res["fldsuppaddress"]
    txtpanno.Text = res["fldsupppan"]
    cmbledger.Text = res["fldsuppledger"]
    txtsuphone.Text = res["fldsuppphone"]
    txtsupname.Text = res["fldsuppname"]
    txtemail.Text = res["fldsuppemail"]
    dtstart.Value = res["fldstartdate"]
    cmbpaymode.Text = res["fldpaymentmode"]
    txtconphone.Text = res["fldcontactphone"]
    txtcontname.Text = res["fldcontactname"]
    txtcreday.Value = res["fldcreditday"]
    cmbstatus.Text = res["fldactive"]
  Endif

End

Private Sub FillSupplierInfo()

  Dim sql As String

  sql = "select fldsuppname,fldsuppaddress,fldsupppan,fldsuppphone,fldsuppemail,fldactive from tblsupplier"
  $rData1 = modDatabase.$myConn.Exec(sql)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)
  With GridView2
    .Columns[0].Width = 275 * modBasic.$AppWidthRatio
    .Columns[1].Width = 225 * modBasic.$AppWidthRatio
    .Columns[2].Width = 150 * modBasic.$AppWidthRatio
    .Columns[3].Width = 150 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio
    .Columns[5].Width = 100 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Supplier"
    .Columns[1].Text = "Address"
    .Columns[2].Text = "PAN"
    .Columns[3].Text = "PHONE"
    .Columns[4].Text = "EMAIL"
    .Columns[5].Text = "STATUS"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData1.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  GridView2.Data.Text = $rData1[$aMyFields1[Column]]

End

Public Sub btneditmulti_Click()

  Dim xnew As String

  If modBasic.$SuperUser = True Then
    If Message.Question(("This process will affect multiple tables. Are you sure?"), ("No"), ("Yes")) = 2 Then
      xnew = InputBox("Provide new name of the supplier")
      If xnew Then
        modPharmChange.RecodeSupplierName(txtsupname.Text, xnew)
      Endif
    Endif
  Endif

End

''======================== Summary  =======================================
Public Sub cmbcomp_Click()

  If cmbcomp.Text Then
    FillGridSupplier()
  Endif

End

Private Sub FillGridSupplier()

  Dim sql As String
  Dim debit As Float
  Dim credit As Float
  Dim xtotl As Float

  If cmbcomp.Text = "%" Then
    sql = "select fldsuppname,fldsuppname,SUM(fldcredit) as fldleftcredit, SUM(flddebit) as fldpaiddebit,fldsuppname from tblpurchasebill GROUP BY fldsuppname"
  Else
    sql = "select fldsuppname,fldsuppname,SUM(fldcredit) as fldleftcredit, SUM(flddebit) as fldpaiddebit,SUM(fldcredit-flddebit) as xtot from tblpurchasebill where fldcomp like &1 GROUP BY fldsuppname"
  Endif
  $rData = modDatabase.$myConn.Exec(sql, cmbcomp.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = 275 * modBasic.$AppWidthRatio
    .Columns[1].Width = 225 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 125 * modBasic.$AppWidthRatio
    .Columns[4].Width = 125 * modBasic.$AppWidthRatio

    .Columns[0].Text = "Supplier"
    .Columns[1].Text = "Address"
    .Columns[2].Text = "PURCHASE"
    .Columns[3].Text = "PAYMENT"
    If cmbcomp.Text = "%" Then
      .Columns[4].Text = "ACC PAID"
      lblbalance.Text = "ACC PAID"
    Else
      .Columns[4].Text = "PENDING"
      lblbalance.Text = "PENDING"
    Endif
  End With

  debit = 0
  credit = 0
  xtotl = 0
  For Each $rData
    credit = credit + $rData["fldleftcredit"]
    debit = debit + $rData["fldpaiddebit"]
    If cmbcomp.Text = "%" Then
      xtotl = xtotl + modSupplier.GetTotalPaymentToSupplier($rData["fldsuppname"])
    Else
      xtotl = xtotl + $rData["xtot"]
    Endif
  Next
  txttotdebit.Value = Round(debit, -2)
  txttotcredit.Value = Round(credit, -2)
  txtbalance.Value = Round(xtotl, -2)

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 1 Then
    GridView1.Data.Text = modSupplier.GetAddFromSuppName($rData[$aMyFields[Column]])
  Else If Column = 2 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else If Column = 4 Then
    If cmbcomp.Text = "%" Then
      GridView1.Data.Text = modReportVar.GetLocaleNumberFormat(modSupplier.GetTotalPaymentToSupplier($rData[$aMyFields[Column]]), gb.Currency)
    Else
      GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
    Endif
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  $suppName = GridView1[GridView1.Row, 0].Text

End

Public Sub btndetail_Click()

  Dim hForm As FmPurdetail

  If $suppName Then
    hForm = New FmPurdetail($suppName, cmbcomp.Text)
    hForm.ShowModal
  Endif

End

Public Sub btninvoice_Click()

  Dim xx As Date[]
  Dim xPath As String

  If $suppName Then
    xx = DoubleDates(("Select Purchase Invoice Dates"), $suppName, [Date(), Date()])
    If xx Then
      Inc Application.Busy
      xPath = modHTMLStock.ShowSupplierInnvoices($suppName, xx[0], xx[1], cmbcomp.Text)
      Dec Application.Busy
      modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
    Endif
  Endif

End

Public Sub btnitems_Click()

  Dim xx As Date[]
  Dim xPath As String

  If $suppName Then
    xx = DoubleDates(("Select Purchase Invoice Dates"), $suppName, [Date(), Date()])
    If xx Then
      Inc Application.Busy
      xPath = modHTMLStock.ShowSupplierItemList($suppName, xx[0], xx[1], cmbcomp.Text)
      Dec Application.Busy
      modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
    Endif
  Endif

End

Public Sub btnpayment_Click()

  Dim xPath As String

  If $suppName Then
    Inc Application.Busy
    xPath = modHTMLStock.ShowTotalPaymentSupplier($suppName)
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub btnrepototal_Click()

  Dim xx As Date[]
  Dim xPath As String

  xx = DoubleDates(("Select Purchase Invoice Dates"), "Summary", [Date(), Date()])
  If xx Then
    Inc Application.Busy
    xPath = modHTMLStock.GetSupplierSummaryAmount(xx[0], xx[1], cmbcomp.Text)
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End
