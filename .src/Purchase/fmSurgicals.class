' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $totalrec As Long

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Column")
  Me.Icon = Picture[modGeneralMain.$strLogo]

  If modHelpVariable.$LogInCategory = "Pharmacist" Then
    btnstuname.Enabled = True
    btnorthoedit.Enabled = True
    btnamesurg.Enabled = True
  Endif

  cmbsurgcateg.List = ["msurg", "ortho"]
  cmbsuttype.List = SutureType()
  cmbsuture.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldsurgname as col from tblsurgicalname where fldsurgcateg=&1", "suture"))
  cmbcurrentuse.List = ["Active", "Inactive"]
  cmbtaxgroup.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldgroup as col from tbltaxgroup"))
  cmbmanufacture.List = modMedicine.GetManufacturerList()

  txtspinval.Value = 0

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Private Function TotalRecordNo() As Integer

  Dim res As Result
  Dim totalrec As Integer

  res = modDatabase.$myConn.Exec("select distinct(fldsurgname) as fldparent from tblsurgicalname")
  If res.Available Then
    totalrec = res.Count
  Else
    totalrec = 0
  Endif
  Return totalrec

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Down Then
    TreeView1.SetFocus
  Else If Key.Code = Key["R"] And If Key.Control Then
    btnload_Click()
  Else If Key.Code = Key["F"] And If Key.Control Then
    btnserach_Click()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnload_Click()

  TabStrip1.Enabled = True
  $totalrec = TotalRecordNo()
  CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
  If $totalrec Then
    txtspinval.MaxValue = Ceil($totalrec / 100)
    lbltotal.Text = modMisc.RecordNumberLabel(100 * txtspinval.Value, 100, $totalrec)
  Endif

End

Public Sub TreeView1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    TreeView1_Click()
  Endif

End

Public Sub TreeView1_Click()

  If TreeView1.Selection.Count Then
    Inc Application.Busy
    If TreeView1.Current.Picture = Picture["icons/coll1.png"] Then
      BlankSuture()
      BlankSurg()
      EnableAllButton()
      modFillContainer.ExpandClickTreeView(TreeView1)
    Else If TreeView1.Current.Picture = Picture["icons/coll2.png"] Then
      TabStrip1.Index = 0
      ShowSurgInfo(TreeView1.Current.Text)
      modFillContainer.ExpandClickTreeView(TreeView1)
    Else If TreeView1.Current.Picture = Picture["icons/coll3.png"] Then
      TabStrip1.Index = 1
      ShowAllBrand()
    Endif
    Dec Application.Busy
  Endif

End

Private Sub EnableAllButton()

  btnsutadd.Enabled = True
  btnsutdel.Enabled = True
  btnsutedit.Enabled = True
  btnsuradd.Enabled = True
  btnsurdel.Enabled = True
  btnsuredit.Enabled = True

End

Public Sub btnsutnew_Click()

  Dim hForm As FmAddVariable1

  hform = New FmAddVariable1("fldsurgname", "fldsurgcateg", "suture", "tblsurgicalname")
  hform.ShowModal
  cmbsuture.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldsurgname as col from tblsurgicalname where fldsurgcateg=&1", "suture"))

End

Public Sub btnsurnew_Click()

  Dim hForm As FmAddVariable1

  If cmbsurgcateg.Text Then
    hform = New FmAddVariable1("fldsurgname", "fldsurgcateg", cmbsurgcateg.Text, "tblsurgicalname")
    hform.ShowModal
    btnsurgrefresh_Click()
  Endif

End

Public Sub btnsutnew2_Click()

  Dim hForm As FmAddVarTwo
  Dim xx As String[]
  Dim yy As String[]

  xx = New String[]
  yy = New String[]
  hform = New FmAddVarTwo("fldsuturetype", "fldsuturecode", "tblsuturetype", xx, yy)
  hform.ShowModal
  cmbsuttype.List = SutureType()

End

''''XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Info XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Private Sub ShowSurgInfo(surgid As String)

  Dim categ As String
  Dim res As Result

  BlankSuture()
  BlankSurg()
  res = modDatabase.$myConn.Exec("select fldsurgid,fldsurgname,fldsurgcateg,fldsurgsize,fldsurgtype,fldsurgcode,fldsurgdetail from tblsurgicals where fldsurgid=&1", surgid)
  categ = modMedicine.GetSurgicalCategory(surgid)
  If categ = "suture" Then
    cmbsuture.Text = res["fldsurgname"]
    txtsutsize.Text = res["fldsurgsize"]
    txtsuttype.Text = res["fldsurgtype"]
    cmbsuttype.Text = ""
    txtsutcode.Text = res["fldsurgcode"]
    txtsutdetail.Text = res["fldsurgdetail"]
    txtsuturetotid.Text = res["fldsurgid"]
    btnsutadd.Enabled = True
    btnsutdel.Enabled = True
    btnsutedit.Enabled = True
    btnsuradd.Enabled = False
    btnsurdel.Enabled = False
    btnsuredit.Enabled = False
  Else If categ = "msurg" Or categ = "ortho" Then
    cmbsurgcateg.Text = categ
    cmbsurgname.Text = res["fldsurgname"]
    txtsurgsize.Text = res["fldsurgsize"]
    txtsurgtype.Text = res["fldsurgtype"]
    txtsurgdetail.Text = res["fldsurgdetail"]
    txtsurgtotid.Text = res["fldsurgid"]
    btnsutadd.Enabled = False
    btnsutdel.Enabled = False
    btnsutedit.Enabled = False
    btnsuradd.Enabled = True
    btnsurdel.Enabled = True
    btnsuredit.Enabled = True
  Endif

End

Public Function SutureType() As String[]

  Dim res As Result
  Dim xxx As String[]

  res = modDatabase.$myConn.Exec("select fldsuturetype as col from tblsuturetype")
  xxx = modControlSub.GetDirectFillresult(res)
  Return xxx

End

Public Sub cmbsuttype_Click()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldsuturecode from tblsuturetype where fldsuturetype=&1", cmbsuttype.Text)
  txtsuttype.Text = res["fldsuturecode"]

End

Public Sub btnsutadd_Click()

  Dim res As Result

  If btnsutadd.Text = "Add" And If btnsutedit.Text = "Edit" Then
    BlankSuture()
    BlankSurg()
    btnsutadd.Text = "Save"

  Else If btnsutadd.Text = "Save" And If btnsutedit.Text = "Edit" Then
    If cmbsuture.Text Then
      If Not txtsuttype.Text And If Not txtsutcode.Text Then
        txtsuturetotid.Text = cmbsuture.Text & " -" & Trim(txtsutsize.Text)
      Else
        txtsuturetotid.Text = cmbsuture.Text & " -" & Trim(txtsutsize.Text) & "(" & txtsuttype.Text & "-" & Trim(txtsutcode.Text) & ")"
      Endif
      res = modDatabase.$myConn.Create("tblsurgicals")
      res["fldsurgid"] = txtsuturetotid.Text
      res["fldsurgname"] = cmbsuture.Text
      res["fldsurgcateg"] = "suture"
      res["fldsurgsize"] = Trim(txtsutsize.Text)
      res["fldsurgtype"] = txtsuttype.Text
      res["fldsurgcode"] = Trim(txtsutcode.Text)
      res["fldsurgdetail"] = txtsutdetail.Text
      res.Update()
      btnsutadd.Text = "Add"
      CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value, cmbsuture.Text)
      Balloon.Info(("Information saved"), btnsutadd)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnsutedit_Click()

  Dim res As Result

  If btnsutedit.Text = "Edit" And If btnsutadd.Text = "Add" Then
    btnsutedit.Text = "Update"

  Else If btnsutedit.Text = "Update" And If btnsutadd.Text = "Add" Then
    If txtsuturetotid.Text Then
      res = modDatabase.$myConn.Edit("tblsurgicals", "fldsurgid=&1 and fldsurgcateg=&2", txtsuturetotid.Text, "suture")
      res["fldsurgname"] = cmbsuture.Text
      res["fldsurgsize"] = Trim(txtsutsize.Text)
      res["fldsurgtype"] = txtsuttype.Text
      res["fldsurgcode"] = Trim(txtsutcode.Text)
      res["fldsurgdetail"] = txtsutdetail.Text
      res.Update()
      btnsutedit.Text = "Edit"
      Balloon.Info(("Information updated"), btnsutedit)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnsutdel_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Exec("select fldbrandid from tblsurgbrand where fldsurgid=&1", txtsuturetotid.Text)
    If res.Available = False Then
      modDatabase.$myConn.Delete("tblsurgicals", "fldsurgid=&1 and fldsurgcateg=&2", txtsuturetotid.Text, "suture")
      CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
      BlankSuture()
    Else
      Message.Warning(("Deletion not allowed"), ("OK"))
    Endif
  Endif

End

Public Sub btnsuradd_Click()

  Dim res As Result

  If btnsuradd.Text = "Add" And If btnsuredit.Text = "Edit" Then
    BlankSuture()
    BlankSurg()
    btnsuradd.Text = "Save"

  Else If btnsuradd.Text = "Save" And If btnsuredit.Text = "Edit" Then
    If cmbsurgname.Text Then
      If txtsurgtype.Text Then
        txtsurgtotid.Text = cmbsurgname.Text & " -" & Trim(txtsurgsize.Text) & "(" & Trim(txtsurgtype.Text) & ")"
      Else
        txtsurgtotid.Text = cmbsurgname.Text & " -" & Trim(txtsurgsize.Text)
      Endif
      res = modDatabase.$myConn.Create("tblsurgicals")
      res["fldsurgid"] = txtsurgtotid.Text
      res["fldsurgname"] = cmbsurgname.Text
      res["fldsurgcateg"] = cmbsurgcateg.Text
      res["fldsurgsize"] = Trim(txtsurgsize.Text)
      res["fldsurgtype"] = Trim(txtsurgtype.Text)
      res["fldsurgcode"] = ""
      res["fldsurgdetail"] = txtsurgdetail.Text
      res.Update()
      btnsuradd.Text = "Add"
      CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value, cmbsurgname.Text)
      Balloon.Info(("Information saved"), btnsuradd)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnsuredit_Click()

  Dim res As Result

  If btnsuredit.Text = "Edit" And If btnsuradd.Text = "Add" Then
    btnsuredit.Text = "Update"

  Else If btnsuredit.Text = "Update" And If btnsuradd.Text = "Add" Then
    If txtsurgtotid.Text Then
      res = modDatabase.$myConn.Edit("tblsurgicals", "fldsurgid=&1", txtsurgtotid.Text)
      res["fldsurgname"] = cmbsurgname.Text
      res["fldsurgcateg"] = cmbsurgcateg.Text
      res["fldsurgsize"] = Trim(txtsurgsize.Text)
      res["fldsurgtype"] = Trim(txtsurgtype.Text)
      res["fldsurgdetail"] = txtsurgdetail.Text
      res.Update()
      btnsuredit.Text = "Edit"
      Balloon.Info(("Information updated"), btnsuredit)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnsurdel_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Exec("select fldbrandid from tblsurgbrand where fldsurgid=&1", txtsurgtotid.Text)
    If res.Available = False Then
      modDatabase.$myConn.Delete("tblsurgicals", "fldsurgid=&1", txtsurgtotid.Text)
      CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
      BlankSurg()
    Else
      Message.Warning(("Deletion not allowed"), "OK")
    Endif
  Endif

End

Private Sub BlankSuture()

  cmbsuture.Text = ""
  txtsutsize.Text = ""
  cmbsuttype.Text = ""
  txtsuttype.Text = ""
  txtsutcode.Text = ""
  txtsutdetail.Text = ""
  txtsuturetotid.Text = ""

End

Public Sub btnsurgrefresh_Click()

  cmbsurgname.Clear()
  If cmbsurgcateg.Text Then
    cmbsurgname.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldsurgname as col from tblsurgicalname where fldsurgcateg=&1", cmbsurgcateg.Text))
  Endif

End

Private Sub BlankSurg()

  cmbsurgcateg.Text = ""
  cmbsurgname.Text = ""
  txtsurgsize.Text = ""
  txtsurgtype.Text = ""
  txtsurgdetail.Text = ""
  txtsurgtotid.Text = ""

End

''''''XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  for Brand Info XXXXXXXXXXXXXXXXXXXXXXxxx'''''''''
Private Sub ShowAllBrand()

  Dim sql As String
  Dim sxl As String
  Dim res As Result

  sql = "select fldbrandid,fldsurgid,fldbrand,fldmanufacturer,flddetail,fldstandard,flddepart,fldmaxqty,fldminqty,fldleadtime,fldactive,fldtaxcode from tblsurgbrand where fldbrandid=&1"
  res = modDatabase.$myConn.Exec(sql, TreeView1.Current.Text)
  If res.Available Then
    txtbrandid.Text = res["fldbrandid"]
    'txtdrug.Text = res["fldsurgid"]
    txtbrandname.Text = res["fldbrand"]
    cmbmanufacture.Text = res["fldmanufacturer"]
    txtbranddetail.Text = res["flddetail"]
    txtstandard.Text = res["fldstandard"]
    cmbdepartment.Text = res["flddepart"]
    txtmaxstock.Value = res["fldmaxqty"]
    txtminstock.Value = res["fldminqty"]
    txtleadtime.Value = res["fldleadtime"]
    cmbcurrentuse.Text = res["fldactive"]
    cmbtaxgroup.Text = res["fldtaxcode"]
    lblcurrstock.Text = modStock.TotalQTYbyBrand(txtbrandid.Text, "%")
    btnBrOK.Text = "Add"
    btnBrChange.Text = "Edit"
  Endif

  If modBasic.$ViewLockInventory = "Location" Then
    sxl = "select tblentry.fldstockno,tblsurgbrand.fldbrand,tblentry.fldbatch,tblentry.fldexpiry,tblentry.fldsellpr,tblentry.fldqty,tblentry.fldstatus,tblentry.fldcomp from tblentry inner join tblsurgbrand on tblentry.fldstockid=tblsurgbrand.fldbrandid  where tblentry.fldstockid=&1 and tblentry.fldqty>&2 and tblentry.fldcomp=&3"                      ''
  Else
    sxl = "select tblentry.fldstockno,tblsurgbrand.fldbrand,tblentry.fldbatch,tblentry.fldexpiry,tblentry.fldsellpr,tblentry.fldqty,tblentry.fldstatus,tblentry.fldcomp from tblentry inner join tblsurgbrand on tblentry.fldstockid=tblsurgbrand.fldbrandid  where tblentry.fldstockid=&1 and tblentry.fldqty>&2"
  Endif
  $rData = modDatabase.$myConn.Exec(sxl, txtbrandid.Text, 0, modBasic.$compID)
  $aMyFields = New String[]
  modGridView.ReadSmallData(tblbrand, $rData, $aMyFields)
  With tblbrand
    .Columns[0].Text = "Code"
    .Columns[2].Text = "Batch"
    .Columns[3].Text = "Expiry"
    .Columns[4].Text = "SellP"
    .Columns[5].Text = "QTY"
    .Columns[6].Text = "Stat"
    .Columns[7].Text = "Comp"

    .Columns[0].Width = 50 * modBasic.$AppWidthRatio
    .Columns[1].Width = 1
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 125 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
  End With

End

Public Sub tblbrand_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(tblbrand, Row)
  If Column = 3 Then
    tblbrand.Data.Text = modReportVar.GetDateTimeReport($rData["fldexpiry"], gb.MediumDate)
  Else
    tblbrand.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Sub BlankAllBrandItem()

  txtbrandid.Text = ""
  txtbrandname.Text = ""
  cmbmanufacture.Text = ""
  txtbranddetail.Text = ""
  txtstandard.Text = ""
  cmbdepartment.Text = ""
  lblcurrstock.Text = 0
  txtmaxstock.Value = 0
  txtminstock.Value = 0
  txtleadtime.Value = 0
  cmbcurrentuse.Text = ""
  cmbtaxgroup.Text = ""

End

Public Sub btnaddmfg_Click()

  Dim hForm As FmAddVariableGrid

  hform = New FmAddVariableGrid("flditem", "tblmanufacturer")
  hform.ShowModal
  cmbmanufacture.List = modMedicine.GetManufacturerList()

End

Public Sub cmbmanufacture_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmanufacture)
  modFillContainer.RestrictComboToContent(cmbmanufacture)

End

Public Sub cmbcurrentuse_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcurrentuse)
  modFillContainer.RestrictComboToContent(cmbcurrentuse)

End

Public Sub btnBrOK_Click()

  Dim res As Result
  Dim surgid As String

  If btnBrOK.Text = "Add" And If btnBrChange.Text = "Edit" Then
    btnBrOK.Text = "Save"
    BlankAllBrandItem()

  Else If btnBrOK.Text = "Save" And If btnBrChange.Text = "Edit" Then
    If txtsuturetotid.Text And If txtsurgtotid.Text = "" Then
      surgid = txtsuturetotid.Text
    Else If txtsuturetotid.Text = "" And If txtsurgtotid.Text Then
      surgid = txtsurgtotid.Text
    Endif
    If surgid And If txtbrandname.Text Then
      txtbrandname.Text = UCase(txtbrandname.Text)
      txtbrandid.Text = surgid & " (" & Trim(txtbrandname.Text) & ")"
      res = modDatabase.$myConn.Create("tblsurgbrand")
      res["fldbrandid"] = txtbrandid.Text
      res["fldsurgid"] = surgid
      res["fldbrand"] = txtbrandname.Text
      res["fldmanufacturer"] = cmbmanufacture.Text
      res["flddetail"] = txtbranddetail.Text
      res["fldstandard"] = txtstandard.Text
      res["flddepart"] = cmbdepartment.Text
      res["fldmaxqty"] = txtmaxstock.Value
      res["fldminqty"] = txtminstock.Value
      res["fldleadtime"] = txtleadtime.Value
      res["fldactive"] = cmbcurrentuse.Text
      res["fldtaxcode"] = cmbtaxgroup.Text
      res.Update()
      btnBrOK.Text = "Add"
      CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
      Balloon.Info(("Information saved"), btnBrOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnBrChange_Click()

  Dim res As Result

  If btnBrChange.Text = "Edit" And If btnBrOK.Text = "Add" Then
    btnBrChange.Text = "Update"

  Else If btnBrChange.Text = "Update" And If btnBrOK.Text = "Add" Then
    If txtbrandid.Text Then
      res = modDatabase.$myConn.Edit("tblsurgbrand", "fldbrandid=&1", txtbrandid.Text)
      res["fldmanufacturer"] = cmbmanufacture.Text
      res["flddetail"] = txtbranddetail.Text
      res["fldstandard"] = txtstandard.Text
      res["flddepart"] = cmbdepartment.Text
      res["fldmaxqty"] = txtmaxstock.Value
      res["fldminqty"] = txtminstock.Value
      res["fldleadtime"] = txtleadtime.Value
      res["fldactive"] = cmbcurrentuse.Text
      res["fldtaxcode"] = cmbtaxgroup.Text
      res.Update()
      btnBrChange.Text = "Edit"
      Balloon.Info(("Information updated"), btnBrChange)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnBrCancel2_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Exec("select fldstockno from tblentry where fldstockid=&1 and fldqty>&2", txtbrandid.Text, 0)
    If res.Available = False Then
      modDatabase.$myConn.Delete("tblsurgbrand", "fldbrandid=&1", txtbrandid.Text)
      CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
      BlankAllBrandItem()
    Else
      Message.Warning(("Deletion not allowed"), "OK")
    Endif
  Endif

End

Public Sub btnback_Click()

  TabStrip1.Enabled = True
  txtspinval.Value = txtspinval.Value - 1
  CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
  lbltotal.Text = modMisc.RecordNumberLabel(100 * txtspinval.Value, 100, $totalrec)

End

Public Sub btnfront_Click()

  TabStrip1.Enabled = True
  txtspinval.Value = txtspinval.Value + 1
  CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value)
  lbltotal.Text = modMisc.RecordNumberLabel(100 * txtspinval.Value, 100, $totalrec)

End

Public Sub btnserach_Click()

  Dim xx As String

  TabStrip1.Enabled = True
  xx = InputBox(("Search Surgical Names starting with .."), ("Generic List"), "")
  CompleteSurgicalTree(TreeView1, 100 * txtspinval.Value, xx)
  lbltotal.Text = ""

End

Public Sub btnsutadd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If btnsutadd.Text = "Save" Then
      btnsutadd_Click()
    Endif
  Endif

End

Public Sub btnsuradd_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If btnsuradd.Text = "Save" Then
      btnsuradd_Click()
    Endif
  Endif

End

Public Sub btnBrOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If btnBrOK.Text = "Save"
      btnBrOK_Click()
    Endif
  Endif

End

Public Sub txtbrandname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub cmbsurgcateg_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbsurgcateg)
  modFillContainer.RestrictComboToContent(cmbsurgcateg)

End

Public Sub cmbtaxgroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtaxgroup)
  modFillContainer.RestrictComboToContent(cmbtaxgroup)

End

''------------------------------- TreeView ----------------------
Private Sub CompleteSurgicalTree(xTreeView1 As TreeView, stint As Integer, Optional txtsearch As String)

  Dim sstr1 As String
  Dim sstr2 As String
  Dim sstr3 As String

  Inc Application.Busy
  If txtsearch Then
    sstr1 = "select distinct(fldsurgname) as fldparent from tblsurgicalname where lower(fldsurgname) like &1"
  Else
    sstr1 = Subst("select distinct(fldsurgname) as fldparent from tblsurgicalname ORDER BY fldsurgname LIMIT 100 OFFSET &1", stint)
  Endif
  sstr2 = "select distinct(fldsurgid) as fldparent from tblsurgicals where fldsurgname=&1"
  sstr3 = "select distinct(fldbrandid) as fldparent from tblsurgbrand where fldsurgid=&1"
  If txtsearch Then
    modFillContainer.FillTreeView(modDatabase.$medConn, xTreeView1, "", sstr1, "", sstr2, "", sstr3, "", "", LCase(txtsearch) & "%", "", "", "")
  Else
    modFillContainer.FillTreeView(modDatabase.$medConn, xTreeView1, "", sstr1, "", sstr2, "", sstr3, "", "", "", "", "", "")
  Endif
  Dec Application.Busy

End

Public Sub btnstuname_Click()

  Dim xval As String

  If modBasic.$SuperUser = True Then
    If Message.Question(("This process will affect multiple tables. Are you sure?"), ("No"), ("Yes")) = 2 Then
      xval = InputBox(("New Name for ") & cmbsuture.Text, "Suture", cmbsuture.Text)
      If xval Then
        modPharmChange.RecodeSurgicalCode(cmbsuture.Text, xval, "Suture")
      Endif
    Endif
  Endif

End

Public Sub btnorthoedit_Click()

  Dim xval As String

  If modBasic.$SuperUser = True Then
    If Message.Question(("This process will affect multiple tables. Are you sure?"), ("No"), ("Yes")) = 2 Then
      xval = InputBox(("New Name for ") & cmbsurgname.Text, "Surgicals", cmbsurgname.Text)
      If xval Then
        modPharmChange.RecodeSurgicalCode(cmbsurgname.Text, xval, "Surgicals")
      Endif
    Endif
  Endif

End

Public Sub btnamesurg_Click()

  If modBasic.$SuperUser = True Then
    If Message.Question(("This process will affect multiple tables. Are you sure?"), ("No"), ("Yes")) = 2 Then
      modPharmChange.RecodeSurgicalStock(txtbrandid.Text, Trim(txtbrandname.Text))
    Endif
  Endif

End

Public Sub txtsutsize_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtsutcode_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtsurgsize_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub txtsurgtype_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End
