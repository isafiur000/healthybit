' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $totalrec As Long

Public Sub Form_Open()

  Dim xList As String[]

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Column")
  Me.Icon = Picture[modGeneralMain.$strLogo]

  cmbcategory.List = ["Consumer", "Non-consumer"]
  cmbcurrentuse.List = ["Active", "Inactive"]
  xList = modMedicine.GetExtraDepartments()
  cmbdepartment.List = xList
  cmbdept.List = xList
  cmbtaxgroup.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldgroup as col from tbltaxgroup"))
  cmbmanufacture.List = modMedicine.GetManufacturerList()
  txtspinval.Value = 0

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Down Then
    TreeView1.SetFocus
  Else If Key.Code = Key["R"] And If Key.Control Then
    btnload_Click()
  Else If Key.Code = Key["F"] And If Key.Control Then
    btnserach_Click()
  Else
    modGeneralmain.GoToNextControlTab()
  Endif

End

Public Sub btnload_Click()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select distinct(fldextraid) as fldparent from tblextra")
  If res.Available Then
    $totalrec = res.Count
  Else
    $totalrec = 0
  Endif
  CompleteExtraTree(TreeView1, 100 * txtspinval.Value, cmbdept.Text)
  If $totalrec Then
    txtspinval.MaxValue = Ceil($totalrec / 100)
    lbltotal.Text = modMisc.RecordNumberLabel(100 * txtspinval.Value, 100, $totalrec)
  Endif

End

Public Sub TreeView1_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    TreeView1_Click()
  Endif

End

Public Sub TreeView1_Click()

  Inc Application.Busy

  If TreeView1.Current.Picture = Picture["icons/coll1.png"] Then
    BlankAllBrandItem()
    cmbitemname.Text = TreeView1.Current.Text
    modFillContainer.ExpandClickTreeView(TreeView1)
  Else If TreeView1.Current.Picture = Picture["icons/coll2.png"] Then
    BlankAllBrandItem()
    ShowAllBrand()
  Endif

  Dec Application.Busy

End

Public Sub tlbtnadd_Click()

  Dim hForm As FmAddVariableGrid

  hForm = New FmAddVariableGrid("fldextraid", "tblextra")
  hForm.ShowModal

End

Private Sub ShowAllBrand()

  Dim sql As String
  Dim sxl As String
  Dim res As Result
  Dim sCompLst As String[]
  Dim i As Integer

  sql = "select fldbrandid,fldextraid,fldbrand,fldpackvol,fldvolunit,fldmanufacturer,flddepart,flddetail,fldstandard,fldmaxqty,fldminqty,fldleadtime,fldactive,fldtaxcode from tblextrabrand where fldbrandid=&1"
  res = modDatabase.$myConn.Exec(sql, TreeView1.Current.Text)
  If res.Available Then
    txtbrandid.Text = res["fldbrandid"]
    cmbitemname.Text = res["fldextraid"]
    txtbrandname.Text = res["fldbrand"]
    txtpackvol.Value = res["fldpackvol"]
    txtpackvolunit.Text = res["fldvolunit"]
    cmbmanufacture.Text = res["fldmanufacturer"]
    cmbdepartment.Text = res["flddepart"]
    txtbranddetail.Text = res["flddetail"]
    cmbcategory.Text = res["fldstandard"]
    txtmaxstock.Value = res["fldmaxqty"]
    txtminstock.Value = res["fldminqty"]
    txtleadtime.Value = res["fldleadtime"]
    cmbcurrentuse.Text = res["fldactive"]
    cmbtaxgroup.Text = res["fldtaxcode"]
    lblcurrstock.Text = modStock.TotalQTYbyBrand(txtbrandid.Text, "%")
    btnBrOK.Text = "Add"
    btnBrChange.Text = "Edit"
  Endif

  If modBasic.$ViewLockInventory = "Location" Then
    sxl = "select tblentry.fldstockno,tblsurgbrand.fldbrand,tblentry.fldbatch,tblentry.fldexpiry,tblentry.fldsellpr,tblentry.fldqty,tblentry.fldstatus,tblentry.fldcomp from tblentry inner join tblsurgbrand on tblentry.fldstockid=tblsurgbrand.fldbrandid  where tblentry.fldstockid=&1 and tblentry.fldqty>&2 and tblentry.fldcomp=&3"                     ''
  Else If modBasic.$ViewLockInventory = "Branch" Then
    sCompLst = modBasic.$branchList.Copy()
    For i = 0 To sCompLst.Count - 1
      sCompLst[i] = "'" & sCompLst[i] & "'"
    Next
    sxl = "select tblentry.fldstockno,tblsurgbrand.fldbrand,tblentry.fldbatch,tblentry.fldexpiry,tblentry.fldsellpr,tblentry.fldqty,tblentry.fldstatus,tblentry.fldcomp from tblentry inner join tblsurgbrand on tblentry.fldstockid=tblsurgbrand.fldbrandid  where tblentry.fldstockid=&1 and tblentry.fldqty>&2 and tblentry.fldcomp in(" & sCompLst.Join(",") & ")"
  Else
    sxl = "select tblentry.fldstockno,tblsurgbrand.fldbrand,tblentry.fldbatch,tblentry.fldexpiry,tblentry.fldsellpr,tblentry.fldqty,tblentry.fldstatus,tblentry.fldcomp from tblentry inner join tblsurgbrand on tblentry.fldstockid=tblsurgbrand.fldbrandid  where tblentry.fldstockid=&1 and tblentry.fldqty>&2"
  Endif
  $rData = modDatabase.$myConn.Exec(sxl, txtbrandid.Text, 0, modBasic.$compID)
  $aMyFields = New String[]
  modGridView.ReadSmallData(tblbrand, $rData, $aMyFields)
  With tblbrand
    .Columns[0].Text = "Code"
    .Columns[2].Text = "Batch"
    .Columns[3].Text = "Expiry"
    .Columns[4].Text = "SellP"
    .Columns[5].Text = "QTY"
    .Columns[6].Text = "Stat"
    .Columns[7].Text = "Comp"

    .Columns[0].Width = 50 * modBasic.$AppWidthRatio
    .Columns[1].Width = 1
    .Columns[2].Width = 100 * modBasic.$AppWidthRatio
    .Columns[3].Width = 125 * modBasic.$AppWidthRatio
    .Columns[4].Width = 75 * modBasic.$AppWidthRatio
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 50 * modBasic.$AppWidthRatio
    .Columns[7].Width = 75 * modBasic.$AppWidthRatio
  End With

End

Public Sub tblbrand_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(tblbrand, Row)
  If Column = 3 Then
    tblbrand.Data.Text = modReportVar.GetDateTimeReport($rData["fldexpiry"], gb.MediumDate)
  Else
    tblbrand.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Sub BlankAllBrandItem()

  txtbrandid.Text = ""
  txtpackvol.Value = 0
  txtpackvolunit.Text = ""
  txtbrandname.Text = ""
  cmbmanufacture.Text = ""
  cmbdepartment.Text = ""
  txtbranddetail.Text = ""
  cmbcategory.Text = ""
  lblcurrstock.Text = 0
  txtmaxstock.Value = 0
  txtminstock.Value = 0
  txtleadtime.Value = 0
  cmbcurrentuse.Text = ""
  cmbtaxgroup.Text = ""

End

Public Sub btnaddmfg_Click()

  Dim hForm As FmAddVariableGrid

  hform = New FmAddVariableGrid("flditem", "tblmanufacturer")
  hform.ShowModal
  cmbmanufacture.List = modMedicine.GetManufacturerList()

End

Public Sub btnadddepart_Click()

  Dim hForm As FmAddVariableGrid

  hform = New FmAddVariableGrid("flddepart", "tblextradepartment")
  hform.ShowModal
  cmbdepartment.List = modMedicine.GetExtraDepartments()

End

Public Sub cmbmanufacture_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbmanufacture)
  modFillContainer.RestrictComboToContent(cmbmanufacture)

End

Public Sub cmbdepartment_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbdepartment)
  modFillContainer.RestrictComboToContent(cmbdepartment)

End

Public Sub cmbcategory_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcategory)
  modFillContainer.RestrictComboToContent(cmbcategory)

End

Public Sub cmbcurrentuse_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbcurrentuse)
  modFillContainer.RestrictComboToContent(cmbcurrentuse)

End

Public Sub btnBrOK_Click()

  Dim res As Result

  If btnBrOK.Text = "Add" And If btnBrChange.Text = "Edit" Then
    btnBrOK.Text = "Save"
    BlankAllBrandItem()

  Else If btnBrOK.Text = "Save" And If btnBrChange.Text = "Edit" Then
    If cmbitemname.Text And If txtbrandname.Text Then
      txtbrandname.Text = UCase(txtbrandname.Text)
      txtbrandid.Text = cmbitemname.Text & " (" & Trim(txtbrandname.Text) & ")"
      res = modDatabase.$myConn.Create("tblextrabrand")
      res["fldbrandid"] = txtbrandid.Text
      res["fldextraid"] = cmbitemname.Text
      res["fldbrand"] = txtbrandname.Text
      res["fldpackvol"] = txtpackvol.Value
      res["fldvolunit"] = txtpackvolunit.Text
      res["fldmanufacturer"] = cmbmanufacture.Text
      res["flddepart"] = cmbdepartment.Text
      res["flddetail"] = txtbranddetail.Text
      res["fldstandard"] = cmbcategory.Text
      res["fldmaxqty"] = txtmaxstock.Value
      res["fldminqty"] = txtminstock.Value
      res["fldleadtime"] = txtleadtime.Value
      res["fldactive"] = cmbcurrentuse.Text
      res["fldtaxcode"] = cmbtaxgroup.Text
      res.Update()
      btnBrOK.Text = "Add"
      CompleteExtraTree(TreeView1, 100 * txtspinval.Value, cmbdept.Text, cmbitemname.Text)
      Balloon.Info(("Information saved"), btnBrOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnBrChange_Click()

  Dim res As Result

  If btnBrChange.Text = "Edit" And If btnBrOK.Text = "Add" Then
    btnBrChange.Text = "Update"

  Else If btnBrChange.Text = "Update" And If btnBrOK.Text = "Add" Then
    If txtbrandid.Text Then
      res = modDatabase.$myConn.Edit("tblextrabrand", "fldbrandid=&1", txtbrandid.Text)
      res["fldpackvol"] = txtpackvol.Value
      res["fldvolunit"] = txtpackvolunit.Text
      res["fldmanufacturer"] = cmbmanufacture.Text
      res["flddepart"] = cmbdepartment.Text
      res["flddetail"] = txtbranddetail.Text
      res["fldstandard"] = cmbcategory.Text
      res["fldmaxqty"] = txtmaxstock.Value
      res["fldminqty"] = txtminstock.Value
      res["fldleadtime"] = txtleadtime.Value
      res["fldactive"] = cmbcurrentuse.Text
      res["fldtaxcode"] = cmbtaxgroup.Text
      res.Update()
      btnBrChange.Text = "Edit"
      Balloon.Info(("Information updated"), btnBrChange)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnBrCancel_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Exec("select fldstockno from tblentry where fldstockid=&1 and fldqty>&2", txtbrandid.Text, 0)
    If res.Available = False Then
      modDatabase.$myConn.Delete("tblextrabrand", "fldbrandid=&1", txtbrandid.Text)
      CompleteExtraTree(TreeView1, 100 * txtspinval.Value, cmbdept.Text)
      BlankAllBrandItem()
    Else
      Message.Warning(("Deletion not allowed"), ("OK"))
    Endif
  Endif

End

Public Sub btnback_Click()

  txtspinval.Value = txtspinval.Value - 1
  CompleteExtraTree(TreeView1, 100 * txtspinval.Value, cmbdept.Text)
  lbltotal.Text = modMisc.RecordNumberLabel(100 * txtspinval.Value, 100, $totalrec)

End

Public Sub btnfront_Click()

  txtspinval.Value = txtspinval.Value + 1
  CompleteExtraTree(TreeView1, 100 * txtspinval.Value, cmbdept.Text)
  lbltotal.Text = modMisc.RecordNumberLabel(100 * txtspinval.Value, 100, $totalrec)

End

Public Sub btnserach_Click()

  Dim xx As String

  xx = InputBox(("Search Extra Item Names starting with .."), ("Generic List"), "")
  CompleteExtraTree(TreeView1, 100 * txtspinval.Value, cmbdept.Text, xx)
  lbltotal.Text = ""

End

Public Sub btnBrOK_KeyRelease()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
    If btnBrOK.Text = "Save" Then
      btnBrOK_Click()
    Endif
  Endif

End

Public Sub txtbrandname_KeyPress()

  modGeneralMain.InputTextKeyOnly()

End

Public Sub cmbtaxgroup_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbtaxgroup)
  modFillContainer.RestrictComboToContent(cmbtaxgroup)

End

''---------------------------- Treeview -----------------------
Private Sub CompleteExtraTree(xTreeView1 As TreeView, stint As Integer, sDept As String, Optional txtsearch As String)

  Dim sstr1 As String
  Dim sstr2 As String

  Inc Application.Busy
  If txtsearch Then
    sstr1 = "select distinct(fldextraid) as fldparent from tblextra where lower(fldextraid) like &1"
  Else
    sstr1 = Subst("select distinct(fldextraid) as fldparent from tblextra ORDER BY fldextraid LIMIT 100 OFFSET &1", stint)
  Endif
  If sDept Then
    sstr2 = db.Subst("select distinct(fldbrandid) as fldparent from tblextrabrand where flddepart=&1 and fldextraid", sDept) & "=&1"
  Else
    sstr2 = "select distinct(fldbrandid) as fldparent from tblextrabrand where fldextraid=&1"
  Endif
  If txtsearch Then
    modFillContainer.FillTreeView(modDatabase.$medConn, xTreeView1, "", sstr1, "", sstr2, "", "", "", "", LCase(txtsearch) & "%", "", "", "")
  Else
    modFillContainer.FillTreeView(modDatabase.$medConn, TreeView1, "", sstr1, "", sstr2, "", "", "", "", "", "", "", "")
  Endif
  Dec Application.Busy

End
