' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $strTitle As String
Private $sComp As String

Public Sub _new(strTitle As String, sComp As String)

  $strTitle = strTitle
  $sComp = sComp

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Title = $strTitle
  dtlisted.Value = Now()
  DateSelectionSett()
  ShowGridView()

End

Public Sub chklist_Click()

  DateSelectionSett()

End

Private Sub DateSelectionSett()

  If chklist.Value = True Then
    Panel13.Enabled = True
  Else
    Panel13.Enabled = False
  Endif

End

Public Sub dtneplist_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlisted.Value))
  If xx Then
    dtlisted.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub txtsearch_Change()

  ShowGridView()

End

Public Sub txtinvoice_Change()

  ShowGridView()

End

Public Sub btnrefresh_Click()

  ShowGridView()

End

Private Sub ShowGridView()

  Dim sql As String
  Dim xstr As String

  Dim xpurchase As Float
  Dim xpayment As Float

  If $sComp Then
    If $sComp = "%" Then
      xstr = ""
    Else
      xstr = " and fldcomp like &6"
    Endif
  Else
    xstr = ""
  Endif

  If chklist.Value = True Then
    sql = "select fldid,fldreference,fldpurtype,fldpurdate,fldbillno,fldcategory,fldcredit,flddebit,fldreference,fldreference,fldpurorder,fldcomp from tblpurchasebill where fldsuppname=&1 and lower(fldreference) like &2 and lower(fldbillno) like &3 and fldpurdate>=&4 and fldpurdate<=&5" & xstr
  Else
    sql = "select fldid,fldreference,fldpurtype,fldpurdate,fldbillno,fldcategory,fldcredit,flddebit,fldreference,fldreference,fldpurorder,fldcomp from tblpurchasebill where fldsuppname=&1 and lower(fldreference) like &2 and lower(fldbillno) like &3" & xstr
  Endif
  $rData = modDatabase.$syConn.Exec(sql, $strTitle, LCase(txtsearch.Text) & "%", LCase(txtinvoice.Text) & "%", modDate.StartSqlDate(dtlisted.Value), modDate.EndSqlDate(DateAdd(dtlisted.Value, gb.Day, txtrange.Value)), $sComp)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  xpurchase = 0
  xpayment = 0
  For Each $rData
    xpurchase = xpurchase + $rData["fldcredit"]
    xpayment = xpayment + $rData["flddebit"]
  Next
  ResizeGridView()

  txtpurchase.Value = Round(xpurchase, -2)
  txtpayment.Value = Round(xpayment, -2)
  txtpending.Value = Round(xpurchase - xpayment, -2)

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView1, Row)
  If Column = 3 Then
    GridView1.Data.Text = modReportVar.GetDateTimeReport($rData["fldpurdate"], gb.MediumDate)
  Else If Column = 6 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else If Column = 7 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else If Column = 8 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat(modSupplier.GetSuppPaymentByRef($strTitle, $rData[$aMyFields[Column]]), gb.Currency)
  Else If Column = 9 Then
    GridView1.Data.Text = modReportVar.GetLocaleNumberFormat(modSupplier.GetSuppCrdNoteByRef($strTitle, $rData[$aMyFields[Column]]), gb.Currency)
  Else
    GridView1.Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Sub ResizeGridView()

  With GridView1
    .Columns[0].Width = 75 * modBasic.$AppWidthRatio
    .Columns[1].Width = 175 * modBasic.$AppWidthRatio
    .Columns[2].Width = 125 * modBasic.$AppWidthRatio
    .Columns[3].Width = 100 * modBasic.$AppWidthRatio
    .Columns[4].Width = 150 * modBasic.$AppWidthRatio
    .Columns[5].Width = 1
    .Columns[6].Width = 125 * modBasic.$AppWidthRatio
    .Columns[7].Width = 125 * modBasic.$AppWidthRatio
    .Columns[8].Width = 125 * modBasic.$AppWidthRatio
    .Columns[9].Width = 125 * modBasic.$AppWidthRatio
    .Columns[10].Width = 125 * modBasic.$AppWidthRatio
    .Columns[11].Width = 100 * modBasic.$AppWidthRatio

    .Columns[0].Text = "SN"
    .Columns[1].Text = "Reference"
    .Columns[2].Text = "PayMode"
    .Columns[3].Text = "Date"
    .Columns[4].Text = "Invoice"
    .Columns[6].Text = "Purchase"
    .Columns[7].Text = "Payments"
    .Columns[8].Text = "Pending"
    .Columns[9].Text = "Credit Note"
    .Columns[10].Text = "Pur Order"
    .Columns[11].Text = "Location"
  End With

End

Public Sub GridView1_Menu()

  mnuhide.Popup

End

Public Sub mnudetreport_Click()

  Dim xPath As String

  If GridView1.Rows.Selection.Count > 0 Then
    Inc Application.Busy
    xPath = modHTMLStockReport.PurchaseEntryReport(GridView1[GridView1.Row, 1].Text, "PurEntry")      '
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub btnprintformat_Click()

  Dim xPath As String

  Inc Application.Busy
  xPath = modMaLePaFormat.PurchaseEntryMaLePa(GridView1[GridView1.Row, 1].Text, "PurEntry")
  Dec Application.Busy
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub mnuexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub mnueditpo_Click()

  Dim res As Result
  Dim res1 As Result
  Dim xval As String

  If GridView1.Rows.Selection.Count Then
    If Not GridView1[GridView1.Row, 10].Text Then

      xval = InputBox("Update Purchase Order", "Purchase", GridView1[GridView1.Row, 10].Text)
      If xval Then
        res = modDatabase.$myConn.Edit("tblpurchasebill", "fldreference=&1 and fldbillno=&2", GridView1[GridView1.Row, 1].Text, GridView1[GridView1.Row, 4].Text)
        If res.Available Then
          Inc Application.Busy
          res1 = modDatabase.$myConn.Edit("tblpurchase", "fldreference=&1 and fldbillno=&2", GridView1[GridView1.Row, 1].Text, GridView1[GridView1.Row, 4].Text)
          For Each res1
            res1["fldpurorder"] = xval
            res1.Update
          Next
          res["fldpurorder"] = xval
          res.Update
          Dec Application.Busy
          ShowGridView()
        Endif
      Endif

    Endif
  Endif

End

Public Sub mnuedit_Click()

  Dim res As Result
  Dim res1 As Result
  Dim xval As String

  If GridView1.Rows.Selection.Count Then

    xval = InputBox("Update Invoice No", "Purchase", GridView1[GridView1.Row, 4].Text)
    If xval Then
      res = modDatabase.$myConn.Edit("tblpurchasebill", "fldreference=&1 and fldbillno=&2", GridView1[GridView1.Row, 1].Text, GridView1[GridView1.Row, 4].Text)
      If res.Available Then
        Inc Application.Busy
        res1 = modDatabase.$myConn.Edit("tblpurchase", "fldreference=&1 and fldbillno=&2", GridView1[GridView1.Row, 1].Text, GridView1[GridView1.Row, 4].Text)
        For Each res1
          res1["fldbillno"] = xval
          res1.Update
        Next
        res["fldbillno"] = xval
        res.Update
        Dec Application.Busy
        ShowGridView()
      Endif
    Endif

  Endif

End

Public Sub mnuitems_Click()

  Dim sql As String
  Dim res As Result
  Dim hForm As FRequest

  If GridView1.Rows.Selection.Count Then
    sql = "select fldpurdate,fldcategory,fldstockid,fldstockno,fldstockno,flsuppcost,fldqtybonus,fldtotalqty,fldnetcost,fldtotalcost from tblpurchase where fldreference=&1"
    res = modDatabase.$syConn.Exec(sql, GridView1[GridView1.Row, 1].Text)
    If res.Available Then
      hForm = New FRequest(res, GridView1[GridView1.Row, 1].Text, "PurchaseReference", False)
      hForm.ShowModal
    Endif
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $strTitle, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End
