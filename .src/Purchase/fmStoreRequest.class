' Gambas class file

Private $rData1 As Result
Private $aMyFields1 As String[]

Public Sub Form_Open()

  modGeneralMain.ArrangeEmbedForms(Me, Panel1, "Vertical")

  cmbstatus.List = ["Requested", "Completed", "Approved", "Cancelled"]
  cmbstatus.Text = "Requested"
  If modHelpVariable.$LogInCategory = "Purchase" Then
    cmbtarget.List = modBasic.$AllCompPerList
    cmbtarget.Text = "%"
    btntransall.Visible = False
  Else If modHelpVariable.$LogInCategory = "Store" Then
    cmbtarget.Text = modBasic.$compID
    cmbtarget.Enabled = False
    btnverify.Visible = False
  Endif
  cmbsource.List = modBasic.$AllCompPerList
  cmbsource.Text = "%"
  If modBasic.$AccessPurchaseRequest = "Disable" Then
    btnverify.Enabled = False
  Endif

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_Resize()

  modGeneralMain.ShowSelectedFormMenu(Me)

End

Public Sub Form_KeyRelease()

  modGeneralmain.GoToNextControlTab()

End

Public Sub cmbstatus_KeyRelease()

  modFillContainer.AutoFillComboBox(cmbstatus)
  modFillContainer.RestrictComboToContent(cmbstatus)

End

''======================= ORDERED VIEW ===========================
Public Sub btnrefresh_Click()

  If cmbtarget.Text And If cmbsource.Text And If cmbstatus.Text Then
    SHowOrderGridView()
  Endif

End

Private Sub SHowOrderGridView()

  Dim sql As String

  sql = "select fldid,fldordtime,fldordcomp,fldstockid,fldcategory,fldfinalqty,fldurgent,fldexpdate,fldstatus,fldid,fldreference,fldtime,flduserid,fldcomp from tblrequest where fldcomp like &1 and fldstatus=&2 and fldordcomp like &3 and fldsave=&4"
  $rData1 = modDatabase.$myConn.Exec(sql, cmbtarget.Text, cmbstatus.Text, cmbsource.Text, True)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)

  With GridView2
    .Columns[0].Width = 1
    .Columns[1].Width = 125 * modBasic.$AppWidthRatio
    .Columns[2].Width = 75 * modBasic.$AppWidthRatio
    .Columns[3].Width = 275 * modBasic.$AppWidthRatio
    .Columns[4].Width = 1
    .Columns[5].Width = 75 * modBasic.$AppWidthRatio
    .Columns[6].Width = 100 * modBasic.$AppWidthRatio
    .Columns[7].Width = 125 * modBasic.$AppWidthRatio
    .Columns[8].Width = 100 * modBasic.$AppWidthRatio
    .Columns[9].Width = 25 * modBasic.$AppWidthRatio
    .Columns[10].Width = 125 * modBasic.$AppWidthRatio
    .Columns[11].Width = 125 * modBasic.$AppWidthRatio
    .Columns[12].Width = 100 * modBasic.$AppWidthRatio
    .Columns[13].Width = 75 * modBasic.$AppWidthRatio

    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Source"
    .Columns[3].Text = "Particulars"
    .Columns[5].Text = "QTY"
    .Columns[6].Text = "Urgency"
    .Columns[7].Text = "Delivery"
    .Columns[8].Text = "Status"
    .Columns[10].Text = "Reference"
    .Columns[11].Text = "Approval"
    .Columns[12].Text = "Approver"
    .Columns[13].Text = "Target"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer)

  $rData1.MoveTo(Row)
  modGeneralMain.GridViewDecoration(GridView2, Row)
  If Column = 1 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.MediumDate)
  Else If Column = 5 Then
    GridView2.Data.Text = modReportVar.GetLocaleNumberFormat($rData1[$aMyFields1[Column]], -2)
  Else If Column = 7 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.MediumDate)
  Else If Column = 9 Then
    GridView2.Data.Picture = Picture["icon:/tiny/apply"]
    GridView2.Data.Text = ""
  Else
    GridView2.Data.Text = $rData1[$aMyFields1[Column]]
  Endif
  GridView2.Rows[Row].Height = Max(GridView2.Rows[Row].Height, GridView2.Data.Font.RichTextHeight(GridView2.Data.Text, GridView2.Columns[Column].Width - 5 * modBasic.$AppWidthRatio) + (GridView2.Rows.Height - GridView2.Font.Height))
  GridView2.Data.WordWrap = True

End

Public Sub GridView2_Click()

  Dim res As Result
  Dim xbool As Boolean
  Dim xid As Long
  Dim xval As Integer

  If GridView2.Column = 9 Then
    If modHelpVariable.$LogInCategory = "Store" Then
      If CInt(GridView2[GridView2.Row, 5].Text) > 0 Then
        If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then

          xid = GridView2[GridView2.Row, 0].Text
          res = modDatabase.$myConn.Edit("tblrequest", "fldid=&1 and fldstatus=&2 and fldsave=&3", xid, "Requested", True)
          If res.Available Then
            xbool = TransferItem(GridView2[GridView2.Row, 3].Text, CInt(GridView2[GridView2.Row, 5].Text), GridView2[GridView2.Row, 2].Text)
            If xbool = True Then
              res["fldstatus"] = "Completed"
              res.Update
            Endif
            Wait
            SHowOrderGridView()
          Endif

        Endif
      Endif
    Endif

  Else If GridView2.Column = 5 Then
    If modHelpVariable.$LogInCategory = "Purchase" Then
      If Not GridView2[GridView2.Row, 12].Text Then
        xval = InputValue("Enter Approved Quantity", "Store Request", GridView2[GridView2.Row, 5].Text)
        res = modDatabase.$myConn.Edit("tblrequest", "fldid=&1 and fldstatus=&2 and fldsave=&3", GridView2[GridView2.Row, 0].Text, "Requested", True)
        If res.Available Then
          res["fldfinalqty"] = xval
          res.Update
          SHowOrderGridView()
        Endif
      Else
        Message.Warning("Information already approved", ("OK"))
      Endif
    Endif

  Else
    txtreference.Text = GridView2[GridView2.Row, 10].Text
  Endif

End

Public Sub GridView2_Menu()

  mnuhide.Popup()

End

Public Sub mnudelete_Click()

  Dim res As Result

  If GridView2.Rows.Selection.Count Then
    res = modDatabase.$myConn.Edit("tblrequest", "fldid=&1 and fldreference=&2 and flduserid IS NULL", GridView2[GridView2.Row, 0].Text, txtreference.Text)
    If res.Available Then
      res["fldstatus"] = "Cancelled"
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res.Update
      SHowOrderGridView()
    Endif
  Endif

End

Private Function TransferItem(sItem As String, sQTY As Float, sComp As String) As Boolean

  Dim res As Result
  Dim newstno As Long
  Dim xstock As Boolean
  Dim res1 As Result
  Dim xclear As Boolean

  xclear = False
  res1 = modDatabase.$myConn.Exec("select fldstockno,fldstockid,fldcategory,fldbatch,fldexpiry,fldqty,fldstatus,fldcategory,fldsellpr,fldmrp,fldcost from tblentry where fldstockid=&1 and fldstatus=&2 and fldcomp=&3", sItem, 1, modBasic.$compID)                                                   ''
  If res1.Available Then
    res1.MoveFirst
    If sComp <> modBasic.$compID And If sQTY <= res1["fldqty"] Then

      xstock = modStock.NewStock(res1["fldstockid"], res1["fldbatch"], sComp)

      modDatabase.$myConn.Begin
      newstno = modStock.GetItemStockNo(res1["fldstockid"], res1["fldbatch"], sComp)
      If newstno Then
        If xstock = True Then
          res = modDatabase.$myConn.Create("tblentry")
          res["fldstockno"] = newstno
          res["fldstockid"] = res1["fldstockid"]
          res["fldbatch"] = res1["fldbatch"]
          res["fldexpiry"] = res1["fldexpiry"]
          res["fldqty"] = 0
          res["fldstatus"] = modStock.ItemSaleStatus(res1["fldstockid"], sComp)
          If res1["fldcost"] Then
            res["fldcost"] = res1["fldcost"]
          Else
            res["fldcost"] = 0
          Endif
          res["fldsellpr"] = res1["fldsellpr"]
          If res1["fldmrp"] Then
            res["fldmrp"] = res1["fldmrp"]
          Else
            res["fldmrp"] = 0
          Endif
          res["fldsav"] = False
          res["fldcategory"] = res1["fldcategory"]
          res["fldcomp"] = sComp
          res["fldcode"] = 0
          res["xyz"] = True
          res.Update()
        Endif                                                              ''
        res = modDatabase.$myConn.Create("tbltransfer")
        res["fldstockno"] = newstno
        res["fldoldstockno"] = res1["fldstockno"]
        res["fldstockid"] = res1["fldstockid"]
        res["fldcategory"] = res1["fldcategory"]
        res["fldqty"] = sQTY
        If res1["fldcost"] Then
          res["fldnetcost"] = res1["fldcost"]
        Else
          res["fldnetcost"] = 0
        Endif
        res["fldsellpr"] = res1["fldsellpr"]
        res["fldsav"] = False
        res["fldfromentrytime"] = Now()
        res["fldfromuser"] = modBasic.$lbluser
        res["fldfromcomp"] = modBasic.$compID
        res["fldfromsav"] = False
        res["fldtoentrytime"] = ""
        res["fldtouser"] = ""
        res["fldtocomp"] = sComp
        res["fldtosav"] = False
        res["fldreference"] = ""
        res["xyz"] = True
        res.Update()
      Endif
      modDatabase.$myConn.Commit
      xclear = True
      Dec Application.Busy

    Endif
  Endif

  Return xclear

Catch
  modDatabase.$myConn.Rollback
  xclear = False
  Dec Application.Busy
  Return xclear
  modHelpVariable.CreateErrorReport()

End

Public Sub btnverify_Click()

  Dim res As Result
  Dim Row As Integer

  If txtreference.Text Then
    For Row = 0 To GridView2.Rows.Count - 1
      If GridView2.Rows[Row].Selected = True Then
        res = modDatabase.$myConn.Edit("tblrequest", "fldid=&1 and fldreference=&2 and flduserid IS NULL", GridView2[Row, 0].Text, txtreference.Text)
        If res.Available Then
          res["fldstatus"] = "Approved"
          res["flduserid"] = modBasic.$lbluser
          res["fldtime"] = Now()
          res.Update
        Endif
      Endif
    Next
    SHowOrderGridView()
    Balloon.Info(("Information saved"), btnverify)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub chkselallorder_Click()

  If chkselallorder.Value = True Then
    GridView2.SelectAll()
  Else If chkselallorder.Value = False Then
    GridView2.UnSelectAll()
  Endif

End

Public Sub btnreportverify_Click()

  Dim xPath As String

  If txtreference.Text Then
    Inc Application.Busy
    xPath = modHTMLStockReport.StoreRequestReport(txtreference.Text, True)
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub btnformat_Click()

  Dim xPath As String

  If txtreference.Text Then
    Inc Application.Busy
    xPath = modHTMLStockReport.StoreRequestReportEstimate(txtreference.Text, True)
    Dec Application.Busy
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView2, txtreference.Text, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btntransall_Click()

  Dim Row As Integer
  Dim res As Result
  Dim xbool As Boolean

  If modHelpVariable.$LogInCategory = "Store" Then
    For Row = 0 To GridView2.Rows.Count - 1

      res = modDatabase.$myConn.Edit("tblrequest", "fldid=&1 and fldstatus=&2 and fldsave=&3", GridView2[Row, 0].Text, "Requested", True)
      If res.Available Then
        xbool = TransferItem(GridView2[Row, 3].Text, CInt(GridView2[Row, 5].Text), GridView2[Row, 2].Text)
        If xbool = True Then
          res["fldstatus"] = "Completed"
          res.Update
        Endif
      Endif

    Next
    SHowOrderGridView()
  Endif

End
