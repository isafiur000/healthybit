' Gambas class file

Private aObjLabel As New Object[50]
Private aObjText As New Object[50]
Private aObjValue As New Object[50]
Private aObjDate As New Object[50]
Private aBSDate As New Object[50]
Private aObjBool As New Object[50]

Private $sTable As String
Private $sIndex As String
Private $sValue As Variant
Private $sFls As Integer

Public Sub _new(sTable As String, sIndex As String, sValue As Variant)

  $sTable = sTable
  $sIndex = sIndex
  $sValue = sValue

End

Public Sub Form_Open()

  Dim i As Integer
  Dim ref As ResultField
  Dim res As Result
  Dim AppFactor As Float

  Me.Center
  AppFactor = modBasic.$AppScaleFactor
  res = modDatabase.$myConn.Exec(Subst("select * from &1 where &2", $sTable, $sIndex) & "=&1", $sValue)
  If res.Available Then

    i = 0
    For Each ref In res.Fields
      aObjLabel[i] = New Label(Frame1) As "LabelGroup"
      With aObjLabel[i]
        .Left = 5 * AppFactor
        .Top = i * 25 * AppFactor
        .Width = 150 * AppFactor
        .Height = 20 * AppFactor
        .Border = Border.Plain
        .Text = ref.Name
        .Tag = i
      End With
      If aObjLabel[i].Text = $sIndex Then
        aObjLabel[i].Font.Bold = True
      Endif

      If ref.Type = gb.String Then
        aObjText[i] = New TextBox(Frame1) As "TextBoxGroup"
        With aObjText[i]
          .Left = 175 * AppFactor
          .Top = i * 25 * AppFactor
          .Width = 200 * AppFactor
          .Height = 20 * AppFactor
          .Border = Border.Plain
          .Text = res[ref.Name]
          .Tag = i
        End With
        If aObjText[i].Text = $sValue Then
          aObjText[i].ReadOnly = True
        Endif

      Else If ref.Type = gb.Integer Or If ref.Type = gb.Long Or If ref.Type = gb.Float Then
        aObjValue[i] = New ValueBox(Frame1) As "ValueBoxGroup"
        With aObjValue[i]
          .Left = 175 * AppFactor
          .Top = i * 25 * AppFactor
          .Width = 200 * AppFactor
          .Height = 20 * AppFactor
          .Border = Border.Plain
          If res[ref.Name] Then
            .Value = res[ref.Name]
          Else
            .Value = 0
          Endif
          .Tag = i
        End With
        If aObjValue[i].Value = $sValue Then
          aObjValue[i].ReadOnly = True
        Endif

      Else If ref.Type = gb.Date Then
        aObjDate[i] = New DateBox(Frame1) As "dateBoxGroup"
        With aObjDate[i]
          .Left = 175 * AppFactor
          .Top = i * 25 * AppFactor
          .Width = 200 * AppFactor
          .Height = 20 * AppFactor
          .Border = Border.Plain
          .Value = res[ref.Name]
          .Tag = i
        End With
        aBSDate[i] = New ToolButton(Frame1) As "BSButton"
        With aBSDate[i]
          .X = 380 * AppFactor
          .Y = i * 25 * AppFactor
          .Width = 30 * AppFactor
          .Height = 20 * AppFactor
          .Picture = Picture["icon:/small/calendar"]
          .Tag = i
        End With

      Else If ref.Type = gb.Boolean Then
        aObjBool[i] = New CheckBox(Frame1) As "CheckBoxGroup"
        With aObjBool[i]
          .Left = 175 * AppFactor
          .Top = i * 25 * AppFactor
          .Width = 200 * AppFactor
          .Height = 20 * AppFactor
          .Value = res[ref.Name]
          .Tag = i
        End With

      Endif

      i = i + 1
    Next
    $sFls = i
    Frame1.Height = i * 25 * AppFactor

  Endif

End

Public Sub BSButton_Click()

  Dim j As Integer
  Dim xx As String

  j = Last.Tag
  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(aObjDate[j].Value))
  If xx Then
    aObjDate[j].Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnok_Click()

  Dim res As Result
  Dim i As Integer

  If Message.Question(("Are you Sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Edit($sTable, $sIndex & "=&1", $sValue)
    For i = 0 To $sFls - 1
      res[aObjLabel[i].Text] = GetFieldValue(aObjLabel[i].Text)
    Next
    res.Update
    Balloon.Info(("Information updated"), btnok)
  Endif

End

Private Function GetFieldValue(sField As String) As Variant

  Dim xx As Variant
  Dim i As Integer

  For i = 0 To $sFls - 1
    If aObjLabel[i].Text = sField Then
      If aObjText[i] Then
        xx = aObjText[i].Text
      Else If aObjValue[i] Then
        xx = aObjValue[i].Value
      Else If aObjDate[i] Then
        xx = aObjDate[i].Value
      Else If aObjBool[i] Then
        xx = aObjBool[i].Value
      Endif
      Break
    Endif
  Next
  Return xx

End
