' Gambas class file

Private $toCon As Connection

Public Sub Form_Open()

  cmbtypeB.List = ["sqlite3", "mysql", "postgresql", "odbc"]

End

Public Sub chkcurB_Click()

  cmbtypeB.Text = modDatabase.$myConn.Type
  txtserverB.Text = modDatabase.$myConn.Host
  txtdbB.Text = modDatabase.$myConn.Name
  txtuserB.Text = modDatabase.$myConn.User
  txtpassB.Text = modDatabase.$myConn.Password

End

Public Sub btnconB_Click()

  $toCon = modDBApp.OpenConnection(cmbtypeB.Text, txtserverB.Text, txtdbB.Text, txtuserB.Text, txtpassB.Text)
  If $toCon.Opened = True Then
    conB.Value = True
    TabPanel1.Enabled = True
    FillTableList()
    Balloon.Info(("Connected"), btnconB)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub FillTableList()

  Dim hTable1 As Table

  cmbtablecsv.Clear
  cmbtablecsv2.Clear
  cmbtablejson.Clear
  For Each hTable1 In $toCon.Tables
    If hTable1.System = True Then
    Else If hTable1.Name = "tblkey" Then
    Else If hTable1.Name = "tblnewkey" Then
    Else
      cmbtablecsv.Add(hTable1.Name)
      cmbtablecsv2.Add(hTable1.Name)
      cmbtablejson.Add(hTable1.Name)
    Endif
  Next

End

''---------------------------------------- CSV Import ------------------------------
Public Sub txtimport_Click()

  If Dialog.OpenFile() Then Return
  txtimport.Text = Dialog.Path

End

Public Sub btntransfercsv_Click()

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB($toCon, cmbtablecsv.Text, txtimport.Text, txtdelim.Text)
    Balloon.Info(("Information updated"), btntransfercsv)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

''---------------------------------- CSV Update -------------------------
Public Sub txtimport2_Click()

  If Dialog.OpenFile() Then Return
  txtimport2.Text = Dialog.Path

End

Public Sub cmbtablecsv2_Click()

  Dim xList As String[]

  cmbprifield.Clear
  cmbtargetfield.Clear
  xList = modDBApp.GetFIeldsinTable(cmbtablecsv2.Text, True, $toCon)
  cmbprifield.List = xList
  cmbtargetfield.List = xList

End

Public Sub btntransfercsv2_Click()

  Dim hCSV As CsvFile
  Dim xcoll As Collection
  Dim res As Result

  If Exist(txtimport2.Text) And If cmbprifield.Text And If cmbtargetfield.Text And If txtdelim2.Text Then
    If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then

      Inc Application.Busy
      hCSV = New CsvFile(txtimport2.Text, txtdelim2.Text)
      While hCSV.Eof = False
        xcoll = hCSV.Read()

        res = $toCon.Edit(cmbtablecsv2.Text, cmbprifield.Text & "=&1", xcoll[cmbprifield.Text])
        If res.Available Then
          res[cmbtargetfield.Text] = xcoll[cmbtargetfield.Text]
          Try res.Update()
        Endif

      Wend
      hCSV.Close()
      Dec Application.Busy

    Endif
  Endif

End

''=========================== JSON Import ====================
Public Sub txtimpojson_Click()

  If Dialog.OpenFile() Then Return
  txtimpojson.Text = Dialog.Path

End

Public Sub btntransferjson_Click()

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    Inc Application.Busy
    modTextDB.ImportJSONToCurrentDB($toCon, cmbtablejson.Text, txtimpojson.Text)
    Dec Application.Busy
    Balloon.Info(("Information updated"), btntransferjson)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

''=================== Bulk json import ===============

Public Sub txtjsondir_Click()

  If Dialog.SelectDirectory() Then Return
  txtjsondir.Text = Dialog.Path

End

Public Sub btnjsondir_Click()

  Dim FileList As String[]
  Dim sFile As String

  If txtjsondir.Text Then
    FileList = Dir(txtjsondir.Text, Null, gb.File)
    Inc Application.Busy
    For Each sFile In FileList
      lblcurrent.Text = File.BaseName(sFile)
      Wait
      modTextDB.ImportJSONToCurrentDB($toCon, lblcurrent.Text, txtjsondir.Text &/ sFile)
    Next
    Dec Application.Busy
    Balloon.Info(("Information updated"), btnjsondir)
    Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
